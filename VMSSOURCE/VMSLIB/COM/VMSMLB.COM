$ !
$ !		[ VMSLIB.COM ] V M S M L B . C O M
$ !
$ !	Build the user interface macro libraries:
$ !		STARLET.MLB
$ !		STARLET.L32
$ !		STARLET.REQ
$ !		TPAMAC.L32
$ !		TPAMAC.REQ
$ !		CLIMAC.L32
$ !		CLIMAC.REQ
$ !
$ !	Build STARLET.MLB
$ !
$ SET NOON
$ DELETE LIB$:STARLET.MLB;*
$ LIB/MACRO /CREATE=(BLOCKS=500, MODULES=750) LIB$:STARLET 
$ LIB/MACRO /SQUEEZE LIB$:STARLET -
	SRC$:UTLDEFM, STARMISC, TPAMAR, EODEFM, -
	LIB$:STARDEF, STARLET, SSMSG, SHRMSG, OPDEF, -
	LIB$:OPCDEF, OPCMSG, LIBCQDEF, SRMDEF, SCRDEF, -
	LIB$:LBRUSR, OBJFMT, -
	LIB$:DSTDEF, -
	LIB$:MOUNTMSG
$ LIB/MACRO /SQUEEZE LIB$:STARLET -
	SHRLIB$:CJFRUFMAC, JNLDEF, JNLUSR, -		! From CJF
		RUFUSR, -				! From RUF
		RMS32MAC, RMSCALLS, -			! From RMS
		RMSUSR, RMSDEF, RMSFALMSG, -		! From RMS
		JBCMSG, -				! From JOBCTL
		BASMSG, COBMSG, FORMSG, LIBMSG, -	! From RTL 
		MTHMSG, OTSMSG, STRMSG, -		! From RTL
		RTLMACMAR, -				! From RTL
		LIBCLIDEF, LIBDCFDEF, -			! From RTL
		SMGDEF, -				! From RTL
		SMGTRMPTR, -				! From RTL
		CLIMAC, -				! From CLIUTL
		CLIMSG, -				! From DCL
!**JNL**	CHKSRVMAC, -				! From CHKPNT
		CRF, CRFMACROS				! From CRF
$ !
$ !	Build STARLET.REQ & .L32
$ !
$ DELETE LIB$:STARLET.REQ;*
$ COPY -
	SRC$:UTLDEFB.B32, -
	LIB$:STARDEF.R32, STARLET, -
	LIB$:SSMSG.R32, SHRMSG, OPCMSG.B32, -
	LIB$:SCRDEF.R32, DSTDEF, SRMDEF, OPCDEF, -
	LIB$:LBRUSR.R32, LIBCQDEF, -
	LIB$:OBJFMT.R32, -
	SRC$:EODEFB.B32, -
	LIB$:OPDEF.R32,  -
	LIB$:MOUNTMSG.B32 -
  LIB$:STARLET.REQ
$
$ APPEND -
	SHRLIB$:CJFRUFMAC.R32, -			! From CJF
		JNLDEF.R32, JNLBUSR, -			! From CJF
		RUFUSR.R32, -				! From RUF
		RMSFALMSG.B32, RMSDEF.R32, RMSUSR, -	! From RMS
		RMSMAC.REQ, -				! From RMS
		JBCMSG.B32, -				! From JOBCTL
		CRF.R32, CRFMAC.REQ, -			! From CRF
		RTLMACB32.REQ, -			! From RTL
		LIBCLIDEF.REQ, LIBDCFDEF.REQ, -		! From RTL
		SMGDEF.R32,-				! From RTL
		SMGTRMPTR.R32,-				! From RTL
!**JNL**	CHKSRVMAC.R32, -			! From CHKPNT
		MSGDEF.R32 -				! From MSGFIL
  LIB$:STARLET.REQ
$
$ DELETE LIB$:STARLET.L32;*
$ BLISS/NOLIST/LIBRARY=LIB$:STARLET LIB$:STARLET.REQ
$ !
$ DELETE LIB$:TPAMAC.L32;*
$ BLISS/NOLIST/LIBRARY=LIB$:TPAMAC SRC$:TPAMAC.REQ
$ DELETE LIB$:CLIMAC.L32;*
$ BLISS/NOLIST/LIBRARY=LIB$:CLIMAC SRC$:CLIMAC.REQ
