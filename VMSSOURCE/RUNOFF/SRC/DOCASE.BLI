%TITLE 'Setup character case translation rules'
MODULE DOCASE (
		IDENT = 'V04-000'
%BLISS32[,
	ADDRESSING_MODE(EXTERNAL=LONG_RELATIVE,NONEXTERNAL=LONG_RELATIVE)
	]
		) =
BEGIN
!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982, 1984 BY				    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************
!
!++
! FACILITY:	 DSR (Digital Standard RUNOFF) / DSRPLUS
!
! ABSTRACT: Processes .UPPER CASE and .LOWER CASE commands, and
!	    other case translation requests.
!
!
! ENVIRONMENT: Transportable
!
! AUTHOR: R.W.Friday	 CREATION DATE: May, 1978
!

%SBTTL 'Revision History'
!
! MODIFIED BY:
!
!	003	REM00003	Ray Marshall	7-November-1983
!		Added supporting logic for the case rules for the DEC multi-
!		  national character set.
!
!	002	KFA00002	Ken Alden	07-Mar-1983
!		Global edit of all modules. Updated module names, idents,
!		copyright dates. Changed require files to BLISS library.
!
!--

%SBTTL 'Module Level Declarations'

!
! TABLE OF CONTENTS:
!

FORWARD ROUTINE
    SETCAS : NOVALUE;

!
! INCLUDE FILES:
!

LIBRARY 'NXPORT:XPORT';			! XPORT Library
REQUIRE 'REQ:RNODEF';			! RUNOFF variant definitions

%IF DSRPLUS %THEN
LIBRARY 'REQ:DPLLIB';			! DSRPLUS BLISS Library
%ELSE
LIBRARY 'REQ:DSRLIB';			! DSR BLISS Library
%FI

!
! MACROS:
!
!
! EQUATED SYMBOLS:
!
!
! OWN STORAGE:
!
!
! EXTERNAL REFERENCES:
!

EXTERNAL
    GCA : GCA_DEFINITION,
    SCA : SCA_DEFINITION;


GLOBAL ROUTINE DOCASE (HANDLER_CODE) : NOVALUE =

!++
! FUNCTIONAL DESCRIPTION:
!
!	Establishes the default case translation rules until they
!	are changed.
!
! FORMAL PARAMETERS:
!
!	HANDLER_CODE indicates the command that is to be processed.
!
! IMPLICIT INPUTS:	None
!
! IMPLICIT OUTPUTS:
!
!	GCA_CASE is set to remember the case set by the most
!	recent .UPPER CASE or .LOWER CASE command.
!
! ROUTINE VALUE:
! COMPLETION CODES:	None
!
! SIDE EFFECTS:	None
!
!--

    BEGIN

    IF 						!
	.HANDLER_CODE EQL H_UPPER_CASE
    THEN 					!
	BEGIN
	GCA_CASE = LEAVE_CASE;
	SETCAS (LEAVE_CASE);
	END
    ELSE 					!
	BEGIN
	GCA_CASE = FORCE_LOWER;
	SETCAS (FORCE_LOWER);
	END;

    END;					!End of DOCASE


GLOBAL ROUTINE SETCAS (TRANSLATION) : NOVALUE = 	!

!++
! FUNCTIONAL DESCRIPTION:
!
!	This routine sets up case rules.
!
! FORMAL PARAMETERS:
!
!	TRANSLATION can have one of the values defined in SETCAS.REQ.
!
! IMPLICIT INPUTS:
!
!	SCA_WORD_SET:
!	If being called as a result of a command, then SCA_WORD_SET
!	will be FALSE, so case translation will be established
!	for everything.  If not being called as a result of a command,
!	case rules for the current word take precedence over the
!	default case translation.  For example, assuming the normal
!	flag character definitions, and the capitalize flag enabled,
!	the sequence ^^<lower generates 'LOWER', as should <^^lower.
!	The latter situation is what the SCA_WORD_SET test catches.
!	In any case, the requested case translation takes effect as
!	soon as a new word is started, or SCA_WORD_SET becomes false.
!
! IMPLICIT OUTPUTS:	None
!
! ROUTINE VALUE:
! COMPLETION CODES:	None
!
! SIDE EFFECTS:	None
!
!--

    BEGIN

    CASE .TRANSLATION FROM SETCASE_LOW TO SETCASE_HIGH OF
	SET

	[FORCE_UPPER] :
	    !Force all letters to upper case.
	    BEGIN
	    IF 	NOT .SCA_WORD_SET
	    THEN 				!
		!Case rules take effect immediately.
		BEGIN
		SCA_WRD_FC_LT = %C'A' - %C'a';
		SCA_WRD_FC_UT = 0;
		SCA_WRD_OC_LT = %C'A' - %C'a';
		SCA_WRD_OC_UT = 0;

		SCA_MNWRD_FC_LT = %DECIMAL'192' - %DECIMAL'224';
		SCA_MNWRD_FC_UT = 0;
		SCA_MNWRD_OC_LT = %DECIMAL'192' - %DECIMAL'224';
		SCA_MNWRD_OC_UT = 0;
		END;

	    SCA_FCBE_LT = %C'A' - %C'a';
	    SCA_FCBE_UT = 0;
	    SCA_OCBE_LT = %C'A' - %C'a';
	    SCA_OCBE_UT = 0;
	    SCA_FC_LT = %C'A' - %C'a';
	    SCA_FC_UT = 0;
	    SCA_OC_LT = %C'A' - %C'a';
	    SCA_OC_UT = 0;

	    SCA_MNFCBE_LT = %DECIMAL'192' - %DECIMAL'224';
	    SCA_MNFCBE_UT = 0;
	    SCA_MNOCBE_LT = %DECIMAL'192' - %DECIMAL'224';
	    SCA_MNOCBE_UT = 0;
	    SCA_MNFC_LT = %DECIMAL'192' - %DECIMAL'224';
	    SCA_MNFC_UT = 0;
	    SCA_MNOC_LT = %DECIMAL'192' - %DECIMAL'224';
	    SCA_MNOC_UT = 0;
	    END;

	[FORCE_LOWER] :
	    !Force all letters to lower case.
	    BEGIN

	    IF 	NOT .SCA_WORD_SET
	    THEN 				!
		!Case translation takes effect immediately.
		BEGIN
		SCA_WRD_FC_LT = 0;
		SCA_WRD_OC_LT = 0;
		SCA_WRD_FC_UT = %C'a' - %C'A';
		SCA_WRD_OC_UT = %C'a' - %C'A';

		SCA_MNWRD_FC_LT = 0;
		SCA_MNWRD_OC_LT = 0;
		SCA_MNWRD_FC_UT = %DECIMAL'224' - %DECIMAL'192';
		SCA_MNWRD_OC_UT = %DECIMAL'224' - %DECIMAL'192';
		END;

	    SCA_FC_LT = 0;
	    SCA_OC_LT = 0;
	    SCA_FC_UT = %C'a' - %C'A';
	    SCA_OC_UT = %C'a' - %C'A';
	    SCA_FCBE_LT = 0;
	    SCA_FCBE_UT = %C'a' - %C'A';
	    SCA_OCBE_LT = 0;
	    SCA_OCBE_UT = %C'a' - %C'A';

	    SCA_MNFC_LT = 0;
	    SCA_MNOC_LT = 0;
	    SCA_MNFC_UT = %DECIMAL'224' - %DECIMAL'192';
	    SCA_MNOC_UT = %DECIMAL'224' - %DECIMAL'192';
	    SCA_MNFCBE_LT = 0;
	    SCA_MNFCBE_UT = %DECIMAL'224' - %DECIMAL'192';
	    SCA_MNOCBE_LT = 0;
	    SCA_MNOCBE_UT = %DECIMAL'224' - %DECIMAL'192';
	    END;

	[LEAVE_CASE] :
	    !Don't change case at all.
	    BEGIN

	    IF 	NOT .SCA_WORD_SET
	    THEN 				!
		!Case rules take effect immediately.
		BEGIN
		SCA_WRD_FC_LT = 0;
		SCA_WRD_FC_UT = 0;
		SCA_WRD_OC_LT = 0;
		SCA_WRD_OC_UT = 0;

		SCA_MNWRD_FC_LT = 0;
		SCA_MNWRD_FC_UT = 0;
		SCA_MNWRD_OC_LT = 0;
		SCA_MNWRD_OC_UT = 0;
		END;

	    SCA_FCBE_LT = 0;
	    SCA_FCBE_UT = 0;
	    SCA_OCBE_LT = 0;
	    SCA_OCBE_UT = 0;
	    SCA_FC_LT = 0;
	    SCA_FC_UT = 0;
	    SCA_OC_LT = 0;
	    SCA_OC_UT = 0;

	    SCA_MNFCBE_LT = 0;
	    SCA_MNFCBE_UT = 0;
	    SCA_MNOCBE_LT = 0;
	    SCA_MNOCBE_UT = 0;
	    SCA_MNFC_LT = 0;
	    SCA_MNFC_UT = 0;
	    SCA_MNOC_LT = 0;
	    SCA_MNOC_UT = 0;
	    END;

	[FIRST_CAPS] :
	    !Capitalize words.
	    BEGIN
	    LOCAL
		OTHER_CASE_MN,	! Multinational char. set flag
		OTHER_CASE;

	    IF .GCA_CASE EQL FORCE_LOWER
	    THEN
		BEGIN
		!For files all in upper case, force remaining
		!characters to lower case.
		OTHER_CASE = %C'a' - %C'A';
		OTHER_CASE_MN = %DECIMAL'224' - %DECIMAL'192'
		END
	    ELSE
		BEGIN
		!For files from terminals having both upper and
		!lower case, leave remaining characters alone.
		OTHER_CASE = 0;
		OTHER_CASE_MN = 0
		END;
!
	    IF 	NOT .SCA_WORD_SET
	    THEN 				!
		!Case rules take effect immediately.
		BEGIN
		SCA_WRD_FC_LT = %C'A' - %C'a';
		SCA_WRD_FC_UT = 0;
		SCA_WRD_OC_LT = 0;
		SCA_WRD_OC_UT = .OTHER_CASE;

		SCA_MNWRD_FC_LT = %DECIMAL'192' - %DECIMAL'224';
		SCA_MNWRD_FC_UT = 0;
		SCA_MNWRD_OC_LT = 0;
		SCA_MNWRD_OC_UT = .OTHER_CASE_MN
		END;

	    SCA_FCBE_LT = %C'A' - %C'a';
	    SCA_FCBE_UT = 0;
	    SCA_OCBE_LT = 0;
	    SCA_OCBE_UT = .OTHER_CASE;
	    SCA_FC_LT = %C'A' - %C'a';
	    SCA_FC_UT = 0;
	    SCA_OC_LT = 0;
	    SCA_OC_UT = .OTHER_CASE;

	    SCA_MNFCBE_LT = %DECIMAL'192' - %DECIMAL'224';
	    SCA_MNFCBE_UT = 0;
	    SCA_MNOCBE_LT = 0;
	    SCA_MNOCBE_UT = .OTHER_CASE_MN;
	    SCA_MNFC_LT = %DECIMAL'192' - %DECIMAL'224';
	    SCA_MNFC_UT = 0;
	    SCA_MNOC_LT = 0;
	    SCA_MNOC_UT = .OTHER_CASE_MN
	    END;

	[ONE_CAP] :
	    !Capitalize first word, all other letters to lower case.
	    BEGIN
	    !Case translation takes effect immediately.
	    SCA_WRD_FC_LT = %C'A' - %C'a';
	    SCA_WRD_OC_LT = 0;
	    SCA_WRD_FC_UT = 0;
	    SCA_WRD_OC_UT = %C'a' - %C'A';

	    SCA_FC_LT = 0;
	    SCA_OC_LT = 0;
	    SCA_FC_UT = %C'a' - %C'A';
	    SCA_OC_UT = %C'a' - %C'A';
	    SCA_FCBE_LT = 0;
	    SCA_FCBE_UT = %C'a' - %C'A';
	    SCA_OCBE_LT = 0;
	    SCA_OCBE_UT = %C'a' - %C'A';

	    SCA_MNWRD_FC_LT = %DECIMAL'192' - %DECIMAL'224';
	    SCA_MNWRD_OC_LT = 0;
	    SCA_MNWRD_FC_UT = 0;
	    SCA_MNWRD_OC_UT = %DECIMAL'224' - %DECIMAL'192';

	    SCA_MNFC_LT = 0;
	    SCA_MNOC_LT = 0;
	    SCA_MNFC_UT = %DECIMAL'224' - %DECIMAL'192';
	    SCA_MNOC_UT = %DECIMAL'224' - %DECIMAL'192';
	    SCA_MNFCBE_LT = 0;
	    SCA_MNFCBE_UT = %DECIMAL'224' - %DECIMAL'192';
	    SCA_MNOCBE_LT = 0;
	    SCA_MNOCBE_UT = %DECIMAL'224' - %DECIMAL'192';
	    END;
	TES;

    END;					!End of SETCAS

END						!End of module
ELUDOM
