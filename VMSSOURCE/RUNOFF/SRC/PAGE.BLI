%TITLE 'Processes .PS, .LM, and .RM directives'
MODULE PAGE (	IDENT = 'V04-000'
		%BLISS32[, ADDRESSING_MODE (EXTERNAL    = long_relative,
					    NONEXTERNAL = long_relative)]
		) =
BEGIN
!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982, 1984 BY				    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************
!
!++
! FACILITY:	 DSR (Digital Standard RUNOFF) / DSRPLUS
!
! ABSTRACT:
!
!	Processes .PAGE SIZE, .LEFT MARGIN, and .RIGHT MARGIN commands.
!
! ENVIRONMENT:	Transportable
!
! AUTHOR:	R.W.Friday	CREATION DATE:	June, 1978
!

%SBTTL 'Revision History'
!
! MODIFIED BY:
!
!	008	KFA00008	Ken Alden	06-Jul-1983
!		When GETNUM does not receive a value for .RM, the
!		  right margin will now get set to 70 (default).
!
!	007	REM00007	Ray Marshall	20-April-1983
!		Retrofit decoupling of page size and right margin that was
!		  done for DSRPLUS to now be in affect for DSR.
!
!	006	KAD00006	Keith Dawson	07-Mar-1983
!		Global edit of all modules. Updated module names, idents,
!		copyright dates. Changed require files to BLISS library.
!
!--

%SBTTL 'Module Level Declarations'
!
! TABLE OF CONTENTS:
!
! INCLUDE FILES:
!

LIBRARY 'NXPORT:XPORT';			! XPORT Library
REQUIRE 'REQ:RNODEF';			! RUNOFF variant definitions

%IF DSRPLUS %THEN
LIBRARY 'REQ:DPLLIB';			! DSRPLUS BLISS Library
%ELSE
LIBRARY 'REQ:DSRLIB';			! DSR BLISS Library
%FI

!
! EXTERNAL REFERENCES:
!
EXTERNAL
    gca		: gca_definition,
    ira		: fixed_string,
    nmlst	: number_list,
    numprm	: numprm_define,
    phan	: phan_definition,
    sca		: sca_definition;

EXTERNAL LITERAL
    rnfbms,
    rnfinm;

EXTERNAL ROUTINE
    erma,
    glnm;

!
! OWN STORAGE:
!
OWN
  rm_temp : INITIAL (0);


GLOBAL ROUTINE page (handler_code) : NOVALUE =

!++
! FUNCTIONAL DESCRIPTION:
!
!	See the ABSTRACT, above.
!
! FORMAL PARAMETERS:
!
!	handler_code - indicates which command is to be processed.
!
! IMPLICIT INPUTS:
!
!	numprm - contains a number as processed by getnum.
!
! IMPLICIT OUTPUTS:	None
!
! ROUTINE VALUE:
! COMPLETION CODES:	None
!
! SIDE EFFECTS:		None
!--

    BEGIN

    SELECT .handler_code OF
	SET

	[h_right_margin, h_left_margin] :
	    IF NOT .num_result
	    THEN
		!
		! Ignore command if number is in error.
		!
		RETURN;

	[h_right_margin] :
	    BEGIN

	    IF .num_sign NEQ 0
	    THEN
		!
	        ! Adjust existing margin, + or -.
		!
		BEGIN
		!
		! Save adjustment value if it is positive.
		! Otherwise, reset it to 0.
		!
		IF .num_value GTR 0
		THEN
		    rm_temp = .num_value
		ELSE
		    rm_temp = 0;

		num_value = .num_value + .sca_rm;
		END;

	    IF .num_length EQL 0
	    THEN
		BEGIN
		sca_rm = 70;	!Current default 6-Jul-1983  [KFA]
		RETURN;
		END;

	    !
	    ! Right margin is not unlimited.
	    !
	    IF (.num_value LEQ .sca_lm) OR (.num_value GTR 150)
	    THEN
		BEGIN
		!
		! Bad margin.
		!
		erma (rnfbms, false);
		RETURN;
		END
	    ELSE
		sca_rm = .num_value;

	    END;

	[h_left_margin] :
	    BEGIN

	    IF .num_sign NEQ 0
	    THEN
		!
	        ! Adjust existing margin, + or -.
		!
		num_value = .num_value + .sca_lm;

	    IF .num_value GEQ .sca_rm
	    THEN
		!
		! Bad margin.
		!
		BEGIN
		erma (rnfbms, false);
		RETURN;
		END
	    ELSE
		sca_lm = .num_value;

	    END;

	[h_page_size] :
	    BEGIN
	    !
	    ! Turn on paging unless /NOPAGING.
	    !
	    phan_paging = .phan_cmd_paging;
	    !
	    ! Get parameters.
	    !
	    glnm (2);

	    IF .nmlst_count GEQ 1
	    THEN
		BEGIN
		!
		! First parameter defines lines per page.
		!
		CASE .nmlst_descr (1) FROM 0 TO 4 OF
		    SET

		    [nm_bad, nm_null] :
			!
			! Use current value if input was bad or not supplied.
			!
			nmlst_value (1) = .phan_llines;

		    [nm_plus, nm_minus] :
			!
			! Adjust value if adjustment was specified, + or -.
			!
			nmlst_value (1) = .nmlst_value (1) + .phan_llines;

		    [nm_unsigned] :
			!
			! Nothing to do if an unsigned number.
			!
			0;

		    TES;

		IF .nmlst_value (1) LSS phan_min_page_length
		THEN
		    !
		    ! Page is too short.
		    !
		    erma (rnfinm, false)
		ELSE
		    !
		    ! Set new page length.
		    !
		    phan_llines = .nmlst_value (1);

		END;
	    !
	    ! Process page width, if supplied.
	    !
	    IF .nmlst_count GEQ 2
	    THEN
		BEGIN
		!
		! Second parameter defines page width.
		!
		CASE .nmlst_descr (2) FROM 0 TO 4 OF
		    SET

		    [nm_bad, nm_null] :
			!
			! Use current value if input was bad or not supplied.
			!
			nmlst_value (2) = .gca_lwidth;

		    [nm_plus] :
			!
			! Increase page width.
			!
			nmlst_value (2) = .nmlst_value (2) + .gca_lwidth;

		    [nm_minus] :
			!
			! Decrease page width or right margin.
			!
			BEGIN
			!
			! See if adjustment value is same as saved value.
			!
			IF .rm_temp + .nmlst_value (2) EQL 0
			THEN
			    !
			    ! If so, use it to pull in the right margin only.
			    !
			    BEGIN
			    sca_rm = .sca_rm + .nmlst_value (2);
			    !
			    ! Do not change setting of page width.
			    !
			    nmlst_value (2) = .gca_lwidth;
			    !
			    ! Reset saved value to zero.
			    !
			    rm_temp = 0;
			    END
			ELSE
			    !
			    ! Decrease page width.
			    !
			    nmlst_value (2) = .nmlst_value (2) + .gca_lwidth;

			END;

		    [nm_unsigned] :
			!
			! Nothing to do if just a number.
			!
			0;

		    TES;

		IF  .nmlst_value (2) GTR 150
		THEN
		    !
		    ! Bad margin specification.
		    !
		    erma (rnfbms, false)
		ELSE
		    gca_lwidth = .nmlst_value (2);

		END;
	    END;
	TES;
    END;					! End of PAGE

END						! End of module
ELUDOM
