MODULE BAS$ERRTXT (				!
		IDENT = '1-004'			! File: BASERRTXT.B32
		) =				! Edit: ML1004
BEGIN
!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982, 1984 BY				    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************
!
!

!++
! FACILITY:  BASIC-PLUS-2 Error Messages
!
! ABSTRACT:
!
!	This module returns to a BLISS program the 
!	error text corresponding to a BASIC
!	error code.  This module is used to produce BASMSG.MSG.
!	It is not part of the Run-time library.
!
! ENVIRONMENT:  VAX-11 user mode
!
! AUTHOR: John Sauter, CREATION DATE: 03-Nov-78
!
! MODIFIED BY:
!
! 1-001	- Original.
! 1-002	- Ignore fifth argument of ERR macro in ERROR_LIST.  JBS 28-DEC-78
! 1-003 - modified to produce .MSG output rather than .MDL output.
!	  MDL 24-Jun-1982
! 1-004 - interface to BLISS caller.  MDL 25-Jun-1982
!
!--

!
!<BLF/PAGE>

!
! SWITCHES:
!

SWITCHES ADDRESSING_MODE (EXTERNAL = GENERAL, NONEXTERNAL = WORD_RELATIVE);

!
! LINKAGES:
!
!	NONE
!
! TABLE OF CONTENTS:
!

FORWARD ROUTINE
    ERRTXT;					! Return name & text

!
! INCLUDE FILES:
!

LIBRARY 'RTLSTARLE';				! Define system symbols

REQUIRE 'RTLIN:BASERRMSG.REQ';			! Define the ERROR_LIST macro

!
! MACROS:
!
!	NONE
!
! EQUATED SYMBOLS:
!

LITERAL
    BAS$K_FAC_NO = 26;				! The facility code number for BASIC

!
! OWN STORAGE:
!
!	NONE
!
! EXTERNAL REFERENCES:
!

EXTERNAL ROUTINE
    LIB$SCOPY_R_DX;				! copy a string


GLOBAL ROUTINE ERRTXT (			! get error info
	CODE,
						! BASIC error code
	NAMLEN,
						! length of mnemonic
	NAME,
						! mnemonic
	TXTLEN,
						! length of text
	TEXT,					! text of error message

	SEVLEN,					! length of severity

	SEVERITY				! severity
    ) =

!++
! FUNCTIONAL DESCRIPTION:
!
!	Return mnemonic and text of a BASIC
!	error code to a BLISS program.
!
! FORMAL PARAMETERS:
!
!	CODE.rl.r	The BASIC-PLUS-2 error code.  The codes and
!			their meanings are listed in file BASERRMSG.REQ.
!	NAMLEN.wl.r	The length of the VAX/VMS mnemonic for the
!			condition value.
!	NAME.wt.d	The VAX/VMS mnemonic for the condition value.
!			(Length as given above.)
!	TXTLEN.wl.r	The length of the text corresponding to the
!			BASIC error code.
!	TEXT.wt.d	The text corresponding to the BASIC error code.
!			(Length as given above.)
!	SEVLEN.wl.r	The length of the severity level corresponding
!			to the BASIC error code.
!	SEVERITY.wt.d	the severity level corresponding to the BASIC
!			error code.
!
! IMPLICIT INPUTS:
!
!	NONE
!
! IMPLICIT OUTPUTS:
!
!	NONE
!
! ROUTINE VALUE:
!
!	Always success (SS$_NORMAL)
!
! COMPLETION CODES:
!
!	NONE
!
! SIDE EFFECTS:
!
!	NONE
!
!--

    BEGIN

!+
! define the SEVERITY LENGTH of each message
!-
    MACRO
	ERR (NUMBER, CODE, TEXT, SEVERITY, SYSTEM_HANDLING) =
	%CHARCOUNT (SEVERITY)
    %;

    BIND
	SEVERITY_LEN = UPLIT(ERROR_LIST) : VECTOR [256];

    UNDECLARE %QUOTE
	ERR;


!+
! define the SEVERITY of each message
!-

    MACRO
	ERR (NUMBER, CODE, TEXT, SEVERITY, SYSTEM_HANDLING) =
	UPLIT (%ASCIZ %STRING (SEVERITY))
    %;

    BIND
	SEVERITY_TEXT = UPLIT(ERROR_LIST) : VECTOR [256];

    UNDECLARE %QUOTE
	ERR;

!+
! Define the length of each message.
!-

    MACRO
	ERR (NUMBER, CODE, TEXT, SEVERITY, SYSTEM_HANDLING) =
	%CHARCOUNT (TEXT)
    %;

    BIND
	MESSAGE_LEN = UPLIT(ERROR_LIST) : VECTOR [256];

    UNDECLARE %QUOTE
	ERR;

!+
! Define the text of each message.
!-

    MACRO
	ERR (NUMBER, CODE, TEXT, SEVERITY, SYSTEM_HANDLING) =
	UPLIT (%ASCIZ TEXT)
    %;

    BIND
	MESSAGE_TEXT = UPLIT(ERROR_LIST) : VECTOR [256];

    UNDECLARE %QUOTE
	ERR;

!+
! Define the length of each name (will be 3, 6 or 9).
!-

    MACRO
	ERR (NUMBER, CODE, TEXT, SEVERITY, SYSTEM_HANDLING) =
	%CHARCOUNT (CODE)
    %;

    BIND
	NAME_LEN = UPLIT(ERROR_LIST) : VECTOR [256];

    UNDECLARE %QUOTE
	ERR;

!+
! Define the name of each message.
!-

    MACRO
	ERR (NUMBER, CODE, TEXT, SEVERITY, SYSTEM_HANDLING) =
	UPLIT (%ASCIZ %STRING (CODE))
    %;

    BIND
	NAME_TEXT = UPLIT(ERROR_LIST) : VECTOR [256];

    UNDECLARE %QUOTE
	ERR;

!+
! Copy the message severity to the caller's descriptor.
!-
    LIB$SCOPY_R_DX (SEVERITY_LEN [..CODE], .SEVERITY_TEXT [..CODE], .SEVERITY);

!+
! Copy the message text to the caller's descriptor.
!-
    LIB$SCOPY_R_DX (MESSAGE_LEN [..CODE], .MESSAGE_TEXT [..CODE], .TEXT);

!+
! Copy the message name to the caller's descriptor.
!-
    LIB$SCOPY_R_DX (NAME_LEN [..CODE], .NAME_TEXT [..CODE], .NAME);

!+
! Also return the lengths.
!-
    .NAMLEN = .NAME_LEN [..CODE];
    .TXTLEN = .MESSAGE_LEN [..CODE];
    .SEVLEN = .SEVERITY_LEN [..CODE];

!+
! All done.
!-
    RETURN (SS$_NORMAL);
    END;					! of ERRTXT

END

ELUDOM
