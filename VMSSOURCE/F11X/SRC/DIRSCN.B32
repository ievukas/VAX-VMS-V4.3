MODULE DIRSCN (
		LANGUAGE (BLISS32),
		IDENT = 'V04-000'
		) =
BEGIN

!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982, 1984 BY				    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************

!++
!
! FACILITY:  F11ACP Structure Level 2
!
! ABSTRACT:
!
!	This routine performs the basic directory scan, searching for the
!	given entry.
!
! ENVIRONMENT:
!
!	STARLET operating system, including privileged system services
!	and internal exec routines.
!
!--
!
!
! AUTHOR:  Andrew C. Goldstein,	 CREATION DATE:  30-Dec-1977  11:14
!
! MODIFIED BY:
!
!	V03-009	CDS0006		Christian D. Saether	29-Aug-1984
!		Establish last block for search in directory scan also.
!
!	V03-008	CDS0005		Christian D. Saether	 5-Aug-1984
!		Fix directory index search still.
!
!	V03-007	CDS0004		Christian D. Saether	 5-Aug-1984
!		Fix sense of length extended compare in directory
!		index search.  Make dirindx cell size 15 bytes.
!
!	V03-006	CDS0003		Christian D. Saether	 2-Aug-1984
!		Add support for revamped directory index cache.
!
!	V03-005	CDS0002		Christian D. Saether	25-Apr-1984
!		Remove references to DIRIDX in the FCB.
!
!	V03-004	ACG0408		Andrew C. Goldstein,	23-Mar-1984  11:17
!		Make rest of global storage based
!
!	V03-003	CDS0002		Christian D. Saether	29-Dec-1983
!		Use L_NORM linkage and BIND_COMMON macro.
!
!	V03-002	CDS0001		Christian D. Saether	12-Dec-1983
!		Move GLOBAL data declaration to COMMON module.
!
!	V03-001	LMP0080		L. Mark Pilant,		15-Feb-1983  12:26
!		Add support for propagation of file attributes.  This
!		consists of remembering the FID of the highest version
!		of the file of the same name and type.
!
!	V02-008	ACG0259		Andrew C. Goldstein,	27-Jan-1982  20:16
!		Fix counting of entries when skipping records
!
!	V02-006	ACG0208		Andrew C. Goldstein,	26-Oct-1981  16:27
!		Add support for segmented directory records.
!
!	V02-005	ACG0167		Andrew C. Goldstein,	16-Apr-1980  19:25
!		Previous revision history moved to F11B.REV
!**


LIBRARY 'SYS$LIBRARY:LIB.L32';
REQUIRE 'SRC$:FCPDEF.B32';


FORWARD ROUTINE
	DIR_SCAN	: L_NORM,	! directory scanner
	NEXT_REC	: L_NORM,	! get next directory record
	UPDATE_INDX	: NOVALUE,	! update directory index entry
	NEXT_DIR_REC	: L_NORM;	! get next matching directory record

GLOBAL ROUTINE DIR_SCAN (NAME_DESC, FILE_ID, START_BLOCK, START_REC, START_VER, START_PRED, REC_COUNT)
	: L_NORM =

!++
!
! FUNCTIONAL DESCRIPTION:
!
!	This routine scans a directory, searching for the given entry.
!
!
! CALLING SEQUENCE:
!	DIR_SCAN (ARG1, ARG2, ARG3, ARG4, ARG5, ARG6, ARG7)
!
! INPUT PARAMETERS:
!	ARG1: address of file name descriptor block
!	ARG2: address of file ID block
!	ARG3: relative block number to start search
!	ARG4: address of record at which to start
!	ARG5: address of version entry at which to start
!	ARG6: address of predecessor record
!	ARG7: maximum number of records to scan
!	      (functions only with FIND_FID and a non-matching FID)
!
! IMPLICIT INPUTS:
!	LAST_ENTRY: name string of last record in previous block
!
! OUTPUT PARAMETERS:
!	NONE
!
! IMPLICIT OUTPUTS:
!	DIR_VBN: relative block + 1 of current directory buffer
!	DIR_BUFFER: address of current directory block buffer
!	DIR_RECORD: record number within block of found entry
!	DIR_ENTRY: address in buffer of found record
!	DIR_VERSION: address if buffer of found version entry
!	DIR_PRED: predecessor record to record found
!	LAST_ENTRY: name string of last record in previous block
!	VERSION_LIMIT: version limit of last file name processed
!	VERSION_COUNT: number of versions of current file name passed
!
! ROUTINE VALUE:
!	1 if entry found
!	0 if no match, in which case:
!		DIR_ENTRY = next record in collating sequence
!			  = 0 if whole directory scanned (name belongs off the end)
!		DIR_VERSION = next version in collating sequence if name & type matched
!			    = 0 if name or type did not match
!
! SIDE EFFECTS:
!	directory blocks read
!	directory index in FCB updated
!
!--

BEGIN

MAP
	NAME_DESC	: REF BBLOCK;	! name descriptor block arg

LABEL
	DIRINDX_SCAN,			! block to search directory index
	SEARCH_LOOP;			! body of search code

LINKAGE
	L_MATCH_NAME	= JSB (REGISTER = 2, REGISTER = 3, REGISTER = 4, REGISTER = 5)
			: NOTUSED (10, 11);

LOCAL
	DIRINDX		: REF BBLOCK FIELD (DIRC), ! directory index cache
	STATUS,				! routine return status
	COUNT,				! entry count within current block
	BLOCK,				! relative block number
	LAST_BLOCK,			! last block of directory to read
	MATCHED,			! flag indicating name match encountered
	DN		: REF BBLOCK,	! address of name descriptor block
	ENTRY		: REF BBLOCK,	! pointer to current directory record
	P		: REF BBLOCK,	! pointer to current directory version
	PREV_ENTRY	: REF BBLOCK;	! pointer to previous record

BIND_COMMON;

DIR_CONTEXT_DEF;			! define directory context fields

EXTERNAL ROUTINE
	READ_BLOCK	: L_NORM,	! read a disk block
	MARK_DIRTY	: L_NORM,	! mark buffer for write back
	FMG$MATCH_NAME	: L_MATCH_NAME;	! match general wild card string


! Initialize basic pointers. Compute the cluster factor of the directory
! index from the directory size.
!

DN = .NAME_DESC;
STATUS = 0;
BLOCK = .START_BLOCK;
ENTRY = .START_REC;
P = .START_VER;
PREV_ENTRY = .START_PRED;
COUNT = .DIR_RECORD;
MATCHED = 0;

IF .BLOCK GTRU .DIR_FCB[FCB$L_EFBLK]
THEN BLOCK = .DIR_FCB[FCB$L_EFBLK];

SEARCH_LOOP: BEGIN			! outer directory search loop

! Initialize the last block for the search to be the end of file.
!

LAST_BLOCK = .DIR_FCB[FCB$L_EFBLK] - 1;

IF NOT (   CH$RCHAR (.DN[FND_STRING]) EQL '*'
	OR CH$RCHAR (.DN[FND_STRING]) EQL '%'
	OR .DN[FND_FIND_FID]
	)
AND (DIRINDX = .DIR_FCB [FCB$L_DIRINDX]) NEQ 0
THEN

! There is a directory index.  This cache contains the last entry
! of blocks that have already been read.  It allows us to move the
! starting block for the search into the directory file instead
! of always starting at the beginning of the file, and limit the
! end of the search also.
!

DIRINDX_SCAN:
    BEGIN
    LOCAL
	CELL_ADDR,
	CELLSIZE,
	CMPSIZE,
	FNDSTRNG,
	PTR,
	SEARCH_CELL	: WORD;

    IF .DIRINDX [DIRC$W_INUSE] EQL 0
    THEN
	LEAVE DIRINDX_SCAN;

    SEARCH_CELL = .BLOCK/.DIRINDX [DIRC$W_BLKSPERCELL];

    CELLSIZE = CMPSIZE = .DIRINDX [DIRC$W_CELLSIZE];

    FNDSTRNG = .DN [FND_STRING];

    IF .DN [FND_COUNT] LSSU .CMPSIZE
    THEN
	CMPSIZE = .DN [FND_COUNT];

    IF (PTR = CH$FIND_CH (.CMPSIZE, .FNDSTRNG, '*')) NEQ 0
    THEN
	CMPSIZE = .PTR - .FNDSTRNG;

    IF (PTR = CH$FIND_CH (.CMPSIZE, .FNDSTRNG, '%')) NEQ 0
    THEN
	CMPSIZE = .PTR - .FNDSTRNG;

    CELL_ADDR = DIRINDX [DIRC$T_FIRSTCELL]
		+ (.SEARCH_CELL)*(.CELLSIZE);

!

    UNTIL .SEARCH_CELL GEQU .DIRINDX [DIRC$W_INUSE]
    DO
	CASE CH$COMPARE (.CMPSIZE, .FNDSTRNG,
			.CELLSIZE, .CELL_ADDR,
			0)
	    FROM -1 TO 1 OF
	SET

! Directory index cell is greater than the string we are searching with.
! Drop out of the loop.
!

	[-1]:	EXITLOOP;

! Exact match.  Drop out of the loop.  We must start the search
! at the start of this group.
!

	[0]:	EXITLOOP;

! The search string is greater than this directory index entry.  It
! therefore cannot be in this group or any that we have passed.
! Update the starting block to reflect that fact, bump cell pointers
! and keep searching.
!

	[1]:	BEGIN
		BLOCK = (.SEARCH_CELL)*(.DIRINDX [DIRC$W_BLKSPERCELL]);
		SEARCH_CELL = .SEARCH_CELL + 1;
		CELL_ADDR = .CELL_ADDR + .CELLSIZE;
		END;
	TES;

! The second part of the search is to establish the end block for the
! scan.  This time we want the fill character for the compare to
! extend the potentially shorter search string with hex FF instead
! of zero so that possible wildcards do not make us stop short.
! If we exceed the number of cells in use without finding a directory
! index entry greater than our search string, LAST_BLOCK will remain
! set to end of file.
!

    UNTIL .SEARCH_CELL GEQU .DIRINDX [DIRC$W_INUSE]
    DO
	CASE CH$COMPARE (.CMPSIZE, .FNDSTRNG,
			.CELLSIZE, .CELL_ADDR,
			-1)
	    FROM -1 TO 1 OF
	SET

! Directory index cell is greater than the string we are searching with.
! The entry cannot be beyond here.  Reset the LAST_BLOCK variable to
! limit the search.
!

	[-1]:	BEGIN
		LOCAL
		    TEMP;

		TEMP = (.SEARCH_CELL + 1)*(.DIRINDX [DIRC$W_BLKSPERCELL]) - 1;

		IF .TEMP LSSU .LAST_BLOCK
		THEN
		    LAST_BLOCK = .TEMP;

		EXITLOOP;
		END;

! The search string is equal to or greater than the directory entry.
! Continue scanning the directory index.
!

	[0,1]:	BEGIN
		SEARCH_CELL = .SEARCH_CELL + 1;
		CELL_ADDR = .CELL_ADDR + .CELLSIZE;
		END;

	TES;

    END;

! If checking against EOF and the directory index has changed the starting
! block number, discard the starting record pointers, which are now irrelevant.
!

IF .BLOCK NEQ .START_BLOCK
THEN
    BEGIN
    ENTRY = 0;
    P = 0;
    COUNT = 0;
    PREV_ENTRY = 0;
    LAST_ENTRY[0] = 0;
    END;

! Loop, scanning blocks of the directory until we hit EOF.
!

WHILE 1 DO
    BEGIN

    IF .BLOCK GTRU .LAST_BLOCK
    THEN LEAVE SEARCH_LOOP;
    IF .ENTRY EQL 0
    THEN
	BEGIN
	ENTRY = READ_BLOCK (.BLOCK+.DIR_FCB[FCB$L_STLBN],
			    .LAST_BLOCK - .BLOCK + 1,
			    DIRECTORY_TYPE);
	DIR_BUFFER = .ENTRY;
	END;

! Loop, scanning the records of the directory. A record size of -1 indicates
! the end of the block. We attempt to match name and type against the entry,
! under control of the various name control flags.
!

    WHILE 1
    DO
	BEGIN
	IF .ENTRY[DIR$W_SIZE] EQL 65535
	THEN
	    BEGIN
	    IF .PREV_ENTRY NEQ 0
	    THEN CH$MOVE (.PREV_ENTRY[DIR$B_NAMECOUNT]+1,
			  PREV_ENTRY[DIR$B_NAMECOUNT], LAST_ENTRY);
	    PREV_ENTRY = 0;
	    EXITLOOP;
	    END;

! Do setup and validation for the record.
!

	IF .ENTRY[DIR$W_SIZE] + .ENTRY + 2 GEQA .DIR_BUFFER + 512
	OR .ENTRY[DIR$V_TYPE] NEQ DIR$C_FID
	OR .ENTRY[DIR$B_NAMECOUNT] GTRU FILENAME_LENGTH
	OR .ENTRY[DIR$B_NAMECOUNT] GTRU .ENTRY[DIR$W_SIZE] + 2 - DIR$C_LENGTH - DIR$C_VERSION
	THEN ERR_EXIT (SS$_BADIRECTORY);

! If this is a lookup for lowest version and a name has previously matched,
! see if the name in the record has changed from the previous record. If
! so, the previous record has the lowest version. This test is made in
! a seemingly redundant manner with the name change test below to minimize
! its actual execution.
!

	IF .MATCHED
	AND .DN[FND_VERSION] EQL -32768
	AND (IF .PREV_ENTRY EQL 0
	    THEN CH$NEQ (.ENTRY[DIR$B_NAMECOUNT]+1,
			  ENTRY[DIR$B_NAMECOUNT],
			 .ENTRY[DIR$B_NAMECOUNT]+1,
			  LAST_ENTRY)
	    ELSE CH$NEQ (.ENTRY[DIR$B_NAMECOUNT]+1,
			  ENTRY[DIR$B_NAMECOUNT],
			 .ENTRY[DIR$B_NAMECOUNT]+1,
			  PREV_ENTRY[DIR$B_NAMECOUNT])
	    )
	THEN LEAVE SEARCH_LOOP;

! Attempt to match the name, using a simple string compare if there are
! no wild cards, otherwise the general wild card match routine.
!

	IF
	    BEGIN

	    IF .DN[FND_FIND_FID]
	    THEN 1

	    ELSE
		BEGIN
		IF NOT .DN[FND_WILD]
		THEN
		    CASE CH$COMPARE (.ENTRY[DIR$B_NAMECOUNT],
				      ENTRY[DIR$T_NAME],
				     .DN[FND_COUNT],
				     .DN[FND_STRING]
				     )
		    FROM -1 TO 1 OF
			SET

			[-1]:	0;	! no match - dir entry precedes name

			[0]:	1;	! match

			[1]:	BEGIN	! no match - dir entry is past name
				P = 0;
				LEAVE SEARCH_LOOP;
				END;
			TES

		ELSE
		    FMG$MATCH_NAME (.ENTRY[DIR$B_NAMECOUNT],
				     ENTRY[DIR$T_NAME],
				    .DN[FND_COUNT],
				    .DN[FND_STRING]
				    )
		END
	    END

! If the name and type match on a record, loop to process the versions of
! the record.
!

	THEN
	    BEGIN
	    IF .P EQL 0
	    THEN
		BEGIN
		P = .ENTRY + DIR$C_LENGTH + .ENTRY[DIR$B_NAMECOUNT] + 1 AND NOT 1;

		IF (IF .PREV_ENTRY EQL 0
		    THEN CH$NEQ (.ENTRY[DIR$B_NAMECOUNT]+1,
				  ENTRY[DIR$B_NAMECOUNT],
				 .ENTRY[DIR$B_NAMECOUNT]+1,
				  LAST_ENTRY)
		    ELSE CH$NEQ (.ENTRY[DIR$B_NAMECOUNT]+1,
				  ENTRY[DIR$B_NAMECOUNT],
				 .ENTRY[DIR$B_NAMECOUNT]+1,
				  PREV_ENTRY[DIR$B_NAMECOUNT])
		    )
		THEN
		    BEGIN
		    VERSION_COUNT = 0;
		    VERSION_LIMIT = .ENTRY[DIR$W_VERLIMIT];
		    END;
		END;

	    UNTIL .P GEQA .ENTRY + .ENTRY[DIR$W_SIZE] + 2
	    DO
		BEGIN
		IF NOT .MATCHED
		AND NOT .DN[FND_WILD]
		AND NOT .DN[FND_FIND_FID]
		THEN CH$MOVE (FID$C_LENGTH, P[DIR$W_FID], OLD_VERSION_FID);
		MATCHED = 1;
		IF .COUNT GEQU .REC_COUNT THEN LEAVE SEARCH_LOOP;

		IF
		    BEGIN

		    IF .DN[FND_FIND_FID]
		    THEN CH$EQL (FID$C_LENGTH, .FILE_ID, FID$C_LENGTH, P[DIR$W_FID])

		    ELSE IF .DN[FND_WILD_VER]
		    OR .DN[FND_MAX_VER]
		    OR .DN[FND_VERSION] EQL -.VERSION_COUNT
		    THEN 1

		    ELSE IF .DN[FND_VERSION] GTR .P[DIR$W_VERSION]
		    THEN
			BEGIN
			IF .DN[FND_FLAGS] EQL 0
			THEN LEAVE SEARCH_LOOP
			ELSE EXITLOOP
			END

		    ELSE .DN[FND_VERSION] EQL .P[DIR$W_VERSION]

		    END

		THEN
		    BEGIN
		    STATUS = 1;
		    LEAVE SEARCH_LOOP;
		    END;

		P = .P + DIR$C_VERSION;
		COUNT = .COUNT + 1;
		VERSION_COUNT = .VERSION_COUNT + 1;
		END;			! end of record scanning loop
	    END;			! end of record processing conditional

! Set up the next record to process.
!

	IF .P EQL 0
	THEN P = .ENTRY + DIR$C_LENGTH + .ENTRY[DIR$B_NAMECOUNT] + 1 AND NOT 1;
	PREV_ENTRY = .ENTRY;
	ENTRY = NEXT_REC (.ENTRY);
	COUNT = .COUNT + (.ENTRY-.P) / DIR$C_VERSION;
	P = 0;
	END;				! end of block scanning loop

! We have tripped out of the record scan loop, either because we reached
! the end of the block or we ran out the record count. In the latter case
! (i.e., if this was a position to record number call), we are done.
! Otherwise update the directory index (causing it to be built on the fly)
! and read the next block.
!

    IF .LAST_ENTRY[0] NEQ 0
    THEN
	UPDATE_INDX (.BLOCK, .LAST_ENTRY [0], LAST_ENTRY[1], .DIR_FCB);

    BLOCK = .BLOCK + 1;
    ENTRY = 0;
    P = 0;
    COUNT = 0;
    IF .REC_COUNT LSSU 63 THEN LEAVE SEARCH_LOOP;
    END;				! end of block loop

END;					! end of block SEARCH_LOOP

! We are done searching the directory, and have either found the
! desired entry or have passed the point where it should be. If we
! matched on the name of the previous record, back up to it. Point to
! the last version in the record if we were searching for lowest
! version; else point off the end of the record.
!

IF NOT .STATUS
AND .MATCHED
AND .P EQL 0
THEN
    BEGIN
    IF .PREV_ENTRY NEQ 0
    THEN
	BEGIN
	ENTRY = .PREV_ENTRY;
	END
    ELSE
	BEGIN
	COUNT = 0;
	BLOCK = .BLOCK - 1;
	P = READ_BLOCK (.BLOCK+.DIR_FCB[FCB$L_STLBN], 1, DIRECTORY_TYPE);
	DIR_BUFFER = .P;
	DO
	    BEGIN
	    ENTRY = .P;
	    P = NEXT_REC (.P);
	    COUNT = .COUNT + (.P - .ENTRY - (.ENTRY[DIR$B_NAMECOUNT]+1 AND NOT 1)) / DIR$C_VERSION;
	    END
	UNTIL .P[DIR$W_SIZE] EQL 65535;
	END;
    P = .ENTRY + .ENTRY[DIR$W_SIZE] + 2;
    IF .DN[FND_VERSION] EQL -32768
    THEN
	BEGIN
	P = .P - DIR$C_VERSION;
	VERSION_COUNT = .VERSION_COUNT - 1;
	COUNT = .COUNT - 1;
	STATUS = 1;
	END;
    END;

! Return the record count and pointer in global storage and return status.
!

DIR_VBN = .BLOCK + 1;
DIR_RECORD = .COUNT;
DIR_ENTRY = .ENTRY;
DIR_VERSION = .P;
DIR_PRED = .PREV_ENTRY;

RETURN .STATUS;

END;					! end of routine DIR_SCAN

GLOBAL ROUTINE NEXT_REC (ENTRY) : L_NORM =

!++
!
! FUNCTIONAL DESCRIPTION:
!
!	This routine locates the next directory record and checks it for
!	consistency.
!
!
! CALLING SEQUENCE:
!	NEXT_REC (ARG1)
!
! INPUT PARAMETERS:
!	ARG1: address of present record
!
! IMPLICIT INPUTS:
!	NONE
!
! OUTPUT PARAMETERS:
!	NONE
!
! IMPLICIT OUTPUTS:
!	NONE
!
! ROUTINE VALUE:
!	address of next directory record
!
! SIDE EFFECTS:
!	NONE
!
!--

BEGIN

MAP
	ENTRY		: REF BBLOCK;	! current directory record

LOCAL
	NEXT_ENTRY	: REF BBLOCK;	! new directory record

BIND_COMMON;

DIR_CONTEXT_DEF;

! Find the next record by adding in the record size of the current entry.
! Make sure the record is valid.
!

IF .ENTRY[DIR$W_SIZE] LSSU DIR$C_LENGTH
THEN ERR_EXIT (SS$_BADIRECTORY);
NEXT_ENTRY = .ENTRY + .ENTRY[DIR$W_SIZE] + 2;
IF .NEXT_ENTRY GEQA (.ENTRY + 512 AND NOT 511)
OR .NEXT_ENTRY<0,1>
THEN ERR_EXIT (SS$_BADIRECTORY);

RETURN .NEXT_ENTRY

END;					! end of routine NEXT_REC

GLOBAL ROUTINE UPDATE_INDX (BLOCK, STR_SIZE, STR_ADDR, DIRFCB) : NOVALUE =

!++
!
! FUNCTIONAL DESCRIPTION:
!
!	This routine updates the indicated cell in the directory file index.
!
!
! CALLING SEQUENCE:
!	see above
!
! INPUT PARAMETERS:
!	BLOCK	 - zero-based block within directory file
!	STR_SIZE - size of string
!	STR_ADDR - address of string
!	DIRFCB	 - address of directory FCB
!
! OUTPUT PARAMETERS:
!	NONE
!
! IMPLICIT OUTPUTS:
!	NONE
!
! SIDE EFFECTS:
!	directory index updated
!
!--

BEGIN

MAP
    DIRFCB	: REF BBLOCK;

LOCAL
    BLKSPERCELL,
    CELL_ADDR,
    CELLSIZE,
    CELLINDX	: WORD,
    DIRINDX	: REF BBLOCK FIELD (DIRC);

IF (DIRINDX = .DIRFCB [FCB$L_DIRINDX]) EQL 0
THEN
    RETURN;

IF .DIRINDX [DIRC$W_INUSE] EQL 0
THEN
    BEGIN
    LOCAL
	MAXCELLS;

    DIRINDX [DIRC$W_CELLSIZE] = 15;
    MAXCELLS = (512 - (DIRINDX [DIRC$T_FIRSTCELL] - DIRINDX [DIRC$W_INUSE]))/15;
    BLKSPERCELL = (.DIRFCB [FCB$L_EFBLK]/.MAXCELLS) + 1;
    DIRINDX [DIRC$W_BLKSPERCELL] = .BLKSPERCELL;
    DIRINDX [DIRC$W_TOTALCELLS] = .DIRFCB [FCB$L_EFBLK]/.BLKSPERCELL;
    END
ELSE
    BLKSPERCELL = .DIRINDX [DIRC$W_BLKSPERCELL];

IF (.BLOCK + 1) MOD .BLKSPERCELL NEQ 0
THEN
    RETURN;

CELLINDX = .BLOCK/.BLKSPERCELL;

IF .CELLINDX GTRU .DIRINDX [DIRC$W_INUSE]
THEN
    RETURN;

IF .CELLINDX EQL .DIRINDX [DIRC$W_INUSE]
THEN
    DIRINDX [DIRC$W_INUSE] = .DIRINDX [DIRC$W_INUSE] + 1;

IF .CELLINDX GEQU .DIRINDX [DIRC$W_TOTALCELLS]
THEN
    BUG_CHECK (XQPERR, 'exceeded total number of directory index cells');

CELLSIZE = .DIRINDX [DIRC$W_CELLSIZE];

CELL_ADDR = DIRINDX [DIRC$T_FIRSTCELL] + (.CELLINDX)*.(CELLSIZE);

CH$COPY (.STR_SIZE, .STR_ADDR, 0, .CELLSIZE, .CELL_ADDR);

END;					! end of routine UPDATE_INDX

GLOBAL ROUTINE NEXT_DIR_REC (OLD_REC, VBN) : L_NORM =

!++
!
! FUNCTIONAL DESCRIPTION:
!
!	This routine advances to the next directory record if the name
!	matches the one given. Note that the directory context pointers
!	are NOT updated.
!
! CALLING SEQUENCE:
!	DIR_REC (ARG1, ARG2)
!
! INPUT PARAMETERS:
!	ARG1: address of current directory record
!	ARG2: address of current VBN
!
! IMPLICIT INPUTS:
!	DIR_FCB: FCB of directory file
!
! OUTPUT PARAMETERS:
!	ARG2: new VBN if block read
!
! IMPLICIT OUTPUTS:
!	NONE
!
! ROUTINE VALUE:
!	address of next record or 0
!
! SIDE EFFECTS:
!	directory blocks read
!
!--

BEGIN

MAP
	OLD_REC		: REF BBLOCK;	! old directory record

LOCAL
	NAME_BUFFER	: VECTOR [FILENAME_LENGTH+1, BYTE],
					! buffer to save file name
	NEW_REC		: REF BBLOCK;	! address of new record

BIND_COMMON;

EXTERNAL ROUTINE
	READ_BLOCK	: L_NORM;	! read a disk block


! Save away the name string of this record. Then advance to the next
! record, reading the next block if necessary.
!

CH$MOVE (.OLD_REC[DIR$B_NAMECOUNT]+1, OLD_REC[DIR$B_NAMECOUNT], NAME_BUFFER);
NEW_REC = NEXT_REC (.OLD_REC);
IF .NEW_REC[DIR$W_SIZE] EQL 65535
THEN
    BEGIN
    .VBN = ..VBN + 1;
    IF ..VBN GTRU .DIR_FCB[FCB$L_EFBLK]
    THEN RETURN 0;
    NEW_REC = READ_BLOCK (..VBN-1 + .DIR_FCB[FCB$L_STLBN], 1, DIRECTORY_TYPE);
    END;

IF .NEW_REC[DIR$W_SIZE] + .NEW_REC + 2 GEQA (.NEW_REC + 512 AND NOT 511)
OR .NEW_REC[DIR$B_NAMECOUNT] GTRU FILENAME_LENGTH
OR .NEW_REC[DIR$B_NAMECOUNT] GTR .NEW_REC[DIR$W_SIZE] + 2 - DIR$C_LENGTH - DIR$C_VERSION
THEN ERR_EXIT (SS$_BADIRECTORY);

! Compare the name string with the old one. If it matches, return the
! new entry; else 0.
!

IF CH$NEQ (.NEW_REC[DIR$B_NAMECOUNT]+1, NAME_BUFFER,
	   .NEW_REC[DIR$B_NAMECOUNT]+1, NEW_REC[DIR$B_NAMECOUNT])
THEN 0
ELSE .NEW_REC

END;					! End of routine NEXT_DIR_REC

END
ELUDOM
