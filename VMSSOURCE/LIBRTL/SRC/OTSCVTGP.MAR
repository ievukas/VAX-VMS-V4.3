	.TITLE	OTS$CVTGP_R9	Convert G Floating to Packed
	.IDENT	/1-005/		; File: OTSCVTGP.MAR Edit: DG1005

;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982, 1984 BY				    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;
; FACILITY: LANGUAGE INDEPENDENT SUPPORT
;++
; ABSTRACT:
;	This module contains the routine that converts g floating
;	numbers to packed.
;
;
;--
;
; VERSION: 1
;
; HISTORY:
;
; AUTHOR:
;	Pamela Levesque, 18-Jan-1982
;
; MODIFIED BY:
;
;
;

	.SBTTL	HISTORY		; Detailed Current Edit History


; Edit History for Version 1 of OTSCVTGP
;
; 1-001	- Original.  PLL 18-Jan-1982
; 1-002 - Call g floating conversion routine.  PLL 2-Mar-1982
; 1-003 - Expect a 2 digit exponent from conversion routine.  PLL 8-Mar-1982
; 1-004 - Determine how many digits there are in the exponent on the fly.
;	  MDL 13-Sep-1982
; 1-005 - Make sure result will not overflow destination after calculating
;	  longword exponent.  DG 3-Apr-1984

	.SBTTL	DECLARATIONS

;
; INCLUDE FILES:
;
	$DSCDEF

;
; EXTERNAL SYMBOLS:
	.DSABL	GBL		; Prevent undeclared symbols from
				;  being automatically global

	.EXTRN	OTS$CNVOUT_G	; E-format conversion
	.EXTRN	LIB$STOP	; error halt
	.EXTRN	OTS$_FATINTERR	; Fatal internal error code

;

;
; MACROS:
;	NONE
;

;
; PSECT DECLARATIONS:
	.PSECT	_OTS$CODE	PIC, SHR, LONG, EXE, NOWRT,-
				USR, CON, REL, LCL, RD

;
; EQUATED SYMBOLS:
;	NONE
;

;
; OWN STORAGE:
;	NONE
;

	.SBTTL	OTS$CVTGP_R9

;++
; FUNCTIONAL DESCRIPTION:
;
;	Converts a g floating number to packed.
;
; CALLING SEQUENCE:
;
;	JSB OTS$CVTGP_R9 (scale.rl.v, src.rg.r, dstlen.rl.v, dst.wp.r)
;
;	Arguments are passed in R6, R7, R8 and R9.
;
; INPUT PARAMETERS:
;	
;	SCALE.rl.v		The power of ten by which the internal
;				representation of the source must be
;				multiplied to scale the same as the 
;				internal representation of the dest.
;	SRC.rg.r		The number to be converted
;	DSTLEN.rl.v		The number of digits in the destination
;
; IMPLICIT INPUTS:
;
;	All of the trap bits in the PSL are assumed off.
;
; OUTPUT PARAMETERS:
;
;	DST.wp.r		The place to store the converted number
;
; IMPLICIT OUTPUTS:
;
;	NONE
;
; FUNCTION VALUE:
;
;	1 = SUCCESS, 0 = FAILURE
;
; SIDE EFFECTS:
;
;	Destroys registers R0 through R9.
;	
;--


OTS$CVTGP_R9::
	SUBL2	#48,SP			; Allocate temp space
	MOVC5	#0,(SP),#^A'',#48,(SP)	; Clear temp space
	MOVG	(R7),(SP)		; Get input number
;
; Make a descriptor for the temporary string.
;
	PUSHAB	8(SP)			; Address = space reserved
	MOVB	#DSC$K_CLASS_S,-(SP)	; Class = static
	MOVB	#DSC$K_DTYPE_T,-(SP)	; Data type = ASCII string
	MOVW	#38,-(SP)		; Length in bytes
;
; Call OTS$CNVOUT.
;
	PUSHL	#31			; Digits in fraction
	PUSHAB	4(SP)			; Output string descriptor
	PUSHAB	16(SP)			; Number to convert
	CALLS	#3,G^OTS$CNVOUT_G	; Call conversion routine
	BLBC	R0,20$			; Should never fail
;
; Determine the number of digits in the exponent.
;
					; find beginning of exponent (sign byte)
	LOCC	#^A'+',#36,20(SP)	; look for sign byte (+)
	BNEQ	5$			; branch if we found it
	LOCC	#^A'-',#36,20(SP)	; look for sign byte (-)
	BEQL	20$			; branch to error if we didn't find it
					;
5$:	MOVL	R1,8(SP)		; save location of sign byte
					; (contained in R1 after above LOCC)
					;
	LOCC	#^A'',#5,@B^8(SP)	; look for NUL (end of string)
	SUBL3	8(SP),R1,12(SP)		; determine number of digits by subtracting
					; location of beginning of exponent from
					; location of end of exponent, and save it
	DECL	12(SP)			; adjust for sign byte
					; at this point, 12(SP) = length of exponent.
;
; Convert the exponent and correct for scale factor.
;	
	CVTSP	12(SP),@B^8(SP),12(SP),(SP)	; Make packed exponent
	CVTPL	12(SP),(SP),R0		; Make longword exponent
	;
	; Here must check that the result will not overflow
	; the destination.  The destination length (total number of
	; digits) minus the scale factor will give the number of digits
	; to the left of the decimal point.  If that number is greater than
	; the longword exponent, then the result WILL overflow the dest.
	;
	SUBL3	R6,R8,R4		; Determine num of digits on left
	CMPL	R0,R4			; Compare with longword exponent
	BGTR	15$			; Branch if overflowed

	MOVAB	-31(R6)[R0],R6		; Correct for fraction size and scale
;
; Convert the fraction to packed.
;
	MOVB	16(SP),18(SP)		; Move sign over "."
	CVTSP	#31,18(SP),#31,(SP)	; Make packed fraction
	ASHP	R6,#31,(SP),#0,R8,(R9)	; Scale to destination
					;     (also clears R0)
	BVS	10$			; Branch if overflowed
	INCL	R0			; Indicate success, R0 = 1
10$:	ADDL2	#56,SP			; Delete stack temps
	RSB

15$:	CLRL	R0			; Indicate failure, R0 = 0
	ADDL2	#56,SP			; Delete stack temps
	RSB
;
; Come here on failure of OTS$CNVOUT.  This should never happen.
;
20$:	PUSHL	#OTS$_FATINTERR		; OTS fatal error message
	CALLS	#1,G^LIB$STOP		; Signal and don't return
;
	.END
