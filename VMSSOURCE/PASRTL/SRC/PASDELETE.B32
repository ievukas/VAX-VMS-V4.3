MODULE PAS$DELETE ( %TITLE 'DELETE procedure'
		IDENT = '1-001'			! File: PASDELETE.B32 Edit: SBL1001
		) =
BEGIN
!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982, 1984 BY				    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************
!

!++
! FACILITY:	Pascal Language Support
!
! ABSTRACT:
!
!	This module contains PAS$DELETE, which implements the
!	VAX-11 Pascal DELETE procedure.
!
! ENVIRONMENT:	User mode - AST reentrant
!
! AUTHOR: Steven B. Lionel, CREATION DATE: 29-January-1982
!
! MODIFIED BY:
!
! 1-001	- Original.  SBL 29-January-1982
!--


%SBTTL 'Declarations'
!
! PROLOGUE DEFINITIONS:
!

REQUIRE 'RTLIN:PASPROLOG';			! Externals, linkages, PSECTs, structures

!
! TABLE OF CONTENTS:
!

FORWARD ROUTINE
    PAS$DELETE: NOVALUE;			! Do a DELETE

!
! MACROS:
!
!	NONE
!
! EQUATED SYMBOLS:
!
!	NONE
!
! FIELDS:
!
!	NONE
!
! OWN STORAGE:
!
!	NONE

%SBTTL 'PAS$DELETE - DELETE procedure'
GLOBAL ROUTINE PAS$DELETE (
	PFV: REF $PAS$PFV_FILE_VARIABLE, 		! File variable
	ERROR						! Unwind address if error
    ): NOVALUE =

!++
! FUNCTIONAL DESCRIPTION:
!
!	PAS$DELETE implements the VAX-11 Pascal DELETE procedure.  It
!	deletes the current file component.
!
! CALLING SEQUENCE:
!
!	CALL PAS$DELETE (PFV.mr.r [, ERROR.j.r])
!
! FORMAL PARAMETERS:
!
!	PFV		- The Pascal File Variable (PFV) passed by reference.
!			  The structure of the PFV is defined in PASPFV.REQ.
!
!	ERROR		- Optional.  If specified, the address to unwind to
!			  if an error occurs.
!
! IMPLICIT INPUTS:
!
!	NONE
!
! IMPLICIT OUTPUTS:
!
!	NONE
!
! ROUTINE VALUE:
!
!	NONE
!
! SIDE EFFECTS:
!
!	NONE
!
! SIGNALLED ERRORS:
!
!	FILNOTOPE - File not open
!	FILNOTINS - File is not in Inspection mode
!	DELNOTALL - DELETE is not allowed for a sequential organization file
!	FILNOTDIR - file is not opened for direct access
!	FILNOTKEY - file is not opened for keyed access
!	CURCOMUND - current component is undefined
!	ERRDURDEL - Error during DELETE
!	
!
!--

    BEGIN

    LOCAL
	FCB: REF $PAS$FCB_CONTROL_BLOCK,		! File Control Block
	PFV_ADDR: VOLATILE,				! Enable argument
	UNWIND_ACT: VOLATILE,				! Enable argument
	ERROR_ADDR: VOLATILE;				! Enable argument

    BIND
	RAB = FCB: REF BLOCK [, BYTE],			! RMS RAB
	FAB = FCB: REF $PAS$FAB_FCB_STRUCT;		! RMS FAB

    BUILTIN
	ACTUALCOUNT;

    ENABLE
	PAS$$IO_HANDLER (PFV_ADDR, UNWIND_ACT, ERROR_ADDR);

    IF ACTUALCOUNT () GEQU 2
    THEN
	ERROR_ADDR = .ERROR;				! Set unwind address

    !+
    ! Set PFV address enable argument.
    !-

    PFV_ADDR = PFV [PFV$R_PFV];

    !+
    ! Validate and lock PFV
    !-

    PAS$$VALIDATE_PFV (PFV [PFV$R_PFV]; FCB);

    !+
    ! Set unwind action to unlock file.
    !-

    UNWIND_ACT = PAS$K_UNWIND_UNLOCK;

    !+
    ! Open file if it should be implicitly opened.
    !-

    IF NOT .PFV [PFV$V_VALID]
    THEN
	PAS$$OPEN_IMPLICIT (PFV [PFV$R_PFV], FCB [FCB$R_FCB]; FCB);

    !+
    ! Verify that the file is open.
    !-

    IF NOT .PFV [PFV$V_OPEN]
    THEN
	$PAS$IO_ERROR (PAS$_FILNOTOPE,0);

    !+
    ! Verify that the file is in Inspection mode.
    !-

    IF NOT .FCB [FCB$V_INSPECTION]
    THEN
	$PAS$IO_ERROR (PAS$_FILNOTINS,0);

    !+
    ! Verify that the file is not sequential organization.
    !-

    IF .FAB [FAB$B_ORG] EQL FAB$C_SEQ
    THEN
	$PAS$IO_ERROR (PAS$_DELNOTALL,0);

    !+
    ! Verify that the file was opened either direct or keyed.
    !-

    IF .FCB [FCB$V_SEQUENTIAL]	! Neither DIRECT nor KEYED
    THEN
	IF .FAB [FAB$B_ORG] EQL FAB$C_REL	! Relative?
	THEN
	    $PAS$IO_ERROR (PAS$_FILNOTDIR,0)
	ELSE 	! Must be INDEXED
	    $PAS$IO_ERROR (PAS$_FILNOTKEY,0);

    !+
    ! Do the $DELETE to delete the current component.
    !-

    IF NOT $PAS$RMS_OP ($DELETE (RAB=.RAB))
    THEN
	BEGIN
	!+
	! $DELETE failed.  Determine if the error was "no current record"
	! If so, give error "current component is undefined", otherwise
	! generic error
	!-

	IF .RAB [RAB$L_STS] EQL RMS$_CUR
	THEN
	    $PAS$IO_ERROR (PAS$_CURCOMUND)	! Current component undefined
	ELSE
	    $PAS$IO_ERROR (PAS$_ERRDURDEL);	! Error during DELETE
	END;

    !+
    ! Undefine file buffer
    ! Cancel previous LOCATE
    ! Indicate successful operation
    ! Unlock the file
    !-

    FCB	[FCB$L_STATUS] = 0;
    FCB [FCB$V_LOCATE] = 0;
    PFV [PFV$V_DFB] = 0;
    PFV [PFV$V_LOCK] = 0;

    RETURN;

    END;					! End of routine PAS$DELETE

!<BLF/PAGE>

END						! End of module PAS$DELETE

ELUDOM
