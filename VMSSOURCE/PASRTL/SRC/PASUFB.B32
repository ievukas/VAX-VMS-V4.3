MODULE PAS$UFB ( %TITLE 'Inquire if undefined file buffer'
		IDENT = '1-001'			! File: PASUFB.B32 Edit: SBL1001
		) =
BEGIN
!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982, 1984 BY				    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************
!

!++
! FACILITY:	Pascal Language Support
!
! ABSTRACT:
!
!	This procedure implements the Pascal UFB procedure.  It
!	is not called directly from compiled code, but rather can
!	be passed as a procedure by users.
!
! ENVIRONMENT:	User mode - AST reentrant
!
! AUTHOR: Steven B. Lionel, CREATION DATE: 1-April-1981
!
! MODIFIED BY:
!
! 1-001	- Original.  SBL 1-April-1981
!--


%SBTTL 'Declarations'
!
! PROLOGUE DEFINITIONS:
!

REQUIRE 'RTLIN:PASPROLOG';			! Externals, linkages, PSECTs, structures

!
! TABLE OF CONTENTS:
!

FORWARD ROUTINE
    PAS$UFB;					! Inquire if undefined file buffer

!
! MACROS:
!
!	NONE
!
! EQUATED SYMBOLS:
!
!	NONE
!
! FIELDS:
!
!	NONE
!
! OWN STORAGE:
!
!	NONE
!

%SBTTL 'PAS$UFB - Inquire if undefined file buffer'
GLOBAL ROUTINE PAS$UFB (			! Inquire if undefined file buffer
	PFV: REF $PAS$PFV_FILE_VARIABLE 	! File variable
    ) =

!++
! FUNCTIONAL DESCRIPTION:
!
!	This procedure returns a boolean value TRUE if the given file's
!	buffer is undefined, FALSE otherwise.
!
! CALLING SEQUENCE:
!
!	UFB.wv.v = PAS$UFB (PFV.mr.r)
!
! FORMAL PARAMETERS:
!
!	PFV		- The Pascal File Variable (PFV) passed by reference.
!			  The structure of the PFV is defined in PASPFV.REQ.
!
! IMPLICIT INPUTS:
!
!	NONE
!
! IMPLICIT OUTPUTS:
!
!	NONE
!
! ROUTINE VALUE:
!
!	Boolean TRUE (1) if file buffer is undefined.
!	Boolean FALSE (0) if file buffer is defined.
!
! SIDE EFFECTS:
!
!	Resolves any lazy lookahead in progress, possibly opening the file.
!
! SIGNALLED ERRORS:
!
!	FILNOTOPE - file not open
!
!--

    BEGIN

    LOCAL
	FCB: REF $PAS$FCB_CONTROL_BLOCK,	! Control block
	RESULT,					! Function result
	PFV_ADDR: VOLATILE,			! Enable argument
	UNWIND_ACT: VOLATILE;			! Enable argument

    ENABLE
	PAS$$IO_HANDLER (PFV_ADDR, UNWIND_ACT);

    !+
    ! Set the enable argument for the PFV address.
    !-

    PFV_ADDR = PFV [PFV$R_PFV];

    !+
    ! Validate and lock the PFV.
    !-

    PAS$$VALIDATE_PFV (PFV [PFV$R_PFV]; FCB);

    !+
    ! Set unwind action to unlock file.
    !-

    UNWIND_ACT = PAS$K_UNWIND_UNLOCK;

    !+
    ! Call PAS$$LOOK_AHEAD to resolve lazy lookahead.
    !-

    IF NOT .PFV [PFV$V_VALID]
    THEN
	PAS$$LOOK_AHEAD (PFV [PFV$R_PFV], FCB [FCB$R_FCB]; FCB);

    !+
    ! Verify that file is open.
    !-

    IF NOT .PFV [PFV$V_OPEN]	! Not open
    THEN
	$PAS$IO_ERROR (PAS$_FILNOTOPE,0);

    !+
    ! Determine if UFB
    !-

    RESULT = NOT .PFV [PFV$V_DFB];

    !+
    ! Indicate successful completion
    ! Unlock file and return.
    !-

    FCB [FCB$L_STATUS] = 0;
    PFV [PFV$V_LOCK] = 0;

    RETURN .RESULT;

    END;					! End of routine PAS$UFB

!<BLF/PAGE>

END						! End of module PAS$UFB

ELUDOM
