 	$BEGIN	RM0RELEAS,000,RM$RMS0,<RELEASE BUFFER ROUTINE>

;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982, 1984 BY				    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;
;++
; Facility: rms32
;
; Abstract:
;		this module releases a buffer for all file
;		organizations, handling writing if dirty
;		and sharing.
;
; Environment:
;		star processor running starlet exec.
;
; Author: L F Laverdure,	 creation date: 30-MAR-1977
;
; Modified By:
;
;	V03-022	JEJ0044		J E Johnson		21-Jun-1984
;		Tweak the instructions for a performance boost.
;
;	V03-021	JEJ0037		J E Johnson		14-May-1984
;		Have BLKASTFIN save all the registers that it uses.
;
;	V03-020	JEJ0036		J E Johnson		11-May-1984
;		Fix bug in INVGBL that was counting on AP being preserved
;		across a possible stall.
;
;	V03-019	JEJ0033		J E Johnson		02-May-1984
;		Fix bug in BLOCK_AST so that RMS will bugcheck if it
;		runs out of AST quota in a state with no path available
;		back to the user.
;
;	V03-018	SHZ0007		Stephen H. Zalewski,	30-Apr-1984
;		Fix bug in BLKASTFIN that was clearing the owner field
;		of the BLB when it wasn't supposed to.  This was causing 2
;		streams to $ENQ for a bucket using the same lock id.
;
;		Also, fix bug where 2 streams would convert their bucket locks
;		to system locks for the same bucket, thus causing one system 
;		lock to be lost.
;
;	V03-017	JEJ0008		J E Johnson		16-Mar-1984
;		Add in global buffer quota accounting.
;
;	V03-016	JWT0160		Jim Teague		29-Feb-1984
;		Remove calls to RM$DEALLEFN.
;
;	V03-015	SHZ0006		Stephen H. Zalewski	01-Dec-1983
;		Clear BLB flags before doing the conversion to a PW lock
;		instead of after because if a blocking ast arrived between the
;		conversion and the clearing of the flags, the wrong
;		flags would be cleared.
;
;	V03-014	SHZ0005		Stephen H. Zalewski	20-Oct-1983
;		When a global buffer is invalidated, the user count on the
;		GBD must be greater than one for it to remain in the cache.
;
;	V03-013	SHZ0004		Stephen H. Zalewski	28-Jul-1983
;		Add support for cluster global buffers.
;
;	V03-012	KPL0001		Peter Lieberwirth	21-Jun-1983
;		Change use of JNLFLG to JNLFLG2.
;
;	V03-011	TMK0001		Todd M. Katz		20-Feb-1983
;		Add support for AI and BI Journalling of ISAM files. Whenever
;		RM$RELEASE is called with a dirty buffer and the file's
;		organization is index, then the original bucket contents are
;		journalled if BI Journalling is enabled, and the modified
;		bucket's contents are journalled if AI Journalling is enabled
;		before the bucket is actually released.
;
;	V03-010	JWH0175		Jeffrey W. Horn		31-Jan-1983
;		Save some more registers around call to RM$FRCJNL.
;
;	V03-009	SHZ0003		Stephen H. Zalewski,	18-Jan-1983  15:40
;		Added code to bugcheck if $ENQ or $DCLAST system services
;		fail.
;
;	V03-008	SHZ0002		Stephen H. Zalewski,	22-Sep-1982  13:53
;		Take EXCLUSIVE lock on Global buffer cache when invalidating
;		a buffer, or updating the position of a buffer in the cache.
;
;	V03-007	SHZ0001		Stephen H. Zalewski,	6-Sep-1982  20:21
;		Use interlocked self-relative queue instructions when
;		placing and removing GBDs.
;
;	V03-006	KBT0214		Keith B. Thompson	23-Aug-1982
;		Reorganize psects
;
;	V03-005	JWH0001		Jeffrey W. Horn		28-Jun-1982
;		Perform journal force using high water mark(s) stored
;		in BDB$T_JNLSEQ before writing out a buffer.
;
;	V03-004	CDS0019		C D Saether		13-Apr-1982
;		If write back qio (in blocking ast routine) fails,
;		note error, reset DFW flag and dismiss AST without
;		releasing lock, and setting WRITEBACK.
;		If a reader finds DFW and WRITEBACK set, the buffer
;		is written on release (a locker also writes
;		it back if WRITEBACK is set).
;		Invalidate global buffer if GBPB released with
;		VAL flag cleared.
;
;	V03-003	CDS0018		C D Saether		 5-Apr-1982
;		Force total release of buffer when released and invalid.
;
;	V03-002	CDS0017		C D Saether		31-Mar-1982
;		Fixup BLB pointer from BDB when doing KEEPLOCK release
;		and other accessors are present.
;
;	V03-001	CDS0016		C D Saether		 1-Mar-1982
;		Decrement GBD use count in code that clears out
;		GBPB as all GBPB's are dequeued.  Increment use
;		count on GBPB before calling routine to free local
;		buffer in case stall occurs so that other streams
;		will not select it.
;		Only clear out BDB on keeplock release if use count
;		is zero.
;		Bugcheck if a reader (didn't say LOCK initially)
;		has modified a buffer and the write was deferred.
;		If this happens we get into trouble further on.
;							26-Feb-1982
;		Reflect fact that all accesses except read lock on
;		DFW buffer hold lock in EX mode.  Situation previously
;		being checked where CR lock held on release of dirty
;		buffer no longer possible.
;							17-Feb-1982
;		Changes to allow for release of modified global
;		buffer.
;
;--
;


	.SBTTL	DECLARATIONS

;
; Include Files:
;

;
; Macros:
;

	$BDBDEF
	$BLBDEF
	$CJFDEF
	$CSHDEF
	$DEQDEF
	$ENQDEF
	$FABDEF
	$GBDDEF
	$GBHDEF
	$GBPBDEF
	$IFBDEF
	$IMPDEF
	$IODEF
	$IRBDEF
	$LCKDEF
	$RLSDEF
	$RMSDEF
	
;
; Equated Symbols:
;

;
; Own Storage:
;


	.SBTTL	RM$RELEASE - RELEASE BUFFER FOR ALL FILE ORGS

;++
; RM$RELEASE
;
; For all file organizations:
;	1) if the buffer valid and dirty and
;	   any of the flags rls$v_wrt_thru, rls$v_keep_lock* or
;	   rls$v_return set, writes the buffer out,
;	   possibly writing behind
;	This routine is not prepared for anyone to call it with
;	   rls$v_return set and R4 pointing to a GBPB, however.
;	2) if the flag rls$v_return set, waits for
;	   read-ahead or write-behind to complete,
;	   if any, and returns the bdb and
;	   buffer to the free memory lists
;	3) if the rls$v_keep_lock* bit off release access
;	   to the block, otherwise, retain access to block
;	4) if entry rm$rlnerr is invoked clear all error
;	   producing bdb flags, clear release control flags
;	   and fall thru to rm$release
;	5) if an error occurs and rls$v_keep_acc is not set in flags,
;	   call rm$rlnerr before returning (i.e., deaccess bdb)
;
;	*  note: rls$v_keep_lock is only input for the
;	   relative and indexed file organizations.
;
; Calling sequence:
;
;	bsbw	rm$release
;	bsbw	rm$rlnerr	- alternate entry to merely free the bdb
;
;
; Input Parameters:
;
;	r11	impure area address
;	r10	ifab address
;	r9	irab/ifab address
;	r8	rab/fab address
;	r4	bdb address
;	r3	release control flags	(not an input for entry at rm$rlnerr)
;
; Implicit Inputs:
;
;	ifb$b_caseorg
;	bdb$b_flgs
;	if block written, all inputs to rm$seqwtbd
;
; Output Parameters:
;
;	r0	status code
;	r1,r2,ap destroyed
;	r3	zeroed if entry at rm$rlnerr otherwise unchanged
;	r4,r5	destroyed if rls$v_return set
;
; Implicit Outputs:
;
;	bdb$v_drt  -  cleared if not defered write
;	bdb$v_val and bdb$v_prm	cleared on entry at rm$rlnerr
;	the various outputs from rm$seqwtbd
;
; Completion Codes:
;
;	standard rms.
;
; Side Effects:
;
;	Journalling of the modified and/or un-modified contents of the bucket
;	being released may have been done if the file is an ISAM file marked 
;	for AI or BI Journalling.
;	May have switched to running at ast level.
;--


TRACE:
	JSB	RM$RLS_IN
	BSBB	RLS
	JSB	RM$RLS_OUT
	RSB


;
; entry for unconditional release
; clear all possible error producing flags and fall thru
;

RM$RLNERR::
	BICB2	#BDB$M_VAL!BDB$M_DRT!BDB$M_PRM,BDB$B_FLGS(R4)
RM$RLNER1::
	MOVL	#RLS$M_DEQ, R3		; Release entirely.

;
; normal entry
;

RM$RELEASE::
	$TSTPT	RELEASE
	NOP				; Patch for tracing.
	NOP

RLS:	PUSHL	R3			; save flags
	TSTL	R4			; Is there a structure?
	BEQL	SUCXIT			; Assume this was lock blb and no locks.
	ASSUME	BLB$B_BID EQ BDB$B_BID
	ASSUME	BDB$B_BID EQ GBPB$B_BID
	CMPB	#BLB$C_BID, BDB$B_BID(R4) ; Is this a BLB?
	BEQL	ISBLB			; This is lock BLB.
	ASSUME	BDB$V_VAL EQ 0
	BLBS	BDB$B_FLGS(R4),10$	; branch if buffer valid
	BICB2	#BDB$M_DRT,BDB$B_FLGS(R4); make sure dirty not set
	BISL2	#RLS$M_DEQ, (SP)	; force total release if invalid.
10$:
	ASSUME	FAB$C_SEQ EQ 0
	TSTB	IFB$B_ORGCASE(R10)	; sequential file org?
	BEQL	15$
	BRW	RLS_SHARED		; branch if not

;
; release for sequential file org (non-shared)
;
; write the buffer if dirty
;

15$:	BBC	#BDB$V_DRT,BDB$B_FLGS(R4),20$; branch if not dirty
	BSBW	RM$SEQWTBD		; write the buffer

;
; note: this code not executed if unit record
;

	BLBC	R0,ERRXFR		; branch if errors

;
; return bdb and buffer if requested
;

20$:	ASSUME	RLS$V_RETURN EQ 0
EXIT:	BLBC	(SP),SUCXIT		; branch if return not requested

;
;!!!!! if write behind, must await i/o done !!!!!
;

	BSBW	RM$RETBDB		; return bdb & buffer
SUCXIT:	RMSSUC

;
; clean up stack and return
;

RLSXIT:	MOVL	(SP)+,R3		; Restore release flags.
	RSB

;
;  error occurred  -  do rlnerr call 
;

ERRXFR:	PUSHL	R0			; save status
	BSBB	RM$RLNERR		; release access
	POPR	#^M<R0>			; restore status code
	BRB	RLSXIT			; and return

	.SBTTL	SUPPORT ROUTINES AND INFREQUENT PATHS

ISBLB:	MOVL	R4,R2			; Get BLB into right register.
	CLRL	R4			; Note no BDB/GBPB.
	BRW	DEQ			; Dequeue the lock.

NOLOCK:
	BBS	#RLS$V_DEQ, (SP), DQBDB	; Make BDB free if DEQ spec'd.
	BBC	#RLS$V_KEEP_LOCK, (SP), EXIT ; If not keep lock, exit.
	CLRL	R4			; So we know there is no BLB when this
					; non-existent lock is released.
	BRB	SUCXIT			; And return with success.
DQBDB:	ASSUME	RLS$V_RETURN EQ 0
	BLBS	(SP), EXIT		; Exit if 'return' spec'd.
	RMSSUC				; Load R0 with success.
	BRW	DEQBDB			; Branch to free up BDB.

;
; This is a lock on a global buffer.
; Deq the GBPB entirely.  If the buffer was marked invalid,
; mark the GBD invalid also.  Also, if first accessor, then convert lock
; to system owned null lock in order to keep buffer cached.  If not first
; accessor, then just $DEQ our lock on the buffer.
;
; R1 = GBD Address
; R2 = BLB Address
; R4 = GBPB Address
;
GBPB:	BISL2	#RLS$M_DEQ, (SP)	; Force deq of gbpb.
	MOVL	GBPB$L_GBD_PTR(R4),R1	; Get address of GBD.
	MOVL	GBPB$L_VBNSEQNO(R4),-	; Move sequence number from gbpb to gbd.
		GBD$L_VBNSEQNUM(R1)
	ASSUME	BDB$V_VAL EQ 0
	BLBS	GBPB$B_FLGS(R4), VALID	; Br if valid.
BADGBL:	BSBW	INVGBL			; Invalidate GBD.
DQBR:	BRW	DEQ			; And go release lock.

;
; At this time we must look at the global buffer quota and the global buffer
; usage to determine if we will be able to convert the enq to a system lock.
; If we are out of quota, we must dump the buffer.
;
; Note: This piece of code has non-closeable timing hole in that the process
; can be deleted after we have taken the quota for the lock, but before we
; have actually converted the lock.  This can result in the permanent loss of
; a global buffer from the quota.  Also the reverse can occur in RMS0CLOSE.
;

VALID:	TSTL	GBD$L_LOCK_ID(R1)	; Has buffer been cached?
	BNEQ	DQBR			; Yes, go release lock.
	MOVW	@#SYS$GW_GBLBUFQUO,R0	; Get the global buffer quota
	ADAWI	#-1,@#RMS$GW_GBLBUFQUO	; Now try to get the quota for this lock
	ADDW2	@#RMS$GW_GBLBUFQUO,R0	; Can we really take the quota?
	BLSS	NOCSH			; Branch if out of quota
	MOVL	BLB$L_LOCK_ID(R2),-	; Save lock id of cached buffer
		GBD$L_LOCK_ID(R1)	;  in GBD.
	PUSHL	R1			; Save GBD address aroung $ENQ.
	BSBW	RM$SETEFN		; Get efn
	POPL	R0			; Put efn in R0.
	$ENQ_S	EFN 	= R0,-		; Convert lock to system owned NULL lock.
		LKMODE	= #LCK$K_NLMODE,-
		LKSB	= BLB$W_LKSTS(R2),-
		FLAGS	= #LCK$M_VALBLK!LCK$M_CONVERT!LCK$M_CVTSYS!LCK$M_SYSTEM!LCK$M_SYNCSTS
	POPL	R1			; Restore GBD address.
	BLBC	R0,NOCSH		; If conversion failed trash buffer.
	CLRL	BLB$L_LOCK_ID(R2)	; Don't $DEQ lock
	BRW	DEQ			; Go release structures.

NOCSH:	ADAWI	#1,@#RMS$GW_GBLBUFQUO	; Unable to convert the lock, so give
					; back the quota
	CLRL	GBD$L_LOCK_ID(R1)	; Conversion failed, clear lock_id in GBD.
	MOVL	IFB$L_GBH_PTR(R10), AP	; Get global buffer header.
	INCL	GBH$L_OUTBUFQUO(AP)	; Count this one as missed,
	BRB	BADGBL			; and invalidate the global buffer.

NOTLOCKED:
	RMSPBUG	FTL$_NOTLOCKED		; Not locked correctly.

	.SBTTL	SHARED FILE SUPPORT (RELATIVE AND INDEXED)

RLS_SHARED:
	TSTW	BDB$W_USERS(R4)		; Check use count.
	BEQL	NOTLOCKED		; Bug if zero.
	BBC	#BDB$V_DRT, BDB$B_FLGS(R4),- ; Branch if not dirty.
		CHECK_LOCK

	BITB	#IFB$M_AI!IFB$M_BI,-	; If neither AI nor BI Journalling is
		IFB$B_JNLFLG(R10)	; enabled, then skip the journalling 
	BEQL	5$			; of the bucket about to be released

	CMPB	#IFB$C_IDX,-		; AI and BI Journalling on a bucket
		IFB$B_ORGCASE(R10)	; basis is only performed if the
	BNEQ	5$			; file's organization is index

	BBC	#IFB$V_AI,-		; if this file is to be AI Journalled
		IFB$B_JNLFLG(R10),2$	; then write out a journalling entry
	PUSHL	#CJF$_AI		; to the AI Journal containing the
	JSB	RM$AI_AND_BI_3		; modified bucket before releasing
	ADDL2	#4,SP			; the dirty bucket
	BLBS	R0,2$
1$:	BRW	ERRXFR

2$:	BBC	#IFB$V_BI,-		; if this file is to be BI Journalled
		IFB$B_JNLFLG(R10),5$	; then write out a journalling entry
	PUSHL	#CJF$_BI		; to the BI Journal containing the 
	JSB	RM$AI_AND_BI_3		; un-modified bucket before releasing
	ADDL2	#4,SP			; the dirty bucket
	BLBC	R0,1$

5$:	BBS	#RLS$V_DEQ, R3, WRITE	; If DEQ just write (seqnum correct).
	BITL	#RLS$M_RETURN - 	; Check for options that
		! RLS$M_WRT_THRU ! RLS$M_KEEP_LOCK,- ; disable deferred write.
		R3
	BNEQ	10$			; If any set, then write it.
	BBS	#IFB$V_DFW, (R10), CHECK_LOCK ; Don't write if deferred write.

;
; The sequence number is only bumped here if the buffer is actually written.
; This prevents invalidating cached buffers when readers have accessed
; a dirty buffer.  The sequence number is also bumped later when a locker
; converts EX to PW.
;

	ASSUME	BDB$L_VBNSEQNO EQ GBPB$L_VBNSEQNO
10$:	INCL	BDB$L_VBNSEQNO(R4)	; Bump count, inv gbl if necessary.
WRITE:
	BBC	#IFB$V_JNL,IFB$B_JNLFLG2(R10),10$ ; Branch if not journaling.
	PUSHR	#^M<R2,R3,R5>		; Save registers.
	JSB	RM$FRCJNL		; Perform journal force(s).
	POPR	#^M<R2,R3,R5>		; Restore registers.
	BLBC	R0,20$			; Branch on error.
10$:	BSBW	RM$SEQWTBD		; Write the buffer.
	BLBS	R0,CHECK_LOCK		; If success, continue, else ...
20$:	BRW	ERRXFR			; stop if error detected

CHECK_LOCK:
	DECW	BDB$W_USERS(R4)		; Decrement use count.
	TSTL	BDB$L_VBN(R4)		; Was this buffer only (vbn 0)?
	BNEQ	5$			; If so, then make buffer free.
	BRW	DQBDB
5$:	MOVL	BDB$L_BLB_PTR(R4), R2	; Get pointer to BLB.
	BNEQ	10$			; continue unless there is no
	BRW	NOLOCK			; BLB structure

10$:	MOVL	BLB$L_OWNER(R2), AP	; Get owner of this BLB.
	BEQL	HAVE_BLB		; EQL is NL lock.
	CMPL	R9, AP			; Check owner of this BLB.
	BEQL	HAVE_BLB		; EQL then it's ours.
	ASSUME	BLB$L_FLNK EQ 0
	MOVL	(R2), R2		; Get next BLB.
	CMPL	BLB$L_BDB_ADDR(R2), R4	; Still in the chain?
	BEQL	10$			; Check the owner.
	BSBW	NOTLOCKED		; Something is wrong.

;
; This is a locker who wanted to defer write back of the buffer, but
; a blocking AST has already arrived so we must write it now.
;

WB:
	INCW	BDB$W_USERS(R4)		; Bump user count back up
	BRB	WRITE			; and write the buffer back.
HAVE_BLB:

;
; Assumption is being made that DFW can only be set at this time
; if this lock is being held by a reader.  If this is a write lock (LOCK)
; then DFW was cleared when the lock was granted, even if it was DFW
; prior to that time.  The exception is when rm$release has been called
; from rm$cache to free a DFW buffer - which was just written above -
; with the rls$v_deq flag set.  In this case, the blb$v_lock flag is
; still set from the original cache call that made the buffer dirty.
; At any rate, if this is really a reader, and this is a DFW buffer,
; then the lock is simply retained and no further action taken.
;

	ASSUME	BDB$L_VBNSEQNO EQ GBPB$L_VBNSEQNO
	MOVL	BDB$L_VBNSEQNO(R4),BLB$L_VALSEQNO(R2) ; Update value block.
	BBC	#RLS$V_KEEP_LOCK, (SP), 25$ ; Continue if not keeplock.
	BRW	KEEPLOCK		; Branch if keeplock.
25$:
	BBS	#BDB$V_DRT, BDB$B_FLGS(R4), 35$ ; Branch if dirty.
	BICB2	#BLB$M_DFW, BLB$B_BLBFLGS(R2) ; Can't be DFW if not dirty.
	ASSUME	<BDB$C_BID & 1> EQ 0
	ASSUME	<GBPB$C_BID & 1> EQ 1
	ASSUME	BDB$B_BID EQ GBPB$B_BID
	BLBC	BDB$B_BID(R4), 28$	; 
	BRW	GBPB			; Br to DEQ GBPB.

28$:	TSTW	BDB$W_USERS(R4)		; Any other accessors?
	BEQL	30$			; EQL then branch to convert the lock.
	BRW	DEQ1			; If other accessors, dq blb entirely.
30$:	BBC	#RLS$V_DEQ, (SP), 33$	; Continue if no dequeue requested.
	BRW	DEQ			; Otherwise Branch
33$:	CLRL	BLB$L_OWNER(R2)		; No owner for a NL lock.
	ASSUME	LCK$K_NLMODE EQ 0
	CLRL	-(SP)			; Want to convert to NL.
	CLRQ	-(SP)			; Acmode and protection.
	CLRQ	-(SP)			; No block AST or parameter.
	BRB	FINISH_ARGS		; Rejoin main line.

35$:

;
; At this point we have a dirty buffer.  This can only happen if
; deferred write was enabled, as otherwise the dirty flag got
; cleared when the buffer was written above.
;

	ASSUME	BLB$V_LOCK EQ 0
	BLBC	BLB$B_BLBFLGS(R2), DFW_RD ; Branch if a reader.

;
; This is a locker and deferred write is desired.
; If this is a global buffer, branch off to copy it to a local buffer
; so that a blocking ast will write out the local copy.  This simplifies
; the global cache replacement algorithm so that it doesn't have to
; deal with the problem of forcing another process to write a modified
; buffer from the global cache.
; The COPY_GBL code will return with R4 pointing to the BDB clone if
; it succeeds at the DFW_FIN label.  If it fails, it branches back to
; the WRITE label above to flush the buffer to disk, and it won't come
; here again.
;

	ASSUME	BDB$L_VBNSEQNO EQ GBPB$L_VBNSEQNO
	INCL	BDB$L_VBNSEQNO(R4)	; Bump seq num, inv gbl if local.
	INCL	BLB$L_VALSEQNO(R2)	; Do value block also.
	ASSUME	<BDB$C_BID & 1> EQ 0
	ASSUME	<GBPB$C_BID & 1> EQ 1
	ASSUME	BDB$B_BID EQ GBPB$B_BID
	BLBC	BDB$B_BID(R4), CHKWB	; Branch if this is a BDB.
	BRW	COPY_GBL		; Else branch to copy buffer.

;
; If the WRITEBACK flag is set in the BLB we must go back and write through
; the buffer because a blocking AST arrived after we selected the BLB.
;

CHKWB:	BBSC	#BLB$V_WRITEBACK,-	; Need to write back?
		BLB$B_BLBFLGS(R2), WB	; Branch if yes.
DFW_FIN:
	MOVL	R10, BLB$L_OWNER(R2)	; Ifab owns a dfw lock.
	PUSHL	#LCK$K_PWMODE		; Convert to PW mode.
	CLRQ	-(SP)			; Acmode and protection.
	PUSHAL	W^BLOCK_AST		; Want block ast for DFW.
	PUSHL	R2			; BLB is AST parameter.
	BISB2	#BLB$M_DFW, BLB$B_BLBFLGS(R2) ; Note this is DFW lock.
FINISH_ARGS:
	CLRQ	-(SP)			; No comp AST or parid.
	CLRL	-(SP)			; No resource name.
	PUSHL	#LCK$M_SYSTEM ! LCK$M_SYNCSTS - ; lock options.
		! LCK$M_CONVERT
	BBS	#BLB$V_NOBUFFER,-	; Check if originally nobuffer req.
		BLB$B_BLBFLGS(R2), 50$	; and don't store value if so.
	BITB	#BLB$M_LOCK ! BLB$M_DFW,- ; Check if value block needs
		BLB$B_BLBFLGS(R2)	; to be stored.
	BEQL	50$			; Readers don't update VALBLK.
	BISL2	#LCK$M_VALBLK, (SP)	; Store value block.
50$:	PUSHAL	BLB$W_LKSTS(R2)		; Lock status block.
	CLRL	BLB$W_LKSTS(R2)		; Init to zero.
	PUSHL	9*4(SP)			; Lock mode requested.
	BICB2	#^CBLB$M_DFW, BLB$B_BLBFLGS(R2) ; Clear all blbflgs except dfw
	BSBW	RM$SETEFN		; Get EFN to use.
	CALLS	#11, @#SYS$ENQ		; Do the conversion.
	POPL	R3			; Pop requested mode off stack.
	BLBC	R0, 70$			; Exit on error.
	MOVZWL	BLB$W_LKSTS(R2), R0	; Get status.
	BLBC	R0, 70$			; Branch on error.
	MOVB	R3, BLB$B_MODEHELD(R2)	; Store mode granted in blb.
70$:	BRW	RLSXIT			; And exit.

;
; Out of line check to make sure that WRITEBACK is in progress if DFW
; was clear.  Normally expect either DFW is set, or buffer wasn't dirty
; to begin with and we wouldn't have gotten here.
;

L1:	BBS	#BLB$V_WRITEBACK,-	; Writeback is the only reason DFW
		BLB$B_BLBFLGS(R2), L2	; should be clear.
	BSBW	NOTLOCKED		; BSBW because we won't come back
					; from the bugcheck and we can tell
					; where it came from.
DFW_RD:
;
; This is a reader, yet the buffer is dirty.
; Make sure that this is a deferred write lock as otherwise it means
; the caller did not lock the bucket when RM$CACHE was called.
; If WRITEBACK is also set, there was a problem on the blocking AST qio
; and the buffer should be written back now and released.
; The BLB we are using is the PW lock.
;

	BBC	#BLB$V_DFW, BLB$B_BLBFLGS(R2), L1 ; If not dfw, check further.
	BBS	#BLB$V_WRITEBACK,-	; Br if buffer must be written back.
		BLB$B_BLBFLGS(R2), CHKWB ; Only occurs if block ast qio failed.
L2:	MOVL	R10, BLB$L_OWNER(R2)	; Ifab is owner of DFW locks.
	BRW	SUCXIT			; and exit.
DEQ1:

;
; There are other streams que'd for the same lock, so DEQ this
; lock and make the BLB available.  Also must get BDB$L_BLB_PTR
; pointing to the next BLB if it was pointing to this one.
; A separate GBPB is associated with every access to a GBD so
; therefore the structure through this path will always be a BDB.
;

	BICL2	#RLS$M_DEQ, (SP)	; Clear DEQ so BDB isn't released.
	BSBW	FIX_BLBPTR		; Fixup BLB pointer.
	INCW	IFB$W_AVLCL(R10)	; Note BLB is free.
DEQ:
	MOVL	BLB$L_LOCK_ID(R2), R1	; Is lock id 0?
	BEQL	15$			; Yes it is, don't do DEQ.
	CLRL	R0			; Assume no value block.
	BBS	#BLB$V_NOBUFFER,-	; Check if originally nobuffer req.
		BLB$B_BLBFLGS(R2), 10$	; and don't update value if so.
	BITB	#BLB$M_LOCK ! BLB$M_DFW,- ; Check if value block needs
		BLB$B_BLBFLGS(R2)	; to be stored.
	BEQL	10$			; EQL then don't store.
	MOVAL	BLB$L_VALBLK(R2), R0	; Note value block.
10$:	$DEQ_S	LKID=R1 VALBLK=(R0)	; Dequeue the lock.
15$:
	ASSUME	BLB$B_MODEHELD EQ <BLB$B_BLBFLGS + 1>
	CLRW	BLB$B_BLBFLGS(R2)	; Clear out all flags.
	CLRL	BLB$L_BDB_ADDR(R2)	; Disassociate from BDB.
	ASSUME	BLB$L_VBN EQ <BLB$L_OWNER + 4>
	CLRQ	BLB$L_OWNER(R2)		; Clear owner and vbn.
	CLRQ	BLB$W_LKSTS(R2)		; Clear lock status block.
	REMQUE	(R2), R2		; Remove from chain.
	INSQUE	(R2), @IFB$L_BLBBLNK(R10) ; Insert at end.
	TSTL	R4			; Is there a BDB?
	BNEQ	30$			; Continue if BDB present.
20$:	BRW	RLSXIT			; Otherwise done now.
30$:	BBC	#RLS$V_DEQ, (SP), 20$	; Return if deq flag not set.
					; (came from deq1 label above).
DEQBDB:
	POPL	R3			; Restore release flags.
DQBDB1:
	ASSUME	<BDB$C_BID & 1> EQ 0
	ASSUME	<GBPB$C_BID & 1> EQ 1
	ASSUME	BDB$B_BID EQ GBPB$B_BID
	BLBC	BDB$B_BID(R4), 10$	; Br if this is a BDB.
	MOVL	GBPB$L_GBD_PTR(R4), AP	; Get pointer to GBD.
	DECW	GBD$W_USECNT(AP)	; Reduce use count on GBD.
	MOVB	GBPB$B_CACHE_VL(R4),-	; Store cache value in GBD.
		GBD$B_CACHE_VAL(AP)
	BRB	20$			; Br to finish up.
10$:	INCW	IFB$W_AVLCL(R10)	; Note another buffer available.
	ASSUME	BDB$B_CACHE_VAL EQ <BDB$B_FLGS + 1>
20$:	CLRW	BDB$B_FLGS(R4)		; Clear cache_val and flgs.
	CLRL	BDB$L_BLB_PTR(R4)	; Remove pointer to BLB.
	ASSUME	BDB$L_VBNSEQNO EQ <BDB$L_VBN + 4>
	CLRQ	BDB$L_VBN(R4)		; Clear vbn and seq num.
	REMQUE	(R4), R4		; Remove from chain.
	INSQUE	(R4), @IFB$L_BDB_BLNK(R10) ; Insert at end.
	RSB				; And return.

KEEPLOCK:
	TSTW	BDB$W_USERS(R4)		; Is use count zero?
	BNEQ	20$			; NEQ others are queued for BDB.
	BSBB	DQBDB1			; Free up buffer.
10$:	CLRL	BLB$L_BDB_ADDR(R2)	; No BDB assoc. with BDB now.
	MOVL	R2, R4			; Return BLB address in R4.
	BRW	SUCXIT			; And return.

20$:	BSBB	FIX_BLBPTR		; Fixup BLB pointer.
	REMQUE	(R2), R2		; Remove from chain.
	INSQUE	(R2), @IFB$L_BLBBLNK(R10) ; Insert at end.
	INCW	IFB$W_AVLCL(R10)	; Note one less accessor on buffer.
	BRB	10$			; Clear pointer and exit.


;++
; FIX_BLBPTR
;
; Routine to point BLB_PTR from BDB to next BLB when a BLB is being removed.
;
; Input:
;
; R4 = BDB
; R2 = BLB
;
;--
FIX_BLBPTR:
	CMPL	BDB$L_BLB_PTR(R4), R2	; Is BDB pointing to this BLB?
	BNEQ	20$			; NEQ it's not, so continue.
	ASSUME	BLB$L_FLNK EQ 0
	MOVL	(R2), R0		; Get next BLB.
	CMPL	BLB$L_BDB_ADDR(R0), R4	; Does it point to this BDB?
					; This assumes this offset in the
					; ifab from the listhead fails always.
	BEQL	10$			; EQL points to same BDB.
	CLRL	R0			; No other BLB's point to this BDB.
10$:	MOVL	R0, BDB$L_BLB_PTR(R4)	; Set or clear as appropriate.
20$:	RSB				; Return.


;++
; INVGBL
;
; This routine invalidates a buffer in the global buffer cache.  It is done
; as follows.  If any other accessors are queued waiting for their lock
; on this bucket to be granted, simply force a read by stuffing the sequence 
; number.   If no other accessors are present, move the GBD to the end of the 
; list to identify it as a free buffer.
;
; Inputs:
;
; R1 - Address of GBD
; R4 - Address of associated with BLB, if any.
; R10 - Address of IFAB.
;
; Destroys R0, R1, R3, AP.
;--
INVGBL:
	ASSUME	GBD$L_FLINK EQ 0
	ASSUME	GBD$L_BLINK EQ 4
	BSBW	RM$RAISE_GBS_LOCK	; Get EX lock on GBS.
	MOVL	IFB$L_GBH_PTR(R10), AP	; Get global buffer header.
	CMPW	#1,GBD$W_USECNT(R1)	; Anyone que'd for this buffer?
	BEQL	10$			; EQL no, so put at end of queue.
	MNEGL	#1,GBD$L_VBNSEQNUM(R1)	; Invalidate seq number to force read.
	BRB	20$			; Return.

;
; Remove from current position in list.
;

10$:	ADDL3	(R1), R1, R3		; R3 = successor.
	REMQTI	(R3), R1		; Remove from queue.
;
; Set VBN to -1.  Zero out cache_val, number bytes in use, flags.
;
	ASSUME	<GBD$B_FLAGS + 1> EQ GBD$B_CACHE_VAL
	CLRW	GBD$B_FLAGS(R1)		; Clear flags, cache_val.
	CLRW	GBD$W_NUMB(R1)		; Clear numb bytes used.
	MNEGL	#1, GBD$L_VBN(R1)	; Set invalid VBN.
	CLRL	GBD$L_VBNSEQNUM(R1)	; Clear sequence number.
	TSTL	GBD$L_LOCK_ID(R1)	; Is there a system lock to drop?
	BEQL	15$			; Branch if not.
	PUSHL	R1			; Save address of GBD
	ADAWI	#1,@#RMS$GW_GBLBUFQUO	; Count the quota back
	$DEQ_S	LKID=GBD$L_LOCK_ID(R1)	; Remove buffer from cache.
	POPL	R1			; Restore GBD address
	CLRL	GBD$L_LOCK_ID(R1)	; Zero the old cached buffer lock id.

;
; Now put this GBD at the end of the list.
; AP = list head.
;

15$:	INSQTI	(R1), (AP)		; Insert GBD at tail of queue.

20$:	BSBW	RM$LOWER_GBS_LOCK	; Release EX lock on GBS.
	RSB				; All done.


;++
; COPY_GBL
;
; Copy the global buffer to a local buffer if deferred write is
; desired to avoid the problems of deferred write from the global
; cache.
;
; Input:
; R4 - GBPB address.
;
; Output:
; R4 - BDB copy addr if branch to DFW_FIN
;	else same as input GBPB if branch to WRITE.
;
; Destroys R0, R1, R3, AP.
;--
COPY_GBL:

;
; First try to get a local buffer to copy to
;

	DECW	IFB$W_AVLCL(R10)	; Reduce local available count.
	BGEQ	10$			; Branch if one is already free.
	INCW	GBPB$W_USERS(R4)	; Bump use count so other streams
					; won't take it if free_lcl stalls.
	BSBW	RM$FREE_LCL		; Else call routine to free one.
	BLBC	R0, NO_BUFF		; Branch if that fails.
	DECW	GBPB$W_USERS(R4)	; Put use count back.
10$:	PUSHR	#^M<R2,R5>		; Save registers.
	MOVL	GBPB$L_VBN(R4), R1	; VBN of bucket to copy.
	MOVZWL	GBPB$W_NUMB(R4), R2	; Size of bucket to copy.
	BSBW	RM$GET_LCL_BUFF		; Get the local buffer.
					; Returns BDB addr in R5.
	MOVB	GBPB$B_CACHE_VL(R4),-	; Copy cache value.
		BDB$B_CACHE_VAL(R5)
	MOVL	GBPB$L_VBNSEQNO(R4),-	; Copy sequence number from GBPB.
		BDB$L_VBNSEQNO(R5)
	MOVL	(SP), AP		; Pickup saved BLB address.
	MOVL	AP, BDB$L_BLB_PTR(R5)	; Point BDB to BLB.
	MOVL	R5, BLB$L_BDB_ADDR(AP)	; Point BLB to BDB.
	MOVL	GBPB$L_ADDR(R4), R1	; Get source addr for copy.
	BSBW	DQBDB1			; Reset the fields in the GBPB.
	PUSHL	R5			; Save the BDB address.
	MOVC3	R2, (R1), @BDB$L_ADDR(R5) ; Copy the buffer.
	POPL	R4			; Get BDB addr into R4.
	BISB2	#BDB$M_VAL!BDB$M_DRT,-	; Note valid and dirty.
		BDB$B_FLGS(R4)
	POPR	#^M<R2,R5>		; Restore original values.
	BRW	DFW_FIN			; Jump back into mainline.
NO_BUFF:

;
; Couldn't get a local buffer to copy to.
; Fix local available count and branch back to force write of the
; global buffer.
;

	INCW	IFB$W_AVLCL(R10)	; Put count back.
	BRW	WRITE			; Go write it through then.

;++
; BLOCK_AST
;
; This is the deferred buffer write back routine which is specified
; as the blocking AST when a dirty buffer is held in the cache with
; a PW lock.
;
;--
BLOCK_AST:
	.WORD	^M<R4,R5,R10>
	MOVL	4(AP), R4		; AST parameter is BLB address.
	BSBB	SETUP			; Setup R4, R5, and R10.

;
; Note that the fact the BDB is checked out prior to checking the DFW
; flag is assuming that those pointers in the BLB are not being mucked
; with after CACHE clears the DFW flag and prior to the time the lock
; is converted or DEQ'd in RELEASE.
;

	BBCC	#BLB$V_DFW,-		; Check if BLB is being accessed
		BLB$B_BLBFLGS(R4),-	; already for lock access, and if
		SETWRTBCK		; so, simply set writeback flag.
	ASSUME	IFB$B_BID EQ IRB$B_BID
	CMPB	IFB$B_BID(R10), #IFB$C_BID ; Is this an ifab?
	BEQL	WRTBCK
	CMPB	IRB$B_BID(R10), #IRB$C_BID ; Then this better be an irab.
	BNEQ	BADOWN			; If not, then complain.
	ASSUME	IRB$L_IFAB_LNK EQ 0
	MOVL	(R10), R10		; Get ifab address into R10 then.
WRTBCK:
	MOVZWL	IFB$W_CHNL(R10), R0	; Get channel into R0.
	MOVZWL	BDB$W_NUMB(R5), R1	; Get size of buffer into R1.
	MOVL	BDB$L_ADDR(R5), R10	; Address of buffer.

	$QIO_S	EFN = #IMP$C_ASYQIOEFN,- ; Initiate write to disk.
		CHAN = R0,-
		FUNC = #IO$_WRITEVBLK,-
		IOSB = BDB$L_IOSB(R5),-
		ASTADR = B^BLKASTFIN,-
		ASTPRM = R4,-
		P1 = (R10),-
		P2 = R1,-
		P3 = BDB$L_VBN(R5)
	BLBC	R0, WBQIOERR		; Branch if qio failed.

SETWRTBCK:
	BISB2	#BLB$M_WRITEBACK,-	; Note buffer writeback necessary,
		BLB$B_BLBFLGS(R4)	; in progress, or attempted.
	RET				; Exit from the blocking AST.

; An error has occured attempting to write back the dirty buffer.
; Expected problem here is lack of AST quota.

WBQIOERR:
	RMSPBUG	FTL$_CANTDOAST		; bugcheck with the likely error.

;++
; SETUP
;
; Setup registers and verify structures.
;
; Input:
;	R4 - BLB address
;		BLB$L_BDB_ADDR
;		BLB$L_OWNER
;
; Output:
;	R4 - BLB
;	R5 - BDB
;	R10 - owner
;
; Bugcheck if BLB not a BLB or BDB not a BDB.  Owner not checked.
;
;--

SETUP:
	ASSUME	<BLB$B_BID + 1> EQ BLB$B_BLN
	CMPW	BLB$B_BID(R4),-		; Verify this is really a BLB
		#<BLB$C_BID +<BLB$C_BLN/4@8>> ; by checking BID and BLN fields.
	BNEQ	10$			; Bugcheck if no good.
	MOVL	BLB$L_BDB_ADDR(R4), R5	; Get BDB address.
	ASSUME	<BDB$B_BID + 1> EQ BDB$B_BLN
	CMPW	BDB$B_BID(R5),-		; Verify this is a BDB
		#<BDB$C_BID +<BDB$C_BLN/4@8>> ; by checking BID and BLN fields.
	BNEQ	20$			; NEQ then BDB not right.
	MOVL	BLB$L_OWNER(R4), R10	; Assume owner is the ifab.
	RSB				; Return.
10$:
	RMSPBUG	FTL$_BADBLB		; BLB is bad.
20$:
	RMSPBUG	FTL$_BADBDB		; BDB is bad.
BADOWN:
	RMSPBUG	FTL$_BADOWNER		; Owner field no good.


;++
; BLKASTFIN
;
; This is the completion routine for the deferred write back qio.
;
;--

BLKASTFIN:
	.WORD	^M<R2,R3,R4,R5,R9,R10>
	BSBW	RM$BLKFINCHK		; Check for AST's inhibited.
	MOVL	R9, R4			; Want BLB addr into R4.
	BSBB	SETUP			; Setup R4, R5, and R10.
	BICB2	#BDB$M_DRT, BDB$B_FLGS(R5) ; Clear dirty.
	BICB2	#BLB$M_DFW, BLB$B_BLBFLGS(R4) ; Not dfw anymore.

;
; The LOCK flag is being used here to indicate that a thread has
; stalled after finding the DFW flag clear on a DFW BLB.
;

	BBSC	#BLB$V_LOCK,-		; Br if necessary to start thread.
		BLB$B_BLBFLGS(R4), STARTTHREAD
	ASSUME	IFB$B_BID EQ IRB$B_BID
	CMPB	IRB$B_BID(R10), #IRB$C_BID ; Is this an irab?
	BEQL	CLRWRTBCK		; Don't convert lock if so.
	CMPB	IFB$B_BID(R10), #IFB$C_BID ; It should be an ifab then.
	BNEQ	BADOWN			; Bugcheck if not an ifab.
	TSTW	BDB$W_USERS(R5)		; Any other streams have this accessed?
	BNEQ	DQ			; Branch if so.
	CLRL	BLB$L_OWNER(R4)		; No owner anymore.
CONVNL:
	$ENQ_S	EFN = #IMP$C_ASYQIOEFN,- ; Convert the lock to NL.
		LKMODE = #LCK$K_NLMODE,-
		LKSB = BLB$W_LKSTS(R4),-
		FLAGS = #LCK$M_SYSTEM!LCK$M_CONVERT!LCK$M_SYNCSTS
	BLBC	R0,ENQBUG		; BUGCHECK if failure....
	ASSUME	LCK$K_NLMODE EQ 0
	CLRB	BLB$B_MODEHELD(R4)	; NL lock held now.
CLRWRTBCK:
	BICB2	#BLB$M_WRITEBACK,-	; Clear writeback flag.
		BLB$B_BLBFLGS(R4)
SETASTDCL:
	BISB2	#BDB$M_AST_DCL,-	; Note writeback has occurred.
		BDB$B_FLGS(R5)
	RET				; And exit.

ENQBUG:
	RMSPBUG	FTL$_ENQDEQFAIL		; $ENQ failed......
;
; Starting a stalled thread.
; Declare the AST, then convert the lock.
;

STARTTHREAD:
	$DCLAST_S ASTADR = RM$STALLAST,- ; Declare AST to start stalled thread.
		ASTPRM = BLB$L_OWNER(R4)
	BLBS	R0,CONVNL		; And exit on success.
	RMSPBUG	FTL$_ASTDECERR		; AST failed, BUGCHECK......

;
; Other streams have this buffer accessed.  Setup registers correctly and
; branch into release code that will dequeue the lock entirely.
;

DQ:	MOVL	R4, R2			; BLB expected in R2.
	MOVL	R5, R4			; BDB expected in R4.
	PUSHAB	SETASTDCL		; Release will RSB back.
	CLRL	-(SP)			; No release flags.
	BRW	DEQ1			; Go do it.
	.END
