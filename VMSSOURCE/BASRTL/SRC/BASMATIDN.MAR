	.TITLE	BAS$MAT_IDN
	.IDENT	/1-012/			; File: BASMATIDN.MAR Edit: MDL1012
;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982, 1984 BY				    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;

;++
; FACILITY: BASIC code support
;
; ABSTRACT:
;
;	This module initializes a matrix to have zeros everywhere except
;	ones on the diagonal.
;
; ENVIRONMENT: User Mode, AST Reentrant
;
;--
; AUTHOR: R. Will, CREATION DATE: 29-May-79
;
; MODIFIED BY:
;++
; 1-001	- Original 
; 1-002 - Reference bounds as signed, not unsigned.  RW 7-Jun-79
; 1-003 - Add support for byte, g and h floating.  PLL 17-Sep-81
; 1-004 - More modifications for new data types.  PLL 24-Sep-81
; 1-005 - Changed shared external references to G^ RNH 25-Sep-81
; 1-006 - Substitute a macro for the calls the array store
;	  routines.  This should speed things up.  PLL 6-Nov-81
; 1-007 - STORE macro must handle g & h floating.  PLL 12-Nov-81
; 1-008 - Correct a run-time expression in the STORE macro.
;	  PLL 20-Jan-82
; 1-009 - Changed macro STORE to handle arrays of descriptors.
;	  Also added check in mainline code to handle arrays of
;	  descriptors.  LEB  28-JUN-1982.
; 1-010 - Fixed bug in STORE macro.  LEB  4-JUL-1982.
; 1-011 - Change own storage to stack storage.  LEB 9-Jul-1982
; 1-012 - use G^ for ALL externals.  MDL 26-May-1983
;--

	.SBTTL	DECLARATIONS
;
; INCLUDE FILES:
;

	$DSCDEF
	$SFDEF

;
; EXTERNAL DECLARATIONS:
;
	.DSABL	GBL				; Prevent undeclared
						;  symbols from being
						;  automatically global.
	.EXTRN	BAS$K_ARGDONMAT			; signalled if all 3 blocks
						;  not present in array desc
	.EXTRN	BAS$K_DATTYPERR			; signalled if dtype of array
						; isn't word long float double
	.EXTRN	BAS$K_MATDIMERR			; array wasn't 2 dimensional
	.EXTRN	BAS$STO_FA_B_R8			; array element store for byte
	.EXTRN	BAS$STO_FA_W_R8			; array element store for word
	.EXTRN	BAS$STO_FA_L_R8			; array element store for long
	.EXTRN	BAS$STO_FA_F_R8			; array element store - float
	.EXTRN	BAS$STO_FA_D_R8			; array element store - double
	.EXTRN 	BAS$STO_FA_G_R8			; array element store - gfloat
	.EXTRN	BAS$STO_FA_H_R8			; array element store - hfloat
	.EXTRN	BAS$$STOP			; signal fatal errors
	.EXTRN	BAS$$SCALE_R1			; get the scale for double
	.EXTRN	BAS$STORE_BFA			; store value in array

;
; MACROS:
;

;	$BAS$MAT_IDN	see below, defines entire identity init algorithm
;	STORE		store an element into an array

;
; EQUATED SYMBOLS:
;

	lower_bnd2 = 0				; stack offset for temp
	lower_bnd1 = 4				; stack offset for temp
	upper_bnd1 = 8				; stack offset for temp
	value_desc = 12				; output descriptor
	str_len = 12				; length field within desc
	dtype = 14				; data type field in desc
	class = 15				; class field within desc
	pointer = 16
	data = 20				; data
	one_cvt = 36				; stack offset,converted one 
	dsc$l_l1_2 = 28				; desc offset if 2 sub
	dsc$l_u1_2 = 32				; desc offset if 2 sub
	dsc$l_l2_2 = 36				; desc offset if 2 sub
	dsc$l_u2_2 = 40				; desc offset if 2 sub

;
; OWN STORAGE:
;

;
; PSECT DECLARATIONS:
;
	.PSECT _BAS$CODE PIC, USR, CON, REL, LCL, SHR, -
			EXE, RD, NOWRT, LONG


	.SBTTL	BAS$MAT_IDN  - Initialize a  matrix to identity matrix
;++
; FUNCTIONAL DESCRIPTION:
;
;	This routine initializes the input matrix to the identity matrix
;	by setting all diagonal elements to 1 and all the remaining elements
;	to zero.  The algorithm is the same for all the supported
;	BASIC data types.  In order to keep the code for the data types
;	the same and to simplify the reading, the code has been done as
;	a macro, which all the data types use varying only the letters
;	(B, W, L, F, D, G, H) in converting the ones and zeros, in passing the constant
;	and calling the array store routines.
;
; CALLING SEQUENCE:
;
;	CALL BASMAT_IDN (matrix.wx.da)
;
; INPUT PARAMETERS:
;
;	NONE
;
; IMPLICIT INPUTS:
;
;	Scaling from the callers frame (for the double precision one)
;
; OUTPUT PARAMETERS:
;
	matrix = 4
;
; IMPLICIT OUTPUTS:
;
;	NONE
;
; FUNCTION VALUE:
; COMPLETION CODES:
;
;	NONE
;
; SIDE EFFECTS:
;
;	This routine will call the BASIC array store routines and so may
;	cause any of their errors to be signalled.  It may also signal any
;	of the errors listed in the externals section.
;
;--


;+
; This macro is a substitute for calls to the array store
; routines.  It will call the BAS$ routines only if the array is a 
; virtual array.  Otherwise, it will calculate the linear index into
; the array via the INDEX instruction.  (Note that BASIC programs must
; be able to handle FORTRAN arrays, so the code must check for arrays
; stored by column.)  The INDEX instructions should provide a significant
; performance improvement over calling a routine for each element of
; the array.
;-
	.MACRO	STORE	array_dtype,?L1,?L2,?L3,?L4,?L5,?L6,?L7,?L8,?L9,?L10,?L12,?L20,?L22,?L30,?L32,?L40,?L42

	.IF	IDN	'array_dtype', H	; array is hfloat
	CMPB	dsc$b_dtype(R4), #dsc$k_dtype_dsc ; descriptor?
	BNEQ	L10
	MOVL	4(R4), R0			; fetch addr of descriptor
	MOVB	dsc$b_dtype(R0), dtype(sp)	; load in data type
	MOVB	dsc$b_class(R0), class(sp)	; load in class field
	MOVAQ	data(SP), pointer(SP)		; load in pointer field
	CMPB	dsc$b_dimct(R4), #1		; check # of dimensions
	BNEQ	L12				; branch if 2 dimensions
	PUSHL	R5				; value of 1st index
	PUSHL	R4				; addr of array desc
	PUSHAL	value_desc+8(SP)		; addr of value desc
	CALLS	#3,G^BAS$STORE_BFA
	BRW	L9
L12:	PUSHL	R6				; value of 2nd index
	PUSHL	R5				; value of 1st index
	PUSHL	R4				; addr of array desc
	PUSHAL	value_desc+12(SP)		; addr of value desc
	CALLS	#4,G^BAS$STORE_BFA
	BRW	L9
L10:	CMPB	dsc$b_class(R4), #dsc$k_class_bfa ; virtual array?
	BNEQ	L1				; no
	JSB	G^BAS$STO_FA_'array_dtype'_R8	; yes, call store routine
	BRW	L9				; done
L1:	BBS	#5, 10(R4), L2			; br if stored row-wise
	INDEX	R5, dsc$l_l1_2(R4), dsc$l_u1_2(R4), dsc$l_m2(R4), #0, R7
						; I * M2
	MOVZWL	dsc$w_length(R4), R8		; longword length for INDEX
	INDEX	R6, dsc$l_l2_2(R4), dsc$l_u2_2(R4), R8, R7, R7
						; (J + (I * M2)) * length
	ADDL	dsc$a_a0(R4), R7		; compute addr of element
	MOV'array_dtype'	R0, (R7)	; store element from R0
	BRW	L9				
L2:	INDEX	R6, dsc$l_l2_2(R4), dsc$l_u2_2(R4), dsc$l_m1(R4), #0, R7
						; J * M1
	MOVZWL	dsc$w_length(R4), R8		; longword length for INDEX
	INDEX	R5, dsc$l_l1_2(R4), dsc$l_u1_2(R4), R8, R7, R7
						; (I + (J * M1)) * length
	ADDL	dsc$a_a0(R4), R7		; compute addr of element
	MOV'array_dtype'	R0, (R7)	; store element from R0
	.IFF
	.IF	IDN	'array_dtype', G	; array is gfloat
	CMPB	dsc$b_dtype(R2), #dsc$k_dtype_dsc ; descriptor?
	BNEQ	L20
	MOVL	4(R2), R0			; fetch addr of descriptor
	MOVB	dsc$b_dtype(R0), dtype(SP)	; load in data type
	MOVB	dsc$b_class(R0), class(SP)	; load in class field
	MOVAQ	data(SP), pointer(SP)		; load in pointer field
	CMPB	dsc$b_dimct(R2), #1		; check # of dimensions
	BNEQ	L22				; branch if 2 dimensions
	PUSHL	R3				; value of 1st index
	PUSHL	R2				; addr of array desc
	PUSHAL	value_desc+8(SP)		; addr of value desc
	CALLS	#3,G^BAS$STORE_BFA
	BRW	L9
L22:	PUSHL	R4				; value of 2nd index
	PUSHL	R3				; value of 1st index
	PUSHL	R2				; addr of array desc
	PUSHAL	value_desc+12(SP)		; addr of value desc
	CALLS	#4,G^BAS$STORE_BFA
	BRW	L9
L20:	CMPB	dsc$b_class(R2), #dsc$k_class_bfa  ; virtual array?
	BNEQ	L3				; no
	JSB	G^BAS$STO_FA_'array_dtype'_R8	; yes, call store routine
	BRW	L9				; done
L3:	BBS	#5, 10(R2), L4			; br if stored row-wise
	INDEX	R3, dsc$l_l1_2(R2), dsc$l_u1_2(R2), dsc$l_m2(R2), #0, R5
						; I * M2
	MOVZWL	dsc$w_length(R2), R6		; longword length for INDEX
	INDEX	R4, dsc$l_l2_2(R2), dsc$l_u2_2(R2), R6, R5, R5
						; (J + (I * M2)) * length
	ADDL	dsc$a_a0(R2), R5		; compute addr of element
	MOV'array_dtype'	R0, (R5)	; store element from R0
	BRW 	L9
L4:	INDEX	R4, dsc$l_l2_2(R2), dsc$l_u2_2(R2), dsc$l_m1(R2), #0, R5
						; J * M1
	MOVZWL	dsc$w_length(R2), R6		; longword length for INDEX
	INDEX	R3, dsc$l_l1_2(R2), dsc$l_u1_2(R2), R6, R5, R5
						; (I + (J * M1)) * length
	ADDL	dsc$a_a0(R2), R5		; compute addr of element
	MOV'array_dtype'	R0, (R5)	; store element from R0
	.IFF					
	.IF	IDN	'array_dtype', D	; array is double
	CMPB	dsc$b_dtype(R2), #dsc$k_dtype_dsc ; descriptor?
	BNEQ	L30
	MOVL	4(R2), R0			; fetch addr of descriptor
	MOVB	dsc$b_dtype(R0), dtype(SP)	; load in data type
	MOVB	dsc$b_class(R0), class(SP)	; load in class field
	MOVAQ	data(SP), pointer(SP)		; load in pointer field
	CMPB	dsc$b_dimct(R2), #1		; check # of dimensions
	BNEQ	L32				; branch if 2 dimensions
	PUSHL	R3				; value of 1st index
	PUSHL	R2				; addr of array desc
	PUSHAL	value_desc+8(SP)		; addr of value desc
	CALLS	#3,G^BAS$STORE_BFA
	BRW	L9
L32:	PUSHL	R4				; value of 2nd index
	PUSHL	R3				; value of 1st index
	PUSHL	R2				; addr of array desc
	PUSHAL	value_desc+12(SP)		; addr of value desc
	CALLS	#4,G^BAS$STORE_BFA
	BRW	L9
L30:	CMPB	dsc$b_class(R2), #dsc$k_class_bfa  ; virtual array?
	BNEQ	L5				; no
	JSB	G^BAS$STO_FA_'array_dtype'_R8	; call store routine
	BRW	L9				; done
L5:	BBS	#5, 10(R2), L6			; br if stored col-wise
	INDEX	R3, dsc$l_l1_2(R2), dsc$l_u1_2(R2), dsc$l_m2(R2), #0, R5
						; I * M2
	MOVZWL	dsc$w_length(R2), R6		; longword length for INDEX
	INDEX	R4, dsc$l_l2_2(R2), dsc$l_u2_2(R2), R6, R5, R5
						; (J + (I * M2)) * length
	ADDL	dsc$a_a0(R2), R5		; compute addr of element
	MOV'array_dtype'	R0, (R5)	; store element from R0
	BRW	L9				; done
L6:	INDEX	R4, dsc$l_l2_2(R2), dsc$l_u2_2(R2), dsc$l_m1(R2), #0, R5
						; J * M1
	MOVZWL	dsc$w_length(R2), R6		; longword length for INDEX
	INDEX	R3, dsc$l_l1_2(R2), dsc$l_u1_2(R2), R6, R5, R5
						; (I + (J * M1)) * length
	ADDL	dsc$a_a0(R2), R5		; compute addr of element
	MOV'array_dtype'	R0, (R5)	; store element from R0
	.IFF					; array type other than double
	CMPB	dsc$b_dtype(R1), #dsc$k_dtype_dsc ; descriptor?
	BNEQ	L40
	MOVL	4(R1), R0			; fetch addr of descriptor
	MOVB	dsc$b_dtype(R0), dtype(SP)	; load in data type
	MOVB	dsc$b_class(R0), class(SP)	; load in class field
	MOVAQ	data(SP), pointer(SP)		; load in pointer field
	CMPB	dsc$b_dimct(R1), #1		; check # of dimensions
	BNEQ	L42				; branch if 2 dimensions
	PUSHL	R2				; value of 1st index
	PUSHL	R1				; addr of array desc
	PUSHAL	value_desc+8(SP)		; addr of value desc
	CALLS	#3,G^BAS$STORE_BFA
	BRW	L9
L42:	PUSHL	R3				; value of 2nd index
	PUSHL	R2				; value of 1st index
	PUSHL	R1				; addr of array desc
	PUSHAL	value_desc+12(SP)		; addr of value desc
	CALLS	#4,G^BAS$STORE_BFA
	BRW	L9
L40:	CMPB	dsc$b_class(R1), #dsc$k_class_bfa  ;virtual array?
	BNEQ	L7				; no
	JSB	G^BAS$STO_FA_'array_dtype'_R8	; call store routine
	BRW	L9				; done
L7:	BBS	#5, 10(R1), L8			; br if stored col-wise
	INDEX	R2, dsc$l_l1_2(R1), dsc$l_u1_2(R1), dsc$l_m2(R1), #0, R4
						; I * M2
	MOVZWL	dsc$w_length(R1), R5		; longword length for INDEX
	INDEX	R3, dsc$l_l2_2(R1), dsc$l_u2_2(R1), R5, R4, R4
						; (J + (I * M2)) * length
	ADDL	dsc$a_a0(R1), R4		; compute addr of element
	MOV'array_dtype'	R0, (R4)	; store element from R0
	BRW	L9				; done
L8:	INDEX	R3, dsc$l_l2_2(R1), dsc$l_u2_2(R1), dsc$l_m1(R1), #0, R4
						; J * M1
	MOVZWL	dsc$w_length(R1), R5		; longword length for INDEX
	INDEX	R2, dsc$l_l1_2(R1), dsc$l_u1_2(R1), R5, R4, R4
						; (I + (J * M1)) * length
	ADDL	dsc$a_a0(R1), R4		; compute addr of element
	MOV'array_dtype'	R0, (R4)	; store element from R0
	.ENDC
	.ENDC
	.ENDC
L9:
	.ENDM


	.MACRO	$BAS$MAT_IDN dtype		; identity init algorithm

;+
;	REGISTER USAGE
;	R0 - R8 destroyed by store routines
;	R9	upper bound for 2nd subscript
;	R10	pointer to array descriptor
;	R11	current value of 2nd subscript
;-

;+
; Set up limits for looping through all elements
;-

	MOV'dtype'	#1, -(SP)		; make constant same data type
						;  as array, save on stack
	.IF 	IDN	dtype, D		; array is double
	MOVL	SF$L_SAVE_FP(FP), R0		; pass FP to get scale
	JSB	G^BAS$$SCALE_R1			; get scale in R0 & R1
						; call a BLISS routine because
						; the frame offsets are only
						; defined for BLISS
	MULD2	R0, (SP)			; scale
	.ENDC

	CLRQ	-(SP)				; alloc data
	CLRQ	-(SP)				;  may be hfloat
	CLRQ	-(SP)				; alloc value_desc

1$:	CMPB	DSC$B_DIMCT(R10), #2		; determine # of subscripts
	BEQLU	INIT_TWO_SUBS'dtype'		; 2 subs, go init
	BRW	ERR_MATDIMERR			; not 2 subs, error

;+
; There are 2 subscripts.  Put the upper bound for both subscripts on the
; stack and make sure that the lower bound for both subscripts will start
; at 1 (do not alter row or col 0 or any negative subscript)
;-

INIT_TWO_SUBS'dtype':
	PUSHL	dsc$l_u1_2(R10)			; 1st upper bound
	PUSHL	dsc$l_l1_2(R10)			; 1st lower bound
	BGTR	1$				; not row 0 or neg, do cols
	MOVL	#1, (SP)			; start with row 1
1$:	MOVL	dsc$l_u2_2(R10), R9		; 2nd upper bound
	PUSHL	dsc$l_l2_2(R10)			; 2nd lower bound
	BGTR	LOOP_2ND_SUB'dtype'		; not col 0 or neg, go loop
	MOVL	#1, (SP)			; start with col 1

;+
; Loop through all the rows.  Row and column upper and lower bounds have been
; initialized on the stack.
;-

LOOP_1ST_SUB'dtype':
	MOVL	lower_bnd2(SP), R11		; R11 has 2nd lower bound

;+
; Loop through all the elements (columns) of the current row.  Column lower
; bound is initialized in R11.  Column upper bound is on the stack.
;-

LOOP_2ND_SUB'dtype':

	CMPL	R11, lower_bnd1(SP)		; see if diagonal element
	BEQL	1$				; yes, go put 1
	CLR'dtype'	R0			; no, zero to be stored
	BRB	2$				; continue
1$:	MOV'dtype'	one_cvt(SP), R0		; put scaled 1 into R0
						;  R0 & R1 for double
;+
; When passed by value, H takes 4 words, G and D take 2 words, and all
; others take 1 word.
;-

2$:	.IF	IDN	dtype, H		; is datatype hfloat
	MOVL	R10, R4				; pointer to array desc
	MOVL	lower_bnd1(SP), R5		; current row
	MOVL	R11, R6				; current column
	.IFF
	.IF	IDN	dtype, G		; datatype gfloat
	MOVL	R10, R2				; pointer to array desc
	MOVL	lower_bnd1(SP), R3		; current row
	MOVL	R11, R4				; current column
	.IFF
	.IF	IDN	dtype, D		; datatype double
	MOVL	R10, R2				; pointer to array desc
	MOVL	lower_bnd1(SP), R3		; current row
	MOVL	R11, R4				; current column
	.IFF				
						; none of the above
	MOVL	R10, R1				; pointer to array desc
	MOVL	lower_bnd1(SP), R2		; current row
	MOVL	R11, R3				; current column
	.ENDC
	.ENDC
	.ENDC					; code now same for all types
	MOV'dtype'	R0, data(SP)
	STORE	'dtype'				; store in array
	INCL	R11				; get next column
	CMPL	R11, R9				; see if last column done
	BGTR	3$
	BRW	LOOP_2ND_SUB'dtype'		; no, continue inner loop

;+
; Have completed entire row.  See if it was the last row.  If not,
; continue with next row.
;-

3$:	INCL	lower_bnd1(SP)			; get next row
	CMPL	lower_bnd1(SP), upper_bnd1(SP)	; see if last row done
	BGTR	5$
	BRW	LOOP_1ST_SUB'dtype'		; no, continue outer loop

5$:	RET					; yes, finished
	.ENDM

	.ENTRY BAS$MAT_IDN, ^M<R2,R3,R4,R5,R6,R7,R8,R9,R10,R11,IV>

;+
; Put routine arguments into registers for ease of use.
; If block 2 of array descriptor (multipliers) is not present then error.
;-

	MOVL	matrix(AP), R10			; ptr to array descr in R10
	BBC	#DSC$V_FL_BOUNDS, DSC$B_AFLAGS(R10), ERR_ARGDONMAT
						; exit if block 3 not
						; present in descriptor

;+
; Algorithm now differs according to data types
;-

	MOVL	R10, R8				; save original pointer
4$:	CASEB	DSC$B_DTYPE(R8), #DSC$K_DTYPE_B, #<DSC$K_DTYPE_D - DSC$K_DTYPE_B>
1$:	.WORD	BYTE-1$				; code for byte dtype
	.WORD	WORD-1$				; code for word dtype
	.WORD	LONG-1$				; code for long dtype
	.WORD	ERR_DATTYPERR-1$		; quad not supported
	.WORD	FLOAT-1$			; code for float dtype
	.WORD	DOUBLE-1$			; code for double dtype

;+
; G and H floating fall outside the range of the CASEB.
;-

	CMPB	DSC$B_DTYPE(R8), #DSC$K_DTYPE_G
	BNEQ	2$
	BRW	GFLOAT				; code for gfloat dtype

2$:	CMPB	DSC$B_DTYPE(R8), #DSC$K_DTYPE_H
	BNEQ	3$
	BRW	HFLOAT				; code for hfloat dtype

3$:	CMPB	DSC$B_DTYPE(R8), #DSC$K_DTYPE_DSC
	BNEQ	ERR_DATTYPERR
	MOVL	4(R8), R8			; R8 <-- addr of descriptor
	BRB	4$				; CASE again for dtype in desc
	
ERR_DATTYPERR:
	PUSHL	#BAS$K_DATTYPERR		; Signal error, unsupported
	CALLS	#1, G^BAS$$STOP			; dtype in array desc

ERR_ARGDONMAT:
	PUSHL	#BAS$K_ARGDONMAT		; signal error,
	CALLS	#1, G^BAS$$STOP			; block 2 or 3 absent

ERR_MATDIMERR:
	PUSHL	#BAS$K_MATDIMERR		; signal error not 2 for dimct
	CALLS	#1, G^BAS$$STOP

	.PAGE
BYTE:	$BAS$MAT_IDN B				; expand to byte operations
	.PAGE
WORD:	$BAS$MAT_IDN W				; expand to word operations
	.PAGE
LONG:	$BAS$MAT_IDN L				; expand to long operations
	.PAGE
FLOAT:	$BAS$MAT_IDN F				; expand to float operations
	.PAGE
DOUBLE:	$BAS$MAT_IDN D				; expand to double operations
	.PAGE
GFLOAT: $BAS$MAT_IDN G				; expand to gfloat operations
	.PAGE
HFLOAT: $BAS$MAT_IDN H				; expand to hfloat operations

	.END					; end of BAS$MAT_IDN
