%TITLE	'FDLDRIVER'
%SBTTL	'FDL Parse Table Drivers'
MODULE	FDLDRIVER	( IDENT='V04-000',
			ADDRESSING_MODE ( EXTERNAL = GENERAL ),
			ADDRESSING_MODE ( NONEXTERNAL = GENERAL ),
			OPTLEVEL=3
			) =
BEGIN

!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982, 1984 BY				    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************

!++
!
! Facility:	RMS-32 FDL Utilities
!
! Abstract:
!
! Contents:
!		GET_LINE
!		UPCASE
!		SET_LINE
!		SET_TERM
!		SET_PRIMARY
!		SET_SECONDARY
!		START_STR
!		END_STR
!		SET_DATE_TIME
!		SET_COMMENT
!		SYNTAX_ERROR
!		ERROR_CHK
!		NEGATE
!		SET_BLANK
!		CLR_BLANK
!		FDL$$READ_ERROR
!		RMS_ERROR
!		RMS_OPEN_ERROR
!
! Environment:
!
!		VAX/VMS Operating System
!
!--

!
! Author:	Keith B Thompson	Creation date:	January-1981
!
!
! Modified by:
!
!	V03-012	KFH0009		Ken Henderson		23 Aug 1983
!		Fix to FDL$$GET_LINE to allow null
!		FDL spec string.
!		Fix calls to GET_VM and FREE_VM.
!
!	V03-011	KFH0008		Ken Henderson		10 Aug 1983
!		Fixes to END_STR and SET_DATE_TIME
!		Addition of EXTRACT_QUOTE routine
!		Addition of TRIM_LEADING routine
!
!	V03-010	KFH0007		Ken Henderson		29 Jul 1983
!		Check status of LIB$TPARSE call
!
!	V03-009	KFH0006		Ken Henderson		26 Apr 1983
!		Fixed call to $BINTIM
!
!	V03-008	KFH0005		Ken Henderson		30 Dec 1982
!		Fixed broken branches
!
!	V03-007	KFH0004		Ken Henderson		21 Dec 1982
!		Fixed signal of FDL$_UNSECKW
!
!	V03-006	KFH0003		Ken Henderson		15-Nov-1982
!		Added support for default and
!		main parses
!		Added support for more than 32
!		secondaries per primary
!		Added move to .FDL$GL_STNUMPTR of
!		.FDL$GL_STMNTNUM
!
!	V03-005	KFH0002		Ken Henderson		6-Oct-1982
!		Removed numtype
!		Added support for ACL primary
!		Added support for FDL STRINGS
!
!	V03-004	KFH0001		Ken F. Henderson	28-Jul-1982
!		Changed terminator character from "/" to "\"
!
!	V03-003	KBT0067		Keith B. Thompson	23-Jun-1982
!		Add support for multiple keywords per line
!
!	V03-002	KBT0029		Keith Thompson		30-Mar-1982
!		Add upcase/lowercase processing and date/time routine
!
!	V03-001	KBT0019		Keith Thompson		22-Mar-1982
!		Fix error message processing
!
!****


PSECT
	OWN	= _FDL$OWN	(PIC),
	GLOBAL	= _FDL$GLOBAL	(PIC),
	PLIT	= _FDL$PLIT	(SHARE,PIC),
	CODE	= _FDL$CODE	(SHARE,PIC);

LIBRARY	'SYS$LIBRARY:STARLET';
REQUIRE	'SRC$:FDLUTIL';
REQUIRE	'LIB$:FDLPARDEF';

EXTERNAL ROUTINE
	FDL$$GET_VM,
	FDL$$FREE_VM,
	LIB$TPARSE,
	STR$TRIM,
	SYS$BINTIM;

DEFINE_ERROR_CODES;

FORWARD ROUTINE
	EXTRACT_QUOTE,
	TRIM_LEADING,
	UPCASE			: NOVALUE,
	FDL$$SET_PRIMARY,
	FDL$$SET_SECONDARY,
	FDL$$START_STR,
	FDL$$END_STR,
	FDL$$SET_COMMENT,
	FDL$$SYNTAX_ERROR,
	FDL$$ERROR_CHK,
	FDL$$READ_ERROR		: NOVALUE;

EXTERNAL

!	Parse control
!
	FDL$AB_LINE		: DESC_BLK,
	FDL$AB_UPCASED		: DESC_BLK,
	FDL$AB_ITEM		: DESC_BLK,
	FDL$AB_FDL_STRING	: DESC_BLK,
	FDL$AB_PRE_PARSE_BLOCK	: BLOCK [ ,BYTE ],
	FDL$AB_PRE_PARSE_STATE,
	FDL$AB_PRE_PARSE_KEY,
	FDL$GL_STNUMPTR,
	FDL$GL_MAXLINE,
	FDL$AB_CTRL		: BLOCK [ ,BYTE ],
	FDL$GL_PRIMARY,
	FDL$GL_PRINUM,
	FDL$AB_PRICTRL		: BLOCK [ ,BYTE ],
	FDL$GL_SECONDARY,
	FDL$GL_SECNUM,
	FDL$AB_SECCTRL		: BITVECTOR [ FDL$K_SCTRL_VEC ],
	FDL$AB_SECCTRLL		: VECTOR [ FDL$K_SCTRL_LONG, LONG ],
	FDL$GL_QUALIFIER,
	FDL$GL_NUMBER,
	FDL$GL_SWITCH,
	FDL$GL_PROTECTION,
	FDL$AL_DATE_TIME	: VECTOR [ ,LONG ],
	FDL$AB_STRING		: DESC_BLK,
	FDL$AB_COMMENT		: DESC_BLK,
	FDL$GL_STMNTNUM,
	FDL$AB_FDL_RAB		: $RAB_DECL;

LITERAL
	SMALL_A		= 97,		! ASCII character "a"
	SMALL_Z		= 122,		! ASCII character "z"
	UPCASE_MASK	= 32,		! Mask to convert to upercase ASCII
	COMMENT_MARK	= 33;		! ASCII character "!"

OWN
	STRING_DESC	: DESC_BLK;

%SBTTL	'GET_LINE'
GLOBAL ROUTINE	FDL$$GET_LINE =
!++
!
! Functional Description:
!
!	Set up a new item for the parse tables.  If there are no
!	more items on a line it then reads from the input file (or uses
!	the FDL STRING)
!	It then upcases it; inits some values and returns
!
! Calling Sequence:
!
!	Called from the parse tables
!
! Input Parameters:
!	none
!
! Implicit Inputs:
!	none
!
! Output Parameters:
!	none
!
! Implicit Outputs:
!	none
!
! Routine Value:
!	none
!
! Side Effects:
!	none
!
!--

    BEGIN

    TPARSE_ARGS;

    ! Main processing loop
    !
    DO
	BEGIN

	! If there are no more items in the line get a new line
	!
	IF .FDL$AB_ITEM [ DSC$W_LENGTH ] EQL 0
	THEN
	    BEGIN

	    IF .FDL$AB_CTRL [ FDL$V_STRING_SPEC ]
	    THEN
		BEGIN

		! Only go thru once for the string.
		! Don't go thru at all if the string is null.
		!
		IF (
		(.FDL$AB_CTRL [ FDL$V_USED_STRING ])
		OR
		(.FDL$AB_FDL_STRING [ DSC$W_LENGTH ] EQLU 0)
		) THEN
		    RETURN 0;

		CH$MOVE ( .FDL$AB_FDL_STRING [ DSC$W_LENGTH ],
			  .FDL$AB_FDL_STRING [ DSC$A_POINTER ],
			  .FDL$AB_LINE [ DSC$A_POINTER ] );

		FDL$AB_LINE [ DSC$W_LENGTH ] = .FDL$AB_FDL_STRING [ DSC$W_LENGTH ];
		FDL$AB_CTRL [ FDL$V_USED_STRING ] = _SET;

		END
	    ELSE
		BEGIN

		! Loop until we get a non-zero line
		!
		DO
		    BEGIN

		    ! Get the new line from the FDL file.
		    !
		    RET_ON_ERROR( $GET ( RAB=FDL$AB_FDL_RAB,ERR=FDL$$READ_ERROR ) );

		    END

		UNTIL ( FDL$AB_LINE [ DSC$W_LENGTH ] =
					.FDL$AB_FDL_RAB [ RAB$W_RSZ ] ) NEQ 0;

		END;

	    ! Up case the whole line and move it into the upcase buffer
	    !
	    UPCASE();

	    ! Point the tables to the upcased line
	    !
	    FDL$AB_ITEM [ DSC$A_POINTER ] = .FDL$AB_UPCASED [ DSC$A_POINTER ]

	    END;

	! Point to the next item
	!
	FDL$AB_ITEM [ DSC$A_POINTER ] = .FDL$AB_ITEM [ DSC$A_POINTER ] +
						.FDL$AB_ITEM [ DSC$W_LENGTH ];

	BEGIN

	! Get the string
	!
	FDL$AB_PRE_PARSE_BLOCK [ TPA$L_STRINGCNT ] =
				    .FDL$AB_UPCASED [ DSC$W_LENGTH ]
				    - (
					.FDL$AB_ITEM [ DSC$A_POINTER ]
					- .FDL$AB_UPCASED [ DSC$A_POINTER ]
				    );
	FDL$AB_PRE_PARSE_BLOCK [ TPA$L_STRINGPTR ] = .FDL$AB_ITEM [ DSC$A_POINTER ];

	! Find where to chop it off - the Tparse will set these flags if
	! it finds " or '
	!
	FDL$AB_CTRL [ FDL$V_QUOTE_PRES ] = _CLEAR;
	FDL$AB_CTRL [ FDL$V_APOST_PRES ] = _CLEAR;

	RET_ON_ERROR ( LIB$TPARSE (
	FDL$AB_PRE_PARSE_BLOCK,FDL$AB_PRE_PARSE_STATE,FDL$AB_PRE_PARSE_KEY ));

	! Now set up the main tparse block to find our 'line'
	!
	TPARSE_BLOCK [ TPA$L_STRINGPTR ] = .FDL$AB_ITEM [ DSC$A_POINTER ];
	TPARSE_BLOCK [ TPA$L_STRINGCNT ] =
		  .FDL$AB_PRE_PARSE_BLOCK [ TPA$L_STRINGPTR ]
		  - .FDL$AB_ITEM [ DSC$A_POINTER ];

	FDL$AB_ITEM [ DSC$W_LENGTH ] = .TPARSE_BLOCK [ TPA$L_STRINGCNT ];

	END	! of local

	END	! of main loop

    UNTIL .FDL$AB_ITEM [ DSC$W_LENGTH ] NEQ 0;

    FDL$GL_STMNTNUM = .FDL$GL_STMNTNUM + 1;

    ! Update the user's cell that contains the statement number.
    !
    IF .FDL$AB_CTRL [ FDL$V_STVALID ]
    THEN
	.FDL$GL_STNUMPTR = .FDL$GL_STMNTNUM;

    ! Since there is a new secondary for each item clear some flags
    !
    FDL$GL_SECONDARY	= _CLEAR;
    FDL$GL_SECNUM	= _CLEAR;
    FDL$GL_SWITCH	= _CLEAR;
    FDL$GL_PROTECTION	= _CLEAR;
    FDL$AB_STRING [ DSC$W_LENGTH ] = 0;
    FDL$AB_CTRL [ FDL$V_WARNING ] = _CLEAR;
    FDL$AB_CTRL [ FDL$V_COMMENT ] = _CLEAR;
    FDL$AB_CTRL [ FDL$V_LINECMT ] = _CLEAR;

    RETURN SS$_NORMAL

    END;

%SBTTL	'UPCASE'
ROUTINE	UPCASE : NOVALUE =
!++
!
! Functional Description:
!
!	Upcases the input line while moving it into the upcase buffer
!
! Calling Sequence:
!
!	UPCASE()
!
! Input Parameters:
!	none
!
! Implicit Inputs:
!
!	FDL$AB_LINE	- Descriptor of the input line
!
! Output Parameters:
!	none
!
! Implicit Outputs:
!
!	FDL$AB_UPCASED	- Descriptor of the upcased input line
!
! Routine Value:
!	none
!
! Side Effects:
!	none
!
!--

    BEGIN

    LOCAL
	CHAR	: REF VECTOR [ ,BYTE ],
	UPCR	: REF VECTOR [ ,BYTE ];

    ! Point to the string of characters and the upcase buffer
    !
    CHAR = .FDL$AB_LINE [ DSC$A_POINTER ];
    UPCR = .FDL$AB_UPCASED [ DSC$A_POINTER ];

    ! Loop for all the characters in a line
    !
    INCR I FROM 0 TO ( .FDL$AB_LINE [ DSC$W_LENGTH ] - 1 ) BY 1
    DO

	! If the char. is a lower case letter upcase it
	! else just copy it over
	!
	IF ( .CHAR [ .I ] GEQU SMALL_A ) AND ( .CHAR [ .I ] LEQU SMALL_Z )
	THEN
	    UPCR [ .I ] = .CHAR [ .I ] AND ( NOT UPCASE_MASK )
	ELSE
	    UPCR [ .I ] = .CHAR [ .I ];

    ! Set the length of the upcased line
    !
    FDL$AB_UPCASED [ DSC$W_LENGTH ] = .FDL$AB_LINE [ DSC$W_LENGTH ];

    RETURN

    END;

%SBTTL	'SET_LINE'
GLOBAL ROUTINE	FDL$$SET_LINE =
!++
!
! Functional Description:
!
! Calling Sequence:
!
! Input Parameters:
!	none
!
! Implicit Inputs:
!	none
!
! Output Parameters:
!	none
!
! Implicit Outputs:
!	none
!
! Routine Value:
!	none
!
! Side Effects:
!	none
!
!--

    BEGIN

    TPARSE_ARGS;

    FDL$AB_ITEM [ DSC$A_POINTER ] = .TPARSE_BLOCK [ TPA$L_TOKENPTR ];

    RETURN SS$_NORMAL

    END;

%SBTTL	'SET_TERM'
GLOBAL ROUTINE	FDL$$SET_TERM =
!++
!
! Functional Description:
!
! Calling Sequence:
!
! Input Parameters:
!	none
!
! Implicit Inputs:
!	none
!
! Output Parameters:
!	none
!
! Implicit Outputs:
!	none
!
! Routine Value:
!	none
!
! Side Effects:
!	none
!
!--

    BEGIN

    TPARSE_ARGS;

    FDL$AB_PRE_PARSE_BLOCK [ TPA$L_STRINGPTR ] =
		.FDL$AB_PRE_PARSE_BLOCK [ TPA$L_STRINGPTR ] - 1;

    RETURN SS$_NORMAL

    END;

%SBTTL	'SET_PRIMARY'
GLOBAL ROUTINE	FDL$$SET_PRIMARY =
!++
!
! Functional Description:
!
! Calling Sequence:
!
! Input Parameters:
!	none
!
! Implicit Inputs:
!	none
!
! Output Parameters:
!	none
!
! Implicit Outputs:
!	none
!
! Routine Value:
!	none
!
! Side Effects:
!	none
!
!--

    BEGIN

    TPARSE_ARGS;

    OWN
	NXTPRINUM;	! The next key or area primary number

    LOCAL
	PRIMASK;

    PRIMASK = .TPARSE_BLOCK [ TPA$L_PARAM ];

    ! If this is the first call then clear an go else check to make sure a
    ! secondary was processed.
    !
    IF .FDL$AB_CTRL [ FDL$V_INITIAL ]
    THEN
	FDL$AB_CTRL [ FDL$V_INITIAL ] = _CLEAR
    ELSE

	! If a secondary was processed the ok else null primary warning
	!
	IF .FDL$AB_CTRL [ FDL$V_SECONDARY ]
	THEN
	    FDL$AB_CTRL [ FDL$V_SECONDARY ] = _CLEAR
	ELSE
	    SIGNAL ( FDL$_NULLPRI );

    IF (
    ( NOT .FDL$AB_CTRL [ FDL$V_DFLT_PRES ] )
    OR
    ( .FDL$AB_CTRL [ FDL$V_REPARSE ] )
    ) THEN
	BEGIN

	! If this primary has been defied before check to see if it's a
	! key or area primary
	!
	IF ( .PRIMASK AND .FDL$AB_PRICTRL ) NEQU 0
	THEN

	    ! Is it a key, area, analysis_of_key or analysis_of_area primary
	    ! check the order in case the last was the same
	    !
	    IF (
	    (
		( .PRIMASK )
		AND
		( FDL$M_KEY OR FDL$M_AREA OR FDL$M_ANALK OR FDL$M_ANALA )
	    ) NEQU 0

	    ) THEN

		! What was the last primary
		!
		IF (
		(.FDL$GL_PRIMARY EQLU FDL$C_KEY)
		OR
		(.FDL$GL_PRIMARY EQLU FDL$C_AREA)
		OR
		(.FDL$GL_PRIMARY EQLU FDL$C_ANALK)
		OR
		(.FDL$GL_PRIMARY EQLU FDL$C_ANALA)
		) THEN

		    ! Check to see if the number is correct
		    !
		    IF .FDL$GL_PRINUM EQLU .NXTPRINUM
		    THEN
			NXTPRINUM = .NXTPRINUM + 1
		    ELSE
			BEGIN
			SIGNAL( FDL$_OUTORDER,1,.FDL$GL_STMNTNUM );
			RETURN FDL$_SYNTAX
			END

		ELSE
		    NXTPRINUM = 0

	    ELSE

		! Multiple primaries is only a warning
		!
		SIGNAL( FDL$_MULPRI,1,.FDL$GL_STMNTNUM )

	ELSE

	    ! Is it a first key or area or ect. primary check the number
	    !
	    IF ( .PRIMASK AND ( FDL$M_KEY OR FDL$M_AREA OR FDL$M_ANALK OR
							    FDL$M_ANALA ) ) NEQU 0
	    THEN

		! If so check to see if the number is correct
		!
		IF .FDL$GL_PRINUM EQLU 0
		THEN
		    NXTPRINUM = 1
		ELSE
		    BEGIN
		    SIGNAL( FDL$_OUTORDER,1,.FDL$GL_STMNTNUM );
		    RETURN FDL$_SYNTAX
		    END;

	END;

    ! Flag it for latter
    !
    FDL$AB_PRICTRL = .FDL$AB_PRICTRL OR .PRIMASK;

    ! Clear FDL$PRIMARY so that tparse can set it on return
    !
    FDL$GL_PRIMARY = _CLEAR;

    ! Indicate that a new primary has been found
    !
    FDL$AB_CTRL [ FDL$V_NEWPRI ] = _SET;

    ! Get ready for a new set of secondaries
    !
    INCR I FROM 0 TO (FDL$K_SCTRL_LONG-1)
    DO
	FDL$AB_SECCTRLL [ .I ] = _CLEAR;

    RETURN SS$_NORMAL;

    END;

%SBTTL	'SET_SECONDARY'
GLOBAL ROUTINE	FDL$$SET_SECONDARY =
!++
!
! Functional Description:
!
! Calling Sequence:
!
! Input Parameters:
!	none
!
! Implicit Inputs:
!	none
!
! Output Parameters:
!	none
!
! Implicit Outputs:
!	none
!
! Routine Value:
!	none
!
! Side Effects:
!	none
!
!--

    BEGIN

    TPARSE_ARGS;

    LOCAL
	SECBIT	: LONG;

    SECBIT = .TPARSE_BLOCK [ TPA$L_PARAM ];

    ! See if the secondary has been defined before
    !
    IF .FDL$AB_SECCTRL [ .SECBIT ]
    THEN

	! If it has then see if it was a key segment thing
	!
	IF (
	( .SECBIT EQLU FDL$C_SEGPOS )
	OR
	( .SECBIT EQLU FDL$C_SEGLEN )
	OR
	( .SECBIT EQLU FDL$C_SEGTYP )
	) THEN
	    BEGIN

	    ! If it's out of bounds it's an error
	    !
	    IF .FDL$GL_SECNUM GTR 7
	    THEN
		BEGIN
		SIGNAL( FDL$_UNSECKW,3,
			.FDL$GL_STMNTNUM,
			.TPARSE_BLOCK [ TPA$L_TOKENCNT ],
			.TPARSE_BLOCK [ TPA$L_TOKENPTR ] );
		RETURN FDL$_SYNTAX
		END
	    END
	ELSE

	    ! If it has been defined before it's only a warning
	    !
	    SIGNAL( FDL$_MULSEC,1,.FDL$GL_STMNTNUM )
    ELSE

	! Flag it for next time (unless it's an ACL ENTRY - which can be repeated)
	!
	IF .SECBIT NEQU FDL$C_ACE
	THEN
	    FDL$AB_SECCTRL [ .SECBIT ] = _SET;

    ! Get ready for a new an wonderous qualifier
    !
    FDL$GL_QUALIFIER = _CLEAR;

    RETURN SS$_NORMAL

    END;

%SBTTL	'START_STR'
GLOBAL ROUTINE	FDL$$START_STR =
!++
!
! Functional Description:
!
!	Initializes the string descriptor
!
! Calling Sequence:
!
!	Called from the parse tables
!
! Input Parameters:
!	none
!
! Implicit Inputs:
!	none
!
! Output Parameters:
!	none
!
! Implicit Outputs:
!	none
!
! Routine Value:
!	none
!
! Side Effects:
!	none
!
!--

    BEGIN

    TPARSE_ARGS;

    ! Start the makings of a descriptor
    !
    FDL$AB_STRING [ DSC$A_POINTER ] = .TPARSE_BLOCK [ TPA$L_TOKENPTR ];

    ! Process blanks
    !
    TPARSE_BLOCK [ TPA$V_BLANKS ] = _SET;

    RETURN SS$_NORMAL

    END;

%SBTTL	'END_STR'
GLOBAL ROUTINE	FDL$$END_STR =
!++
!
! Functional Description:
!	
!	Terminates the processing of a string and determines the length
!
! Calling Sequence:
!
!	Called from the parse tables
!
! Input Parameters:
!	none
!
! Implicit Inputs:
!	none
!
! Output Parameters:
!	none
!
! Implicit Outputs:
!	none
!
! Routine Value:
!	none
!
! Side Effects:
!	none
!
!--

    BEGIN

    LOCAL
	SAVE_LEN	: WORD,
	CUT_LEN		: WORD;

    TPARSE_ARGS;

    TPARSE_BLOCK [ TPA$V_BLANKS ] = _CLEAR;

    ! The size is from where we are minus from where we is
    !
    FDL$AB_STRING [ DSC$W_LENGTH ] = .TPARSE_BLOCK [ TPA$L_STRINGPTR ] -
					.FDL$AB_STRING [ DSC$A_POINTER ];

    ! If the last char was a "!" then subtract one
    !
    IF .TPARSE_BLOCK [ TPA$B_CHAR ] EQL COMMENT_MARK
    THEN
	FDL$AB_STRING [ DSC$W_LENGTH ] = .FDL$AB_STRING [ DSC$W_LENGTH ] - 1;

    ! Save this length
    !
    SAVE_LEN = .FDL$AB_STRING [ DSC$W_LENGTH ];

    ! Remove trailing blanks
    !
    STR$TRIM ( FDL$AB_STRING,FDL$AB_STRING,CUT_LEN );

    ! Set the trimmed length
    !
    FDL$AB_STRING [ DSC$W_LENGTH ] = .CUT_LEN;

    ! Remove any leading white space from the string
    !
    FDL$AB_STRING [ DSC$W_LENGTH ] = TRIM_LEADING ();

    ! Remove any quotes from the upcased string
    !
    FDL$AB_STRING [ DSC$W_LENGTH ] = EXTRACT_QUOTE ();

    ! Adjust the pointer so that we are looking into the original input line
    !
    FDL$AB_STRING [ DSC$A_POINTER ] = .FDL$AB_STRING [ DSC$A_POINTER ] -
								.FDL$GL_MAXLINE;

    ! Restore the original length
    !
    FDL$AB_STRING [ DSC$W_LENGTH ] = .SAVE_LEN;

    ! Remove trailing blanks
    !
    STR$TRIM ( FDL$AB_STRING,FDL$AB_STRING,CUT_LEN );

    ! Set the trimmed length
    !
    FDL$AB_STRING [ DSC$W_LENGTH ] = .CUT_LEN;

    ! Remove any leading white space from the string
    !
    FDL$AB_STRING [ DSC$W_LENGTH ] = TRIM_LEADING ();

    ! Remove any quotes from the original string
    !
    FDL$AB_STRING [ DSC$W_LENGTH ] = EXTRACT_QUOTE ();

    RETURN SS$_NORMAL;

    END;

%SBTTL	'EXTRACT_QUOTE'
ROUTINE	EXTRACT_QUOTE =
!++
!
! Functional Description:
!	
!	It also extracts out embedded or bracketing quotes or apostrophes
!
! Calling Sequence:
!
!	Called from END_STR
!
! Input Parameters:
!	none
!
! Implicit Inputs:
!	none
!
! Output Parameters:
!	none
!
! Implicit Outputs:
!	none
!
! Routine Value:
!	The new string length - after the quotes are removed.
!
! Side Effects:
!	none
!
!--

    BEGIN

    LOCAL
	QCHAR	: BYTE,
	J	: LONG,
	NEW_LEN	: LONG,
	CUT_LEN	: LONG,
	STR	: REF VECTOR [ ,BYTE ],
	TMP_STR	: REF VECTOR [ ,BYTE ];

    NEW_LEN = .FDL$AB_STRING [ DSC$W_LENGTH ];

    ! Now extract out any bracketing or embedded quotes or apostrophes
    !
    IF .FDL$AB_CTRL [ FDL$V_QUOTE_PRES ] OR .FDL$AB_CTRL [ FDL$V_APOST_PRES ]
    THEN
	BEGIN

	CUT_LEN = .FDL$AB_STRING [ DSC$W_LENGTH ];
	TMP_STR = FDL$$GET_VM ( .CUT_LEN );

	STR = .FDL$AB_STRING [ DSC$A_POINTER ];

	IF .FDL$AB_CTRL [ FDL$V_QUOTE_PRES ]
	THEN
	    QCHAR = '"'
	ELSE IF .FDL$AB_CTRL [ FDL$V_APOST_PRES ]
	THEN
	    QCHAR = '''';

	CH$MOVE ( .CUT_LEN,.FDL$AB_STRING [ DSC$A_POINTER ],.TMP_STR );

	NEW_LEN = 0;
	J = 0;

	WHILE .J LEQ (.CUT_LEN - 1)
	DO
	    BEGIN

	    ! Now copy the string back, but stripping the QCHARs
	    ! according to the rules that embedded "" ==> " and '' ==> '
	    !
	    IF .TMP_STR [ .J ] EQLU .QCHAR
	    THEN
		BEGIN

		! If we're not at the beginning or end of the string,
		! copy one qchar and skip the next
		!
		IF NOT ((.J EQLU 0) OR (.J EQLU (.CUT_LEN-1)))
		THEN
		    BEGIN

		    IF .TMP_STR [ .J+1 ] EQLU .QCHAR
		    THEN
			J = .J + 1;

		    STR [ .NEW_LEN ] = .TMP_STR [ .J ];
		    NEW_LEN = .NEW_LEN + 1

		    END;
		END

	    ELSE
		! Just copy the character back and bump the count
		!
		BEGIN
		STR [ .NEW_LEN ] = .TMP_STR [ .J ];
		NEW_LEN = .NEW_LEN + 1
		END;

	    J = .J + 1;

	    END;	! do

	! Release the tmp string
	!
	FDL$$FREE_VM ( .CUT_LEN,.TMP_STR );

	END;	! IF QUOTE OR APOST PRESENT

    ! The routine value is the new length
    !
    RETURN .NEW_LEN;

    END;

%SBTTL	'TRIM_LEADING'
ROUTINE	TRIM_LEADING =
!++
!
! Functional Description:
!	
!	It removes leading spaces and tabs from the input string
!
! Calling Sequence:
!
!	Called from END_STR
!
! Input Parameters:
!	none
!
! Implicit Inputs:
!	none
!
! Output Parameters:
!	none
!
! Implicit Outputs:
!	none
!
! Routine Value:
!	The new string length - after the white space is removed.
!
! Side Effects:
!	none
!
!--

    BEGIN

    LOCAL
	FLAG	: BYTE,
	TMP	: BYTE,
	BLANK	: BYTE,
	TAB	: BYTE,
	J	: LONG,
	NEW_LEN	: LONG,
	CUT_LEN	: LONG,
	STR	: REF VECTOR [ ,BYTE ],
	TMP_STR	: REF VECTOR [ ,BYTE ];

    BLANK = ' ';
    TAB = '	';
    TMP = ..FDL$AB_STRING [ DSC$A_POINTER ];

    NEW_LEN = .FDL$AB_STRING [ DSC$W_LENGTH ];

    ! Now extract out any bracketing or embedded quotes or apostrophes
    !
    IF (.TMP EQLU .BLANK) OR (.TMP EQLU .TAB)
    THEN
	BEGIN

	CUT_LEN = .FDL$AB_STRING [ DSC$W_LENGTH ];
	TMP_STR = FDL$$GET_VM ( .CUT_LEN );

	STR = .FDL$AB_STRING [ DSC$A_POINTER ];

	CH$MOVE ( .CUT_LEN,.FDL$AB_STRING [ DSC$A_POINTER ],.TMP_STR );

	NEW_LEN = 0;
	J = 0;
	FLAG = _CLEAR;

	WHILE .J LEQ (.CUT_LEN - 1)
	DO
	    BEGIN

	    ! Now copy the string back, but stripping the white space
	    !
	    IF (.TMP_STR [ .J ] EQLU .BLANK) OR (.TMP_STR [ .J ] EQLU .TAB)
	    THEN
		BEGIN

		! If we have seen the a non-white character
		! just copy this blank or tab like any other char
		!
		IF .FLAG
		THEN
		    BEGIN

		    STR [ .NEW_LEN ] = .TMP_STR [ .J ];
		    NEW_LEN = .NEW_LEN + 1

		    END;
		END
	    ELSE
		! Just copy the character back and bump the count
		!
		BEGIN

		FLAG = _SET;
		STR [ .NEW_LEN ] = .TMP_STR [ .J ];
		NEW_LEN = .NEW_LEN + 1

		END;

	    J = .J + 1;

	    END;	! do

	! Release the tmp string
	!
	FDL$$FREE_VM ( .CUT_LEN,.TMP_STR );

	END;	! IF THERE IS LEADING WHITE SPACE

    ! The routine value is the new length
    !
    RETURN .NEW_LEN;

    END;

%SBTTL	'SET_DATE_TIME'
GLOBAL ROUTINE	FDL$$SET_DATE_TIME =
!++
!
! Functional Description:
!
!	Sets up the date/time quadword
!
! Calling Sequence:
!
!	Called from the parse tables
!
! Input Parameters:
!	none
!
! Implicit Inputs:
!	none
!
! Output Parameters:
!	none
!
! Implicit Outputs:
!	none
!
! Routine Value:
!	none
!
! Side Effects:
!	none
!
!--

    BEGIN

    TPARSE_ARGS;

    LOCAL
	TEMP_DESC	: DESC_BLK;

    ! We must adjust the pointer so it points to the upcased buffer
    !
    TEMP_DESC [ DSC$W_LENGTH ] = .FDL$AB_STRING [ DSC$W_LENGTH ];
    TEMP_DESC [ DSC$A_POINTER ] = .FDL$AB_STRING [ DSC$A_POINTER ] +
					.FDL$GL_MAXLINE;

    ! If there is an error signal it and return failure
    !
    IF NOT SYS$BINTIM( TEMP_DESC,FDL$AL_DATE_TIME )
    THEN
	BEGIN

	BUILTIN CALLG;

	TPARSE_BLOCK [ TPA$L_PARAM ] = FDL$_INVDATIM;

	CALLG( .TPARSE_BLOCK,FDL$$SYNTAX_ERROR );

	RETURN 0

	END;

    RETURN SS$_NORMAL

    END;

%SBTTL	'SET_COMMENT'
GLOBAL ROUTINE	FDL$$SET_COMMENT =
!++
!
! Functional Description:
!
!	Sets up the comment descriptor
!
! Calling Sequence:
!
!	Called from the parse tables
!
! Input Parameters:
!	none
!
! Implicit Inputs:
!	none
!
! Output Parameters:
!	none
!
! Implicit Outputs:
!	none
!
! Routine Value:
!	none
!
! Side Effects:
!	none
!
!--

    BEGIN

    TPARSE_ARGS;

    ! The comment is the rest of the line
    !
    FDL$AB_COMMENT [ DSC$W_LENGTH ] = .TPARSE_BLOCK [ TPA$L_STRINGCNT ] + 1;
    FDL$AB_COMMENT [ DSC$A_POINTER ] = .TPARSE_BLOCK [ TPA$L_STRINGPTR ] - 1;

    ! Adjust the pointer so that we are looking into the original input line
    !
    FDL$AB_COMMENT [ DSC$A_POINTER ] = .FDL$AB_COMMENT [ DSC$A_POINTER ] -
								.FDL$GL_MAXLINE;

    RETURN SS$_NORMAL

    END;

%SBTTL	'SYNTAX_ERRROR'
GLOBAL ROUTINE	FDL$$SYNTAX_ERROR =
!++
!
! Functional Description:
!
!	Syntax_error has two functions: If called with the argument fdl$_abkw
!	or fdl$_abprikw it checks if there has been an ambigous keyword, if
!	there has been then it signals the error else it returns failure. If
!	it is called with some other error it is signaled and return is normal.
!	
! Calling Sequence:
!
!	Called from the parse tables
!
!	Can be called from a bliss routine by:
!
!	BUILTIN CALLG;
!
!	CALLG( tparse_block,FDL$$SYNTAX_ERROR )
!
! Input Parameters:
!
!	Error code in the tpa$l_param field of the tparse_block
!
! Implicit Inputs:
!	none
!
! Output Parameters:
!	none
!
! Implicit Outputs:
!	none
!
! Routine Value:
!
!	ss$_normal or 0 (see above)
!
! Side Effects:
!
!	Signals an error
!
!--

    BEGIN

    TPARSE_ARGS;

    LOCAL	STATUS	: LONG;

    BIND	CODE = STATUS : BLOCK [ 4,BYTE ];

    ! Get the error code passed to us by the parse tables
    !
    STATUS = .TPARSE_BLOCK [ TPA$L_PARAM ];

    ! If this is a ambiguity check and there is none return failure
    ! else signal the error
    !
    IF ( ( .STATUS EQLU FDL$_ABKW ) OR ( .STATUS EQLU FDL$_ABPRIKW ) ) AND
					( NOT .TPARSE_BLOCK [ TPA$V_AMBIG ] )
    THEN
	RETURN 0;

    ! If this is not a information message the set some error flags
    !
    IF ( NOT ( .CODE [ STS$V_SEVERITY ] EQLU STS$K_INFO ) )
    THEN

	! Say that there is an error on this secondary
	!
	FDL$AB_CTRL [ FDL$V_WARNING ] = _SET;

    ! Signal the error with:
    !
    !	a) Line number
    !	b) Length of the current token
    !	c) Pointer to the token
    !	d) Length of the remainer of the line
    !	e) Pointer to the rest of the line
    !
    SIGNAL ( .TPARSE_BLOCK [ TPA$L_PARAM ],5,
		.FDL$GL_STMNTNUM,
		.TPARSE_BLOCK [ TPA$L_TOKENCNT ],
		.TPARSE_BLOCK [ TPA$L_TOKENPTR ],
		.TPARSE_BLOCK [ TPA$L_STRINGCNT ],
		.TPARSE_BLOCK [ TPA$L_STRINGPTR ] );

    RETURN SS$_NORMAL

    END;

%SBTTL	'NEGATE'
GLOBAL ROUTINE	FDL$$NEGATE : NOVALUE =
!++
!
! Functional Description:
!
!	Produces the negative version of a number
!
! Calling Sequence:
!
!	Called from the parse tables
!
! Input Parameters:
!	none
!
! Implicit Inputs:
!	none
!
! Output Parameters:
!	none
!
! Implicit Outputs:
!	none
!
! Routine Value:
!
!	none
!
! Side Effects:
!	none
!
!--

    BEGIN

    TPARSE_ARGS;

    ! Just negate the number
    !
    FDL$GL_NUMBER = -.FDL$GL_NUMBER;

    RETURN

    END;

%SBTTL	'SET_BLANK'
GLOBAL ROUTINE	FDL$$SET_BLANK : NOVALUE =
!++
!
! Functional Description:
!
!	Sets the Tparse blanks flag to allow parsing of blanks
!
! Calling Sequence:
!
!	Called from the parse tables
!
! Input Parameters:
!	none
!
! Implicit Inputs:
!	none
!
! Output Parameters:
!	none
!
! Implicit Outputs:
!	none
!
! Routine Value:
!
!	none
!
! Side Effects:
!	none
!
!--

    BEGIN

    TPARSE_ARGS;

    ! Just set the flag
    !
    TPARSE_BLOCK [ TPA$V_BLANKS ] = _SET;

    RETURN

    END;

%SBTTL	'CLR_BLANK'
GLOBAL ROUTINE	FDL$$CLR_BLANK : NOVALUE =
!++
!
! Functional Description:
!
!	Clears the Tparse blanks flag
!
! Calling Sequence:
!
!	Called from the parse tables
!
! Input Parameters:
!	none
!
! Implicit Inputs:
!	none
!
! Output Parameters:
!	none
!
! Implicit Outputs:
!	none
!
! Routine Value:
!
!	none
!
! Side Effects:
!	none
!
!--

    BEGIN

    TPARSE_ARGS;

    ! Just clear the flag
    !
    TPARSE_BLOCK [ TPA$V_BLANKS ] = _CLEAR;

    RETURN

    END;

%SBTTL	'ERRROR_CHK'
GLOBAL ROUTINE	FDL$$ERROR_CHK =
!++
!
! Functional Description:
!
!	Does a check if there was a warning
!
! Calling Sequence:
!
!	Called from the parse tables
!
! Input Parameters:
!	none
!
! Implicit Inputs:
!	none
!
! Output Parameters:
!	none
!
! Implicit Outputs:
!	none
!
! Routine Value:
!
!	Value of fdl$ab_ctrl [ fdl$v_warning ]
!
! Side Effects:
!	none
!
!--

    BEGIN

    TPARSE_ARGS;

    ! If there is a warning return true else fail
    !
    RETURN .FDL$AB_CTRL [ FDL$V_WARNING ]

    END;


%SBTTL	'FDL$$READ_ERROR'
GLOBAL ROUTINE	FDL$$READ_ERROR : NOVALUE =
!++
!
! Functional Description:
!
!	This routine will signal an rms error and stop execution if the RMS
!	error is NOT end of file.  It is to be used for detecting errors
!	during rms $GETs or $READs.
!
! Calling Sequence:
!
!	This routine is call as an AST by RMS
!
! Input Parameters:
!
!	AST argument block which has a pointer to a RAB
!
! Implicit Inputs:
!	none
!
! Output Parameters:
!	none
!
! Implicit Outputs:
!	none
!
! Routine Value:
!	none
!
! Routines Called:
!
!	SIGNAL_STOP
!
! Side Effects:
!	none
!
!--

    BEGIN

    BUILTIN
	AP;

    BIND
	AST_BLOCK = AP : REF VECTOR [ ,LONG ];

    LOCAL
	RAB : REF BLOCK [ ,BYTE ],
	FAB : REF BLOCK [ ,BYTE ],
	NAM : REF BLOCK [ ,BYTE ];

    ! Get the rab (Pointer to by the second ast parameter)
    !
    RAB = .AST_BLOCK [ 1 ];

    ! If this is only an end of file then return
    !
    IF .RAB [ RAB$L_STS ] EQLU RMS$_EOF
    THEN
	RETURN;

    ! Now get the fab it points to
    !
    FAB = .RAB [ RAB$L_FAB ];

    ! Get the name block
    !
    NAM = .FAB [ FAB$L_NAM ];

    ! Signal the FDL error with the best file name string
    !
    ! First try the resultant string
    !
    IF .NAM [ NAM$B_RSL ] NEQU 0
    THEN
	BEGIN
	STRING_DESC [ DSC$W_LENGTH ] = .NAM [ NAM$B_RSL ];
	STRING_DESC [ DSC$A_POINTER ] = .NAM [ NAM$L_RSA ]
	END

    ! Next try the expanded string
    !
    ELSE IF .NAM [ NAM$B_ESL ] NEQU 0
    THEN
	BEGIN
	STRING_DESC [ DSC$W_LENGTH ] = .NAM [ NAM$B_ESL ];
	STRING_DESC [ DSC$A_POINTER ] = .NAM [ NAM$L_ESA ]
	END

    ! If all else fails use the name string
    !
    ELSE
	BEGIN
	STRING_DESC [ DSC$W_LENGTH ] = .FAB [ FAB$B_FNS ];
	STRING_DESC [ DSC$A_POINTER ] = .FAB [ FAB$L_FNA ]
	END;

    SIGNAL_STOP( .RAB [ RAB$L_CTX ],1,STRING_DESC,
		 .RAB [ FAB$L_STS ],.RAB [ FAB$L_STV ] )

    END;


%SBTTL	'FDL$$RMS_ERROR'
GLOBAL ROUTINE	FDL$$RMS_ERROR	: NOVALUE =
!++
!
! Functional Description:
!
!	This routine will signal and rms error and stop execution.  It is
!	to be primarly used for detecting errors during asynchronous operations
!
! Calling Sequence:
!
!	This routine is call as an AST by RMS
!
! Input Parameters:
!
!	AST argument block which has a pointer to a rms block
!
! Implicit Inputs:
!	none
!
! Output Parameters:
!	none
!
! Implicit Outputs:
!	none
!
! Routine Value:
!	none
!
! Routines Called:
!
!	SIGNAL_STOP
!
! Side Effects:
!	none
!
!--

    BEGIN

    BUILTIN AP;

    BIND
	AST_BLOCK = AP : REF VECTOR [ ,LONG ];

    LOCAL
	RMS_BLOCK : REF BLOCK [ ,BYTE ];

    ! Get the rms control block (second argument in the block)
    !
    RMS_BLOCK = .AST_BLOCK [ 1 ];

    ! NOTE: We use the RAB$x_zzz codes but they are valid for the FAB as well

    ! Signal the FDL error
    !
    SIGNAL_STOP( .RMS_BLOCK [ RAB$L_CTX ],
		 .RMS_BLOCK [ RAB$L_STS ],.RMS_BLOCK [ RAB$L_STV ] )

    END;


%SBTTL	'FDL$$RMS_OPEN_ERROR'
GLOBAL ROUTINE	FDL$$RMS_OPEN_ERROR : NOVALUE =
!++
!
! Functional Description:
!
!	This routine will signal an rms error and stop execution.  It is
!	to be primarly used for detecting errors during file opens.
!
! Calling Sequence:
!
!	This routine is call as an AST by RMS
!
! Input Parameters:
!
!	AST argument block which has a pointer to a FAB
!
! Implicit Inputs:
!	none
!
! Output Parameters:
!	none
!
! Implicit Outputs:
!	none
!
! Routine Value:
!	none
!
! Routines Called:
!
!	SIGNAL_STOP
!
! Side Effects:
!	none
!
!--

    BEGIN

    BUILTIN
	AP;

    BIND
	AST_BLOCK = AP : REF VECTOR [ ,LONG ];

    LOCAL
	FAB : REF BLOCK [ ,BYTE ],
	NAM : REF BLOCK [ ,BYTE ];

    ! Get the fab (Pointer to by the second ast parameter)
    !
    FAB = .AST_BLOCK [ 1 ];

    ! If this is really a RAB (from a connect) then get the fab it points to
    !
    IF .FAB [ FAB$B_BID ] EQLU RAB$C_BID
    THEN
	FAB = .FAB [ RAB$L_FAB ];	! This looks strange but it's ok!

    ! Get the name block
    !
    NAM = .FAB [ FAB$L_NAM ];

    ! Signal the FDL error with the best file name string
    !
    ! First try the resultant string
    !
    IF .NAM [ NAM$B_RSL ] NEQU 0
    THEN
	BEGIN
	STRING_DESC [ DSC$W_LENGTH ] = .NAM [ NAM$B_RSL ];
	STRING_DESC [ DSC$A_POINTER ] = .NAM [ NAM$L_RSA ]
	END

    ! Next try the expanded string
    !
    ELSE IF .NAM [ NAM$B_ESL ] NEQU 0
    THEN
	BEGIN
	STRING_DESC [ DSC$W_LENGTH ] = .NAM [ NAM$B_ESL ];
	STRING_DESC [ DSC$A_POINTER ] = .NAM [ NAM$L_ESA ]
	END

    ! If all else fails use the name string
    !
    ELSE
	BEGIN
	STRING_DESC [ DSC$W_LENGTH ] = .FAB [ FAB$B_FNS ];
	STRING_DESC [ DSC$A_POINTER ] = .FAB [ FAB$L_FNA ]
	END;

    SIGNAL_STOP( .FAB [ RAB$L_CTX ],1,STRING_DESC,
		 .FAB [ FAB$L_STS ],.FAB [ FAB$L_STV ] )

    END;

END ELUDOM
