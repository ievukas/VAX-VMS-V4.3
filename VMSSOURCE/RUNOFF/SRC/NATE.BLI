%TITLE 'Initialize ATABLE'
MODULE NATE (	IDENT = 'V04-000'
		%BLISS32[, ADDRESSING_MODE (EXTERNAL    = long_relative,
					    NONEXTERNAL = long_relative)]
		) =
BEGIN
!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982, 1984 BY				    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************
!
!++
! FACILITY:	 DSR (Digital Standard RUNOFF) / DSRPLUS
!
! ABSTRACT:	Normalize an action table entry.
!
! ENVIRONMENT:	Transportable
!
! AUTHOR:	R.W.Friday	CREATION DATE:	April, 1978
!

%SBTTL 'Revision History'
!
! MODIFIED BY:
!
!	005	REM00005	Ray Marshall	17-November-1983
!		Modified the external definition of ATABLE to use the new
!		  macro ATABLE_DEFINITION defined in ATCODE.REQ.
!
!	004	REM00004	Ray Marshall	7-November-1983
!		Added definitions for 128 (decimal) more characters.  These
!		  are the added definitions for the DEC multinational
!		  character set.
!
!	003	KAD00003	Keith Dawson	07-Mar-1983
!		Global edit of all modules. Updated module names, idents,
!		copyright dates. Changed require files to BLISS library.
!
!--

%SBTTL 'Module Level Declarations'

!
! TABLE OF CONTENTS:
!
! EQUATED SYMBOLS:
!
!
! INCLUDE FILES:
!

LIBRARY 'NXPORT:XPORT';			! XPORT Library
REQUIRE 'REQ:RNODEF';			! RUNOFF variant definitions

%IF DSRPLUS %THEN
LIBRARY 'REQ:DPLLIB';			! DSRPLUS BLISS Library
%ELSE
LIBRARY 'REQ:DSRLIB';			! DSR BLISS Library
%FI

EXTERNAL LITERAL
    rintes : UNSIGNED (8);

LITERAL
    tab_character = %O'11';

!
! EXTERNAL REFERENCES:
!
EXTERNAL
    atable : atable_definition;	! Action table.  Used to identify what type of
				! action is to be taken on encountering any
				! given character.

GLOBAL ROUTINE nate (kharacter) : NOVALUE =

!++
! FUNCTIONAL DESCRIPTION:
!
!	NATE ensures that an ATABLE entry for a particular character
!	corresponds to what that character usually represents.
!
! FORMAL PARAMETERS:
!
!	kharacter - indicates which action table entry should be reset.
!
! IMPLICIT INPUTS:	None
!
! IMPLICIT OUTPUTS:	None
!
! ROUTINE VALUE:
! COMPLETION CODES:	None
!
! SIDE EFFECTS:		None
!
!--

    BEGIN
    !
    ! The order of statements in the SELECTONE statement is important.
    ! This is because some classifications (such as a_punct) must be
    ! made before others (such as a_other) because they share characters
    ! in common.
    !
    atable [.kharacter] = (SELECTONE .kharacter OF
				SET
				[rintes] :			a_int_esc;
				[tab_character] :		a_tab;
				[0 TO %O'37'] :			a_control;
				[%O'40'] :			a_space;
				[%C'.', %C':', %C'!', %C'?'] :	a_punct;
				[%O'41' TO %O'57'] :		a_other;
				[%C'0' TO %C'9'] :		a_digit;
				[%O'72' TO %O'100'] :		a_other;
				[%C'A' TO %C'Z'] :		a_u_letter;
				[%O'133' TO %O'140'] :		a_other;
				[%C'a' TO %C'z'] :		a_l_letter;
				[%O'173' TO %O'176'] :		a_other;
				[%O'177'] :			a_control;

	!  Additional characters for the multi-national character set:

%( Remove block comment if any of these cause problems.

	!  To start off with, we must map the currently "reserved" character
	!  codes as control characters because they tend to adversly affect
	!  most output devices.  They can be just removed from the code as
	!  they are defined as printable characters since their values are
	!  already included in the ranges below.

				[%DECIMAL'166'] :		a_control;
				[%DECIMAL'172'] :		a_control;
				[%DECIMAL'173'] :		a_control;
				[%DECIMAL'174'] :		a_control;
				[%DECIMAL'175'] :		a_control;
				[%DECIMAL'180'] :		a_control;
				[%DECIMAL'184'] :		a_control;
				[%DECIMAL'190'] :		a_control;
				[%DECIMAL'208'] :		a_control;
				[%DECIMAL'222'] :		a_control;
				[%DECIMAL'240'] :		a_control;
				[%DECIMAL'254'] :		a_control;
)%
	! The following defines all characters from 128 through 255 decimal.

				[%DECIMAL'128' TO %DECIMAL'159'] : a_control;
				[%DECIMAL'160'] :		   a_other;
				[%DECIMAL'161' TO %DECIMAL'191'] : a_other;
				[%DECIMAL'192' TO %DECIMAL'222'] : a_u_alpha;
				[%DECIMAL'223'] :		   a_other;
				[%DECIMAL'224' TO %DECIMAL'254'] : a_l_alpha;
				[%DECIMAL'255'] :		   a_control;

				TES);
    RETURN;
    END;					!End of NATE

END						!End of module
ELUDOM
