MODULE RCS (					!
		IDENT = 'V04-000'
%BLISS32[,
	ADDRESSING_MODE(EXTERNAL=LONG_RELATIVE,NONEXTERNAL=LONG_RELATIVE)
	]
		) =
BEGIN
!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982, 1984 BY				    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************
!
!++
! FACILITY:	 DSR (Digital Standard RUNOFF) / DSRPLUS
!
! ABSTRACT: Recognizes a string of keywords that identifies a command.
!
!
! ENVIRONMENT: Transportable
!
! AUTHOR: R.W.Friday	 CREATION DATE: May, 1978
!

%SBTTL 'Revision History'
!
! MODIFIED BY:
!
!	002	KAD00002	Keith Dawson	07-Mar-1983
!		Global edit of all modules. Updated module names, idents,
!		copyright dates. Changed require files to BLISS library.
!
!--

%SBTTL 'Module Level Declarations'

!
! TABLE OF CONTENTS:
!
!
! INCLUDE FILES:
!

LIBRARY 'NXPORT:XPORT';			! XPORT Library
REQUIRE 'REQ:RNODEF';			! RUNOFF variant definitions

%IF DSRPLUS %THEN
LIBRARY 'REQ:DPLLIB';			! DSRPLUS BLISS Library
%ELSE
LIBRARY 'REQ:DSRLIB';			! DSR BLISS Library
%FI

!
! EQUATED SYMBOLS:
!
!
! OWN STORAGE:
!
!
! EXTERNAL REFERENCES:
!

EXTERNAL ROUTINE
    FIND,
    GSLU,
    RSKIPS;

EXTERNAL
    FS01 : FIXED_STRING,			!Common work area for misc. tasks.
    IRA : FIXED_STRING,
    LP1 : VECTOR;


GLOBAL ROUTINE RCS =

!++
! FUNCTIONAL DESCRIPTION:
!
!	Beginning with LP1, RCS collects keywords in an attempt
!	to recognize a command.  If a command is recognized, the
!	item descriptor is
!	returned.  If the command is not recognized, zero is returned.
!	There is no capability for "partially" recognizing a command.
!
! FORMAL PARAMETERS:	None
!
! IMPLICIT INPUTS:	None
!
! IMPLICIT OUTPUTS:	None
!
! ROUTINE VALUE:
! COMPLETION CODES:
!
!	Either the item descriptor associated with the command,
!	or zero if the command is not recognized.
!
! SIDE EFFECTS:	None
!
!--

    BEGIN
    LOCAL
	x : REF VECTOR,
	result;

    x = lp1;

    WHILE 1 DO
	BEGIN
	fs_init (fs01);
	result = gslu (ira, fs01);

	IF .result EQL gslu_none
	THEN
	    fs_wchar (fs01, %c'@');

	result = find (.fs_start (fs01), .fs_length (fs01), .x);

	IF .result EQL 0
	THEN
	    !This coding makes it possible for the user to omit keywords
	    !when there is no ambiguity.
	    IF (CH$RCHAR (.fs_start(fs01)) EQL %C'@')
		 AND (.x [-1] EQL 1)
	    THEN
		result = .x [0]			!Trailing keyword is optional.
	    ELSE
		RETURN 0;			!In this case, it is ambiguous

	BEGIN				!Start local definition block
	BIND
	    item = result : REF VECTOR;

	IF .kwitem_chain_continues (item)
	THEN
	    BEGIN
	    rskips (ira);
	    x = .kwitem_next_list (item);
	    END
	ELSE
	    RETURN .item
	END				!End local definition block

	END

    END;					!End of RCS

END						!End of module
ELUDOM
