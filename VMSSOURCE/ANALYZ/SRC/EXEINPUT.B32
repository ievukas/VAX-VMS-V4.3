%title 'EXEINPUT - Handle Image Files & Libraries'
	module exeinput	(
			ident='V04-000') = begin

!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982, 1984 BY				    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************
!

!++
! Facility:	VAX/VMS Analyze Facility, Handle Image Files
!
! Abstract:	This module is responsible for handling file specs from
!		the command line, and reading data from image files.
!
!
! Environment:
!
! Author: Paul C. Anagnostopoulos, Creation Date: 31 March 1981
!
! Modified By:
!
!	V03-007	ROP0009		Robert Posniak		15-JUN-1984
!		Clear image buffer valid flag when analyzing
!		more than one image.
!
!	V03-006	BLS0286		Benn Schreiber		20-MAR-1984
!		Correct 005.
!
!	V03-005 LJA0112 	Laurie J. Anderson	21-Feb-1984
!		Add new related file parsing arguments to LIB$FIND_FILE
!		to make search lists behave properly.
!
!	V03-004 LJA0105 	Laurie J. Anderson	25-Jan-1984
!		Changes due because now using the new image activator
!		decode routines.  Now, open the image file through RMS
!		with the UFO option bit set, and do not bother to connect
!		stream.  Will be using QIO's, because the decode routines 
!		do, too.  Add routines to read the image header and isd's.
!
!	V03-003	PCA1011		Paul C. Anagnostopoulos  1-Apr-1983
!		Change the message prefix to ANLOBJ$_ to ensure that
!		message symbols are unique across all ANALYZEs.  This
!		is necessitated by the new merged message files.
!
!	V03-002	PCA0020		Paul Anagnostopoulos	24-Mar-1982
!		Signal errors using the correct STV value.
!
!	V03-001	PCA0014		Paul Anagnostopoulos	22-Mar-1982
!		Use the resultant spec rather than the wildcard spec
!		when complaining about a file to be analyzed.
!--

%sbttl 'Module Declarations'
!
! Libraries and Requires:
!

library 'starlet';
require 'objexereq';

!
! Table of Contents:
!

forward routine
	anl$open_next_image_file,
	anl$get_image_block,
	anl$map_fixup_section,
	anl$get_image_header,
	anl$get_isd ;

!
! External References:
!

external routine
	anl$image_positionals,
	cli$get_value: addressing_mode(general),
	img$decode_ihd: addressing_mode(general),
	img$get_next_isd: addressing_mode(general),
	lib$find_file: addressing_mode(general),
	lib$free_vm: addressing_mode(general),
	lib$get_vm: addressing_mode(general),
	str$trim: addressing_mode(general);

!
! Own Variables:
!
! The following data structures are used to access and read blocks from
! the file we are to analyze.

own
	own_described_buffer(resultant_spec,nam$c_maxrss),
	image_fab: $fab(fop=ufo),	! FAB for RMS UFO $OPEN of image file
	image_buffer: block[512,byte],	! contains decoded IHD
	isd_buf: block[512,byte],	! Contains ISD block
	hdr_blk: block[512,byte],	! contains block of image header
	blk_buf: block[512,byte],	! contains block read by get_image_block
	hdrver,				! Header version
	vbn: long initial(1),		! VBN of image found in hdr_blk
	offset,				! offset to the ISD's returned here
	chan : long initial( 0 ),	! Channel assigned to the image file
	image_buf_valid : long initial(0); ! indicates if the image buffer is valid


%sbttl 'ANL$OPEN_NEXT_IMAGE_FILE - Right'
!++
! Functional Description:
!	This routine is called to open the next image file we are to analyze.
!	It handles multiple file specs and wildcarding.  It does not handle
!	sharable image libraries.
!
! Formal Parameters:
!	opened_spec	Address of descriptor of buffer in which to return
!			the spec of the file we open.  We set the length.
!
! Implicit Inputs:
!	global data
!
! Implicit Outputs:
!	global data
!
! Returned Value:
!	True if there is another image file, false otherwise.
!
! Side Effects:
!
!--


global routine anl$open_next_image_file(opened_spec) = begin

bind
	opened_spec_dsc = .opened_spec: descriptor;

own
	own_described_buffer(wildcard_spec,nam$c_maxrss),
	wildcard_context: long initial(0),
	get_new_spec: long initial(true);

local
	stv: long,
	status: long;

!
! First clear flag to invalidate old image header
!

image_buf_valid = 0;

! If the wildcard context is zero (it means this is the first call), or
! have we finished with a file spec on the previous call, we must obtain
! the next file spec from the command line.

if .get_new_spec then (
	wildcard_spec[len] = nam$c_maxrss;
	status = cli$get_value(describe('file_specs'),wildcard_spec);

	! If there are no more specs, we are all done.

	if not .status then
		return false;
	str$trim(wildcard_spec,wildcard_spec,wildcard_spec);

	! Call a routine to process any positional qualifiers for this spec.
	! We don't know how to do that.

	anl$image_positionals();
);

! On the other hand, if the previous call done is true, we may have just
! finished processing a file.  Better deassign channel.

if .chan nequ 0 then (
	status = $dassgn(chan=.chan);
	check (.status, anlobj$_closein,1,resultant_spec,.status);
	chan = 0;
);

! We have obtained a wildcard spec from the file parameter.  We also have
! all the positional qualifiers associated with it.

! Now we need to find the next file that matches the current wildcard spec.

resultant_spec[len] = nam$c_maxrss;
status = lib$find_file(wildcard_spec,resultant_spec,
		wildcard_context,describe('.EXE'),
		0,stv,%ref(2));
str$trim(resultant_spec,resultant_spec,resultant_spec);

! If we failed to find a file, then reset the wildcard context and call
! ourselves recursively to process the next file spec.  Also give an
! error, unless we just plain ran out of files.

if not .status then (
	if .status nequ rms$_nmf then
		signal (anlobj$_openin,1,resultant_spec,.stv);
	get_new_spec = true;
	return anl$open_next_image_file(opened_spec_dsc);
);

! Hey, we got a file spec.  Open the file and connect the RAB.

get_new_spec = false;

image_fab[fab$b_fns] = .resultant_spec[len];
image_fab[fab$l_fna] = .resultant_spec[ptr];
status = $open(fab=image_fab);
check (.status, anlobj$_openin,1,resultant_spec,.status,.image_fab[fab$l_stv]);

! If the open failed, then we need to recurse to try the next file.

if not .status then
	return anl$open_next_image_file(opened_spec_dsc);

! Finally, we have to return the resultant file spec to the caller.

opened_spec_dsc[len] = .resultant_spec[len];
ch$move(.resultant_spec[len],.resultant_spec[ptr], .opened_spec_dsc[ptr]);

chan = .image_fab[fab$l_stv];

return true;

end;

%sbttl 'ANL$GET_IMAGE_BLOCK - Get Block from Image'
!++
! Functional Description:
!	This routine is called to read a block from the current image
!	file, which is assumed to be open.
!
! Formal Parameters:
!	vbn		Virtual block number of desired block.
!	buffer		Address of buffer pointer to fill in with the
!			address of our buffer.
!
! Implicit Inputs:
!	global data
!
! Implicit Outputs:
!	global data
!
! Returned Value:
!	$QIOW status
!
! Side Effects:
!
!--


global routine anl$get_image_block(vbn,buffer) = begin

bind
	buffer_ptr = .buffer: ref block[,byte];

local
	status: long,
	iosb : vector[ 4, word ] ;


! Read in the desired block to the static buffer.

status = $qiow(
		efn = 7,
		chan = .chan,
		func = io$_readvblk,	! Read a virtual block
		iosb = iosb,		! I/O Status block
		p1 = blk_buf,		! Buffer to read in to
		p2 = 512,		! number of bytes to read
		p3 = .vbn 		! Virtual block number to read
	      );

if not .status
then
	return (.status);

! Point the caller's pointer at our buffer.  Then return the $QIOW status

buffer_ptr = blk_buf;
return (.iosb[0]);

end;

%sbttl 'ANL$MAP_FIXUP_SECTION - Map Fixup Section into Memory'
!++
! Functional Description:
!	This routine is called to map a shareable image's fixup section
!	into memory so we can analyze it.  It is also called to free up
!	the mapping.  We read the section blocks, as opposed to actually
!	mapping the section, so that we can analyze remote images.
!
! Formal Parameters:
!	fixup_size	Number of blocks in fixup section.
!	fixup_vbn	Starting VBN of fixup section.
!
! Implicit Inputs:
!	global data
!
! Implicit Outputs:
!	global data
!
! Returned Value:
!	We return the address of the mapped memory, or zero if we couldn't
!	map it.
!
! Side Effects:
!
!--


global routine anl$map_fixup_section(fixup_size,fixup_vbn) = begin

own
	saved_size: long,
	saved_address: long;

local
	status: long,
	iosb : vector[ 4, word ] ;

builtin
	nullparameter;


! If we are called with both parameters, then we must map the fixup
! section into main memory and return its address.

if not nullparameter(1) and not nullparameter(2) then (

	saved_size = .fixup_size * 512;
	status = lib$get_vm(saved_size,saved_address);
	check (.status, .status);
	status = $qiow(
		efn = 7,
		chan = .chan,
		func = io$_readvblk,	! Read a virtual block
		iosb = iosb,		! I/O Status block
		p1 = .saved_address,	! Buffer to read in to
		p2 = .saved_size,	! number of bytes to read
		p3 = .fixup_vbn		! Virtual block number to read
	      );

	if not .iosb[0] or .iosb[1] nequ .saved_size then (

		! Oops, we couldn't read it in correctly.

		anl$map_fixup_section();
		return 0;
	) else
		return .saved_address;

) else (

	! We are to free up the mapped memory.

	status = lib$free_vm(saved_size,saved_address);
	check (.status, .status);
	return (ss$_normal);
);

end;

%sbttl 'ANL$GET_IMAGE_HEADER - get image header from opened image file'
!++
! Functional Description:
!	Get the image header.  Call the image decode routine in the image
!	activator to return the image header already decoded into an 
!	understantable form.
!
! Formal Parameters:
!	ihdp		Longword address to return buffer address of 
!			decoded image header.
!	alias		Longword address to return the last longword of
!			the image header.
!
! Implicit Inputs:
!	global data
!
! Implicit Outputs:
!	global data
!
! Returned Value:
!	none.
!
! Side Effects:
!
!--


global routine anl$get_image_header(ihdp,alias) = begin

bind
	buffer_ptr = .ihdp: ref block[,byte];

local
	status;

! Point the caller's pointer to our buffer.  If we have already read the
! image header in, it is still in our buffer.
!
buffer_ptr = image_buffer;

if .image_buf_valid
then
    return( ss$_normal)
else
    begin
    status = img$decode_ihd( .chan, 
			hdr_blk,
			image_buffer, 
			vbn, 
			offset,
			hdrver,
			.alias
			);
    if .status
    then
	image_buf_valid = 1;
    return (.status);
    end;

end;


%sbttl 'ANL$GET_ISD - Return the next ISD into buffer'
!++
! Functional Description:
!	Get the image section descriptor. Call the image decode routine 
!	in the image activator to return the image section descriptor.
!
! Formal Parameters:
!	isdp		Longword address to return buffer address of 
!			image section descriptor.
!
! Implicit Inputs:
!	global data
!
! Implicit Outputs:
!	global data
!
! Returned Value:
!	Status from img$get_next_isd - IMG$_ENDOFHDR at EOF
!
! Side Effects:
!
!--


global routine anl$get_isd(isdp) = begin


local
	status;

bind
	buffer_ptr = .isdp: ref block[,byte];

status = img$get_next_isd(
			.chan,
			hdr_blk,
			image_buffer,
			vbn,
			offset,
			isd_buf
			);			

buffer_ptr = isd_buf;
return( .status);

end;

end eludom
