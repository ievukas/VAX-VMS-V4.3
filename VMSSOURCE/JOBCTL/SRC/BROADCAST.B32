MODULE BROADCAST(%TITLE	'Broadcast message to user'
		IDENT = 'V04-000'
		) =
BEGIN

!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982, 1984 BY				    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************
!

!++
! FACILITY:
!	Job controller.
!
! ABSTRACT:
!	This module contains the routines that interface to broadcast.
!
! ENVIRONMENT:
!	VAX/VMS user and kernel mode.
!--
!
! AUTHOR: M. Jack, CREATION DATE: 16-Feb-1982
!
! MODIFIED BY:
!
!	V03-004	MLJ0115		Martin L. Jack,	30-Jul-1983  14:37
!		Add broadcast class.
!
!	V03-003	MLJ0114		Martin L. Jack,	23-Jun-1983  4:57
!		Changes for job controller baselevel.
!
!	V03-002	MLJ0113		Martin L. Jack,	26-May-1983  21:06
!		Changes for job controller baselevel.
!
!	V03-001	MLJ0109		Martin L. Jack,	14-Apr-1983  12:45
!		Changes for job controller baselevel.
!
!**

REQUIRE 'SRC$:JOBCTLDEF';


FORWARD ROUTINE
	BROADCAST_MESSAGE:		NOVALUE;


EXTERNAL ROUTINE
	CREATE_SRQ_RECORD;

GLOBAL ROUTINE BROADCAST_MESSAGE(SYSID,USERNAME,LENGTH,ADDRESS): NOVALUE=

!++
!
! FUNCTIONAL DESCRIPTION:
!	This routine issues a broadcast to all processes with a specified
!	username.
!
! INPUT PARAMETERS:
!	SYSID		- Pointer to system ID of receiver.
!	USERNAME	- Pointer to username of receiver.
!	LENGTH		- Descriptor for message.
!	ADDRESS		-
!
! IMPLICIT INPUTS:
!	NONE
!
! OUTPUT PARAMETERS:
!	NONE
!
! IMPLICIT OUTPUTS:
!	NONE
!
! ROUTINE VALUE:
!	NONE
!
! SIDE EFFECTS:
!	Messages broadcast to terminals.
!
!--

BEGIN
MAP
	SYSID:		REF BBLOCK,		! Pointer to system ID
	USERNAME:	REF BBLOCK;		! Pointer to username


! If this is a remote user, enter a remote request.
!
IF SYSID_NEQ(THIS_SYSID, .SYSID)
THEN
    BEGIN
    CREATE_SRQ_RECORD(
	SRQ$K_BROADCAST_MESSAGE,
	.SYSID, .USERNAME, .LENGTH, .ADDRESS);
    END
ELSE
    BEGIN
    LOCAL
	MSG_DESC:	VECTOR[2],	! Descriptor for message
	USER_DESC:	VECTOR[2];	! Descriptor for username


    ! Initialize descriptors and issue the $BRKTHRU.
    !
    MSG_DESC[0] = .LENGTH;
    MSG_DESC[1] = .ADDRESS;
    USER_DESC[0] = 12;
    USER_DESC[1] = .USERNAME;
    $BRKTHRU(
	MSGBUF=MSG_DESC,
	SENDTO=USER_DESC,
	SNDTYP=BRK$C_USERNAME,
	REQID=BRK$C_QUEUE,
	TIMOUT=10);
    END;
END;

END
ELUDOM
