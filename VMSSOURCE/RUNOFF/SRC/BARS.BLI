MODULE BARS (					!
		IDENT = 'V04-000'
%BLISS32[,
	ADDRESSING_MODE(EXTERNAL=LONG_RELATIVE,NONEXTERNAL=LONG_RELATIVE)
	]
		) =
BEGIN

!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982, 1984 BY				    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************
!

!++
! FACILITY:	 DSR (Digital Standard RUNOFF) / DSRPLUS
!
! ABSTRACT: Processes .BEGIN BAR, .END BAR, .ENABLE BAR and .DISABLE BAR commands
!
!
! ENVIRONMENT: Transportable
!
! AUTHOR: R.W.Friday	 CREATION DATE: September, 1978
!

%SBTTL 'Revision History'
!
! MODIFIED BY:
!
!	005	KFA00005	Ken Alden	27-Jun-1983
!		Restored this module to pre-dsrplus days so that disable
!		  bar does not restore the left margin.
!
!	004	KFA00004	Ken Alden	07-Mar-1983
!		Global edit of all modules. Updated module names, idents,
!		copyright dates. Changed require files to BLISS library.
!
!--

%SBTTL 'Module Level Declarations'

!
! TABLE OF CONTENTS:
!
!
! INCLUDE FILES:
!

LIBRARY 'NXPORT:XPORT';			! XPORT Library
REQUIRE 'REQ:RNODEF';			! RUNOFF variant definitions

%IF DSRPLUS %THEN
LIBRARY 'REQ:DPLLIB';			! DSRPLUS BLISS Library
%ELSE
LIBRARY 'REQ:DSRLIB';			! DSR BLISS Library
%FI

!
! MACROS:
!
!
! EQUATED SYMBOLS:
!
!
! OWN STORAGE:
!
!
! EXTERNAL REFERENCES:
!

EXTERNAL
    GCA : GCA_DEFINITION,
    IRAC : IRAC_DEFINITION,
%IF DSRPLUS %THEN
    IRA : FIXED_STRING,
    SCA : SCA_DEFINITION,
%FI
    TSF : TSF_DEFINITION;

%IF DSRPLUS %THEN
EXTERNAL ROUTINE
    GETQC,
    RSKIPS;
%FI


GLOBAL ROUTINE BARS (HANDLER_CODE) : NOVALUE = 	!

!++
! FUNCTIONAL DESCRIPTION:
!
!	See the ABSTRACT, above.
!
! FORMAL PARAMETERS:
!
!	HANDLER_CODE indicates which command is to be processed.
!
! IMPLICIT INPUTS:	None
!
! IMPLICIT OUTPUTS:	None
!
! ROUTINE VALUE:
! COMPLETION CODES:	None
!
! SIDE EFFECTS:	None
!
!--

    BEGIN

    SELECTONE .HANDLER_CODE OF
	SET

	[H_BEGIN_BAR] :
	    BEGIN
%IF DSRPLUS %THEN
	    LOCAL
		NEW_CHANGE_CHAR;
	    !Skip spaces and tabs until the (optional) quoted character.
	    RSKIPS (IRA);
	    !Get an optional character. If supplied, turn on bars only if the 
	    !character matches the current change-bar character.
	    NEW_CHANGE_CHAR = GETQC ();
	    IF 	.NEW_CHANGE_CHAR EQL -1			!User didn't supply new character.
		OR .NEW_CHANGE_CHAR EQL .SCA_BAR_CHAR	!Character supplied matches current change-bar.
	    THEN				
		BEGIN
%FI
		GCA_BARS_ON = TRUE;			!Indicate input text should be BARed if BARS are enabled.
		GCA_BARS = .GCA_BARS_ENAB;		!...
%IF DSRPLUS %THEN
		END;
%FI
	    END;

	[H_END_BAR] :
	    BEGIN
	    GCA_BARS_ON = FALSE;		!Turn off generation of bars.
	    GCA_BARS = FALSE;			!...
	    END;

	[H_ENABLE_BAR] :
	    BEGIN
%IF DSRPLUS %THEN
	    LOCAL
		NEW_CHANGE_CHAR;
	    !
	    !Skip spaces and tabs until the (optional) quoted character.
	    RSKIPS (IRA);
	    !Get an optional character. If supplied, replace the change-bar
	    !character with it.
	    NEW_CHANGE_CHAR = GETQC ();
	    IF 	.NEW_CHANGE_CHAR GTR 0		!No character supplied (or error)
	    THEN				!if GETQC() returns value LEQ 0.
		SCA_BAR_CHAR = .NEW_CHANGE_CHAR;
%FI
	    GCA_BARS_ENAB = .GCA_CMD_BAR;	!Enable bars unless forbidden on the command line.
	    GCA_BARS = .GCA_CMD_BAR AND .GCA_BARS_ON;
	    GCA_EVER_BARS = .GCA_BARS_ENAB;	!Change bars have been enabled in this document.
	    !
	    END;

	[H_DISABLE_BAR] :
	    BEGIN
	    GCA_BARS_ENAB = FALSE;		!Disable generation of change bars.
	    GCA_BARS = FALSE;			!...
	    END;
	TES;

    !Associate change-bar characteristics with the input file.
    IRAC_BARS = .GCA_BARS;

    END;					!End of BARS

END						!End of module
ELUDOM
