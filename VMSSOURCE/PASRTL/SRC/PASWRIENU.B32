MODULE PAS$WRITE_ENUMERATED ( %TITLE 'Write an enumerated value'
		IDENT = '1-002'			! File: PASWRIENU.B32 Edit: SBL1002
		) =
BEGIN
!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982, 1984 BY				    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************
!

!++
! FACILITY:	Pascal Language Support
!
! ABSTRACT:
!
!	This module contains a procedure which writes an enumerated value
!	to a textfile.
!
! ENVIRONMENT:	User mode - AST reentrant
!
! AUTHOR: Steven B. Lionel, CREATION DATE: 1-April-1981
!
! MODIFIED BY:
!
! 1-001	- Original.  SBL 1-April-1981
! 1-002 - Make total-width a longword.  SBL 29-Jun-1982
!--


%SBTTL 'Declarations'
!
! PROLOGUE DEFINITIONS:
!

REQUIRE 'RTLIN:PASPROLOG';			! Externals, linkages, PSECTs, structures

!
! TABLE OF CONTENTS:
!

FORWARD ROUTINE
    PAS$WRITE_ENUMERATED: NOVALUE,		! Write to textfile
    PAS$WRITEV_ENUMERATED: NOVALUE;		! Write to string

!
! MACROS:
!
!	NONE
!
! EQUATED SYMBOLS:
!
!	NONE
!
! FIELDS:
!
!	NONE
!
! OWN STORAGE:
!
!	NONE
!

%SBTTL 'PAS$WRITE_ENUMERATED - Write an enumerated value to textfile'
GLOBAL ROUTINE PAS$WRITE_ENUMERATED (	
	PFV: REF $PAS$PFV_FILE_VARIABLE, 		! File variable
	PETD: REF VECTOR [, LONG],			! Enumerated type descriptor
	VALUE,						! Value to write
	TOTAL_WIDTH: SIGNED,				! Total field width
	ERROR						! Error unwind address
    ): NOVALUE =

!++
! FUNCTIONAL DESCRIPTION:
!
!	This procedure writes an enumerated value to the specified textfile.
!
! CALLING SEQUENCE:
!
!	CALL PAS$WRITE_ENUMERATED (PFV.mr.r, PETD.rr.r, VALUE.rlu.v,
!				 TOTAL_WIDTH.rl.v [ERROR.j.r])
!
! FORMAL PARAMETERS:
!
!	PFV		- The Pascal File Variable (PFV) passed by reference.
!			  The structure of the PFV is defined in PASPFV.REQ.
!
!	PETD		- Pascal Enumerated Type Descriptor, passed by reference.
!			  The structure of a PETD is as follows:
!
!			  +-------------------------------+
!			  |   offset of ASCIC type name   |   <-- PETD
!			  +-------------------------------+
!			  | count of possible values (n)  |
!			  +-------------------------------+
!			  | offset of ASCIC value name 0  |
!			  +-------------------------------+
!			  | offset of ASCIC value name 1  |
!			  +- - - - - - - - - - - - - - - -+
!		             	  	...
!			  +- - - - - - - - - - - - - - - -+
!			  | offset of ASCIC value name n-1|
!			  +-------------------------------+
!
!			  The offsets are relative to the address
!			  of the descriptor (PETD).  The names are
!			  counted strings with 1-byte counts. It
!			  is assumed that the compiler has upcased
!			  all of the strings. 
!
!	VALUE		- The enumerated value to write.
!
!	TOTAL_WIDTH	- Total field width.
!
!	ERROR		- Optional.  Address to unwind to if an error occurs.
!
! IMPLICIT INPUTS:
!
!	NONE
!
! IMPLICIT OUTPUTS:
!
!	NONE
!
! ROUTINE VALUE:
!
!	NONE
!
! SIDE EFFECTS:
!
!	If the file is the standard file INPUT or OUTPUT, it is implicitly opened.
!
! SIGNALLED ERRORS:
!
!	LINTOOLON - line too long
!	WRIINVENU - WRITE of invalid enumerated value
!	NEGWIDDIG - negative Width or Digits specification is not allowed
!
!--

    BEGIN

    LOCAL
	FCB: REF $PAS$FCB_CONTROL_BLOCK,		! File control block
	FIELD_WIDTH,					! Total width
	VALUE_STRING: REF VECTOR [, BYTE],		! Enumerated value string
	VALUE_WIDTH,					! Width of enumerated value
	PFV_ADDR: VOLATILE,				! Enable argument
	UNWIND_ACT: VOLATILE,				! Enable argument
	ERROR_ADDR: VOLATILE;				! Enable argument

    BUILTIN
	ACTUALCOUNT;

    ENABLE
	PAS$$IO_HANDLER (PFV_ADDR, UNWIND_ACT, ERROR_ADDR);	! Enable error handler

    !+
    ! Get ERROR parameter, if present.
    !-

    IF ACTUALCOUNT () GEQU 5
    THEN
	ERROR_ADDR = .ERROR;		! Set unwind address

    PFV_ADDR = PFV [PFV$R_PFV];			! Set PFV address

    !+
    ! Validate PFV and get PFV.
    !-

    PAS$$VALIDATE_PFV (PFV [PFV$R_PFV]; FCB);

    !+
    ! Set unwind action to unlock file.
    !-

    UNWIND_ACT = PAS$K_UNWIND_UNLOCK;

    !+
    ! Do common initialization.
    !-

    PAS$$INIT_WRITE (PFV [PFV$R_PFV], FCB [FCB$R_FCB]; FCB);

    !+
    ! Check for invalid width.
    !-

    IF .TOTAL_WIDTH LSS 0
    THEN
	$PAS$IO_ERROR (PAS$_NEGWIDDIG,0);

    !+
    ! Get field width
    !-

    FIELD_WIDTH = .TOTAL_WIDTH;
    
    !+
    ! Determine if value is out-of range and then locate value.
    !-

    IF .VALUE GEQU .PETD [1]
    THEN
	$PAS$IO_ERROR (PAS$_WRIINVENU,0);
    VALUE_STRING = .PETD + .PETD [.VALUE+2];	! Relative address
    VALUE_WIDTH = MIN (.VALUE_STRING [0], .FIELD_WIDTH);

    !+
    ! See if field will fit in record.
    !-

    BEGIN
    LOCAL
	EXTRA;			! Extra characters past end of line
    EXTRA = (.FCB [FCB$A_RECORD_CUR] + .FIELD_WIDTH) - .FCB [FCB$A_RECORD_END];
    IF .EXTRA GTR 0
    THEN
	$PAS$IO_ERROR (PAS$_LINTOOLON,1,.EXTRA);
    END;

    !+
    ! Move leading blanks, if any.
    !-

    IF .FIELD_WIDTH - .VALUE_WIDTH GTR 0
    THEN
	FCB [FCB$A_RECORD_CUR] = CH$FILL (%C' ', .FIELD_WIDTH - .VALUE_WIDTH, 
	.FCB [FCB$A_RECORD_CUR]);
    
    !+
    ! Now move value
    !-

    FCB [FCB$A_RECORD_CUR] = CH$MOVE (.VALUE_WIDTH, VALUE_STRING [1],
	.FCB [FCB$A_RECORD_CUR]);

    !+
    ! Call WRITE epilogue routine to move the last character written to the
    ! user's buffer and to unlock the file variable.
    !-

    PAS$$END_WRITE (PFV [PFV$R_PFV], FCB [FCB$R_FCB]);

    RETURN;

    END;					! End of routine PAS$WRITE_ENUMERATED

!<BLF/PAGE>

%SBTTL 'PAS$WRITEV_ENUMERATED - Write ENUMERATED to string'
GLOBAL ROUTINE PAS$WRITEV_ENUMERATED (
	MAX_LENGTH: WORD,				! Maximum length of string
	STRING_LINE: REF VECTOR [, WORD],		! String to write to
	PETD: REF VECTOR [, LONG],			! Enumerated type descriptor
	VALUE,						! Value to write
	TOTAL_WIDTH: SIGNED,				! Total field width
	ERROR						! Error unwind address
    ) : NOVALUE =

!++
! FUNCTIONAL DESCRIPTION:
!
!	This procedure writes an enumerated value to the specified string.
!
! CALLING SEQUENCE:
!
!	CALL PAS$WRITEV_ENUMERATED (MAX_LENGTH.rw.v, STRING_LINE.wvt.r,
!	   VALUE.rlu.v, TOTAL_WIDTH.rl.v [, ERROR.j.r])
!
! FORMAL PARAMETERS:
!
!	MAX_LENGTH	- The maximum length of STRING_LINE.
!
!	STRING_LINE	- A varying string to which the output will be appended.
!
!	VALUE		- The value to write.
!
!	TOTAL_WIDTH	- The width of the field to write.
!
!	ERROR		- Optional.  If specified, the address to unwind to
!			  in case of an error.
!
! IMPLICIT INPUTS:
!
!	NONE
!
! IMPLICIT OUTPUTS:
!
!	NONE
!
! ROUTINE VALUE:
!
!	NONE
!
! SIDE EFFECTS:
!
!	NONE
!
! SIGNALLED ERRORS:
!
!	See PAS$WRITE_ENUMERATED
!
!--

    BEGIN

    LOCAL
	PFV: $PAS$PFV_FILE_VARIABLE,	! Pascal File Variable
	ARG_LIST: VECTOR [5, LONG],	! Argument list
	PFV_ADDR: VOLATILE,		! Enable argument
	UNWIND_ACT: VOLATILE,		! Enable argument
	ERROR_ADDR: VOLATILE;		! Enable argument

    BUILTIN
	ACTUALCOUNT;			! Count of arguments

    ENABLE
	PAS$$IO_HANDLER (PFV_ADDR, UNWIND_ACT, ERROR_ADDR);	! Enable error handler

    !+
    ! Get ERROR parameter, if present.
    !-

    IF ACTUALCOUNT () GEQU 6
    THEN
	ERROR_ADDR = .ERROR;		! Set unwind address

    PFV_ADDR = PFV [PFV$R_PFV];		! Set PFV address

    !+
    ! Set up ARG_LIST.
    !-

    ARG_LIST [0] = 4;			! Four arguments
    ARG_LIST [1] = PFV [PFV$R_PFV];	! PFV address
    ARG_LIST [2] = .PETD;		! Enumerated type descriptor
    ARG_LIST [3] = .VALUE;		! Value to write
    ARG_LIST [4] = .TOTAL_WIDTH;	! Field width

    !+
    ! Call PAS$$DO_WRITEV to do the work, giving it the address of
    ! PAS$WRITE_ENUMERATED to call.
    !-

    PAS$$DO_WRITEV (PFV [PFV$R_PFV], .MAX_LENGTH, STRING_LINE [0], ARG_LIST, 
	PAS$WRITE_ENUMERATED);

    RETURN;

    END;					! End of routine PAS$WRITEV_ENUMERATED

!<BLF/PAGE>

END						! End of module PAS$WRITE_ENUMERATED

ELUDOM

