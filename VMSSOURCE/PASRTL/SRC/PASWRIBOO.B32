MODULE PAS$WRITE_BOOLEAN ( %TITLE 'Write a boolean value'
		IDENT = '1-002'			! File: PASWRIBOO.B32 Edit: SBL1002
		) =
BEGIN
!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982, 1984 BY				    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************
!

!++
! FACILITY:	Pascal Language Support
!
! ABSTRACT:
!
!	This module contains a procedure which writes a boolean value
!	to a textfile.
!
! ENVIRONMENT:	User mode - AST reentrant
!
! AUTHOR: Steven B. Lionel, CREATION DATE: 1-April-1981
!
! MODIFIED BY:
!
! 1-001	- Original.  SBL 1-April-1981
! 1-002 - Make total-width a longword.  SBL 29-Jun-1982
!--


%SBTTL 'Declarations'
!
! PROLOGUE DEFINITIONS:
!

REQUIRE 'RTLIN:PASPROLOG';			! Externals, linkages, PSECTs, structures

!
! TABLE OF CONTENTS:
!

FORWARD ROUTINE
    PAS$WRITE_BOOLEAN: NOVALUE,			! Write to textfile
    PAS$WRITEV_BOOLEAN: NOVALUE;		! Write to string

!
! MACROS:
!
!	NONE
!
! EQUATED SYMBOLS:
!
!	NONE
!
! FIELDS:
!
!	NONE
!
! OWN STORAGE:
!
!	NONE
!

%SBTTL 'PAS$WRITE_BOOLEAN - Write a boolean to textfile'
GLOBAL ROUTINE PAS$WRITE_BOOLEAN (
	PFV: REF $PAS$PFV_FILE_VARIABLE, 		! File variable
	BOOLEAN: BITVECTOR [1],				! Value to write
	TOTAL_WIDTH: SIGNED,				! Total field width
	ERROR						! Error unwind address
    ): NOVALUE =

!++
! FUNCTIONAL DESCRIPTION:
!
!	This procedure writes a boolean value to the specified textfile.
!
! CALLING SEQUENCE:
!
!	CALL PAS$WRITE_BOOLEAN (PFV.mr.r, BOOLEAN.rv.v, TOTAL_WIDTH.rl.v
!				[ERROR.j.r])
!
! FORMAL PARAMETERS:
!
!	PFV		- The Pascal File Variable (PFV) passed by reference.
!			  The structure of the PFV is defined in PASPFV.REQ.
!
!	BOOLEAN		- The boolean value to write.
!
!	TOTAL_WIDTH	- Total field width.
!
!	ERROR		- Optional.  Address to unwind to if an error occurs.
!
! IMPLICIT INPUTS:
!
!	NONE
!
! IMPLICIT OUTPUTS:
!
!	NONE
!
! ROUTINE VALUE:
!
!	NONE
!
! SIDE EFFECTS:
!
!	If the file is the standard file INPUT or OUTPUT, it is implicitly opened.
!
! SIGNALLED ERRORS:
!
!	See PAS$WRITE_ENUMERATED
!
!--

    BEGIN

    LOCAL
	PFV_ADDR: VOLATILE,				! Enable argument
	UNWIND_ACT: VOLATILE,				! Enable argument
	ERROR_ADDR: VOLATILE;				! Enable argument

    BUILTIN
	ACTUALCOUNT;

    ENABLE
	PAS$$IO_HANDLER (PFV_ADDR, UNWIND_ACT, ERROR_ADDR);	! Enable error handler

    !+
    ! Get ERROR parameter, if present.
    !-

    IF ACTUALCOUNT () GEQU 4
    THEN
	ERROR_ADDR = .ERROR;		! Set unwind address

    PFV_ADDR = PFV [PFV$R_PFV];			! Set PFV address

    !+
    ! Call PAS$WRITE_ENUMERATED to do the work.
    !-

    PAS$WRITE_ENUMERATED (
	PFV [PFV$R_PFV],	! File variable
	PAS$$GR_BOOLEAN_PETD,	! Enumerated type descriptor for type BOOLEAN
	.BOOLEAN [0],		! Value to write
	.TOTAL_WIDTH);		! Field width

    RETURN;

    END;					! End of routine PAS$WRITE_BOOLEAN

!<BLF/PAGE>

%SBTTL 'PAS$WRITEV_BOOLEAN - Write BOOLEAN to string'
GLOBAL ROUTINE PAS$WRITEV_BOOLEAN (
	MAX_LENGTH: WORD,				! Maximum length of string
	STRING_LINE: REF VECTOR [, WORD],		! String to write to
	BOOLEAN,					! Value to write
	TOTAL_WIDTH: SIGNED,				! Total field width
	ERROR						! Error unwind address
    ) : NOVALUE =

!++
! FUNCTIONAL DESCRIPTION:
!
!	This procedure writes a BOOLEAN to the specified string.
!
! CALLING SEQUENCE:
!
!	CALL PAS$WRITEV_BOOLEAN (MAX_LENGTH.rw.v, STRING_LINE.wvt.r,
!	   BOOLEAN.rv.v, TOTAL_WIDTH.rl.v [, ERROR.j.r])
!
! FORMAL PARAMETERS:
!
!	MAX_LENGTH	- The maximum length of STRING_LINE.
!
!	STRING_LINE	- A varying string to which the output will be appended.
!
!	BOOLEAN		- The value to write.
!
!	TOTAL_WIDTH	- The width of the field to write.
!
!	ERROR		- Optional.  If specified, the address to unwind to
!			  in case of an error.
!
! IMPLICIT INPUTS:
!
!	NONE
!
! IMPLICIT OUTPUTS:
!
!	NONE
!
! ROUTINE VALUE:
!
!	NONE
!
! SIDE EFFECTS:
!
!	NONE
!
! SIGNALLED ERRORS:
!
!	See PAS$WRITE_BOOLEAN
!
!--

    BEGIN

    LOCAL
	PFV: $PAS$PFV_FILE_VARIABLE,	! Pascal File Variable
	ARG_LIST: VECTOR [4, LONG],	! Argument list
	PFV_ADDR: VOLATILE,		! Enable argument
	UNWIND_ACT: VOLATILE,		! Enable argument
	ERROR_ADDR: VOLATILE;		! Enable argument

    BUILTIN
	ACTUALCOUNT;			! Count of arguments

    ENABLE
	PAS$$IO_HANDLER (PFV_ADDR, UNWIND_ACT, ERROR_ADDR);	! Enable error handler

    !+
    ! Get ERROR parameter, if present.
    !-

    IF ACTUALCOUNT () GEQU 5
    THEN
	ERROR_ADDR = .ERROR;		! Set unwind address

    PFV_ADDR = PFV [PFV$R_PFV];		! Set PFV address

    !+
    ! Set up ARG_LIST.
    !-

    ARG_LIST [0] = 3;			! Three arguments
    ARG_LIST [1] = PFV [PFV$R_PFV];	! PFV address
    ARG_LIST [2] = .BOOLEAN;		! Value to write
    ARG_LIST [3] = .TOTAL_WIDTH;	! Field width

    !+
    ! Call PAS$$DO_WRITEV to do the work, giving it the address of
    ! PAS$WRITE_BOOLEAN to call.
    !-

    PAS$$DO_WRITEV (PFV [PFV$R_PFV], .MAX_LENGTH, STRING_LINE [0], ARG_LIST, 
	PAS$WRITE_BOOLEAN);

    RETURN;

    END;					! End of routine PAS$WRITEV_BOOLEAN

!<BLF/PAGE>

END						! End of module PAS$WRITE_BOOLEAN

ELUDOM

