MODULE PAS$READ_VARYING ( %TITLE 'Read a varying string'
		IDENT = '1-002'			! File: PASREAVAR.B32 Edit: SBL1002
		) =
BEGIN
!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982, 1984 BY				    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************
!

!++
! FACILITY:	Pascal Language Support
!
! ABSTRACT:
!
!	This module contains procedures which read a varying string
!	from a textfile or a string.
!
! ENVIRONMENT:	User mode - AST reentrant
!
! AUTHOR: Steven B. Lionel, CREATION DATE: 1-April-1981
!
! MODIFIED BY:
!
! 1-001	- Original.  SBL 1-April-1981
! 1-002 - Use PAS$$END_READ.  SBL 26-May-1982
!--


%SBTTL 'Declarations'
!
! PROLOGUE DEFINITIONS:
!

REQUIRE 'RTLIN:PASPROLOG';			! Externals, linkages, PSECTs, structures

!
! TABLE OF CONTENTS:
!

FORWARD ROUTINE
    PAS$READ_VARYING: NOVALUE,			! Read from textfile
    PAS$READV_VARYING: NOVALUE;			! Read from string

!
! MACROS:
!
!	NONE
!
! EQUATED SYMBOLS:
!
!	NONE
!
! FIELDS:
!
!	NONE
!
! OWN STORAGE:
!
!	NONE
!

%SBTTL 'PAS$READ_VARYING - Read a varying string from textfile'
GLOBAL ROUTINE PAS$READ_VARYING (
	STRING: REF VECTOR [, WORD],			! String to read into
	PFV: REF $PAS$PFV_FILE_VARIABLE, 		! File variable
	MAX_LENGTH: WORD,				! Maximum string length
	ERROR						! Error unwind address
    ) : NOVALUE =

!++
! FUNCTIONAL DESCRIPTION:
!
!	This function READs a varying string from the specified textfile.
!	Up to MAX_LENGTH characters, or until end-of-line, whichever comes
!	first, will be read.  The number of characters read is stored in
!	the first word of the string.
!
! CALLING SEQUENCE:
!
!	STRING.wvt.v = PAS$READ_VARYING (PFV.mr.r, MAX_LENGTH.rwu.v
!			      [, ERROR.ja.r])
!
! FORMAL PARAMETERS:
!
!	PFV		- The Pascal File Variable (PFV) passed by reference.
!			  The structure of the PFV is defined in PASPFV.REQ.
!
!	MAX_LENGTH	- The maximum length of the string.
!
!	ERROR		- Optional.  If specified, the address to unwind to
!			  in case of an error.
!
! IMPLICIT INPUTS:
!
!	NONE
!
! IMPLICIT OUTPUTS:
!
!	NONE
!
! ROUTINE VALUE:
!
!	STRING		- The varying string read returned as a function value.
!			  In accordance with the VAX Procedure Calling Standard,
!			  the address of the string is passed to this procedure
!			  as the first parameter, and all other parameters are
!			  shifted left one position.
!
!			  If an error occurs and is continued by a user handler,
!			  the value returned is a zero-length string.
!
! SIDE EFFECTS:
!
!	If the file is the standard file INPUT or OUTPUT, it is implicitly opened.
!
! SIGNALLED ERRORS:
!
!	NONE
!
!--

    BEGIN

    LOCAL
	CHARS_REMAINING,		! Number of characters remaining in line
	FCB: REF $PAS$FCB_CONTROL_BLOCK,! File Control block
	PFV_ADDR: VOLATILE,		! Enable argument
	UNWIND_ACT: VOLATILE,		! Enable argument
	ERROR_ADDR: VOLATILE;		! Enable argument

    BUILTIN
	ACTUALCOUNT;			! Count of arguments

    ENABLE
	PAS$$IO_HANDLER (PFV_ADDR, UNWIND_ACT, ERROR_ADDR);	! Enable error handler

    !+
    ! Get ERROR parameter, if present.
    !-

    IF ACTUALCOUNT () GEQU 4
    THEN
	ERROR_ADDR = .ERROR;		! Set unwind address

    PFV_ADDR = PFV [PFV$R_PFV];		! Set PFV address

    !+
    ! Validate PFV and get PFV.
    !-

    PAS$$VALIDATE_PFV (PFV [PFV$R_PFV]; FCB);

    !+
    ! Set unwind action to unlock file.
    !-

    UNWIND_ACT = PAS$K_UNWIND_UNLOCK;

    !+
    ! Do common initialization.
    !-

    PAS$$INIT_READ (PFV [PFV$R_PFV], FCB [FCB$R_FCB]; FCB);

    !+
    ! Determine number of characters remaining in line.
    !-

    CHARS_REMAINING = .FCB [FCB$A_RECORD_END] - .FCB [FCB$A_RECORD_CUR];

    !+
    ! Select smaller of MAX_LENGTH and CHARS_REMAINING for number of
    ! characters to move.
    !-

    CHARS_REMAINING = MINU (.CHARS_REMAINING, .MAX_LENGTH);

    !+
    ! Move characters to user variable and update record pointer.
    !-

    CH$MOVE (.CHARS_REMAINING, .FCB [FCB$A_RECORD_CUR], STRING [1]);
    FCB [FCB$A_RECORD_CUR] = .FCB [FCB$A_RECORD_CUR] + .CHARS_REMAINING;

    !+
    ! Set current string length.
    !-

    STRING [0] = .CHARS_REMAINING;

    !+
    ! Do end-of-READ processing.
    !-

    PAS$$END_READ (PFV [PFV$R_PFV], FCB [FCB$R_FCB]);

    RETURN;

    END;					! End of routine PAS$READ_VARYING

!<BLF/PAGE>

%SBTTL 'PAS$READV_VARYING- Read varying from string'
GLOBAL ROUTINE PAS$READV_VARYING (
	STRING: REF VECTOR [, BYTE],			! String to read into
	LINE_DSC: REF VECTOR [, BYTE],			! Line to read from
	MAX_LENGTH: WORD,				! Maximum length of string
	ERROR						! Error unwind address
    ) : NOVALUE =

!++
! FUNCTIONAL DESCRIPTION:
!
!	This procedure reads a varying-length string from the specified string line.
!	Up to MAX_LENGTH characters, or until end-of-line, whichever comes
!	first, will be read.  The number of characters read is stored in
!	the first word of the string.
!
! CALLING SEQUENCE:
!
!	STRING.wvt.r = PAS$READV_VARYING (LINE_DSC.mq.r, MAX_LENGTH.rwu.v
!			      [, ERROR.ja.r])
!
! FORMAL PARAMETERS:
!
!	LINE_DSC	- The string to read from, passed as a class S
!			  (assumed) descriptor.  The length and pointer
!			  are updated to reflect the unread string.
!
!	MAX_LENGTH	- The maximum length of the string to read.
!
!	ERROR		- Optional.  If specified, the address to unwind to
!			  in case of an error.
!
! IMPLICIT INPUTS:
!
!	NONE
!
! IMPLICIT OUTPUTS:
!
!	NONE
!
! ROUTINE VALUE:
!
!	The value of the string read.
!
! SIDE EFFECTS:
!
!	NONE
!
! SIGNALLED ERRORS:
!
!	See PAS$READ_VARYING
!
!--

    BEGIN

    LOCAL
	PFV: $PAS$PFV_FILE_VARIABLE,	! Pascal File Variable
	RESULT,				! Result value
	ARG_LIST: VECTOR [4, LONG],	! Argument list
	PFV_ADDR: VOLATILE,		! Enable argument
	UNWIND_ACT: VOLATILE,		! Enable argument
	ERROR_ADDR: VOLATILE;		! Enable argument

    BUILTIN
	ACTUALCOUNT;			! Count of arguments

    ENABLE
	PAS$$IO_HANDLER (PFV_ADDR, UNWIND_ACT, ERROR_ADDR);	! Enable error handler

    !+
    ! Get ERROR parameter, if present.
    !-

    IF ACTUALCOUNT () GEQU 3
    THEN
	ERROR_ADDR = .ERROR;		! Set unwind address

    PFV_ADDR = PFV [PFV$R_PFV];		! Set PFV address

    !+
    ! Set up ARG_LIST.
    !-

    ARG_LIST [0] = 3;			! Three arguments
    ARG_LIST [1] = STRING [0];		! Destination string address
    ARG_LIST [2] = PFV [PFV$R_PFV];	! Address of PFV
    ARG_LIST [3] = .MAX_LENGTH;		! Maximum string length

    !+
    ! Call PAS$$DO_READV to do the work, giving it the address of
    ! PAS$READ_VARYING to call.
    !-

    PAS$$DO_READV (PFV [PFV$R_PFV], LINE_DSC [0], ARG_LIST, PAS$READ_VARYING);

    RETURN;

    END;					! End of routine PAS$READV_VARYING

!<BLF/PAGE>

END						! End of module PAS$READ_VARYING

ELUDOM
