MODULE getqs (	IDENT = 'V04-000'
		%BLISS32[, ADDRESSING_MODE (EXTERNAL    = LONG_RELATIVE,
					    NONEXTERNAL = LONG_RELATIVE)]
		) =
BEGIN
!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982, 1984 BY				    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************
!
!++
! FACILITY:	DSR (Digital Standard RUNOFF) / DSRPLUS
!
! ABSTRACT:	Isolates a string delimited by '' or "".
!
! ENVIRONMENT:	Transportable
!
! AUTHOR:	R.W.Friday	 CREATION DATE: May, 1978
!


%SBTTL 'Revision History'
!
! MODIFIED BY:
!
!	003	RER00003	Ron Randall	07-Mar-1983
!		Global edit of all modules. Updated module names, idents,
!		copyright dates. Changed require files to BLISS library.
!
!--


%SBTTL 'Module Level Declarations'

!
! TABLE OF CONTENTS:
!
! INCLUDE FILES:
!
LIBRARY 'NXPORT:XPORT';			! XPORT Library
REQUIRE 'REQ:RNODEF';			! RUNOFF variant definitions

%IF DSRPLUS %THEN
LIBRARY 'REQ:DPLLIB';			! DSRPLUS BLISS Library
%ELSE
LIBRARY 'REQ:DSRLIB';			! DSR BLISS Library
%FI

!
! EXTERNAL REFERENCES:
!
EXTERNAL LITERAL
    RINTES : UNSIGNED (8);

EXTERNAL
    IRA : FIXED_STRING,
    KHAR;


GLOBAL ROUTINE GETQS (OUTPUT) =

!++
! FUNCTIONAL DESCRIPTION:
!
!	GETQS expects the next input character to be either a '"'
!	or a "'".  It collects all characters up through the
!	matching '"' or "'" and returns them in OUTPUT.
!
! FORMAL PARAMETERS:
!
!	The results are returned in OUTPUT.
!
! IMPLICIT INPUTS:	None
!
! IMPLICIT OUTPUTS:	None
!
! ROUTINE VALUE:
! COMPLETION CODES:
!
!	See GETQSC.REQ for a list of completion codes.
!
! SIDE EFFECTS:		None
!--

    BEGIN
    LOCAL
	TERMINATOR;

    MAP
	OUTPUT : REF FIXED_STRING;

    IF  .KHAR EQL RINTES
    THEN 					!Nothing on line.
	RETURN GETQS_NONE;

    TERMINATOR = (SELECTONE .KHAR OF
	SET
	[%C'''', %C'"'] : .KHAR;
	[OTHERWISE] : RETURN GETQS_NONE;
	TES);

    FS_INIT (OUTPUT);
    KCNS ();					!Skip over the initial delimiter.

    WHILE .KHAR NEQ RINTES
	AND .KHAR NEQ .TERMINATOR DO
	BEGIN

	IF  .FS_LENGTH (OUTPUT) EQL .FS_MAXSIZE (OUTPUT)
	THEN
	    BEGIN				!Skip extra characters.

	    WHILE .KHAR NEQ RINTES
		AND (.KHAR NEQ .TERMINATOR) DO
		KCNS ();

	    IF  .KHAR EQL .TERMINATOR
	    THEN
		KCNS ();

	    RETURN GETQS_TOO_LONG;
	    END;

	FS_WCHAR (OUTPUT, .KHAR);
	KCNS ();				!Get next character.
	END;

    IF  .KHAR NEQ RINTES
    THEN
	KCNS ()					!Skip delimiter, unless it's end of record.
    ELSE
	RETURN GETQS_BAD;			!Trailing terminator missing.

    !Reset pointer to start of result, so user can do a simple
    !series of fs_rchar's to process it.
    FS_NEXT (OUTPUT) = .FS_START (OUTPUT);
    RETURN GETQS_NORMAL
    END;					!End of GETQS

END						!End of module
ELUDOM
