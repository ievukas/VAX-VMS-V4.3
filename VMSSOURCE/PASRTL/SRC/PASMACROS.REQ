! Macros for Pascal Run-Time Library
! File: PASMACROS.REQ, Edit: SBL1003
!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982, 1984 BY				    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************
!
! Author: Steven B. Lionel, 1-April-1981
!
! 1-001 - Original.  SBL 1-April-1981
! 1-002 - Change $PAS$RMS_OP so that it doesn't loop on RMS$_ACT unless the
!	  file is on a disk device.  SBL 29-July-1982
! 1-003 - Make that a disk or tape device.  SBL 29-July-1982
!--

!+
! Macro to signal a Pascal bugcheck.
!-

MACRO
    $PAS$BUGCHECK (bugname) =
	BEGIN
	PAS$$BUGCHECK (bugname);
	RETURN 0;
	END %;

!+
! Macro to signal an I/O error.  The source code specifies the first
! message as a PAS$_xxx symbol.  This macro changes it to a PAS$K_xxx
! symbol to result in shorter code.  If it is desired to change back,
! only this macro need be modified.
!-

MACRO

    !+
    ! Internal macro MOD_SIG_NAME takes as its argument a sequence
    ! of characters which is the exploded PAS$_xxx signal name and returns
    ! the equivalent name PAS$K_xxx.  It is only used by $PAS$IO_ERROR.
    !
    MOD_SIG_NAME (cP,cA,cS,c$,c_) =
	%NAME ('PAS$K_',%REMAINING) %,

    $PAS$IO_ERROR (signame) =
	BEGIN
	PAS$$SIGNAL (PFV [PFV$R_PFV], MOD_SIG_NAME(%EXPLODE(signame))
		     %IF %NULL(%REMAINING)
		     %THEN
		     %ELSE
			,%REMAINING
		     %FI
		     );
	RETURN 0;
	END %;

!+
! Macro to do an RMS operation with retry for RMS$_ACT if a disk or tape device.
!-

MACRO
    $PAS$RMS_OP (rms_op) =
	BEGIN
	LOCAL
	    $$STATUS;
	DO $$STATUS = (rms_op) UNTIL (.$$STATUS OR (.$$STATUS NEQ RMS$_ACT)
	    OR NOT .FCB [FCB$V_FOD]);
	.$$STATUS
	END %;

!+
! Structure definitions used to declare the FAB and NAM as being offset from
! FCB.  To use, make the following declarations:
!
!	BIND
!	    FAB = FCB: REF $PAS$FAB_FCB_STRUCT,
!	    NAM = FCB: REF $PAS$NAM_FCB_STRUCT;
!
!-

STRUCTURE
    $PAS$FAB_FCB_STRUCT [O, P, S, E] =
	[FAB$C_BLN]
	($PAS$FAB_FCB_STRUCT+RAB$C_BLN+O)<P,S,E>,

    $PAS$NAM_FCB_STRUCT [O, P, S, E] =
	[NAM$C_BLN]
	($PAS$NAM_FCB_STRUCT+RAB$C_BLN+FAB$C_BLN+O)<P,S,E>;

! End of file PASMACROS.REQ
