	.TITLE	MAIL$HANDLER	- CONDITION HANDLER
	.IDENT	'V04-000'
;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982, 1984 BY				    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;

;++
; FACILITY:	VAX/VMS MAIL UTILITY
;
; ABSTRACT:	CONDITION HANDLER
;
;
; ENVIRONMENT:	NATIVE/USER MODE 
;
; AUTHOR:	LEN KAWELL, CREATION DATE: 2-FEB-79
;
; MODIFICATION HISTORY:
;
;	V03-003	BLS0288		Benn Schreiber		21-MAR-1984
;		Drop privs before putmsg if they are raised.
;
;	V03-002	BLS0255		Benn Schreiber		28-Dec-1983
;		Check inhib_msg before signalling, set it on the way out.
;		If signal is accvio, resignal to get full argument dump.
;
;	V03-001	BLS0177		Benn Schreiber		24-Jun-1982
;		Remove global invocation of $SHRMSG.  Remove invocation of
;		$MAILMSGDEF
;
;--

	.SBTTL	DECLARATIONS
;
; INCLUDE FILES:
;

;
; EQUATED SYMBOLS:
;
	MAIFDEF				;MAIL CONTROL FLAGS
	$CHFDEF				;CONDITION HANDLER DEFINITIONS
	$STSDEF				;STATUS CODE DEFINTIONS

;
; LOCAL MACROS:
;

;
; OWN STORAGE:
;

	.PSECT	$CODE$,NOWRT,EXE


	.SBTTL	CONDITION HANDLER
;++
; FUNCTIONAL DESCRIPTION:
;
;	OUTPUTS ERROR MESSAGE USING SYS$PUTMSG
;
; CALLING SEQUENCE:
;	CALLED BY AN EXCEPTION OR LIB$SIGNAL
;
; INPUT PARAMETERS:
;	STANDARD CONDITION HANDLER ARGUMENTS
;
; IMPLICIT INPUTS:
;	NONE
;
; OUTPUT PARAMETERS:
;	NONE
;
; IMPLICIT OUTPUTS:
;	NONE
;
; COMPLETION CODES:
;	NONE
;
; SIDE EFFECTS:
;	ERROR MESSAGE OUTPUT
;
;--
	.ENTRY	MAIL$HANDLE,-		;CONDITION HANDLER
		^M<R2,R3,R4,R5>		;(ENTRY MASK)
	EXTZV	#MAIF_V_NETJOB,#1,-	;GET NETJOB FLAG
		MAIL$GL_FLAGS,R5
	MOVL	CHF$L_SIGARGLST(AP),R2	;GET ADDR OF CONDITION ARRAY
;
; IF NETWORK JOB, SEND SIGNALLED STATUS VALUE TO OUR MASTER
;
	BLBC	R5,40$	 		;BR IF NOT NETWORK JOB
	PUSHAL	CHF$L_SIG_NAME(R2)	;CREATE STATUS VALUE DESC
	PUSHL	#4			;
	PUSHAL	(SP)			;SEND THE STATUS VALUE
	CALLS	#1,MAIL$PUT_OUTPUT	;TO MASTER MAIL JOB
	BLBS	R0,10$			;BR IF SUCCESS
	BRW	EXIT			;ELSE - EXIT WITH FAILURE STATUS
10$:
	BLBC	CHF$L_SIG_NAME(R2),PUT_MESSAGE ;IF ERROR, OUTPUT ERROR MESSAGE
	BRW	CONTINUE		;ELSE - JUST CONTINUE
40$:	CMPL	CHF$L_SIG_NAME(R2),#SS$_ACCVIO ;INTERACTIVE ACCVIO?
	BNEQ	PUT_MESSAGE		;IF NEQ NO, GO SIGNAL
	BRW	RESIGNAL		;YES, GET FULL DUMP
;
; OUTPUT THE ERROR MESSAGE
;
PUT_MESSAGE:				;
	BBS	#STS$V_INHIB_MSG,CHF$L_SIG_NAME(R2),CONTINUE
	SUBW	#2,CHF$L_SIG_ARGS(R2)	;DON'T PASS PC & PSL
	MOVQ	MAIL$Q_ENHANCED_PRIVS,R3 ;SEE IF PRIVS RAISED
	BEQL	10$			;BRANCH IF NOT
	CALLS	#0,MAIL$DOWN_PRIV	;YES, DROP THEM
10$:	$PUTMSG_S MSGVEC=(R2),-		;OUTPUT THE MESSAGE(S)
		  ACTRTN=ACTION_ROUTINE	;
	PUSHL	R0			;SAVE STATUS
	BISL3	R3,R4,R0		;SEE IF PRIVS WERE RAISED
	BEQL	20$			;IF EQL NO
	MOVQ	R3,-(SP)		;YES--RESTORE THEM
	CALLS	#2,MAIL$UP_PRIV
20$:	POPL	R0			;RESTORE PUTMSG STATUS
	BLBC	R0,EXIT			;BR IF FAILURE
;
; IF NETWORK JOB, SEND END OF MESSAGES FLAG
;
END_OF_MESS:				;SEND END OF MESSAGES FLAG
	BLBC	R5,CONTINUE		;BR IF NOT A NETWORK JOB
	PUSHL	#0			;END OF MESSAGES FLAG
	PUSHL	SP			;CREATE FLAG DESC
	PUSHL	#1			;
	PUSHAQ	(SP)			;SEND THE FLAG
	CALLS	#1,MAIL$PUT_OUTPUT	;TO MASTER MAIL JOB
;
; CONTINUE IF CONDITION WASN'T SEVERE ERROR
;
CONTINUE:				;CONTINUE IF POSSIBLE
	CMPL	CHF$L_SIG_NAME(R2),#SS$_ACCVIO ;WAS IT AN ACCVIO?
	BEQL	RESIGNAL
	MOVL	CHF$L_SIGARGLST(AP),R2	;GET ADDR OF CONDITION ARRAY AGAIN
	MOVL	CHF$L_MCHARGLST(AP),R1	;GET ADDR OF MECHANISM ARRAY
	BISL3	#STS$M_INHIB_MSG,-	;GET STATUS AND INHIBIT MESSAGE
		CHF$L_SIG_NAME(R2),R0	;
	MOVL	R0,CHF$L_MCH_SAVR0(R1)	;SET IN RETURN R0
	CMPZV	#STS$V_SEVERITY,-	;WAS THIS A SEVERE ERROR?
		#STS$S_SEVERITY,R0,#STS$K_SEVERE
	BEQLU	EXIT			;IF SO - EXIT
	MOVZWL	#SS$_CONTINUE,R0	;SET STATUS
	RET

RESIGNAL:
	MOVZWL	#SS$_RESIGNAL,R0
	RET
;
; EXIT WITH STATUS
;
EXIT:					;EXIT IMAGE
	$EXIT_S R0			;


;++
; ACTION_ROUTINE - SYS$PUTMSG ACTION ROUTINE
;
; 	THIS ROUTINE IS CALLED TO PUT AN ERROR MESSAGE TO SYS$OUTPUT.
;	USING THIS ROUTINE FORCES ALL OUTPUT THROUGH MAIL$PUT_OUTPUT.
;
; INPUTS:
;
;	4(AP) = ADDRESS OF MESSAGE DESCRIPTOR
;
; OUTPUTS;
;
;	MESSAGE OUTPUT TO OUTPUT FILE.
;
;--
ACTION_ROUTINE:				;SYS$PUTMSG ACTION ROUTINE
	.WORD	0			;(ENTRY MASK)
	CALLG	(AP),MAIL$PUT_OUTPUT	;
	BLBC	R0,EXIT			;BR IF FAILURE
	BBC	#MAIF_V_NETJOB,MAIL$GL_FLAGS,10$ ;BR IF NOT NET JOB
	CALLG	(AP),G^LIB$PUT_OUTPUT	;PUT MESSAGE IN LOG FILE TOO
10$:
	CLRL	R0			;DON'T OUTPUT ANYTHING 
	RET				;

	.END
