%TITLE 'EDT$TIDELETE - handle DEL key'
MODULE EDT$TIDELETE (				! Handle DEL key
		IDENT = 'V04-000'			! File: TIDELETE.BLI Edit: JBS1005
		) =
BEGIN
!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982, 1984 BY				    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************
!

!++
! FACILITY:	EDT -- The DEC Standard Editor
!
! ABSTRACT:
!
!	Handle the DEL key.
!
! ENVIRONMENT:	Runs at any access mode - AST reentrant
!
! AUTHOR: Bob Kushlis, CREATION DATE: June 9, 1979
!
! MODIFIED BY:
!
! 1-001	- Original.  DJS 18-FEB-1981.  This module was created by
!	extracting routine EDT$$TI_DELK  from module TINPUT.
! 1-002	- Regularize headers.  JBS 11-Mar-1981
! 1-003	- Correct handling of deleting of control characters.  JBS 27-May-1982
! 1-004	- Watch out for insert mode.  JBS 01-Apr-1983
! 1-005	- Correct a minor typo.  JBS 22-Apr-1983
!--


%SBTTL 'Declarations'
!
! TABLE OF CONTENTS:
!

REQUIRE 'EDTSRC:TRAROUNAM';

FORWARD ROUTINE
    EDT$$TI_DELK : NOVALUE;

!
! INCLUDE FILES:
!

REQUIRE 'EDTSRC:EDTREQ';

!
! MACROS:
!
!	NONE
!
! EQUATED SYMBOLS:
!

BIND
    DEL_ONE_CHAR_52 = UPLIT (BYTE (ASC_K_BS, ASC_K_ESC, %C'K'));

LITERAL
    DEL_ONE_CHAR_52_LEN = 3;

BIND
    DEL_ONE_CHAR_100 = UPLIT (BYTE (ASC_K_BS, ASC_K_ESC, %C'[', %C'K'));

LITERAL
    DEL_ONE_CHAR_100_LEN = 4;

!
! OWN STORAGE:
!
!	NONE
!
! EXTERNAL REFERENCES:
!
!	In the routine

%SBTTL 'EDT$$TI_DELK  - handle DEL key'

GLOBAL ROUTINE EDT$$TI_DELK (			! Handle DEL key
    C						! Character being deleted
    ) : NOVALUE =

!++
! FUNCTIONAL DESCRIPTION:
!
!	Delete the last character on the echo line.  This is done by writing
!	out backspace space backspace.  If the character was a control chraracter,
!	we do it twice.
!
! FORMAL PARAMETERS:
!
!  C 			The character which is being deleted.
!
! IMPLICIT INPUTS:
!
!	EDT$$G_TIN_ECHOPOS
!	EDT$$G_TI_TYP
!
! IMPLICIT OUTPUTS:
!
!	EDT$$G_TIN_ECHOPOS
!
! ROUTINE VALUE:
!
!	NONE
!
! SIDE EFFECTS:
!
!	NONE
!
!--

    BEGIN

    EXTERNAL ROUTINE
	EDT$$OUT_FMTBUF,
	EDT$$FMT_LIT;

    EXTERNAL
	EDT$$G_TIN_ECHOPOS,			! Position on the echo line
	EDT$$G_TI_TYP;				! Terminal type

    IF (.C LSS %C' ')
    THEN
	BEGIN
	EDT$$G_TIN_ECHOPOS = .EDT$$G_TIN_ECHOPOS - 1;

	CASE .EDT$$G_TI_TYP FROM TERM_VT52 TO TERM_VT100 OF
	    SET

	    [TERM_VT52] :
		EDT$$FMT_LIT (DEL_ONE_CHAR_52, DEL_ONE_CHAR_52_LEN);

	    [TERM_VT100] :
		EDT$$FMT_LIT (DEL_ONE_CHAR_100, DEL_ONE_CHAR_100_LEN);

	    [INRANGE, OUTRANGE] :
		BEGIN
		0
		END;
	    TES;

	END;

    EDT$$G_TIN_ECHOPOS = .EDT$$G_TIN_ECHOPOS - 1;

    CASE .EDT$$G_TI_TYP FROM TERM_VT52 TO TERM_VT100 OF
	SET

	[TERM_VT52] :
	    EDT$$FMT_LIT (DEL_ONE_CHAR_52, DEL_ONE_CHAR_52_LEN);

	[TERM_VT100] :
	    EDT$$FMT_LIT (DEL_ONE_CHAR_100, DEL_ONE_CHAR_100_LEN);

	[INRANGE, OUTRANGE] :
	    BEGIN
	    0
	    END;
	TES;

    EDT$$OUT_FMTBUF ();
    END;					! of routine EDT$$TI_DELK

!<BLF/PAGE>

END						! of module EDT$TIDELETE

ELUDOM
