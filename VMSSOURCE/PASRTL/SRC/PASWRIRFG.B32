MODULE PAS$WRITE_REALF_G ( %TITLE 'Write a G_floating in F format'
		IDENT = '1-003'		! File: PASWRIRFG.B32 Edit: MDL1003
		) =
BEGIN
!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982, 1984 BY				    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************
!

!++
! FACILITY:	Pascal Language Support
!
! ABSTRACT:
!
!	This module contains procedures which write a G_floating in
!	fixed-point notation to a textfile or string.
!
! ENVIRONMENT:	User mode - AST reentrant
!
! AUTHOR: Steven B. Lionel, CREATION DATE: 1-April-1981
!
! MODIFIED BY:
!
! 1-001	- Original.  SBL 1-April-1981
! 1-002 - Make total-width a longword.  SBL 30-June-1982
! 1-003 - Allocate 6 longwords for ARG_LIST, not 5.  MDL 19-Jul-1984
!--


%SBTTL 'Declarations'
!
! PROLOGUE DEFINITIONS:
!

REQUIRE 'RTLIN:PASPROLOG';			! Externals, linkages, PSECTs, structures

!
! TABLE OF CONTENTS:
!

FORWARD ROUTINE
    PAS$WRITE_REALF_G: NOVALUE,			! Write to textfile
    PAS$WRITEV_REALF_G: NOVALUE;		! Write to string

!
! MACROS:
!
!	NONE
!
! EQUATED SYMBOLS:
!
!	NONE
!
! FIELDS:
!
!	NONE
!
! OWN STORAGE:
!
!	NONE
!

%SBTTL 'PAS$WRITE_REALF_G - Write G_floating in F format to textfile'
GLOBAL ROUTINE PAS$WRITE_REALF_G (
	PFV: REF $PAS$PFV_FILE_VARIABLE, 		! File variable
	VALUE_0,VALUE_1,				! Value to write
	TOTAL_WIDTH: SIGNED,				! Total field width
	FRAC_DIGITS: SIGNED,				! Digits in fraction
	ERROR						! Error unwind address
    ): NOVALUE =

!++
! FUNCTIONAL DESCRIPTION:
!
!	This procedure writes a G_floating value in fixed-point notation
!	to the specified textfile.
!
! CALLING SEQUENCE:
!
!	CALL PAS$WRITE_REALF_G (PFV.mr.r, VALUE.rg.v , TOTAL_WIDTH.rl.v
!			       , FRAC_DIGITS.rl.v [, ERROR.ja.r]])
!
! FORMAL PARAMETERS:
!
!	PFV		- The Pascal File Variable (PFV) passed by reference.
!			  The structure of the PFV is defined in PASPFV.REQ.
!
!	VALUE		- The G_floating value to write, by immediate value.
!			  Note that this takes up two argument list positions.
!
!	TOTAL_WIDTH	- Total field width.
!
!	FRAC_DIGITS	- Number of digits in fraction.
!
!	ERROR		- Optional.  Address to unwind to if an error occurs.
!
! IMPLICIT INPUTS:
!
!	NONE
!
! IMPLICIT OUTPUTS:
!
!	NONE
!
! ROUTINE VALUE:
!
!	NONE
!
! SIDE EFFECTS:
!
!	If the file is the standard file OUTPUT, it is implicitly opened.
!
! SIGNALLED ERRORS:
!
!	LINTOOLON - line too long
!	NEGWIDDIG - negative Width or Digits specification is not allowed
!
!--

    BEGIN

    BUILTIN
	ACTUALCOUNT;

    LOCAL
	FCB: REF $PAS$FCB_CONTROL_BLOCK,		! File control block
	FIELD_WIDTH: SIGNED,				! Minimum/actual width
	REMAINING_WIDTH,				! Maximum width
	PFV_ADDR: VOLATILE,				! Enable argument
	UNWIND_ACT: VOLATILE,				! Enable argument
	ERROR_ADDR: VOLATILE;				! Enable argument

    ENABLE
	PAS$$IO_HANDLER (PFV_ADDR, UNWIND_ACT, ERROR_ADDR);	! Enable error handler

    !+
    ! Get ERROR parameter, if present.
    !-

    IF ACTUALCOUNT () GEQU 6
    THEN
	ERROR_ADDR = .ERROR;		! Set unwind address

    PFV_ADDR = PFV [PFV$R_PFV];		! Set PFV address

    !+
    ! Validate PFV and get PFV.
    !-

    PAS$$VALIDATE_PFV (PFV [PFV$R_PFV]; FCB);

    !+
    ! Set unwind action to unlock file.
    !-

    UNWIND_ACT = PAS$K_UNWIND_UNLOCK;

    !+
    ! Do common initialization.
    !-

    PAS$$INIT_WRITE (PFV [PFV$R_PFV], FCB [FCB$R_FCB]; FCB);

    !+
    ! Get minimum and maximum widths.  Check for invalid width or digits values.
    !-
    FIELD_WIDTH = .TOTAL_WIDTH;
    IF (.FIELD_WIDTH LSS 0) OR (.FRAC_DIGITS LSS 0)
    THEN
	$PAS$IO_ERROR (PAS$_NEGWIDDIG,0);
    REMAINING_WIDTH = .FCB [FCB$A_RECORD_END] - .FCB [FCB$A_RECORD_CUR];

    !+
    ! Do the convert.  If it fails, signal an error.
    !-

    IF NOT PAS$CVT_G_T (VALUE_0,		! Value to convert
    			.FCB [FCB$A_RECORD_CUR],! Destination
    			FIELD_WIDTH,		! Minimum/actual width
    			.REMAINING_WIDTH,	! Maximum width
    			.FRAC_DIGITS)		! Fraction digits
    THEN
	$PAS$IO_ERROR (PAS$_LINTOOLON,1,(.FIELD_WIDTH-.REMAINING_WIDTH));

    !+
    ! Advance buffer pointer.
    !-

    FCB [FCB$A_RECORD_CUR] = .FCB [FCB$A_RECORD_CUR] + .FIELD_WIDTH;

    !+
    ! Call WRITE epilogue routine to move the last character written to the
    ! user's buffer and to unlock the file variable.
    !-

    PAS$$END_WRITE (PFV [PFV$R_PFV], FCB [FCB$R_FCB]);

    RETURN;

    END;					! End of routine PAS$WRITE_REALF_G

!<BLF/PAGE>

%SBTTL 'PAS$WRITEV_REALF_G - Write REALF_G to string'
GLOBAL ROUTINE PAS$WRITEV_REALF_G (
	MAX_LENGTH: WORD,				! Maximum length of string
	STRING_LINE: REF VECTOR [, WORD],		! String to write to
	VALUE0,VALUE1,					! Value to write
	TOTAL_WIDTH: SIGNED,				! Total field width
	FRAC_DIGITS: SIGNED,				! Fraction digits
	ERROR						! Error unwind address
    ) : NOVALUE =

!++
! FUNCTIONAL DESCRIPTION:
!
!	This procedure writes a G_floating in fixed-point format 
!	to the specified string.
!
! CALLING SEQUENCE:
!
!	CALL PAS$WRITEV_REALF_G (MAX_LENGTH.rw.v, STRING_LINE.wvt.r,
!	   VALUE.rg.v, TOTAL_WIDTH.rl.v, FRAC_DIGITS.rl.v [, ERROR.j.r])
!
! FORMAL PARAMETERS:
!
!	MAX_LENGTH	- The maximum length of STRING_LINE.
!
!	STRING_LINE	- A varying string to which the output will be appended.
!
!	VALUE		- The value to write.  Note that it is passed by immediate
!			  value in two argument positions.
!
!	TOTAL_WIDTH	- The width of the field to write.
!
!	FRAC_DIGITS	- The number of digits in the fraction field.
!
!	ERROR		- Optional.  If specified, the address to unwind to
!			  in case of an error.
!
! IMPLICIT INPUTS:
!
!	NONE
!
! IMPLICIT OUTPUTS:
!
!	NONE
!
! ROUTINE VALUE:
!
!	NONE
!
! SIDE EFFECTS:
!
!	NONE
!
! SIGNALLED ERRORS:
!
!	See PAS$WRITE_REALF_G
!
!--

    BEGIN

    LOCAL
	PFV: $PAS$PFV_FILE_VARIABLE,	! Pascal File Variable
	ARG_LIST: VECTOR [6, LONG],	! Argument list
	PFV_ADDR: VOLATILE,		! Enable argument
	UNWIND_ACT: VOLATILE,		! Enable argument
	ERROR_ADDR: VOLATILE;		! Enable argument

    BUILTIN
	ACTUALCOUNT;			! Count of arguments

    ENABLE
	PAS$$IO_HANDLER (PFV_ADDR, UNWIND_ACT, ERROR_ADDR);	! Enable error handler

    !+
    ! Get ERROR parameter, if present.
    !-

    IF ACTUALCOUNT () GEQU 6
    THEN
	ERROR_ADDR = .ERROR;		! Set unwind address

    PFV_ADDR = PFV [PFV$R_PFV];		! Set PFV address

    !+
    ! Set up ARG_LIST.
    !-

    ARG_LIST [0] = 5;			! Five arguments
    ARG_LIST [1] = PFV [PFV$R_PFV];	! PFV address
    ARG_LIST [2] = .VALUE0;		! Value to write
    ARG_LIST [3] = .VALUE1;		!
    ARG_LIST [4] = .TOTAL_WIDTH;	! Field width
    ARG_LIST [5] = .FRAC_DIGITS;	! Fraction digits

    !+
    ! Call PAS$$DO_WRITEV to do the work, giving it the address of
    ! PAS$WRITE_REALF_G to call.
    !-

    PAS$$DO_WRITEV (PFV [PFV$R_PFV], .MAX_LENGTH, STRING_LINE [0], ARG_LIST, 
	PAS$WRITE_REALF_G);

    RETURN;

    END;					! End of routine PAS$WRITEV_REALF_G

!<BLF/PAGE>

END						! End of module PAS$WRITE_REALF_G

ELUDOM
