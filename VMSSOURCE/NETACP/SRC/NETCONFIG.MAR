	.TITLE	NETCONFIG - Local Configuration Data Base
	.IDENT	'V04-000'
	.DEFAULT DISPLACEMENT,LONG	; This code is placed at end of image

;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982, 1984 BY				    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;

; FACILITY:	NETWORK ACP
;
; ABSTRACT:
;	THIS MODULE CONTAINS THE CONTROL BLOCKS FOR THE LOCAL NODE.
;
; ENVIRONMENT:
;	MODE = KERNEL
;--
	.SBTTL	HISTORY
;
; AUTHOR:	A. Eldridge	10-Jan-1980
;
; MODIFIED BY:
;
;	V03-038	RNG0038		Rod Gamache		13-Jul-1984
;		Change default receive buffer count for UNA lines from
;		4 to 6 buffers.
;
;	V03-037	PRB0338		Paul Beck	27-Jun-1984  19:19
;		Change default pipeline quota to 3000.
;
;	V03-036	PRB0324		Paul Beck	23-Mar-1984  22:29
;		Modify definition of NML, MAIL, and PHONE to use the .EXE form.
;
;	V03-035	PRB0035		Paul Beck		23-FEB-1984 18:14 
;		Update version number fields.
;
;	V03-034	RNG0034		Rod Gamache		10-Feb-1984
;		Add a real LLI database.
;
;	V03-033	RNG0033		Rod Gamache	 3-Feb-1984
;		Add System Node Version (SNV) parameter to remote node
;		database.
;
;	V03-032	PRB0306		Paul Beck	11-Jan-1984  14:20
;		Add CTERM object (OBJ_42)
;
;	V031	RNG0031		Rod Gamache		01-Aug-1983
;		Set default for EXECUTOR PROXY ACCESS to be BOTH. Set
;		default PROXY ACCESS for a few OBJECTS to be OUTGOING
;		only.
;
;	V030	TMH0030		Tim Halvorsen		04-Jul-1983
;		Add XAI database for X.25 gateway support.
;		Set limit checks on DELAY FACTOR, DELAY WEIGHT and
;		MAX BUFFERS, because they are stored as signed bytes.
;		Add LNI alias (cluster node address) parameter.
;
;	V029	TMH0029		Tim Halvorsen		01-Jun-1983
;		Restore default DELAY FACTOR to 80 (5*delay) in order
;		to improve recovery of lost datagrams, and yet not so
;		low as to cause queue pileup (due to retransmissions)
;		on high latency datalinks, such as X.25 connections.
;

	.SBTTL	DECLARATIONS
;
; INCLUDE FILES:
;
	$DYNDEF
	$PRVDEF

	$NETSYMDEF

	$ADJDEF
	$CNFDEF
	$CNRDEF
	$NFBDEF
	$NMADEF
	$CRIDEF
	$PLIDEF
	$EFIDEF
	$ESIDEF
	$LLIDEF
	$LNIDEF
	$NDIDEF
	$OBIDEF
	$SPIDEF
	$WQEDEF
;
; MACROS:
;

;
;  The following macros build the buffers used to construct CNF blocks
;  during ACP initialization.
;
.MACRO	$BLDCNF_2  database,type,name,value	; Build list of parameters
	.CNFFLD	database,type,name		; Enter parameter i.d.
	.IF  DIF,S,type
		.BYTE	0			; Flag "not a string"
		.LONG	value			; Store the value
	.IFF
		.ASCIC	"value"			; Store counted string
	.ENDC
.ENDM	$BLDCNF_2

.MACRO	$BLDCNF	database,list			; Build CNF specifier
	.LONG	CNR_'database'			; Point to CNR block
	.IRP	entry,<list>			;
		$BLDCNF_2  entry		; Enter parameter list
	.ENDR
	.LONG	0				; Terminate with a zero
.ENDM	$BLDCNF


;
;  The following macros build the tables used to apply default values to
;  selected CNF parameters when CNF blocks are either added or modified.
;
.MACRO	$CNF_DEF_2  db,param,type,value

	.CNFFLD	db,type,param			; Enter parameter i.d.
	.LONG	value				; Store parameter default value

.ENDM	$CNF_DEF_2

.MACRO	$CNF_DEF  database,list

	_$CNF_DEF = .
	. = NET$AL_CNF_DFLT + <4*nfb$c_db_'database'>
	.ADDRESS _$CNF_DEF
	. = _$CNF_DEF

	.IRP  entry,<list>
	      $CNF_DEF_2  database,entry
	.ENDR
	.LONG	0

.ENDM	$CNF_DEF

.MACRO	$CNF_DEF_TAB  database,list

	.IRP  entry,<list>
	      $CNF_DEF_2  database,entry
	.ENDR
	.LONG	0

.ENDM	$CNF_DEF_TAB


;
; The following macro is used to build the field semantics.  Each field is
; defined by:
;
;	pref	Prefix code identify the component (e.g., LNI,OBI,NDI)
;
;	type	V  bit	 - uses no storage
;		L  longword
;		S  string
;
;	maxs	Maximum allowed parameter value or string size.  If the
;		'field' parameter is "L" (longword) then there is no
;		maximum, otherwize the default is 65535 implying that there
;		is no maximum.
;
;		The maximum and default size for the length of strings is 1023.
;
;	qual	One of the following qualifiers
;
;		for non-strings:	Z   the value zero is allowed.
;
;		for strings:		T   treat all characters transparently.
;					A   only upper case ascii and numeric
;					    characters are allowed.
;					F   parse the string as if it were a
;					    file specification.
;
;	access	Specifies the type of access allowed on that field:
;
;		blank	General read/write access.
;		W	General write access, read access only if the user
;			has "bypass" privilege.
;		R	Read-only access.
;		C	General read/write access if the NFB$C_'pref'_LCK bit
;			is clear, else read-only access.
;		E	External read only.  Can't write field on behalf of a
;			QIO, but can be written for internal ACP use.
;		N	No external read or write access.  Can only be read or
;			written internally.
;
;	req	Controls data base integrity and consistency as follows:
;
;		blank	No special requirements.
;		M	Mandatory.  If this field is not active then the block
;			will not be inserted into the data base.
;		U	Unique.  The value of this field must be unique with
;			respect to the values of this same field for all other
;			blocks currently in the data base.
;		S	Signature.  This implies both M and S.
;
;	field	Specifies how the field is stored:
;
;		B	Byte.
;		W	Word.
;		L	Longword.  If the field is as string then this
;			the self-relative longword string descriptor.
;		$	There is no actual field in the CNF block.  The
;			information must be found by calling an action
;			routine.
;

.MACRO	$entsem  pref,type,name,maxs=65535,qual=T,access=RW,req,field

	;
	;   Initialize the semantic longword by setting the CNF offset to the
	;   field.
	;
	;    o	If the "field" is really a routine (field=$) then the CNR
	;   	action routine offset is used.  
	;    o	If the "field" is type "bit" then the bit offset from the top
	;	of the CNF to the bit in CNF$W_BOOLEAN is used.
	;    o	Else the byte offset from the top of the CNF is used.
	;
	;   The value of the semantic longword can never be zero since that is 
	;   the value used to indicate the field is undefined.
	;
	 ASSUME  cnr$v_sem_off  EQ  0

	.IF IDN,field,$
	  		   _$sem = _$actoff + <1@cnr$v_sem_rt>
	.IFF
	  .IIF IDN,type,L, _$sem = cnf$c_length + 'pref'$'field'_'name'
	  .IIF IDN,type,S, _$sem = cnf$c_length + 'pref'$L_S_'name'
	  .IF  IDN,type,V
	       .IF GT,16-_$maxbool
			   _$sem = cnf$w_boolean*8 + _$maxbool
			   _$maxbool = _$maxbool + 1
	       .IFF
		  .ERROR    0 			; too many boolean parameters
	       .ENDC
	  .ENDC
	.ENDC


	;
	;   Setup field type
	;
	.IIF IDN,type,V, _$sem = _$sem + <cnr$c_sem_bit    @cnr$v_sem_typ>
	.IIF IDN,type,S, _$sem = _$sem + <cnr$c_sem_str    @cnr$v_sem_typ>
	.IF  IDN,type,L
	    .IF IDN,field,$
			   _$sem = _$sem + <cnr$c_sem_L	   @cnr$v_sem_typ>
	    .IFF
			   _$sem = _$sem + <cnr$c_sem_'field'@cnr$v_sem_typ>
	    .ENDC
	.ENDC


	;
	;  Setup maximum allowed value or string size
	;
	.IIF IDN,type,S,   _$sem = _$sem + <<1023&maxs>@cnr$v_sem_max>
	.IIF IDN,type,L,   _$sem = _$sem +      <<maxs>@cnr$v_sem_max>


	;
	;  Specify type of access allowed
	;
	.IF IDN,access,R
	.IF DIF,field,$
		.ERROR	0	; Why is 'name' read-only?
	.ENDC
	.ENDC

			   _$sem = _$sem + <<cnr$c_acc_'access'>@cnr$v_sem_acc>


	;
	;  Specify special qualifiers - zero value allowed if type 'L'
	;				parse table if type 'S'
	;
	.IIF IDN,type,S,   _$sem = _$sem + <<cnr$c_sem_'qual'>@cnr$v_sem_tab>
	.IIF IDN,qual,Z,   _$sem = _$sem + <<		    1>@cnr$v_sem_z>


	;
	;  Enter sematics into the table
	;
	. = _$start + cnr$l_sem_tab + <<nfb$c_'pref'_'name'&nfb$m_inx>*4>
	.LONG	_$sem


	;
	;  If the 'field' is actually a routine then setup its dispatch vector
	;
	.IF  IDN,field,$

	    .IF GT,CNR$L_END_ACT - _$actoff

		.   =  _$start + _$actoff
		.ADDRESS NET$'pref'_'type'_'name'
	    .IFF
		.ERROR    0 			; too many action routines
	    .ENDC
		_$actoff = _$actoff + 4
	.ENDC


	;
	;  If the parameter is mandatory then enter its i.d. into the list
	;
			 _$mand = 1
	.IIF IDN,req,M,  _$mand = 0
	.IIF IDN,req,S,  _$mand = 0
	.IF  EQ,_$mand

	    .IF GT,CNR$L_END_MAND - _$mandoff

		.   =	_$start + _$mandoff
		.CNFFLD pref,type,name
	    .IFF
		.ERROR    0 			; too many mandatory fields
	    .ENDC
		_$mandoff = _$mandoff + 4
	.ENDC


	;
	;  If the parameter must be unique then enter its i.d. into the list
	;
			 _$uniq = 1
	.IIF IDN,req,U,  _$uniq = 0
	.IIF IDN,req,S,  _$uniq = 0
	.IF  EQ,_$uniq

	    .IF GT,CNR$L_END_UNIQ - _$uniqoff

		.   =   _$start + _$uniqoff
		.CNFFLD pref,type,name
	    .IFF
		.ERROR    0 			; too many unique fields
	    .ENDC
		_$uniqoff = _$uniqoff + 4
	.ENDC

.ENDM

.MACRO	$DEFSEM    pre,typ,fldlist

	.IF DIF,typ,-1
	    .IRP	A,<fldlist>
			$entsem  pre,typ,A	; Enter semantics
	    .ENDR
	.ENDC
.ENDM						; slots for this segment

;
; The following macro builds the entire CNR structure
;
	ASSUME	CNR$L_FLINK	 EQ  0
	ASSUME	CNR$L_BLINK	 EQ  4+CNR$L_FLINK
	ASSUME	CNR$W_SIZE	 EQ  4+CNR$L_BLINK
	ASSUME	CNR$B_TYPE	 EQ  2+CNR$W_SIZE
	ASSUME	CNR$B_FLG	 EQ  1+CNR$B_TYPE
	ASSUME	CNR$W_SIZ_CNF	 EQ  1+CNR$B_FLG
	ASSUME	CNR$W_MAX_INX	 EQ  2+CNR$W_SIZ_CNF

	ASSUME	CNR$L_FLD_LOCK	 EQ  2+CNR$W_MAX_INX
	ASSUME	CNR$L_FLD_COLL	 EQ  4+CNR$L_FLD_LOCK

	ASSUME	CNR$L_ACT_QIO	 EQ  4+CNR$L_FLD_COLL
	ASSUME	CNR$L_ACT_SHOW	 EQ  4+CNR$L_ACT_QIO
	ASSUME	CNR$L_ACT_DFLT	 EQ  4+CNR$L_ACT_SHOW
	ASSUME	CNR$L_ACT_INSERT EQ  4+CNR$L_ACT_DFLT
	ASSUME	CNR$L_ACT_DELETE EQ  4+CNR$L_ACT_INSERT
	ASSUME  CNR$L_ACT_REMOVE EQ  4+CNR$L_ACT_DELETE

	ASSUME  CNR$L_SCANNER	 EQ  4+CNR$L_ACT_REMOVE
	ASSUME	CNR$L_INSERT	 EQ  4+CNR$L_SCANNER
	ASSUME	CNR$L_SPCSCAN	 EQ  4+CNR$L_INSERT

	ASSUME	CNR$L_VEC_ACT	 EQ  4+CNR$L_SPCSCAN
	ASSUME	CNR$L_VEC_MAND	 EQ  4+CNR$L_END_ACT
	ASSUME	CNR$L_VEC_UNIQ	 EQ  4+CNR$L_END_MAND
	ASSUME	CNR$L_SEM_TAB	 EQ  4+CNR$L_END_UNIQ


.MACRO	$DEFCNR	  prefix,typ1=-1,list1,typ2=-1,list2,typ3=-1,list3

.ALIGN LONG
_$START = .

	_$maxbool = 0
	_$actoff  = CNR$L_VEC_ACT
	_$mandoff = CNR$L_VEC_MAND
	_$uniqoff = CNR$L_VEC_UNIQ


CNR_'prefix'::
	.ADDRESS .				; Build queue header
	.ADDRESS .-4				;
	.WORD	1024				; Use entire page
	.BYTE	NFB$C_DB_'prefix'		; Setup database id
	.BYTE	CNF$M_FLG_CNR			; Mark as CNR block
	.WORD	CNF$C_LENGTH + 'prefix'$c_length; Total CNF size = CNF header
		              			;  plus fixed structure size
	.WORD	CNR$C_MAX_INX			;&update to "real" value
	.CNFFLD prefix,V,LCK			; Fld id of field used to guard
						; cond. writeable fields
	.CNFFLD	prefix,S,COL			; Fld id of field used to order
		  				; the CNR list

	.ADDRESS NET$PRE_QIO_'prefix'		; Pre-processor for QIO to
						; this database
	.ADDRESS NET$SHOW_'prefix'		; Pre-processor for QIO to a
						; specific CNF
	.ADDRESS NET$DEFAULT_'prefix'		; Defaulting action routine

	.ADDRESS NET$INSERT_'prefix'		; Pre-insert action routine
	.ADDRESS NET$DELETE_'prefix'		; Pre-mark-for-delete routine
	.ADDRESS NET$REMOVE_'prefix'		; Pre-remove action routine

	.ADDRESS NET$SCAN_'prefix'		; Database scanner co-routine
	.ADDRESS NET$SPCINS_'prefix'		; Real insertion routine
	.ADDRESS NET$SPCSCAN_'prefix'		; Special scan routine

	.BYTE	0[CNR$L_VEC_MAND-CNR$L_VEC_ACT]	; Init action routine ptrs
	.BYTE   0[CNR$L_VEC_UNIQ-CNR$L_VEC_MAND]; Init mandatory field list
	.BYTE	0[CNR$L_SEM_TAB -CNR$L_VEC_UNIQ]; Init unique field list
	.LONG	0[CNR$C_MAX_INX]		; Init semantic table

	;
	; Build the semantic vector
	;
	$DEFSEM   prefix,typ1,list1
	$DEFSEM   prefix,typ2,list2
	$DEFSEM   prefix,typ3,list3


. = _$START + CNR$C_LENGTH			; Use two pages

.ENDM

;
; EQUATED SYMBOLS:
;
	$EQULST	NET$C_,GLOBAL,,,<-
		<LINE_CTRS,8>,-		; NO. OF PHYLK LINE ERROR COUNTERS
	>
	;
	;   Create short symbols to cleanup sematic (CNR) table format
	;
	MXNOD = NET$C_MAX_NODES		; Max node address supported by the ACP
	MXDLL = NET$C_MAX_LINES		; Max supported datalinks
	MXLNK = NET$C_MAXLNK		; Max supported logical links
	CTXSZ = NFB$C_CTX_SIZE-2	; Max size of COL context string
	MAX_PLVEC = 33			; Maximum PLVEC index (allows for
					; 33-1=32 lines).
;
; OWN STORAGE:
;

	.PSECT	NET_PURE,NOWRT,NOEXE,LONG

;
;    Setup the configuration data base root block pointers
;
NET$AL_CNR_TAB::			; The following pointers must be in the
					; order of there database indexes
	ASSUME	NFB$C_DB_LNI  EQ  1
	ASSUME	NFB$C_DB_NDI  EQ  2
	ASSUME	NFB$C_DB_OBI  EQ  3
	ASSUME	NFB$C_DB_CRI  EQ  4
	ASSUME	NFB$C_DB_PLI  EQ  5
	ASSUME	NFB$C_DB_EFI  EQ  6
	ASSUME	NFB$C_DB_ESI  EQ  7
	ASSUME	NFB$C_DB_LLI  EQ  8
	ASSUME	NFB$C_DB_SPI  EQ  18
	ASSUME	NFB$C_DB_AJI  EQ  19
	ASSUME	NFB$C_DB_ARI  EQ  20
	ASSUME	NFB$C_DB_SDI  EQ  26

		 .LONG	-1		; There is no CNR with index zero
NET$GL_CNR_LNI:: .ADDRESS CNR_LNI	; Root of Local Node Info list
NET$GL_CNR_NDI:: .ADDRESS CNR_NDI	; Root of common Node Info list
NET$GL_CNR_OBI:: .ADDRESS CNR_OBI	; Root of network Object list
NET$GL_CNR_CRI:: .ADDRESS CNR_CRI	; Root of Circuit Info list
NET$GL_CNR_PLI:: .ADDRESS CNR_PLI	; Root of Physical Link Info list
NET$GL_CNR_EFI:: .ADDRESS CNR_EFI	; Root of Event Filter Info list
NET$GL_CNR_ESI:: .ADDRESS CNR_ESI	; Root of Event Sink Info list
NET$GL_CNR_LLI:: .ADDRESS CNR_LLI	; Root of Logical Link Info list
		 .LONG -1[9]		; Reserve space for 9 X.25 lists
NET$GL_CNR_SPI:: .ADDRESS CNR_SPI	; Root of Server Process Info list
NET$GL_CNR_AJI:: .ADDRESS CNR_AJI	; Root of Adjacency database
NET$GL_CNR_ARI:: .ADDRESS CNR_ARI	; Root of Area database
		 .LONG -1[5]		; Reserve space for 5 X.25 lists
NET$GL_CNR_SDI:: .ADDRESS CNR_SDI	; Root of Service (DLE) database
		 .LONG -1		; Reserve space for XAI X.25 database

	ASSUME	<.-NET$AL_CNR_TAB-4> EQ <NFB$C_DB_MAX*4>


BLD_VEC:				; Setup Data base init vector

		.ADDRESS BLD_LNI
		.ADDRESS BLD_NDI_LOC

		.ADDRESS BLD_OBI_TASK
		.ADDRESS BLD_OBI_NML
		.ADDRESS BLD_OBI_FAL
		.ADDRESS BLD_OBI_HLD
		.ADDRESS BLD_OBI_REMACP
		.ADDRESS BLD_OBI_MIRROR
		.ADDRESS BLD_OBI_EVL
		.ADDRESS BLD_OBI_MAIL
		.ADDRESS BLD_OBI_PHONE
		.ADDRESS BLD_OBI_CTERM
		.ADDRESS BLD_OBI_DTR
		.ADDRESS BLD_OBI_MOM
		.ADDRESS BLD_OBI_NICONFIG

		.LONG	0		; Terminate with a zero


	.PSECT	NET_IMPURE,WRT,NOEXE,LONG

NET$GQ_UTLDESC:: .LONG	NET$C_UTLBUFSIZ	; Utility buffer descriptor
NET$GL_UTLBUF::	 .LONG	0

NET$GL_DUM_NDI::	.LONG 0		; Pointer to the dummy NDI CNF
NET$GQ_TMP_BUF::	.ADDRESS .	; Queue of temporary work buffers
			.ADDRESS .-4
NET$GL_PTR_LNI::	.LONG 0		; Ptr to the LNI CNF
NET$GL_LOCAL_NDI::	.LONG 0		; Ptr to local NDI CNF
NET$GL_SAVE_IRP:: 	.LONG 0		; Holds current IRP
NET$GL_SAVE_UCB::	.LONG 0		; Holds current IRP's UCB address
NET$GL_NET_UCB::	.LONG 0		; Ptr to NET$GW_CHAN's UCB

NET$GQ_USR_STAT::	.BLKQ 1	        ; I/o status block to be returned
NET$GW_NETCHAN::	.BLKW 1	        ; channel to _NET:

;
;  The PLVEC database is an extension of the PLI database.  There is an 
;  entry in each of these tables for each physical line controlled by
;  NETACP.
;
PLVEC$GB_MAX::		.BYTE MAX_PLVEC-1   ; Maximum PLVEC index (-1 since the
					    ; vectors are zero-indexed)
			.ALIGN LONG
PLVEC$AL_ABS_TIM::	.LONG 0[MAX_PLVEC]  ; Seconds since counters last zeroed
PLVEC$AL_UCB::		.LONG 0[MAX_PLVEC]  ; Device UCB
PLVEC$AW_CHAN::		.WORD 0[MAX_PLVEC]  ; NETACP I/O channel
PLVEC$AB_REFC::		.BYTE 0[MAX_PLVEC]  ; PLVEC cell reference count
PLVEC$AB_DEV::		.BYTE 0[MAX_PLVEC]  ; Device type
PLVEC$AB_STATE::	.BYTE 0[MAX_PLVEC]  ; Line state


	.PSECT	NET_LOCK_IMPURE,WRT,GBL,LONG

;
;    Miscellaneous storage
;

NET$GL_FLAGS::		.BLKL 1	        ; Internal control flags
NET$GL_PTR_AQB::	.LONG 0	        ; Ptr to ACP's AQB
NET$GL_PTR_VCB::	.LONG 0		; Ptr to ACP's VCB
NET$GL_PTR_UCB0::	.LONG 0	        ; Ptr to the Unit 0 UCB

	.PSECT	TABLES_IMPURE,WRT,EXE,GBL,LONG
;
; LOCAL NODE INFORMATION
;
$DEFCNR	LNI,-

   V,<<-
	<LCK,	   ,Z,R, ,$>,-	; Clear only if the conditionally writable
	-			; fields are writeable
	<SUP,      ,Z,E, , >,-	; Set if area numbers to be suppressed for
	-			; all node addresses returned to NML/EVL
>>,L,<<-
	<ADD,      ,Z,C,M,$>,-	; Node address
	<SAD,	   ,Z, , ,L>,-	; X.25 sub-address range
	<STA,	  4,Z, ,M,$>,-	; State
	-			;
	-			;  LNI$C_STA_INIT  NETACP starts in this state
	-			;		   Logical link connects are
	-			;		   allowed.
	-			;  LNI$C_STA_OFF   NETACP will allow privileged
	-			;		   connects.  If NETACP is set
	-			;		   to this state from any other
	-			;		   state then all links are
	-			;		   broken and NETACP exits
	-			;  LNI$C_STA_ON	   Network generally available
	-			;  LNI$C_STA_RSTR  connects initiates allowed,
	-			;		   connects confirms allowed if
	-			;		   privileged or if the connect
	-			;		   was initiated locally
	-			;  LNI$C_STA_SHUT  Only privileged connects
	-			;		   are allowed.  When the last
	-			;		   logical link disconnects,
	-			;		   NETACP exits.
	-			;
	<ETY,	  5,Z, ,M,B>,-	; Type	(routing, end-node, etc.)
	<DFA,   127, , ,M,B>,-	; Delay factor	    (see Note1)
	<DWE,   127,Z, ,M,B>,-	; Delay weight	    (see Note1)
	<RFA,	127, , ,M,B>,-	; Retransmit factor (see Note1)
	<DAC,	  3,Z, , ,B>,-	; Default access switch
	<DPX,	  4,Z, , ,B>,-	; Default proxy access switch
	<PIQ, 32767,Z, , ,W>,-	; Pipeline quota

	<ITI,	   , , ,M,W>,-	; Incoming timer    (see Note1)	(units = sec)
	<OTI,	   , , ,M,W>,-	; Outgoing timer    (see Note1)	(units = sec)
	<IAT,	   , , ,M,W>,-	; Inactivity timer  (see Note1)	(units = sec)
	<RTI,	   , , ,M,W>,-	; Routing timer			(units = sec)
	<RSI,	   ,Z, , ,W>,-	; Min routing delay interval	(units = sec)
	<BRT,      , , ,M,W>,-	; Broadcast routing timer       (units = sec)

	<MLN, MXDLL,Z, ,M,B>,-	; Maximum supported circuits
	<MLK, MXLNK, , ,M,W>,-	; Maximum supported logical links
	<MAD, MXNOD, , ,M,W>,-	; Maximum supported node address
	<MCO,  1023, , ,M,W>,-	; Maximum cost
	<MHO,	 31, , ,M,B>,-	; Maximum hops
	<MVI,	255, , ,M,B>,-	; Maximum visits
	<MBU,  1023, , ,M,W>,-	; Maximum buffers
	<MAR,    63, , , ,B>,-	; Maximum area
	<AMC,  1022, , , ,W>,-	; Area maximum cost
	<AMH,    30, , , ,B>,-	; Area maximum hops
	<MBR,      ,Z, ,M,W>,-	; Maximum broadcast routers
	<MBE,      ,Z, ,M,W>,-	; Maximum broadcast endnodes
	<BUS,  4096, ,C,M,W>,-	; Forwarding buffer size (max we can receive)
	<SBS,  4096, ,C, ,W>,-	; Segment buffer size (max we can transmit)
	<ACL,	   , ,R, ,$>,-	; Currently active links
	<LPC,	   ,Z, , ,W>,-	; Default LOOP COUNT
	<LPL,	   ,Z, , ,W>,-	; Default LOOP LENGTH
	<LPD,	   ,Z, , ,B>,-	; Default LOOP data type
	<LPH,      ,Z, , ,B>,-	; Default LOOP help type
	<ALI,      , ,C, ,W>,-	; Alias local node address (cluster address)
>>,S,<<-
	<COL, CTXSZ, ,R,S,$>,-	; A fixed string		(see Note2)
	<IDE,	 32,T, , ,L>,-	; System identification
	<MVE,	  3,T,E, ,L>,-	; Network Management version
	<NVE,	  3,T,E, ,L>,-	; NSP version
	<RVE,	  3,T,E, ,L>,-	; Routing version
	<NAM,	  6,T,R, ,$>,-	; Local node name
	<CNT,	   ,T,R, ,$>,-	; Counters
	<PHA,      , ,R, ,$>,-	; Current NI address for this node
>>



;
;	Note1:	Doesn't effect currently active logical links.  One reason for
;		this is that these parameters may, in future releases, be
;		overridden by values supplied by the user at the time of the
;		connect request.  Thus, once a link is in progress these
;		parameters should not be changed.
;
;	Note2:	Because this field is read-only, mandatory, and unique, it
;		is guarenteed that there will be only one LNI entry in the
;		data base.
;

;
; REMOTE NODE INFORMATION
;
	ASSUME	CNR$L_COLBTE   EQ  0
	ASSUME	CNR$L_NAMEBTE  EQ  4

NET$GL_COL_TREE	 == .			; NDI collating tree root
NET$GL_NAME_TREE == .+4			; NDI name tree root

$DEFCNR	  NDI,-

   V,<<-
	<LCK,	   ,Z,R, ,$>,-	; Set if conditionally writable fields may not
	-			; written.  If this is the local node, then
	-			; it is set only if the state is LNI$C_STA_OFF.
	-			; If the node is non-local then this bit is
	-			; if unreachable or phase III.
	<REA,	   , ,R, ,$>,-	; Set if the node is reachable
	<LOO,	   , ,R, ,$>,-	; Set if the node is a "loopback" nodename
>>,L,<<-
	<ADD,      ,Z,C,M,$>,-	; Address
	<CTI,	   ,Z, , ,W>,-	; Counter delta timer	   (units = sec)
	<CTA,	   , ,E, ,L>,-	; Counter absolute timer   (units = sec)
	<DEL,	   , ,R, ,$>,-	; Average round trip delay (units = sec)
	<DCO,	   , ,R, ,$>,-	; Total cost to node
	<DTY,	   , ,R, ,$>,-	; Node type (routing, phase II, etc)
	<DHO,	   , ,R, ,$>,-	; Total hops to node
	<TAD,	   , ,R, ,$>,-	; Transformed node address
	<ACL,	   , ,R, ,$>,-	; Active links to the node
	<SDV,	   ,Z, , ,B>,-	; Service device code
	<CPU,	   ,Z, , ,B>,-	; CPU type code
	<STY,	   ,Z, , ,B>,-	; Software type code
	<ACC,	  3,Z, , ,B>,-	; Access switch (inbound, outbound, etc.)
	<PRX,	   , ,E, ,B>,-	; Proxy access
	<DAD,	   ,Z, , ,L>,-	; Dump address
	<DCT,	   ,Z, , ,L>,-	; Dump count
	<IHO,      ,Z, , ,W>,-	; Default host (input)
	<OHO,      ,Z, , ,W>,-	; Default host (output)
	<NND,      , ,R, ,$>,-	; Next hop node on way to remote node
	<SNV,	  1,Z, , ,B>,-	; System node version
>>,S,<<-
	<COL, CTXSZ, ,R,S,$>,-	; A variant on HAC used for collating.
	<NNA,	  6,A, ,U,L>,-	; Name
	<NLI,	 15,A, , ,L>,-	; Associate line if this is a "loopback" node
			     -	; (This is actually required to be unique but
			     -	;  its uniqueness falls out of its being used
      -;<NLI,	 15,A, ,U,L>,-	;  as part of "nfb$c_ndi_col".  If the loopnode
			     -	;  address could be anything but the local node
			     -	;  address then the "U" flag must be set.)
	<DLI,	 15,A,R, ,$>,-	; Line used for normal traffic
	<PUS,	 39,T, , ,L>,-	; Priv user id
	<PAC,	 39,T, , ,L>,-	; Priv account
	<PPW,	 39,T,W, ,L>,-	; Priv password
	<NUS,	 39,T, , ,L>,-	; NonPriv  user id
	<NAC,	 39,T, , ,L>,-	; NonPriv  account
	<NPW,	 39,T,W, ,L>,-	; NonPriv  password
	<RPA,	  8,T,W, ,L>,-	; Receive  password
	<TPA,	  8,T,W, ,L>,-	; Transmit password
	<HAC,	   , ,R, ,$>,-	; A combination of the node address and 
	-			; loopback linename 
	<CNT,	   ,T,R, ,$>,-	; Counters
	<SLI,	 15,A, , ,L>,-	; Service line
	<SPA,	  8,T, , ,L>,-	; Service password
	<LOA,	128,F, , ,L>,-	; Load file
	<SLO,   128,F, , ,L>,-	; Secondary loader file
	<TLO,	128,F, , ,L>,-	; Tertiary loader file
	<SID,	128,F, , ,L>,-	; Software i.d.
	<DUM,	128,F, , ,L>,-	; Dumper
	<SDU,	128,F, , ,L>,-	; Secondary Dumper
	<DFL,   128,F, , ,L>,-	; Diagnostic load file
	<HWA,     6,T, , ,L>,-	; NI hardware address of the remote node
	-			; when not initialized (for down-line load)
	<NNN,	   , ,R, ,$>,-	; Name of Next Node to Destination (NND)
>>

;
; CIRCUIT INFORMATION
;
$DEFCNR	CRI,-

  V,<<-
	<LCK,	   ,Z,R, ,$>,-	; Set if conditionally writable fields may not
	-			; be written - set if in the "off" state
	-			; state.
	<SER,	   ,Z,C, , >,-	; Service		(clear if enabled)
	-			;  This bit controls whether the line may enter
	-			;  a service state or substate - not whether it
	-			;  can remain there.  Thus setting this bit
	-			;  while in a service state will not cause any
	-			;  change in its actual state.
	<BLK,      ,Z,C, , >,-	; Set if blocking is allowed (X.25 circuits)
	<VER,      ,Z,C, , >,-	; Set if Transport verification requested
	<DLM,      ,Z,C, , >,-	; Set if X.25 datalink mapping requested
	-			;  This flag corresponds to the NICE OWNER
	-			;  parameter, and if set, reserves the circuit
	-			;  for Transport's use as an X.25 datalink.
	-			;  If clear, the X.25 circuit is to be used
	-			;  in native mode only.  This flag does not
	-			;  apply to non-X.25 circuits.
>>,L,<<-
	<OWPID,	   , ,E, ,L>,-	; Temporary service process PID
	<CTA,	   , ,E, ,L>,-	; Counter absolute timer	(seconds)
	<STA,	  2,Z, , ,B>,-	; State	set by user
	<SUB,	  3,Z,R, ,$>,-	; Substate - what the line is doing
	<LCT, 	   , , , ,W>,-	; Counter delta timer		(seconds)
	<PNA,	   , ,R, ,$>,-	; Partner's node address
	<BLO, 	   , ,R, ,$>,-	; Receive block size
	<COS,    25, , , ,B>,-	; Cost
	<HET,  8191, , , ,W>,-	; Hello timer
	<LIT,	   , ,R, ,$>,-	; Listen timer
	<MRC,	   , , , ,B>,-	; Max recalls
	<RCT,	   , , , ,W>,-	; Recall timer
	<POL,	   ,Z, , ,B>,-	; Polling state
	<PLS,	   ,Z,E, ,B>,-	; Polling sub-state
	<USE,	   ,Z,C, ,B>,-	; X.25 usage (incoming, outgoing, permanent)
	<CHN,	   ,Z,C, ,B>,-	; X.25 channel
	<TYP,	  6,Z,C, ,B>,-	; Type (protocol)
	<MBL,	   , , , ,W>,-	; Maximum block
	<MWI,	   , , , ,B>,-	; Maximum window
	<TRI,   255,Z,C, ,B>,-	; Tributary station address 
	<BBT,	   , , , ,W>,-	; Babbel timer
	<TRT,	   ,Z, , ,W>,-	; Transmit timer
	<MRB,	   , , , ,B>,-	; Maximum receive buffers
	<MTR,	   , , , ,B>,-	; Maximum transmits
	<ACB,	   ,Z, , ,B>,-	; Active base
	<ACI,	   ,Z, , ,B>,-	; Active increment
	<IAB,	   ,Z, , ,B>,-	; Inactive base
	<IAI,	   ,Z, , ,B>,-	; Inactive increment
	<IAT,	   , , , ,B>,-	; Inactive threshold
	<DYB,	   ,Z, , ,B>,-	; Dying base
	<DYI,	   ,Z, , ,B>,-	; Dying increment
	<DYT,	   , , , ,B>,-	; Dying threshold
	<DTH,	   , , , ,B>,-	; Dead threshold
	<MST,	   , ,R, ,$>,-	; Maintenance mode "state" (1 => Off, 0 => On)
	<XPT,	  4, ,C, ,B>,-	; Transport protocol
	<DRT,      , ,R, ,$>,-	; Designated router on this NI
	<MRT,    33,Z, , ,B>,-	; Maximum routers on NI
	<RPR,   127,Z, , ,B>,-	; Router priority on NI
>>,S,<<-
	<COL, CTXSZ, ,R,S,$>,-	; Collating field
	<NAM,	 15,A,E,S,L>,-	; Circuit name
	<VMSNAM,   ,A,R, ,$>,-	; VMS device name
	<CHR,	   ,T,R, ,$>,-	; Control QIO characteristics buffer
	<CNT,	   ,T,R, ,$>,-	; Counters
	-			; loopback nodename if line is in loopback)
	<LOO,	  6,A,R, ,$>,-	; Loopback name
	<PNN,	  6,A,R, ,$>,-	; Partner's node name
	<NUM,	 32,T, , ,L>,-	; X.25 Call number
	<DTE,	 32,T, , ,L>,-	; X.25 DTE
	<DEVNAM,   ,T,R, ,$>,-	; VMS device name, with unit included
>>

;
; PHYSICAL LINK INFORMATION
;
$DEFCNR	PLI,-

  V,<<-
	<LCK,	   ,Z,R, ,$>,-	; Set if conditionally writable fields may not
	-			; be written - set if in the "off" state
	-			; state.
	<SER,	   ,Z, , , >,-	; Service		(clear if enabled)
	-			;
	-			;  This bit controls whether the line may enter
	-			;  a service state or substate - not whether it
	-			;  can remain there.  Thus setting this bit
	-			;  while in a service state will not cause any
	-			;  change in its actual state.
	<DUP,	   ,Z,C, , >,-	; Duplex		(set if halfduplex)
	<CON,	   ,Z,C, , >,-	; Controller		(set if loopback)
	<CLO,	   ,Z,C, , >,-	; Clock mode		(set if internal)
>>,S,<<-
	<COL, CTXSZ, ,R,S,$>,-	; Collating field
	<VMSNAM,   ,T,R, ,$>,-	; VMS device name
	<NAM,	 15,A,E,S,L>,-	; Line name
	<CHR,	   ,T,R, ,$>,-	; Control QIO characteristics buffer
	<CNT,	   ,T,R, ,$>,-	; Counters
	<MCD,	   ,F, , ,L>,-	; X.25 KMX microcode dump file
	<HWA,     6,T,E, ,L>,-	; NI hardware address for this controller
	<DEVNAM,   ,T,R, ,$>,-	; VMS device name, with unit included
>>,L,<<-
	<CTA,	   , ,E, ,L>,-	; Counter absolute timer	(seconds)
	<STA,	  2,Z, , ,B>,-	; State	set by user
	<SUB,	 15,Z,R, ,$>,-	; Substate - what the line is doing
	<LCT, 	   , , , ,W>,-	; Counter delta timer		(seconds)
	<PRO,	  6,Z,C, ,B>,-	; Type
	<STI, 	   , , , ,W>,-	; Service timer			(milli-sec)
	<HTI,	   , , , ,W>,-	; LAPB holdback timer
	<MBL,	   , , , ,W>,-	; LAPB maximum block
	<MRT,	   , , , ,B>,-	; LAPB maximum retransmits
	<MWI,	   , , , ,B>,-	; LAPB maximum window
	<SLT,	   , , , ,W>,-	; Scheduling timer
	<RTT,	   , , , ,W>,-	; Retransmit timer
	<DDT,	   , , , ,W>,-	; Dead timer
	<DLT,	   ,Z, , ,W>,-	; Delay timer
	<SRT,	   , , , ,W>,-	; Stream timer
	<BFN, 	255, ,C, ,B>,-	; Number of rcv buffers in pool
	<BUS,      , ,R, ,$>,-	; Read-only buffer size actually used
	<BFS, 16384, ,C, ,W>,-	; Buffer size to override executor buffer size
	<PLVEC,    , ,R, ,$>,-	; Line's PLVEC index
	<MOD,      , , , ,B>,-	; X.25 mode (DCE, DTE, etc.)
	<EPT,      , ,C, ,W>,-	; Ethernet protocol type
>>

;
; NETWORK OBJECT INFORMATION
;
$DEFCNR	OBI,-

   V,<<-
	<LCK,	   ,Z,R, ,$>,-	; Set if conditionally writable fields may not
	-			; written.  Set if UCB field is active.
	<SET,	   ,Z,E, , >,-	; Set if the CNF was ever modified by a "set"
	-			; QIO and therefore not just a "declared"
	-			; object (note that it may also be declared).
>>,L,<<-
	<NUM,	255,Z,C,M,B>,-	; Object number
	<LPR,	   ,Z, , ,L>,-	; Low order privilege mask
	<HPR,	   ,Z, , ,L>,-	; High order privilege mask
	<UCB,	   , ,E, ,L>,-	; UCB associated with object if declared name
	<PID,	   , ,E, ,L>,-	; PID associated with object if declared name
	<CHN,	   , ,E, ,W>,-	; Channel "	  "	"     "	   "	  "
	<PRX,	  4,Z, , ,B>,-	; Proxy login switch (inbound, outbound, etc.)
>>,S,<<-
	<COL, CTXSZ, ,R,S,$>,-	; Field used to collate the database
	<NAM,	 16,A,C,S,L>,-	; Name
	<FID,	 64,F, , ,L>,-	; File id 
	<USR,	 39,A, , ,L>,-	; User id
	<PSW,	 39,A,W, ,L>,-	; Password
	<ACC,	 39,A, , ,L>,-	; Account
	<ZNA,	   ,T,R,S,$>,-	; Set to make uniqueness check on zero obj+name
	<SFI,	   ,T,R, ,$>,-	; Parsed file i.d. with defaults applied
	<IAC,	   ,T,R, ,$>,-	; Concatenated inbound access control
	-			; - may get it from NDI CNF for local node
>>

;
; Event Sink Information
;
$DEFCNR	ESI,-

   V,<<-
	<LCK,	   ,Z,R, ,$>,-	; Set if conditionally writable fields may not
	-			; written.
>>,L,<<-
	<STA,	  2,Z, , ,B>,-	; Logging state
	-			;
	-			;	ESI$C_STA_ON	On
	-			;	ESI$C_STA_OFF	Off
	-			;	ESI$C_STA_HLD	Hold
	-			;
	<SNK,	  3, ,C,S,B>,-	; Sink type
	-			;
	-			;	ESI$C_SNK_CON	Console
	-			;	ESI$C_SNK_FIL	File
	-			;	ESI$C_SNK_MON	Monitor
	-			;
	<SP1,	   ,Z, , ,W>,-	; Spare
	<B1,	   ,Z,C, ,L>,-	; For user specified use
	<B2,	   ,Z, , ,L>,-	; For user specified use
>>,S,<<-
	<COL, CTXSZ, ,R,S,$>,-	; Field used to collate the database
	<LNA,	255,A,C, ,L>,-	; Sink name 
	<SB1,	255,T, , ,L>,-	; For user specified use
	<SB2,	255,T, , ,L>,-	; For user specified use
	<SB3,	255,T,C, ,L>,-	; For user specified use
>>

;
; Event Filter Information
;
$DEFCNR	EFI,-

   V,<<-
	<LCK,	   ,Z,R, ,$>,-	; Set if conditionally writable fields may not
	-			; written.
>>,L,<<-
	<SIN,      ,Z, ,S,W>,-	; Sink node address
	<SP1,	   ,Z, , ,W>,-	; Spare
	<B1,	   ,Z,C, ,L>,-	; For user specified use
	<B2,	   ,Z, , ,L>,-	; For user specified use
>>,S,<<-
	<COL, CTXSZ, ,R,S,$>,-	; Field used to collate the database
	<EVE,  1023,T, ,M,L>,-	; Event list
	<SB1,	255,T, , ,L>,-	; For user specified use
	<SB2,	255,T, , ,L>,-	; For user specified use
	<SB3,	255,T,C, ,L>,-	; For user specified use
>>

;
; LOGICAL LINK INFORMATION
;
	;
	;  Define some symbols so that the semantic table can be built
	;

$DEFCNR	LLI,-			; Build the semantic table

   V,<<-
	<LCK,	   ,Z,R, ,$>,-	; Set if conditionally writable fields may not
>>,L,<<-
	<PID,	   ,Z,E, ,$>,-	; Process I.D. of owner (external format)
	<IPID,	   ,Z,E, ,$>,-	; Process I.D. of owner (internal format)
	<PNA,	   ,Z,E, ,$>,-	; Partner (remote) node address
	<LLN,	   , ,E, ,$>,-	; Local link number from XWB
	<RLN,	   ,Z,E, ,$>,-	; Remote link number
	<DLY,	   , ,E, ,$>,-	; Round trip delay time
	<STA,	   , ,E, ,$>,-	; State
	<XWB,	   ,Z,N, ,L>,-	; Pointer to associated XWB
>>,S,<<-
	<COL, CTXSZ, ,R,S,$>,-	; Field used to collate the database
	<USR,	 39, ,R, ,$>,-	; User id
	<PRC,	 39, ,R, ,$>,-	; Process name
	<PNN,	  6, ,R, ,$>,-	; Remote node name
	<RID,	   , ,R, ,$>,-	; Remote i.d. (PID, process name, username, etc)
	<CNT,	   ,T,R, ,$>,-	; Counters
>>


;
; Server Process Information
;
$DEFCNR	SPI,-

   V,<<-
	<LCK,	   ,Z,R, ,$>,-	; Set if conditionally writable fields may not
	-			; written.
	<PRL,      ,Z,E, , >,-	; Proxy flag which initially started server
>>,L,<<-
	<PID,      ,Z,E,S,L>,-	; PID of server process
	<IRP,      ,Z,N, ,L>,-	; IRP of waiting DECLSERV QIO.  0 if working.
	<CHN,      ,Z,N, ,W>,-	; Channel associated with IRP (for cancel)
	<RNA,      ,Z,E, ,W>,-	; Remote node address which initially started
	-			; the server process
>>,S,<<-
	<COL, CTXSZ, ,R,S,$>,-	; Field used to collate the database
	<ACS,   255, ,N, ,L>,-	; ACS used to initially start server process
	<RID,   255, ,E, ,L>,-	; Remote user ID which initially started server
	<SFI,   255, ,E, ,L>,-	; Last (current) SFI given to server process
	<NCB,   255, ,E, ,L>,-	; Last (current) NCB given to server process
	<PNM,   255, ,E, ,L>,-	; Last (current) process name given to server
>>

;
; Adjacency Information
;
$DEFCNR	AJI,-

   V,<<-
	<LCK,	   , ,R, ,$>,-	; Set if conditionally writable fields may not
	-			; written.
	<REA,      , ,R, ,$>,-	; True if adjacency in "run" state (two-way)
>>,L,<<-
	<ADD,      , ,R, ,$>,-	; Partner node address
	<TYP,      , ,R, ,$>,-	; Partner node type
	<LIT,      , ,R, ,$>,-	; Adjacency listen interval
	<BLO,      , ,R, ,$>,-	; Partner block size
	<RPR,      , ,R, ,$>,-	; Partner broadcast router priority
>>,S,<<-
	<COL, CTXSZ, ,R,S,$>,-	; Field used to collate the database
	<NNA,      , ,R, ,$>,-	; Partner node name
	<CIR,      , ,R, ,$>,-	; Circuit name
>>

NET$T_CNF_AJI::
	.BLKB	CNF$C_LENGTH+2	; Define dummy AJI CNF block
				; (the extra word is used to hold "previous"
				;  entry in the database)
AJI$C_LENGTH = 0		; There is no actual storage for this database

;
; Service (DLE) Information
;
$DEFCNR SDI,-

   V,<<-
	<LCK,	   , ,R, ,$>,-	; Set if conditionally writable fields may not
	-			; written.
>>,L,<<-
	<SUB,      , ,R, ,$>,-	; Service substate
	<PID,	   , ,R, ,$>,-	; Process I.D. of owner
>>,S,<<-
	<COL, CTXSZ, ,R,S,$>,-	; Field used to collate the database
	<CIR,      , ,R, ,$>,-	; Circuit name
	<PHA,      , ,R, ,$>,-	; Service phys. addr. (BC only)
	<PRC,	   , ,R, ,$>,-	; Process name
>>

NET$T_CNF_SDI::
	.BLKB	CNF$C_LENGTH+2	; Define dummy SDI CNF block
				; (the extra word is used to hold "previous"
				;  entry in the database)
SDI$C_LENGTH = 0		; There is no actual storage for this database

;
; AREA (LEVEL 2) INFORMATION
;
$DEFCNR	  ARI,-

   V,<<-
	<LCK,	   , ,R, ,$>,-	; Set if conditionally writable fields may not
	-			; written.
	<REA,	   , ,R, ,$>,-	; Set if the area is reachable
>>,L,<<-
	<ADD,      , ,R, ,$>,-	; Area number
	<DCO,	   , ,R, ,$>,-	; Total cost to nearest level 2 rtr in area
	<DHO,	   , ,R, ,$>,-	; Total hops to nearest level 2 rtr in area
	<NND,      , ,R, ,$>,-	; Next hop node on way to remote area
>>,S,<<-
	<COL, CTXSZ, ,R,S,$>,-	; Collating field
	<DLI,	   , ,R, ,$>,-	; Circuit used for normal traffic to area
>>

NET$T_CNF_ARI::
	.BLKB	CNF$C_LENGTH+2	; Define dummy ARI CNF block
				; (the extra word is used to hold "previous"
				;  entry in the database)
ARI$C_LENGTH = 0		; There is no actual storage for this database


	.PSECT	TABLES_PURE,NOWRT,NOEXE,LONG


NET$AL_CNF_DFLT::  .LONG  0[NFB$C_DB_MAX+1]  ; Vector of tables used to supply
					     ; default database param values

$CNF_DEF LNI,-			; Local Node Default Parameter Settings

    <-
	<DAC,l, NMA$C_ACES_BOTH>,- ; Default access switch
	<DPX,l, NMA$C_ACES_BOTH>,- ; Default proxy access switch
	<ETY,l, ADJ$C_PTY_PH4>,-   ; Type = Phase IV routing

	<DFA,l,   80>,-		; Delay factor	
	<DWE,l,    5>,- 	; Delay weight	
	<RFA,l,   10>,-		; Retransmit factor 
	<ITI,l,   45>,-		; Incoming timer    		(units = sec)
	<OTI,l,   45>,-		; Outgoing timer    		(units = sec)
	<IAT,l,   60>,-		; Inactivity timer  		(units = sec)
	<RTI,l,  600>,-		; Routing timer			(units = sec)
	<BRT,l,   40>,-		; Broadcast routing timer       (units = sec)
	<RSI,l,    1>,-		; Min routing delay interval	(units = sec)
	<MLK,l,   32>,-		; Maximum supported logical links
	<MCO,l, 1022>,-		; Maximum cost
	<MHO,l,   30>,-		; Maximum hops
	<MVI,l,   63>,-		; Maximum visits
	<MBR,l,   32>,-		; Maximum broadcast routers
	<MBE,l,   64>,-		; Maximum broadcast endnodes
   >

NET$G_LNI_AREA::		; Defaults for area routers
    $CNF_DEF_TAB LNI,-

   <-
	<MAR,l,   63>,-		; Max areas
	<AMC,l, 1022>,-		; Area max cost
	<AMH,l,   30>,-		; Area max hops
   >

$CNF_DEF  NDI,-			; Remote Node Default Parameter Settings
				; No default settings

$CNF_DEF  CRI,-			; Defaults for all types of circuits

   <-
	<STA,l, NMA$C_STATE_OFF>,- ; State set by user
   >

NET$G_CRI_TRN::			; Defaults for all types of Transport circuits
    $CNF_DEF_TAB CRI,-

   <-
	<COS,l,  10>,-		; Cost
	<HET,l,  15>,-		; Hello timer		 (seconds)
   >

NET$G_CRI_DDCMP::		; Defaults for DDCMP circuits
    $CNF_DEF_TAB CRI,-

   <-
	<SER,v, NMA$C_LINSV_ENA>,- ; Service enabled
	<VER,v, NMA$C_CIRVE_DIS>,- ; Transport verification (0 if requested)
   >

NET$G_CRI_X25::			; Defaults for X25 circuits
    $CNF_DEF_TAB CRI,-

   <-
	<TYP,l, NMA$C_CIRTY_X25>,- ; Circuit type
   >

NET$G_CRI_DLM::			; Defaults for DLM circuits
    $CNF_DEF_TAB CRI,-

   <-
	<VER,v, NMA$C_CIRVE_DIS>,- ; Transport verification (0 if requested)
   >

NET$G_CRI_DLMOUT::		; Defaults for outgoing DLM circuits
    $CNF_DEF_TAB CRI,-

   <-
	<MRC,l, 100>,-		; Prevent infinite outgoing calls
	<RCT,l, 60>,-		; Retry call every 60 seconds
   >

NET$G_CRI_NI::			; Defaults for NI circuits
    $CNF_DEF_TAB CRI,-

    <-
	<SER,v,  NMA$C_LINSV_DIS>,- ; Service disabled
	<TYP,l,  NMA$C_CIRTY_NI>,-  ; Circuit type = Ethernet (read only)
	<RPR,l,  64>,-		; Router priority on NI
    >

$CNF_DEF  PLI,-			; Defaults for all types of physical lines

   <-
	<STA,l, NMA$C_STATE_OFF>,- ; State set by user
   >

NET$G_PLI_DDCMP::		; Defaults for DDCMP lines
    $CNF_DEF_TAB PLI,-

   <-
	<DUP,v, NMA$C_DPX_FUL>,-   ; Duplex		(set if halfduplex)
	<CON,v, NMA$C_LINCN_NOR>,- ; Controller		(set if loopback)
	<BFN,l,  4>,-		; Number of rcv buffers in pool	
	<STI,l, 4000>,-		; Service I/O timeout value of 4 seconds
   >

NET$G_PLI_LAPB::		; Defaults for LAPB lines
    $CNF_DEF_TAB PLI,-

   <-
	<CON,v, NMA$C_LINCN_NOR>,- ; Controller		(set if loopback)
	<BFN,l,  4>,-		; Number of rcv buffers in pool	
   >

NET$G_PLI_NI::			; Defaults for NI lines
    $CNF_DEF_TAB PLI,-

   <-
	<CON,v, NMA$C_LINCN_NOR>,- ; Controller		(set if loopback)
	<BFN,l,  6>,-		; Number of rcv buffers in pool	
	<BFS,l,  1498>,-	; Size of desired transport messages
	<STI,l, 4000>,-		; Service I/O timeout value of 4 seconds
   >

$CNF_DEF  OBI,-			; Network Object Default Parameter Settings
				; No default settings

$CNF_DEF  ESI,-			; Event Sink default parameter settings
				; No default settings

$CNF_DEF  EFI,-			; Event Filter default parameter settings
				; No default settings

$CNF_DEF  LLI,-			; Logical-link default parameter settings
				; No default settings

$CNF_DEF  SPI,-			; Server Process default parameter settings
				; No default settings

$CNF_DEF  AJI,-			; Adjacency default parameter settings
				; No default settings

$CNF_DEF  SDI,-			; Service (DLE) default parameter settings
				; No default settings

$CNF_DEF  ARI,-			; Adjacency default parameter settings
				; No default settings


	.PSECT	TABLES_IMPURE,WRT,EXE,GBL,LONG

;
;  Build the TASK OBI
;
BLD_OBI_TASK:	$BLDCNF obi,<-
			    <obi,s,nam, TASK>	-; Object name
			    <obi,l,num, 0>	-; Object number 
		>
BLD_OBI_FAL:	$BLDCNF obi,<-
			    <obi,s,nam, FAL>	-; Object name
			    <obi,l,num, 17>	-; Object number
			    <obi,s,fid, <FAL.EXE>>-; Object filespec
		>
BLD_OBI_HLD:	$BLDCNF obi,<-
			    <obi,s,nam, HLD>	-; Object name
			    <obi,l,num, 18>	-; Object number
		>
BLD_OBI_NML:	$BLDCNF obi,<-
			    <obi,s,nam, NML>	-; Object name
			    <obi,l,num, 19>	-; Object number
			    <obi,s,fid, <NML.EXE>>-; Object filespec
		>
BLD_OBI_REMACP:	$BLDCNF obi,<-
			    <obi,s,nam, REMACP>	-; Object name
			    <obi,l,num, 23>	-; Object number
		>
BLD_OBI_MIRROR:	$BLDCNF obi,<-
			    <obi,s,nam, MIRROR>	-; Object name
			    <obi,l,num, 25>	-; Object number
		>
BLD_OBI_EVL:	$BLDCNF obi,<-
			    <obi,s,nam, EVL>	-; Object name
			    <obi,l,num, 26>	-; Object number
		>
BLD_OBI_MAIL:	$BLDCNF obi,<-
			    <obi,s,nam, MAIL>	-; Object name
			    <obi,l,num, 27>	-; Object number
			    <obi,l,prx, NMA$C_ACES_OUTG> - ; Proxy access
			    <obi,s,fid, <MAIL.EXE>>-; Object filespec
		>
BLD_OBI_PHONE:	$BLDCNF obi,<-
			    <obi,s,nam, PHONE>	-; Object name
			    <obi,l,num, 29>	-; Object number
			    <obi,l,prx, NMA$C_ACES_OUTG> - ; Proxy access
			    <obi,s,fid, <PHONE.EXE>>-; Object filespec
		>
BLD_OBI_CTERM:	$BLDCNF obi,<-
			    <obi,s,nam, CTERM>	-; Object name
			    <obi,l,num, 42>	-; Object number
		>
BLD_OBI_DTR:	$BLDCNF obi,<-
			    <obi,s,nam, DTR>	-; Object name
			    <obi,l,num, 63>	-; Object number
		>
BLD_OBI_MOM:	$BLDCNF obi,<-
			    <obi,s,nam, $MOM>	-; Object name
			    <obi,l,num, 0>	-; Object number
		>
BLD_OBI_NICONFIG:
		$BLDCNF obi,<-
			    <obi,s,nam, $NICONFIG> -; Object name
			    <obi,l,num, 0>	-; Object number
		>

;
;  Build NDI block for the local node
;
BLD_NDI_LOC:	$BLDCNF ndi,<-
			    <ndi,l,ADD, 0>	-; Address is 0 at boot time
		>

;
;  Build the LNI block
;
BLD_LNI:	$BLDCNF lni,<-
			    <lni,l,sta, LNI$C_STA_INIT>	-; Local state

			    <lni,l,add, 0>	-; Local address
			    <lni,l,mln, 16>	-; Max circuits
			    <lni,l,mbu, 100>	-; Max buffers	
			    <lni,l,mad, 32>	-; Max node address
			    <lni,l,bus, 576>	-; Buffer size	
			    <lni,l,piq, 3000>	-; Pipeline quota
			    <lni,s,nve, 400>	-; NSP version is 4.0.0	
			    <lni,s,rve, 200>	-; Routing version is 2.0.0
			    <lni,s,mve, 400>	-; Network Management version 
						-; is 4.0.0

			    <lni,s,ide, <DECnet-VAX V4.0,  VMS 12345678>> -
			>

.=.-12					; Backup in order to stuff version 
NET$GQ_VERSION:: .BLKB	8		; of VMS

	 .LONG	0		; All $BLDCNF blocks are terminated by
				; a zero


	.PSECT	NET_INIT_CODE,NOWRT,EXE
;+
; NET$INI_CONFIG - Init internal configuration data base
;
; FUNCTIONAL DESCRIPTION:
;
;-

NET$INI_CONFIG::
	.WORD ^M<R2,R3,R4,R5,R6,R7,R8,R9,R10,R11>

	CLRL	NET$GL_FLAGS		; Clear internal flags
	;
	;   Allocate the utility buffer
	;
	MOVZWL	#NET$C_UTLBUFSIZ,R1	; Set size of utility buffer
	JSB	NET$ALLOCATE		; Allocate the storage
	MOVL	R2,NET$GL_UTLBUF	; Store address of buffer
	;
	; Allocate a dummy NDI CNF block which is used to represent
	; nodes which are reachable, but not in the NDI list (phantom).
	;
	MOVL	NET$GL_CNR_NDI,R11	; Setup NDI root
	JSB	CNF$INIT_UTL		; Init "utility buffer" as a CNF
	CLRL	R8			; Use harmless field value
	$CNFFLD	ndi,l,add,R9		; Stuff it into the CNF
	JSB	CNF$PUT_FIELD		;
	BLBS	R0,4$			; If LBS then okay
	BUG_CHECK  NETNOSTATE,FATAL	; Programming error
4$:	JSB	CNF$CLONE		; Clone a CNF copy from ACP pool
	BLBS	R0,6$			; If LBS then okay
	BUG_CHECK  NETNOSTATE,FATAL	; Programming error
6$:	BISB	#CNF$M_FLG_ACP,-	; Mark it as the catch-all CNF
		    CNF$B_FLG(R10)	;
	MOVL	R10,NET$GL_DUM_NDI	; Store it
	;
	;   Initialize CNR pointers to the tree root
	;
	CLRL	NET$GL_COL_TREE		; Initialize the COLLATE root
	CLRL	NET$GL_NAME_TREE	; Initialize the NAME root
	;
	;   Insert minimally required entries into data base
	;
	MOVAL	BLD_VEC,R6		; Setup vector pointer
10$:	MOVL	(R6)+,R5		; Get address of parameter buffer
	BEQL	60$			; Done if EQL
	MOVL	(R5)+,R11		; Get the CNR pointer
	JSB	CNF$INIT_UTL		; Init "utility buffer" as a CNF
	BLBS	R0,20$			; If LBS then okay
	BUG_CHECK NETNOSTATE,FATAL	; Else programming bug
20$:	MOVL	(R5)+,R9		; Get next field i.d.
	BEQL	50$			; If EQL then no more fields
	MOVZBL	(R5)+,R7		; Get size of string
	BEQL	30$			; If EQL then its not a string
	MOVAB	(R5),R8			; Point to string
	ADDL	R7,R5			; Advance past string
	BRB	40$			; Continue in common
30$:	MOVL	(R5)+,R8		; Get field value
40$:	JSB	CNF$PUT_FIELD		; Move the field to the CNF block
	BLBS	R0,20$			; If LBS then successful, loop
	BUG_CHECK NETNOSTATE,FATAL	; Else programming bug
50$:	PUSHL	R6			; Save reg
	CLRL	R6			; Say "no former CNF to replace"
	JSB	CNF$INSERT		; Insert the CNF block
	POPL	R6			; Restore reg
	BLBS	R0,10$			; Loop if successful
	BUG_CHECK NETNOSTATE,FATAL	; Else a bug exists
	;
	;   Initialize the NDC counter area
	;
60$:	BSBW	NET$INIT_NDCOU		; Initialize NDC area
	;
	;   Done
	;
100$:	MOVL	#1,R0			; Indicate success
	RET				; Done

.END
