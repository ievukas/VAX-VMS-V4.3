$ !
$ !          [MSGFIL.COM] M S G F I L B L D . C O M
$ !
$ !     ASSUME SRCD$:[SYSMSG]FILES.DAT , SRCD$:[SYSMSG]SUBSYSIDS.DAT
$ !	AND ALL THE FILES REFERENCED IN SRCD$:[SYSMSG]FILES.DAT
$ !
$ GOTO 'PHASE'
$
$INIT:
$ !
$ ! CREATE THE DIRECTORIES
$ !
$ @SYSBLDCOM$:CREATEDIR
$ DELETE SRCD$:[SYSMSG]SYSMSG.TLB;*
$ LIB/TEXT/CREATE SRCD$:[SYSMSG]SYSMSG
$ EXIT
$
$UPDATE:
$ !
$ !	Create updated sources.
$ !
$ DELETE SRC$:*.*;*
$ SRCUPDATE MSGDEF.SDL		! FOR VMSLIB CREATION OF LIB.MLB
$ SRCUPDATE MSGINT.MDL
$ SRCUPDATE CVTMSGCOM.FOR
$ SRCUPDATE MSG.REQ
$ SRCUPDATE SDLGENREQ.REQ
$ COPY MASD$:[SYSMSG]FILES.DAT SRCD$:[SYSMSG]
$ COPY MASD$:[SYSMSG]SUBSYSIDS.DAT SRCD$:[SYSMSG]
$ SRCUPDATE SYSFAC.DAT
$ !
$ !	THIS MDL OUTPUT MUST BE CREATED IN THE UPDATE PHASE BECAUSE
$ !	THE VMSLIB COMPONENT USES IT IN THE SRCLIB PHASE.
$ !
$ DELSHRLIB LIB$:MSGDEF.R32
$ DELSHRLIB LIB$:MSGDEF.MAR
$ DELETE LIB$:MSGDEF.MAR;*, .R32;*
$ SDL/VMS/COPYRIGHT SRC$:MSGDEF/LANGUAGE=(BLISS=LIB$,MACRO=LIB$)
$ CPYSHRLIB LIB$:MSGDEF.R32		! Share file with VMSLIB
$ CPYSHRLIB LIB$:MSGDEF.MAR		! Share file with VMSLIB
$ EXIT
$
$SRCLIB:
$ !
$ !	TRANSLATE THE MDL FILE
$ !
$ DELETE LIB$:MSGINT.MAR;*, .B32;*
$ MCR MDL LIB$:MSGINT=SRC$:MSGINT
$ !
$ ! CREATE THE SYSTEM-WIDE MESSAGE FILE
$ !
$ DELETE LIB$:*.MSG;*
$ DELETE *.MSG;*
$ EXIT 1
$
$ASSEM:
$ !
$ ! DO THE COMPILATIONS
$ !
$ PURGE OBJ$:*.OBJ,*.OBB, LIS$:*.LIS
$ @SYSBLDCOM$:FASM 'FACNAM' "" NOLIB 'UPDATES'
$ CPYSYSMSG OBJ$:MSGFILMSG.OBB
$ EXIT
$
$OBJLIB:
$ !
$ ! CREATE THE OBJECT LIBRARY
$ !
$ @SYSBLDCOM$:OBJLIB MESSAGE
$ EXIT
$
$LNKLIB:
$ !
$ ! Null LNKLIB phase
$ !
$ EXIT
$
$LINK:
$ !
$ ! CREATE THE LINKED IMAGE AND MAP
$ !
$ DELETE EXE$:MESSAGE.EXE;*, MAP$:MESSAGE.MAP;*
$ LINK /EXE=EXE$:MESSAGE/MAP=MAP$:MESSAGE/CROSS/FULL  /NOTRACE -
	LIB$:MESSAGE/LIB/INCLUDE=MAIN
$ DELETE EXE$:CVTMSG.EXE;*, MAP$:CVTMSG.MAP;*
$ LINK /EXE=EXE$:CVTMSG /MAP=MAP$:CVTMSG/CROSS/FULL  -
	LIB$:MESSAGE/INCLUDE=CVTMSG/LIBRARY
$
$ ON ERROR THEN GOTO CLEANUP2
$ !DEFINE MESSAGE EXE$:MESSAGE		! USE COMPILER JUST BUILT
$ DEFINE MESSAGE SYS$SYSTEM:MESSAGE	! USE FOLKLORE - ONE JUST BUILT
$					! REQUIRES FEATURES IN NEW SYS.EXE
$					! (SYS$SRCHANDLER ENTRY POINT)
$
$ @COM$:SYSMSGEDT			! Create smaller SYSMSG.TLB
$ LIB/TEXT/EXTRACT=(SYSFAC,*MSG)/OUT=LIB$:SYSMSG.MSG -
	SRCD$:[SYSMSG]SYSMSG
$
$ DELETE OBJ$:SYSMSG.OBJ;*, LIS$:SYSMSG.LIS;*
$ MESSAGE /OBJ=OBJ$:SYSMSG /LIST=LIS$:SYSMSG -
	LIB$:SYSMSG.MSG
$ DELETE EXE$:SYSMSG.EXE;*, MAP$:SYSMSG.MAP;*
$ LINK  /SHARE=EXE$:SYSMSG /MAP=MAP$:SYSMSG/FULL OBJ$:SYSMSG
$CLEANUP2:
$ DEASSIGN MESSAGE
$ @COM$:BLDSHRMSG
$ EXIT
$
$RESULT:
$ !
$ ! COPY THE SYSTEM-WIDE MESSAGE FILES TO [SYSMSG]
$ !
$ CPYSYSMSG EXE$:SYSMSG.EXE
$ CPYSYSMSG EXE$:CLIUTLMSG.EXE
$ CPYSYSMSG EXE$:DBGTBKMSG.EXE
$ CPYSYSMSG EXE$:FILMNTMSG.EXE
$ CPYSYSMSG EXE$:NETWRKMSG.EXE
$ CPYSYSMSG EXE$:PRGDEVMSG.EXE
$ CPYSYSMSG EXE$:SHRIMGMSG.EXE
$ CPYSYSMSG EXE$:SYSMGTMSG.EXE
$ !
$ ! COPY THE .EXE AND .MAP TO THE SYSTEM AREA
$ !
$ CPYRESEXE EXE$:MESSAGE.EXE
$ CPYRESMAP MAP$:MESSAGE.MAP
$ CPYRESLIS LIS$:*.LIS MESSAGE.LSS
