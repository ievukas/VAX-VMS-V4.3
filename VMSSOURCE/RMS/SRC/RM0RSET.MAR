	$BEGIN	RM0RSET,000,RM$RMS0,<SETUP FOR A RAB FUNCTION>,<NOWRT,QUAD>

;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982, 1984 BY				    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;

;++
; Facility: rms32
;
; Abstract:
;		routine to perform common setup for a rab function
;
; Environment:
;		star processor running starlet exec.
;
; Author: L F Laverdure,	 creation date: 5-JAN-1977
;
; Modified By:
;
;	V03-005	RAS0269		Ron Schaefer		21-Mar-1984
;		Improve performance by not performing the useless
;		probe of the ERR= or SUC= routines here.
;		Arglist gets probed when those routines are used anyway.
;
;	V03-004	DAS0001		David Solomon		2-Feb-1984
;		Don't call RM$RAISE_LOCK unless sharing.
;
;	V03-003	KBT0487		Keith B. Thompson	2-Feb-1983
;		Don't take the file lock out for ISAM
;
;	V03-002	KBT0318		Keith B. Thompson	8-Sep-1982
;		Remove all S0 sharing code
;
;	V03-001	KBT0215		Keith B. Thompson	23-Aug-1982
;		Reorganize psects
;
;	V02-017	CDS0001		C Saether	10-Dec-1981
;		Rename psect.
;
;	V02-016	REFORMAT	P S Knibbe	23-Jul-1980
;
;--


	.SBTTL	DECLARATIONS

;
; Include Files:
;

;
; Macros:
;

	$IMPDEF
	$RABDEF
	$IRBDEF
	$IFBDEF
	$RMSDEF

;
; Equated Symbols:
;

;
; Own Storage:
;


	.SBTTL	RM$RSET - COMMON SETUP FOR RAB FUNCTION ROUTINE

;++
;
;  RM$RSET
;  RM$RSET_ALT
;
;      this routine performs common setup for a rab function call
;      including the following:
;
;	1. call rm$rabchk to check arglist, set base regs
;	2. check for valid isi and set irab and ifab addr
;	3. check for stream idle and set to busy
;	4. clear the sts and stv fields of the rab
;	5. set asy irab bit from rab
;	6. store the arglist addr and caller's mode in the irab
;	   and save sp entry value in imp$l_saved_sp
;	7. perform additional setups including checking fac for
;	   operation and clearing find_last flag
;
;
; Calling sequence:
;
;   called via the $rabset macro which expands into:
;
;	bsbw	rm$rset
;	.byte	function_code
;
;  alternate entry at rm$rset_alt to perform functions  4, 5, 6, & 7 only.
;  r7, r8, r9, r10, and r11 must be set as per output prior to call.
;
;
; Input Parameters:
;
;	sp	stack pointer
;	ap	argument list addr
;
; Implicit Inputs:
;
;	the in-line 1-byte function code.
;	the contents of the rab
;
; Output Parameters:
;
;	r11	impure area address
;	r10	ifab address
;	r9	irab address
;	r8	rab address
;	r7	caller's mode
;	r0 thru r5 destroyed
;
; Implicit Outputs:
;
;	imp$l_saved_sp is set to the value of sp+4
;	the sts and stv fields of the rab are zeroed
;
; Completion Codes:
;
;	none.  if an error is detected returns to user (not caller)
;	after appropriate cleanup.  the user receives a standard
;	rms error code, in particular, isi, act, and those returned by
;	rm$rabchk.
;
;	if all o.k., returns to caller after the 1-byte
;	in-line function code.
;
; Side Effects:
;
;	none
;
;--

;
;  entry point to rm$rset
;
;  validate rab and perform other basic setups
;

RM$RSET::
	BSBW	RM$RABCHK		; valid rab? (exits if invalid)

;
;	NOTE: SETS R11 TO IMPURE ADDR
;	     r9 to isi
;	     r8 to rab addr
;	     r7 to caller's mode
;
;  get the irab address and check for a valid irab
;

	BEQL	100$				; branch if isi value bad
	CMPW	R9,#IMP$C_ENTPERSEG		; simple isi case (in 1st segment)?
	BGTRU	10$				; branch if so
	MOVL	IMP$L_IRBTBLINK(R11)[R9],R9	; get irab address
100$:	BNEQ	5$				; branch if irab allocated
	BRW	ERRISI				; error is irab not allocated

;
;  convert isi value to irab address (general case)
;

10$:	MOVL	#IMP$L_IRABTBL/4,R0		; irab table offset/4
	BSBW	RM$GTIADR			; get irab addr
	BRB	100$				; continue

15$:	BRW	ERRBUG				; error...invalid irab

5$:	CMPB	IRB$B_BID(R9),#IRB$C_BID
	BNEQ	15$				; branch if valid irab

;++
;
;  set r10 = ifab address
;
;--

	MOVL	IRB$L_IFAB_LNK(R9),R10		; get ifab address
	CMPB	IFB$B_BID(R10),#IFB$C_BID	; really an ifab?
	BNEQ	15$
	BBS	#IFB$V_BUSY,(R10),ERRACT	; branch if ifab busy

;++
;
;  set busy, checking if already active, and clear async and ppf_image flags
;
;--

	BBSS	#IRB$V_BUSY,(R9),ERRRSA		; set busy, branch if busy already

	IRB$M_PPF_IMAGE	= 1@<IRB$V_PPF_IMAGE-<IRB$L_BKPBITS*8>>
	IRB$M_ASYNC	= 1@<IRB$V_ASYNC-<IRB$L_BKPBITS*8>>

20$:	BICB2	#IRB$M_PPF_IMAGE!IRB$M_ASYNC,IRB$L_BKPBITS(R9); clear flags
	BBS	#RAB$V_PPF_IND+<RAB$W_ISI*8>,(R8),CHKIND; branch if indirect ppf

;++
;
;  alternate entry from rms0conn here
;
;  clear rab$l_sts and rab$l_stv
;  set asynchronous flag if required and probe async arg list
;  store caller's mode and arglist addr in irab
;
;--

RM$RSET_ALT::

	ASSUME	RAB$L_STV	EQ	RAB$L_STS+4

	CLRQ	RAB$L_STS(R8)		; zero sts and stv in rab

;
;  perform asynchronous setup
;

	ASSUME	RAB$V_ASY	EQ	0

	BLBC	RAB$L_ROP(R8),50$	; branch if sync

	ASSUME	IMP$W_RMSSTATUS	EQ	0
	ASSUME	IMP$V_IIOS	EQ	0

	BLBC	(R11),50$		; branch if this is a ppf forcing
					; synchronous operation

	SSB	#IRB$V_ASYNC,(R9)	; flag async operation

;
;  store caller's mode and arglist addr in irab
;

50$:	MOVB	R7,IRB$B_MODE(R9)	; save caller's mode
	MOVL	AP,IRB$L_ARGLST(R9)	; save pointer to arglist
	MOVL	R8,IRB$L_LAST_RAB(R9)	; save addr this rab

;++
;
;  pick up in-line byte specifying optional functions and checking
;
;--

	MOVZBL	@0(SP),R1		; get byte
	BEQL	SAVESP			; branch if nothing to do
	BBCC	#4,R1,60$		; branch if no need to clr find_last

;++
;
;  cflg (bit 4 set) - clear last-operation-was-a-find flag
;
;--

	CSB	#IRB$V_FIND_LAST,(R9)

;++
;
;  verify accessed for block i/o or not based on bio (bit 3) parameter
;
;--

60$:	BBSC	#3,R1,CHKBIO		; branch if a block i/o function
	BBS	#IFB$V_BIO,-		; branch if file accessed
		IFB$B_FAC(R10),-	;  for block i/o
		RM$ERRIOP		;  (invalid operation)
	BBSC	#IRB$V_BIO_LAST,-	; this is not a block operation so
		(R9),CHKFAC		;  clear block i/o last

;++
;
;  check for appropriate access for function
;  (note: r1 now has bit offset to required fac access)
;
;--

CHKFAC:	BBS	#5,R1,SAVESP			; branch if flag says any fac ok
	BBC	R1,IFB$B_FAC(R10),RM$ERRFAC	; branch if needed access not on

SAVESP:	ADDL3	#4,SP,IMP$L_SAVED_SP(R11)	; save stack entry value

;+
; Take lock on file if sharing and if not ISAM.
;-

	TSTL	IFB$L_SFSB_PTR(R10)		; are we sharing?
	BEQL	10$				; no, no need to lock file
	CMPB	#IFB$C_IDX,IFB$B_ORGCASE(R10)	; is this ISAM?
	BEQL	10$				; if so don't lock yet
	BSBW	RM$RAISE_LOCK			; get lock on the file
	BLBC	R0,ERROR			; get out on error

;
;  bump return pc past the in-line function byte and return
;

10$:	INCL	(SP)
	RSB

;++
;
;  this is an indirect operation on a ppf.  set ppf_image flag.
;
;--

CHKIND:	BBCS	#IRB$V_PPF_IMAGE,(R9),RM$RSET_ALT; say its indirect and branch


;++
;
;  error returns
;
;--

ERRACT:
	RMSERR	ACT			; stream already active
	BRB	ERROR

ERRRSA:
	RMSERR	RSA			; record stream active

ERROR:	BRW	RM$EX_NOSTR

ERRISI:
	RMSERR	ISI			; invalid isi value
	BRB	ERROR

;
;  internal rms problem  -  irab table pointed to an invalid irab
;			    or irab pointed to invalid ifab!
;

ERRBUG:	RMSTBUG	FTL$_BADIFAB

;++
;
;  entry point from rms$delete (attempted delete for seq. file org)
;
;--

RM$ERRIOP::
	RMSERR	IOP			; wrong type of access re. bio
	BRB	ERROR1

RM$ERRFAC::
	RMSERR	FAC			; not accessed for function
ERROR1:	BRW	RM$EXRMS		; get out



;++
;
;  file must be accessed to allow block i/o
;
;--

CHKBIO:	BBS	#IFB$V_BIO,IFB$B_FAC(R10),CHKFAC	; branch if file accessed for bio
	BBC	#IFB$V_BRO,IFB$B_FAC(R10),RM$ERRIOP	; branch if also not block
							;  and record access
;++
;
;  file is accessed for mixed block and record i/o
;
;  set the block i/o last flag and also set the 'switched between block
;  and record i/o' flag if last operation was not a block i/o operation,
;  else clear flag
;
;--

	CSB	#IRB$V_BRO_SW,(R9)
	BBSS	#IRB$V_BIO_LAST,(R9),CHKFAC
	BBCS	#IRB$V_BRO_SW,(R9),CHKFAC		; set bro_sw and branch

	.END
