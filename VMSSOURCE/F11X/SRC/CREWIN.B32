MODULE CREWIN (
		LANGUAGE (BLISS32),
		IDENT = 'V04-000'
		) =
BEGIN

!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982, 1984 BY				    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************

!++
!
! FACILITY:  F11ACP Structure Level 2
!
! ABSTRACT:
!
!	This routine creates and initializes a file window.
! ENVIRONMENT:
!
!	STARLET operating system, including privileged system services
!	and internal exec routines. This routine must be called
!	in kernel mode.
!
!--
!
!
! AUTHOR:  Andrew C. Goldstein,	 CREATION DATE:  14-Dec-1976  17:10
!
! MODIFIED BY:
!
!	V03-003	CDS0002		Christian D. Saether	18-Apr-1984
!		Mask off unused bits when copying access control flags
!		to wcb$w_acon, and copy FIB$V_NOLOCK state to WCB$V_NOACCLOCK.
!
!	V03-002	CDS0001		Christian D. Saether	29-Dec-1983
!		Use L_NORM linkage and BIND_COMMON macro.
!
!	V03-001	LMP0016		L. Mark Pilant,		25-Mar-1982  13:20
!		Remove diddling of the COMPLETE bit in the window segments.
!
!	V02-002	LMP0003		L. Mark Pilant,		17-Nov-1981  14:35
!		Add support for segmented windows.
!
!	V02-001	ACG0167		Andrew C. Goldstein,	16-Apr-1980  19:25
!		Previous revision history moved to F11B.REV
!**


LIBRARY 'SYS$LIBRARY:LIB.L32';
REQUIRE 'SRC$:FCPDEF.B32';

GLOBAL ROUTINE CREATE_WINDOW (ACCTL, SIZE, HEADER, PID, FCB) : L_NORM =

!++
!
! FUNCTIONAL DESCRIPTION:
!
!	This routine creates a file access window.
!
! CALLING SEQUENCE:
!	CREATE_WINDOW (ARG1, ARG2, ARG3, ARG4, ARG5)
!
! INPUT PARAMETERS:
!	ARG1: access control word (from FIB, usually)
!	ARG2: size of window in # of pointers
!	ARG3: address of file header
!	ARG4: PID of accessor
!	ARG5: address of file FCB
!
! IMPLICIT INPUTS:
!	CURRENT_VCB: address of VCB of volume in process
!	CURRENT_UCB: address of UCB of disk in process
!
! OUTPUT PARAMETERS:
!	NONE
!
! IMPLICIT OUTPUTS:
!	NONE
!
! ROUTINE VALUE:
!	address of window
!
! SIDE EFFECTS:
!	window block created
!
!--

BEGIN

MAP
	ACCTL		: BBLOCK,	! access control flags
	HEADER		: REF BBLOCK,	! file header arg
	FCB		: REF BBLOCK;	! FCB arg

LOCAL
	WINDOW_SIZE,			! actual size of window
	WINDOW		: REF BBLOCK,	! window created
	PRIMARY_WINDOW	: REF BBLOCK;	! address of the primary window

BIND_COMMON;

EXTERNAL ROUTINE
	ALLOCATE	: L_NORM,	! allocate dynamic memory
	TURN_WINDOW	: L_NORM;	! window turner routine

! Compute the size of the window. If fixed, allocate it and turn it to
! map VBN 1. If a maximal window is requested (indicated by a size of -1),
! the window turner will allocate the window.
!

WINDOW_SIZE = .SIZE;
IF .WINDOW_SIZE EQL 0
THEN WINDOW_SIZE = .CURRENT_VCB[VCB$B_WINDOW];

IF .WINDOW_SIZE NEQ -1
THEN
    BEGIN
    IF .WINDOW_SIZE GTRU MAX_WINDOW
    THEN WINDOW_SIZE = MAX_WINDOW;
    IF .WINDOW_SIZE LSSU MIN_WINDOW
    THEN WINDOW_SIZE = MIN_WINDOW;
    WINDOW = ALLOCATE (.WINDOW_SIZE * 6 + WCB$C_LENGTH, WCB_TYPE);
    IF .WINDOW NEQ 0
    THEN TURN_WINDOW (.WINDOW, .HEADER, 1, 1);
    END
ELSE
    WINDOW = TURN_WINDOW (0, .HEADER, 1, 1);

! Init cells within the window
!

PRIMARY_WINDOW = .WINDOW;
UNTIL .WINDOW EQL 0
DO
    BEGIN
    WINDOW[WCB$L_PID]	= .PID;			! accessor PID
    WINDOW[WCB$L_ORGUCB]	= .CURRENT_UCB;	! original device UCB
    WINDOW[WCB$W_ACON]	= .ACCTL [0,0,16,0];	! access control bits
    WINDOW[WCB$W_ACON] = .WINDOW[WCB$W_ACON]	! mask off unused flags
	AND (FIB$M_NOWRITE+FIB$M_DLOCK+FIB$M_SPOOL+FIB$M_WRITECK+FIB$M_SEQONLY
		+FIB$M_WRITE+FIB$M_READCK+FIB$M_NOREAD+FIB$M_NOTRUNC);
    IF (.ACCTL [FIB$V_NOLOCK]			! This assumes FIB$L_ACCTL
	AND .CLEANUP_FLAGS [CLF_SYSPRV])
    THEN WINDOW [WCB$V_NOACCLOCK] = 1;		! is first longword in FIB.
    WINDOW[WCB$L_FCB]	= .FCB;			! FCB address
    WINDOW[WCB$V_READ]	= 1;			! read access always allowed
    WINDOW[WCB$V_WRITE]	= .WINDOW[WCB$V_WRITEAC]; ! write access sometimes
    IF .CURRENT_RVN NEQ 0 THEN WINDOW[WCB$L_RVT] = .CURRENT_VCB[VCB$L_RVT];
    IF .HEADER[FH2$V_READCHECK] THEN WINDOW[WCB$V_READCK] = 1;
    IF .HEADER[FH2$V_WRITCHECK] THEN WINDOW[WCB$V_WRITECK] = 1;
    WINDOW = .WINDOW[WCB$L_LINK];		! set address of next segment
    END;

RETURN .PRIMARY_WINDOW;

END;					! end of routine CREATE_WINDOW

END
ELUDOM
