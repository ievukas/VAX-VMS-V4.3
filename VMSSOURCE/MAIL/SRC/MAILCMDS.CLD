!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982, 1984 BY				    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************
module	mail$command_table
!
! Version:	'V04-000'
!
! MODIFIED BY:
!
!	V03-014	ROP0016		Robert Posniak		02-JUL-1984
!		Change p2 in Set Forward to a appropriate
!		type.
!
!	V03-013	BLS0292		Benn Schreiber		 8-APR-1984
!		Add Send/attach hook
!
!	V03-012	BLS0263		Benn Schreiber		 4-FEB-1984
!		Add COMPRESS command.
!
!	V03-011	BLS0255		Benn Schreiber		28-Dec-1983
!		Add some disallows where appropriate.  Add SHOW KEY command.
!
!	V03-010	BLS0246		Benn Schreiber		28-Nov-1983
!		Add /edit to several commands.
!
!	V03-009	BLS0235		Benn Schreiber		23-Aug-1983
!		Add prompts to define/key, remove $infile value types
!		where incorrectly applied.
!
!	V03-008	BLS0229		Benn Schreiber		16-Jul-1983
!		Add SHOW NEW_MAIL_COUNT command
!
!	V03-007	BLS0225		Benn Schreiber		17-Jun-1983
!		Forgot to update SHOW WASTEBASKET_NAME.
!
!	V03-006	BLS0222		Benn Schreiber		15-May-1983
!		Change command to SET WASTEBASKET_NAME
!
!	V03-005	BLS0220		Benn Schreiber		30-Apr-1983
!		Everybody hated typing DE, so D is back.  Add SET/SHOW
!		of AUTO_PURGE, MAIL_DIRECTORY
!
!	V03-004	BLS0217		Benn Schreiber		12-Apr-1983
!		Remove D as a legal command.  Add SPAWN, ATTACH, and
!		/SELF qualifier for SEND, REPLY.  Add SET COPY command.
!
!	V03-003	PCA1008		Paul C. Anagnostopoulos 29-Mar-1983
!		Change the way in which verb name abbreviations are
!		specified in order to conform to the new CDU.
!
!	V03-002	BLS0214		Benn Schreiber		27-Mar-1983
!		Add DELETE/ALL, REPLY/EXTRACT, SHOW DELETED, SHOW FOLDER,
!		and make E and EX synonyms for EXIT
!
define syntax attach_parent
parameter p1,value
qualifier parent,default

define verb attach
routine mail$cmd_attach
parameter p1,value(required),prompt="Process"
qualifier parent,nonnegatable,syntax=attach_parent
disallow p1 and parent

define verb back
routine mail$cmd_back
qualifier edit

define verb compress
routine mail$cmd_compress
parameter p1,value(type=$infile),label=file
!qualifier fdl,value(required,type=$infile)
qualifier output,value(type=$outfile)

define verb current
routine mail$cmd_current
qualifier edit

define syntax define_key
parameter p1,value(required),prompt="Key name"
parameter p2,value(required),prompt="Translation"
qualifier echo,default
qualifier terminate
qualifier lock_state
qualifier if_state,value(required,list)
qualifier set_state,value(required)
qualifier log
disallow (not lock_state and terminate and set_state)
disallow (lock_state and not set_state)

define verb define
routine mail$cmd_define
qualifier key,syntax=define_key
disallow (not key)

define verb delete
synonym d
synonym de
cliflags(abbreviate)
routine mail$cmd_delete
parameter p1,label=delnum
qualifier all,nonnegatable
disallow all and delnum

define verb directory
routine mail$cmd_directory
parameter p1,label=folder_name,value
qualifier before,value(type=$datetime),nonnegatable
!qualifier flag
qualifier folder
!qualifier from,value,nonnegatable
qualifier full,nonnegatable
qualifier new
!qualifier replied
qualifier since,value(type=$datetime),nonnegatable
qualifier start,value(default="1")
disallow folder and (before or full or new or since)

define verb copy
routine mail$cmd_copy
parameter p1,label=folder_name,prompt="Folder",value(required)
parameter p2,label=file,value(type=$infile),prompt="File"
qualifier all,nonnegatable
qualifier confirm,default

define verb erase
routine mail$cmd_erase

define verb extract
routine mail$cmd_extract
parameter p1,label=extf,prompt="File",value(required,type=$infile)
qualifier all,nonnegatable
qualifier append,nonnegatable
qualifier header,default
qualifier mail,nonnegatable

define verb edit
routine mail$cmd_edit
parameter p1,label=input,prompt="File",value(required,type=$infile)
qualifier command,value(type=$infile)
qualifier create
qualifier journal,value(type=$infile)
qualifier output,value(type=$infile)
qualifier read,nonnegatable
qualifier recover,nonnegatable
disallow read and (create or recover)

define verb exit
synonym e
synonym ex
cliflags(abbreviate)
routine mail$cmd_set_exit

define verb first
routine mail$cmd_first
qualifier edit

define verb forward
routine mail$cmd_forward
qualifier edit
qualifier header,default

define verb help
routine mail$cmd_help
parameter p1,label=helpkeys,value(type=$rest_of_line)

define verb last
routine mail$cmd_last
qualifier edit

define verb move
synonym file
routine mail$cmd_move
parameter p1,label=folder_name,prompt="Folder",value(required)
parameter p2,label=file,value(type=$infile),prompt="File"
qualifier all,nonnegatable
qualifier confirm,default

define verb next
routine mail$cmd_next
qualifier edit

define verb print
synonym p
routine mail$cmd_print
qualifier all
qualifier copies,value(required)
qualifier notify
qualifier print
qualifier queue,value(required)
disallow print and (all or copies or queue)

define verb purge
routine mail$cmd_purge
qualifier reclaim,nonnegatable
qualifier statistics,nonnegatable

define verb quit
routine mail$cmd_set_quit

define verb reply
synonym r
synonym re
synonym answer
cliflags(abbreviate)
routine mail$cmd_reply
parameter p1,label=replf,value(type=$infile)
qualifier edit
qualifier extract
qualifier last,nonnegatable
qualifier self
disallow last and (edit or extract or replf)

define verb read
routine mail$cmd_read
parameter p1,label=read1,value
parameter p2,label=read2,value
qualifier before,value(type=$datetime),nonnegatable
qualifier edit
!qualifier flag
!qualifier from,value,nonnegatable
qualifier new
!qualifier replied
qualifier since,value(type=$datetime),nonnegatable

define verb select
routine mail$cmd_select
parameter p1,label=folder_name,prompt="Folder",value
qualifier before,value(type=$datetime),nonnegatable
!qualifier flag
!qualifier from,value,nonnegatable
qualifier new
!qualifier replied
qualifier since,value(type=$datetime),nonnegatable

define verb send
synonym s
synonym se
synonym mail
cliflags(abbreviate)
routine mail$cmd_send
parameter p1,label=send1,value(type=$infile)
qualifier attachment,nonnegatable
qualifier last,nonnegatable
qualifier subject,nonnegatable,value(required)
qualifier edit
qualifier self
disallow last and (send1 or edit)
disallow attachment and not send1

define verb search
routine mail$cmd_search
parameter p1,label=search1,value(type=$rest_of_line)
qualifier next,nonnegatable

define syntax set_auto_syntax
routine mail$cmd_set_auto
parameter p1,value(required)

define syntax set_noauto_syntax
routine mail$cmd_set_noauto
parameter p1,value(required)

define syntax set_dir_syntax
routine mail$cmd_set_dir
parameter p1,value(required)
parameter p2,label=file,value(required,type=$infile),prompt="Directory"
qualifier log

define syntax set_nodir_syntax
routine mail$cmd_set_nodir
parameter p1,value(required)
qualifier log

define syntax set_file_syntax
routine mail$cmd_set_file
parameter p1,value(required)
parameter p2,label=file,value(type=$infile)

define syntax set_folder_syntax
routine mail$cmd_select
parameter p1,value(required)
parameter p2,label=folder_name,prompt="Folder",value
qualifier before,value(type=$datetime),nonnegatable
!qualifier flag
!qualifier from,value,nonnegatable
qualifier new
!qualifier replied
qualifier since,value(type=$datetime),nonnegatable

define syntax set_forward_syntax
routine mail$cmd_set_forward
parameter p1,value(required)
parameter p2,label=setf1,prompt="Address",value(required)
qualifier user,value(required),nonnegatable

define syntax set_noforward_syntax
routine mail$cmd_set_noforward
parameter p1,value(required)
qualifier user,value(required),nonnegatable

define syntax set_name_syntax
routine mail$cmd_set_name
parameter p1,value(required)
parameter p2,label=setn1,prompt="Name",value(required,type=$rest_of_line)

define type set_self_types
keyword nosend
keyword noreply
keyword reply
keyword send

define syntax set_self_syntax
routine mail$cmd_set_self
parameter p1,value(required)
parameter p2,label=setn1,prompt="On",value(list,type=set_self_types)
define syntax set_noname_syntax
routine mail$cmd_clear_name
parameter p1,value(required)

define syntax set_waste_syntax
routine mail$cmd_set_waste
parameter p1,value(required)
parameter p2,label=wastename,prompt="Folder",value(required)

define type set_options
keyword auto_purge,syntax=set_auto_syntax
keyword noauto_purge,syntax=set_noauto_syntax,nonnegatable
keyword copy_self,syntax=set_self_syntax,nonnegatable
keyword file,syntax=set_file_syntax,nonnegatable
keyword folder,syntax=set_folder_syntax,nonnegatable
keyword forward,syntax=set_forward_syntax
keyword mail_directory,syntax=set_dir_syntax
keyword nomail_directory,syntax=set_nodir_syntax,nonnegatable
keyword noforward,syntax=set_noforward_syntax,nonnegatable
keyword personal_name,syntax=set_name_syntax
keyword nopersonal_name,syntax=set_noname_syntax,nonnegatable
keyword wastebasket_name,syntax=set_waste_syntax,nonnegatable

define syntax show_all_syntax
routine mail$cmd_show_all
parameter p1,value(required)

define syntax show_auto_syntax
routine mail$cmd_show_auto
parameter p1,value(required)

define syntax show_del_syntax
routine mail$cmd_show_delmsg
parameter p1,value(required)

define syntax show_dir_syntax
routine mail$cmd_show_dir
parameter p1,value(required)

define syntax show_file_syntax
routine mail$cmd_show_file
parameter p1,value(required)

define syntax show_folder_syntax
routine mail$cmd_show_folder
parameter p1,value(required)

define syntax show_forward_syntax
routine mail$cmd_show_forward
parameter p1,value(required)
qualifier user,value(required),nonnegatable

define syntax show_all_keys
	parameter p1,label=option,value(required)

define syntax show_all_states
	parameter p1,label=option,value(required)

define syntax show_key_syntax
routine mail$cmd_show_key
parameter p1,value(required)
parameter p2,label=keyname,prompt="Key name",value(required)
qualifier all,syntax=show_all_keys
qualifier state,value(required,list)
qualifier brief
qualifier directory,syntax=show_all_states
disallow directory and (brief or state or all)

define syntax show_name_syntax
routine mail$cmd_show_name
parameter p1,value(required)
qualifier user,value(required),nonnegatable

define syntax show_new_syntax
routine mail$cmd_show_new
parameter p1,value(required)

define syntax show_self_syntax
routine mail$cmd_show_self
parameter p1,value(required)

define syntax show_waste_syntax
routine mail$cmd_show_waste
parameter p1,value(required)

define type show_options
keyword all,syntax=show_all_syntax,nonnegatable
keyword auto_purge,syntax=show_auto_syntax,nonnegatable
keyword copy_self,syntax=show_self_syntax,nonnegatable
keyword deleted,syntax=show_del_syntax,nonnegatable
keyword file,syntax=show_file_syntax,nonnegatable
keyword folder,syntax=show_folder_syntax,nonnegatable
keyword forward,syntax=show_forward_syntax,nonnegatable
keyword key,syntax=show_key_syntax,nonnegatable
keyword mail_directory,syntax=show_dir_syntax,nonnegatable
keyword new_mail_count,syntax=show_new_syntax,nonnegatable
keyword personal_name,syntax=show_name_syntax,nonnegatable
keyword wastebasket_name,syntax=show_waste_syntax,nonnegatable

define verb set
routine mail$cmd_null
parameter p1,prompt="What",value(required,type=set_options)

define verb show
routine mail$cmd_null
parameter p1,prompt="What",value(required,type=show_options)

define verb spawn
routine mail$cmd_spawn
parameter p1,value(type=$rest_of_line)
qualifier process,value(required)
qualifier wait,default
qualifier log,default
qualifier input,value(required,type=$infile)
qualifier output,value(required,type=$outfile)
qualifier symbols,default
qualifier logical_names,default
qualifier notify
