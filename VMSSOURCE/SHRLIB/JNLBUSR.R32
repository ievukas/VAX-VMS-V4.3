! JNLBUSR.R32   -   Bliss macros for initializing user structures.
!
!	Version 'V04-000'
!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982, 1984 BY				    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************
!
!++
! Facility:  Common Journaling Facility interface from BLISS-32
!
! Abstract:
!
!	This file is to be used as a REQUIRE FILE in any BLISS-32 program
!	using CJF.  Its MACROs permit the BLISS-32 program to allocate
!	and statically initialize the control blocks of interest to CJF, and
!	dynamically initialize these control blocks.
!
! Environment:
!
! Author:	Larry Yetto,	Creation date: 15-APR-1982
!
! Modified by:
!
!	V03-002	JSV0293		Joost Verhofstad	19-MAY-1983
!		Make JSB changes
!
!	V03-001	GJA0009		Greg J. Awdziewicz,	23-Jul-1982  12:42
!		Service calls removed.  They are now written in Macro and
!		converted to Bliss by MARBLI.  See JNLCALLS.MAR
!
!--



!++
!
! $CJF_BITS
!
!	Used to initial a field to the logical OR of one or more
!	named bits.
!
!	$CJF_BITS ( PREFIX , VALUE ) where PREFIX is the character
!	prefix to attach to each value, and VALUE is one bitname or
!	a list of bits in the form <A,B,C>
!
!--
MACRO
    $CJF_OR [] =
	OR %,

    $CJF_BLDSTR ( P , V )[] =
	%NAME( P , V )  $CJF_OR( %REMAINING )  $CJF_BLDSTR( P , %REMAINING ) %,

    $CJF_BITS ( PREFIX , VALUE ) =
	%IF %NULL( VALUE )
	    %THEN 0
	    %ELSE $CJF_BLDSTR ( PREFIX,%REMOVE( VALUE ))
	%FI %;

!++
!
! $JSB_DECL
!
!	Used to declare a Journal Specification Block where
!	initialization is not required.
!--
MACRO
	$JSB_DECL = 
	BLOCK[JSB$C_LENGTH,BYTE] %;


!++
!
! $JSB
!
!	Used to allocate and statically initialize
!	a Journal Specification Block.
!
!--
KEYWORDMACRO
    $JSB ( JNLNAM,             JNLDEV = JSB$C_DISK,  JNLTYP = %X'FF',
           MASK = %X'FFFFFFFF',APPLID = 0,           FACCOD = 0,
           MAXSIZ = 600,       FILSIZ = 50,          BUFSIZ = 1,
           FILEXT = 10,        QUOTA = 0,            PROT = %X'FFF0',
           ACMODE = JSB$C_USER,UIC = 0,              FLAGS = CREATE,
           ACPNAM,             COPIES = 0,           EXPDAT,
           PRINAMDES = 0,      PRIRESDES = 0,        PRIRESLEN = 0,
	   JNLNAMLEN=0,	       ACPNAMLEN = 0
         ) =
	$JSB_DECL 
	PRESET (
		[ JSB$B_JNLDEV ] = JNLDEV ,
		[ JSB$B_JNLTYP ] = JNLTYP ,
		[ JSB$L_MASK   ] = MASK ,
		[ JSB$W_APPLID ] = APPLID ,
		[ JSB$W_FACCOD ] = FACCOD ,
		[ JSB$W_MAXSIZ ] = MAXSIZ ,
		[ JSB$L_FILSIZ ] = FILSIZ ,
		[ JSB$W_BUFSIZ ] = BUFSIZ ,
		[ JSB$W_FILEXT ] = FILEXT ,
		[ JSB$L_QUOTA  ] = QUOTA ,
		[ JSB$W_PROT   ] = PROT ,
		[ JSB$B_ACMODE ] = ACMODE ,
		[ JSB$L_UIC    ] = UIC ,
		[ JSB$L_FLAGS  ] = $CJF_BITS ( JSB$M_ , FLAGS ),
		[ JSB$B_COPIES ] = COPIES ,
		[ JSB$W_JNLNAMLEN ] = JNLNAMLEN ,
		[ JSB$W_ACPNAMLEN ] = ACPNAMLEN ,
		[ JSB$L_PRINAMDES ] = PRINAMDES ,
		[ JSB$L_PRIRESDES ] = PRIRESDES ,
		[ JSB$L_PRIRESLEN ] = PRIRESLEN 
	       ) % ;

