MODULE settime ( IDENT = 'V04-000',
		ADDRESSING_MODE (EXTERNAL = GENERAL)) =
BEGIN

!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982, 1984 BY				    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************

!++
! FACILITY:  SETPRO Command
!
! ABSTRACT:
!
!	This utility sets the system time.
!
! ENVIRONMENT:
!
!	VAX/VMS operating system. Privileged user mode.
!
! AUTHOR:  Gerry Smith				 6-Jan-1983
!
! Modified by:
!
!	V03-001	GAS0112					29-Mar-1983
!		Remove last traces of old command dispatcher.
!
!--

!
! Include files
!
LIBRARY 'SYS$LIBRARY:STARLET';		! VAX/VMS common definitions

!
! Table of contents
!

FORWARD ROUTINE
    set$time : NOVALUE;			! Routine to set the time

!
! External routines
!
EXTERNAL ROUTINE
    cli$get_value,			! CLI routine to get the time
    lib$cvt_time;			! Routine to convert to system time

!
! External definitions
!
EXTERNAL LITERAL
    set$_writeerr;			! Error modifying...

!
! Declare some shared messages
!
$SHR_MSGDEF	(SET,119,LOCAL,
    		(invquaval,	error),
		(valerr,	error));



GLOBAL ROUTINE set$time : NOVALUE =
BEGIN
!++
! Functional description
!
!	This is the routine for the SET TIME command.
!	It is called from the SET command processor.
!
! Inputs
!	None
!
! Outputs
!	None
!
!----

LOCAL
    status,				! Status return
    time : VECTOR[2] INITIAL(0,0),	! System time initially set to zero
    time_desc : $BBLOCK[dsc$c_s_bln];	! Descriptor for system time

!
! First, get the time value by asking the CLI for it.
!
$init_dyndesc(time_desc);		! Make the time descriptor dynamic
IF cli$get_value(%ASCID 'TIME',		! If there was a value given
    		  time_desc)		! then convert it.
THEN
    BEGIN
    IF NOT (status = lib$cvt_time(time_desc, time))
    THEN
	BEGIN
	SIGNAL(set$_writeerr, 1, %ASCID 'time', set$_valerr);
	RETURN;
	END;
    END;

!
! Set the time.
!
IF NOT (status = $SETIME(TIMADR = time))
THEN SIGNAL(set$_writeerr, 1, %ASCID 'time', .status);

RETURN;
END;

END
ELUDOM
