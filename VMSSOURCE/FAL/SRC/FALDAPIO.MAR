	.TITLE	FALDAPIO - DAP MESSAGE I/O ROUTINES
	.IDENT	'V04-000'

;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982, 1984 BY				    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;

;++
; Facility: FAL (DECnet File Access Listener)
;
; Abstract:
;
;	This module is responsible for receiving and transmitting DAP messages
;	and for receiving messages in the mailbox associated with the logical
;	link.
;
; Environment: VAX/VMS, user mode
;
; Author: James A. Krycka,	Creation Date:	16-JUN-1977
;
; Modified By:
;
;	V03-005	JAK0145		J A Krycka	12-APR-1984
;		Revise DAP message blocking algorithm in FAL$TRANSMIT to
;		eliminate an extra move of the data and rename the message
;		descriptors for clarity.
;
;	V03-004	JAK0139		J A Krycka	02-APR-1984
;		Support DAP buffer size up to 65535 bytes.
;
;	V03-003	JAK0133		J A Krycka	20-MAR-1984
;		Change a signed compare to an unsigned compare in FAL$TRANSMIT.
;
;	V03-002	JAK0137		J A Krycka	12-MAR-1984
;		Minor cleanup.
;
;--

	.SBTTL	DECLARATIONS

;
; Include Files:
;

	$DAPPLGDEF			; Define DAP prologue symbols
	$DAPHDRDEF			; Define DAP message header
	$DAPCNFDEF			; Define DAP Configuration message
	$FALWRKDEF			; Define FAL Work Area symobls
	$FALSTBDEF			; Define Statistics Block symbols
	$IODEF				; Define QIO function codes
	$MSGDEF				; Define mailbox message ID codes

;
; Macros:
;
;	None
;
; Equated Symbols:
;

	ASSUME	DAP$Q_DCODE_FLG EQ 0
	ASSUME	FAL$Q_FLG EQ 0

;
; Own Storage:
;
;	None

	.SBTTL	FAL$RECEIVE - READ AND PARSE DAP MESSAGE
	.PSECT	FAL$CODE	NOSHR,EXE,RD,NOWRT,BYTE

;++
; Functional Description:
;
;	FAL$RECEIVE returns the next DAP message read from the partner process
;	as a decoded message via the DAP control block.
;	DAP inbound message blocking is supported.
;
; Calling Sequence:
;
;	BSBW	FAL$RECEIVE
;
; Input Parameters:
;
;	R8	Address of FAL work area
;	R9	Address of DAP control block
;	R10	Address of FAB
;	R11	Address of RAB
;
; Implicit Inputs:
;
;	DAP$Q_MSG_BUF1
;
; Output Parameters:
;
;	R0-R1	Destroyed
;
; Implicit Outputs:
;
;	DAP$Q_MSG_BUF1 and DAP$Q_MSG_BUF2 plus
;	DAP control block is updated to reflect message parse
;
; Completion Codes:
;
;	DAP$Q_DCODE_STS
;
; Side Effects:
;
;	None
;
;--

FAL$RECEIVE::				; Entry Point
	TSTW	DAP$Q_MSG_BUF1(R9)	; Is there a blocked message in buffer?
	BNEQ	10$			; Branch if yes
	BSBB	FAL$RECEIVE_PKT		; Read next DAP message packet
	MOVQ	FAL$Q_RCV(R8),-		; Copy message descriptor to
		DAP$Q_MSG_BUF1(R9)	;  DAP message descriptor

;
; Parse next DAP message.
;

10$:	PUSHL	R9			; Push address of DAP control block
	CALLS	#1,W^FAL$DECODE_MSG	; Parse the message

;
; Update counter and log the DAP message.
;

	INCL	FAL$L_STB+FAL$L_RCV_MSG(R8)
					; Count DAP message for logging
	BBC	#FAL$V_LOG_MSG,(R8),20$	; Branch if logging disabled
	PUSHL	DAP$Q_MSG_BUF2+4(R9)	; Adddress of DAP message just decoded
	PUSHL	DAP$Q_MSG_BUF2(R9)	; Size of DAP message just decoded
	PUSHAB	W^FAL$GT_DECODE		; Address of counted ASCII string
	CALLS	#3,W^FAL$DISPLAY_MSG	; Print the message
20$:	RSB				; Exit

	.SBTTL	FAL$RECEIVE_PKT - RECEIVE DAP PACKET
	.PSECT	FAL$CODE	NOSHR,EXE,RD,NOWRT,BYTE

;++
; Functional Description:
;
;	FAL$RECEIVE_PKT reads the next DAP message packet from partner process.
;
; Calling Sequence:
;
;	BSBW	FAL$RECEIVE_PKT
;
; Input Parameters:
;
;	R8	Address of FAL work area
;	R9	Address of DAP control block
;	R10	Address of FAB
;	R11	Address of RAB
;
; Implicit Inputs:
;
;	None
;
; Output Parameters:
;
;	R0	Completion code
;	R1	Destroyed
;
; Implicit Outputs:
;
;	FAL$Q_RCV
;
; Completion Codes:
;
;	None
;
; Side Effects:
;
;	FAL terminates execution on encountering a logical link QIO error.
;
;--

FAL$RECEIVE_PKT::			; Entry Point
	BBS	#FAL$V_RCVAST,(R8),10$	; Branch if receive has completed
	INCL	W^FAL$GL_RECVWAIT	; Increment receive wait counter
	$WAITFR_S-			; Otherwise wait for it to complete
		EFN=#FAL$K_RCVEFN	;
	$CHECK_SS			; Check status code and exit on failure
10$:	$CLRBIT	#FAL$V_RCVQIO,(R8)	; Clear receive QIO outstanding flag
	$CLRBIT	#FAL$V_RCVAST,(R8)	; Clear receive AST delivered flag

;
; Form receive descriptor pointing to the DAP message packet just read.
;

	MOVZBL	FAL$B_RCVBUFIDX(R8),R0	; Get index of buffer
	MOVZWL	FAL$Q_RCVIOSB+2(R8),-	; Store message size in receive
		FAL$Q_RCV(R8)		;  descriptor
	MOVL	FAL$L_RCVBUF(R8)[R0],-	; Store buffer address in receive
		FAL$Q_RCV+4(R8)		;  descriptor

;
; Post a receive to replenish the one just completed.
;

	BSBB	FAL$RCV_QIO		; Issue a read with an AST
	RSB				; Exit

	.SBTTL	FAL$RCV_QIO - QUEUE LINK RECEIVE
	.PSECT	FAL$CODE	NOSHR,EXE,RD,NOWRT,BYTE

;++
; Functional Description:
;
;	FAL$RCV_QIO issues a QIO read request with an AST for the logical link.
;
; Calling Sequence:
;
;	BSBW	FAL$RCV_QIO
;
; Input Parameters:
;
;	R8	Address of FAL work area
;	R9	Address of DAP control block
;	R10	Address of FAB
;	R11	Address of RAB
;
; Implicit Inputs:
;
;	None
;
; Output Parameters:
;
;	R0	Completion code
;	R1	Destroyed
;
; Implicit Outputs:
;
;	None
;
; Completion Codes:
;
;	None
;
; Side Effects:
;
;	FAL terminates execution on encountering a logical link QIO error.
;
;--

FAL$RCV_QIO::				; Entry Point
	$SETBIT	#FAL$V_RCVQIO,(R8)	; Signal receive QIO outstanding

;
; Log our intent to post a receive QIO.
;

	BBC	#FAL$V_LOG_QIO,(R8),10$	; Branch if logging disabled
	MOVAB	W^FAL$GT_RCVQIO,R1	; Get address of counted string
	BSBW	FAL$LOG_QIO		; Log QIO posted to print file

;
; Determine next receive buffer to use and update receive buffer index.
;

10$:	MOVZBL	FAL$B_RCVBUFIDX(R8),R0	; Get index of last buffer used
	INCL	R0			; Increment it
	CMPL	R0,#<FAL$K_RCVBUFCNT-1>	; Is it out of range?
	BLEQU	20$			; Branch if not
	CLRL	R0			; Reset it
20$:	MOVB	R0,FAL$B_RCVBUFIDX(R8)	; Store new index value

;
; Issue a read with an AST for the logical link.
;

	MOVL	FAL$L_RCVBUF(R8)[R0],R1	; Get address of receive buffer
	$QIO_S	EFN=#FAL$K_RCVEFN-	; Issue read with AST
		CHAN=FAL$W_LNKCHN(R8)-	;
		FUNC=#IO$_READVBLK-	;
		IOSB=FAL$Q_RCVIOSB(R8)-	;
		ASTADR=W^FAL$RCV_AST-	;
		ASTPRM=R8-		; FAL work area address
		P1=(R1)-		; Receive buffer address
		P2=FAL$W_DAPBUFSIZ(R8)	; Receive buffer size
	$CHECK_SS			; Check status code and exit on failure
	RSB				; Exit

	.SBTTL	FAL$RCV_AST - LINK RECEIVE AST
	.PSECT	FAL$CODE	NOSHR,EXE,RD,NOWRT,BYTE

;++
; Functional Description:
;
;	FAL$RCV_AST processes a receive AST for the logical link.
;
; Calling Sequence:
;
;	Call	#5,FAL$RCV_AST	(invoked by VAX/VMS as an AST)
;
; Input Parameters:
;
;	4(AP)	Address of FAL work area
;
; Implicit Inputs:
;
;	None
;
; Output Parameters:
;
;	None
;
; Implicit Outputs:
;
;	FAL$V_RCVAST
;
; Completion Codes:
;
;	None
;
; Side Effects:
;
;	FAL terminates execution on encountering a logical link QIO error.
;
;--

	.ENTRY	FAL$RCV_AST,^M<R7,R8>	; Entry point from executive
	MOVL	4(AP),R8		; Get address of FAL work area
	$SETBIT	#FAL$V_RCVAST,(R8)	; Signal receive AST delivered
	BLBC	FAL$Q_RCVIOSB(R8),20$	; Branch on receive failure

;
; Update counters and log delivery of receive AST.
;

	MOVZWL	FAL$Q_RCVIOSB+2(R8),R0	; Get message size
	MOVAL	FAL$L_STB(R8),R7	; Get address of statistics block
	INCL	FAL$L_RCV_PKT(R7)	; Increment RCV message packet count
	ADDL2	R0,FAL$L_RCV_LNK(R7)	; Update RCV message byte count
	BBC	#FAL$V_LOG_AST,(R8),10$	; Branch if logging disabled
	MOVAB	W^FAL$GT_RCVQIO,R1	; Get address of counted string
	BSBW	FAL$LOG_AST		; Log AST delivered to print file
10$:	RET				; Exit
20$:	MOVZWL	FAL$Q_RCVIOSB(R8),R0	; Save error code
	$CHECK_SS			; Fatal error--terminate image

	.SBTTL	FAL$TRANSMIT - SEND DAP MESSAGE
	.PSECT	FAL$CODE	NOSHR,EXE,RD,NOWRT,BYTE

;++
; Functional Description:
;
;	FAL$TRANSMIT writes the next DAP message to the partner process.
;	DAP outbound message blocking is supported.
;
; Calling Sequence:
;
;	BSBW	FAL$TRANSMIT
;
; Input Parameters:
;
;	R8	Address of FAL work area
;	R9	Address of DAP control block
;	R10	Address of FAB
;	R11	Address of RAB
;
; Implicit Inputs:
;
;	DAP$V_LENGTH
;	DAP$V_MSGBLK
;	FAL$Q_BLD
;	FAL$Q_XMT
;	FAL$W_DAPBUFSIZ
;	FAL$V_LAST_MSG
;	Flags field of the DAP message
;
; Output Parameters:
;
;	R0	Completion code
;	R1	Destroyed
;
; Implicit Outputs:
;
;	FAL$Q_XMT
;	FAL$V_LAST_MSG cleared
;
; Completion Codes:
;
;	None
;
; Side Effects:
;
;	FAL terminates execution on encountering a logical link QIO error.
;
;--

FAL$TRANSMIT::				; Entry Point
	PUSHR	#^M<R2,R3,R4,R5>	; Save registers

;
; Update counter and log the DAP message.
;

	INCL	FAL$L_STB+FAL$L_XMT_MSG(R8)
					; Count this message for logging
	BBC	#FAL$V_LOG_MSG,(R8),10$	; Branch if logging disabled
	PUSHL	FAL$Q_BLD+4(R8)		; Address of DAP message
	PUSHL	FAL$Q_BLD(R8)		; Size of DAP message
	PUSHAB	W^FAL$GT_ENCODE		; Address of counted ASCII string
	CALLS	#3,W^FAL$DISPLAY_MSG	; Print the message

;
; Does partner support message blocking?
;

10$:	BBC	#DAP$V_MSGBLK,-		; Branch if message blocking is
		DAP$Q_SYSCAP(R9),50$	;  disabled

;
; Are there any messages in XMT buffer waiting to go?
;

	TSTL	FAL$Q_XMT(R8)		; Branch if there are no blocked DAP
	BEQL	20$			;  messages in buffer

;
; Will new message fit in XMT buffer with other messages?
;

	ADDL3	FAL$Q_BLD(R8),-		; Compute total # bytes if new message
		FAL$Q_XMT(R8),R0	;  were added to transmit buffer
	MOVZWL	FAL$W_DAPBUFSIZ(R8),R1	; Prepare for longword compare as size
					;  in R0 may slightly exceed 64K
	CMPL	R0,R1			; Branch if new message can be blocked
	BLEQU	30$			;  with the others

;
; Flush XMT buffer to make room for new message.
;

	MOVQ	FAL$Q_XMT(R8),R4	; Put buffer descriptor in <R4,R5>
	BSBB	FAL$TRANSMIT_PKT	; Send the blocked messages
; *****	BLBC	R0,EXIT			; Branch on failure (no status returned)
	CLRL	FAL$Q_XMT(R8)		; Initialize byte count in descriptor

;
; Determine whether new message should be sent now or deferred (bufferred).
;

20$:	BBC	#<DAP$V_LENGTH+8>,-	; Branch if this message does not allow
		@FAL$Q_BLD+4(R8),50$	;  additional blocking (implicit flush)
	BBS	#FAL$V_LAST_MSG,(R8),50$; Branch if this is last message of
					;  sequence (explicit flush)

;
; Move new message to XMT buffer, or simply map message if BLD and XMT buffers
; are aligned.
;

	MOVL	FAL$Q_BLD(R8),-		; Update byte count in descriptor
		FAL$Q_XMT(R8)		;
	CMPL	FAL$Q_BLD+4(R8),-	; Eliminate move of data if BLD and XMT
		FAL$Q_XMT+4(R8)		;  buffer addresses coincide
	BEQL	SUC			;
	MOVC3	FAL$Q_BLD(R8),-		; Copy the message
		@FAL$Q_BLD+4(R8),-	;
		@FAL$Q_XMT+4(R8)	;
	INCL	W^FAL$GL_COUNTER2	; Update message copy counter
	BRB	SUC			;

;
; Append new message to XMT buffer which already abuts XMT buffer.
;

30$:	MOVL	R0,FAL$Q_XMT(R8)	; Update byte count in descriptor

;
; Determine whether new message should be sent now or deferred (bufferred).
;

	BBC	#<DAP$V_LENGTH+8>,-	; Branch if this message does not allow
		@FAL$Q_BLD+4(R8),40$	;  additional blocking (implicit flush)
	BBC	#FAL$V_LAST_MSG,(R8),SUC; Fall thru if this is last message of 
					;  sequence (explicit flush)

;
; Send blocked messages from XMT buffer.
;

40$:	MOVQ	FAL$Q_XMT(R8),R4	; Put buffer descriptor in <R4,R5>
	BSBB	FAL$TRANSMIT_PKT	; Send the blocked messages
	CLRL	FAL$Q_XMT(R8)		; Initialize byte count in descriptor
	BRB	EXIT			;

;
; Send new message from BLD buffer.
;

50$:	MOVQ	FAL$Q_BLD(R8),R4	; Put buffer descriptor in <R4,R5>
	BSBB	FAL$TRANSMIT_PKT	; Send the message
; *****	BRB	EXIT			;

;
; Return to caller.
;

SUC:					; No status is returned by this routine
; *****	MOVL	#1,R0			; Return success
EXIT:	$CLRBIT	#FAL$V_LAST_MSG,(R8)	; Enable message blocking
	POPR	#^M<R2,R3,R4,R5>	; Restore registers
	RSB				; Exit

	.SBTTL	FAL$TRANSMIT_PKT - SEND DAP PACKET
	.PSECT	FAL$CODE	NOSHR,EXE,RD,NOWRT,BYTE

;++
; Functional Description:
;
;	FAL$TRANSMIT_PKT sends the specified packet of DAP messages to the
;	partner process.
;
; Calling Sequence:
;
;	BSBW	FAL$TRANSMIT_PKT
;
; Input Parameters:
;
;	R4-R5	Descriptor of message packet to transmit
;	R8	Address of FAL work area
;	R9	Address of DAP control block
;	R10	Address of FAB
;	R11	Address of RAB
;
; Implicit Inputs:
;
;	None
;
; Output Parameters:
;
;	R0	Completion code
;	R1	Destroyed
;
; Implicit Outputs:
;
;	None
;
; Completion Codes:
;
;	None
;
; Side Effects:
;
;	FAL terminates execution on encountering a logical link QIO error.
;
;--

FAL$TRANSMIT_PKT::			; Entry Point
	BSBB	FAL$XMT_QIO		; Send packet to partner
	$WAITFR_S-			; Wait for I/O to complete
		EFN=#FAL$K_XMTEFN	;
	$CHECK_SS			; Check status code and exit on failure
	$CLRBIT	#FAL$V_XMTQIO,(R8)	; Clear transmit QIO outstanding flag
	$CLRBIT	#FAL$V_XMTAST,(R8)	; Clear transmit AST delivered flag
	RSB				; Exit

	.SBTTL	FAL$XMT_QIO - QUEUE LINK TRANSMIT
	.PSECT	FAL$CODE	NOSHR,EXE,RD,NOWRT,BYTE

;++
; Functional Description:
;
;	FAL$XMT_QIO issues a QIO write request with an AST for the logical link.
;
; Calling Sequence:
;
;	BSBW	FAL$XMT_QIO
;
; Input Parameters:
;
;	R4-R5	Descriptor of message packet to transmit
;	R8	Address of FAL work area
;	R9	Address of DAP control block
;	R10	Address of FAB
;	R11	Address of RAB
;
; Implicit Inputs:
;
;	None
;
; Output Parameters:
;
;	R0	Completion code
;	R1	Destroyed
;
; Implicit Outputs:
;
;	None
;
; Completion Codes:
;
;	None
;
; Side Effects:
;
;	FAL terminates execution on encountering a logical link QIO error.
;
;--

FAL$XMT_QIO::				; Entry Point
	$SETBIT	#FAL$V_XMTQIO,(R8)	; Signal transmit QIO outstanding

;
; Log out intent to post a transmit QIO.
;

	BBC	#FAL$V_LOG_QIO,(R8),10$	; Branch if logging disabled
	MOVAB	W^FAL$GT_XMTQIO,R1	; Get address of counted string
	BSBW	FAL$LOG_QIO		; Log QIO posted to print file

;
; Issue a write with a AST for the logical link.
;

10$:	$QIO_S	EFN=#FAL$K_XMTEFN-	; Issue write with AST
		CHAN=FAL$W_LNKCHN(R8)-	;
		FUNC=#IO$_WRITEVBLK-	;
		IOSB=FAL$Q_XMTIOSB(R8)-	;
		ASTADR=W^FAL$XMT_AST-	;
		ASTPRM=R8-		; FAL work area address
		P1=(R5)-		; Transmit buffer address
		P2=R4			; Transmit buffer size
	$CHECK_SS			; Check status code and exit on failure
	RSB				; Exit

	.SBTTL	FAL$XMT_AST - LINK TRANSMIT AST
	.PSECT	FAL$CODE	NOSHR,EXE,RD,NOWRT,BYTE

;++
; Functional Description:
;
;	FAL$XMT_AST processes a transmit AST for the logical link.
;
; Calling Sequence:
;
;	Call	#5,FAL$XMT_AST	(invoked by VAX/VMS as an AST)
;
; Input Parameters:
;
;	4(AP)	Address of FAL work area
;
; Implicit Inputs:
;
;	None
;
; Output Parameters:
;
;	None
;
; Implicit Outputs:
;
;	FAL$V_XMTAST
;
; Completion Codes:
;
;	None
;
; Side Effects:
;
;	FAL terminates execution on encountering a logical link QIO error.
;
;--

	.ENTRY	FAL$XMT_AST,^M<R7,R8>	; Entry point from executive
	MOVL	4(AP),R8		; Get address of FAL work area
	$SETBIT	#FAL$V_XMTAST,(R8)	; Signal transmit AST delivered
	BLBC	FAL$Q_XMTIOSB(R8),20$	; Branch on transmit failure

;
; Update counters and log delivery of transmit AST.
;

	MOVZWL	FAL$Q_XMTIOSB+2(R8),R0	; Save # bytes transmitted
	MOVAL	FAL$L_STB(R8),R7	; Get address of statistics block
	INCL	FAL$L_XMT_PKT(R7)	; Increment XMT message packet count
	ADDL2	R0,FAL$L_XMT_LNK(R7)	; Update XMT message byte count
	BBC	#FAL$V_LOG_AST,(R8),10$	; Branch if logging disabled
	MOVAB	W^FAL$GT_XMTQIO,R1	; Get address of counted string
	BSBW	FAL$LOG_AST		; Log AST delivered to print file
10$:	RET				; Exit
20$:	MOVZWL	FAL$Q_XMTIOSB(R8),R0	; Save error code
	$CHECK_SS			; Fatal error--terminate image

	.SBTTL	FAL$RECEIVE_MBX - READ AND PARSE INTERRUPT
	.PSECT	FAL$CODE	NOSHR,EXE,RD,NOWRT,BYTE

;++
; Functional Description:
;
;	FAL$RECEIVE_MBX returns the next DAP message sent as an interrupt
;	message by the partner process. It is returned as a decoded message
;	via the DAP control block.
;
; Calling Sequence:
;
;	BSBW	FAL$RECEIVE_MBX
;
; Input Parameters:
;
;	R8	Address of FAL work area
;	R9	Address of DAP control block
;	R10	Address of FAB
;	R11	Address of RAB
;
; Implicit Inputs:
;
;	FAL$Q_MBX
;
; Output Parameters:
;
;	R0-R1	Destroyed
;
; Implicit Outputs:
;
;	DAP control block is updated to reflect message parse.
;
; Completion Codes:
;
;	DAP$Q_DCODE_STS
;
; Side Effects:
;
;	FAL terminates execution on encountering a mailbox read error.
;
;--

FAL$RECEIVE_MBX::			; Entry Point
	BBS	#FAL$V_MBXAST,(R8),10$	; Branch if receive has completed
	$WAITFR_S-			; Otherwise wait for it to complete
		EFN=#FAL$K_MBXEFN	;
	$CHECK_SS			; Check status code and exit on failure
10$:	$CLRBIT	#FAL$V_MBXQIO,(R8)	; Clear mailbox QIO outstanding flag
	$CLRBIT	#FAL$V_MBXAST,(R8)	; Clear mailbox AST delivered flag

;
; Post a receive to replenish the one just completed.
; Note:	We run the risk of receiving another mailbox message immediately
;	which may overwrite the current message before we can process it.
;	This would be a very very unlikely occurrence from a timing point of
;	view, and the partner process would be misbehaving badly!
;

	BSBB	FAL$MBX_RCV_QIO		; Issue a read with an AST

;
; Parse next DAP message.
;

	MOVQ	FAL$Q_MBX(R8),-		; Copy mailbox message descriptor to
		DAP$Q_MSG_BUF1(R9)	;  DAP message descriptor
	PUSHL	R9			; Push address of DAP control block
	CALLS	#1,W^FAL$DECODE_MSG	; Parse the message

;
; Update counter and log the DAP message.
;

	INCL	FAL$L_STB+FAL$L_RCV_MSG(R8)
					; Count DAP message for logging
	BBC	#FAL$V_LOG_MSG,(R8),20$	; Branch if logging disabled
	PUSHL	DAP$Q_MSG_BUF2+4(R9)	; Adddress of DAP message just decoded
	PUSHL	DAP$Q_MSG_BUF2(R9)	; Size of DAP message just decoded
	PUSHAB	W^FAL$GT_DECODE		; Address of counted ASCII string
	CALLS	#3,W^FAL$DISPLAY_MSG	; Print the message
20$:	RSB				; Exit

	.SBTTL	FAL$MBX_RCV_QIO - QUEUE MAILBOX RECEIVE
	.PSECT	FAL$CODE	NOSHR,EXE,RD,NOWRT,BYTE

;++
; Functional Description:
;
;	FAL$MBX_RCV_QIO issues a QIO read request with an AST for the
;	mailbox associated with the logical link.
;
; Calling Sequence:
;
;	BSBW	FAL$MBX_RCV_QIO
;
; Input Parameters:
;
;	R8	Address of FAL work area
;	R9	Address of DAP control block
;	R10	Address of FAB
;	R11	Address of RAB
;
; Implicit Inputs:
;
;	None
;
; Output Parameters:
;
;	R0	Completion code
;	R1	Destroyed
;
; Implicit Outputs:
;
;	None
;
; Completion Codes:
;
;	None
;
; Side Effects:
;
;	FAL terminates execution on encountering a mailbox read error.
;
;--

FAL$MBX_RCV_QIO::			; Entry Point
	$SETBIT	#FAL$V_MBXQIO,(R8)	; Signal mailbox QIO outstanding

;
; Log our intent to post a mailbox receive QIO.
;

	BBC	#FAL$V_LOG_QIO,(R8),10$	; Branch if logging disabled
	MOVAB	W^FAL$GT_MBXQIO,R1	; Get address of counted string
	BSBW	FAL$LOG_QIO		; Log QIO posted to print file

;
; Issue a read with an AST for the mailbox.
;

10$:	$QIO_S	EFN=#FAL$K_MBXEFN-	; Issue read with AST
		CHAN=FAL$W_MBXCHN(R8)-	;
		FUNC=#IO$_READVBLK-	;
		IOSB=FAL$Q_MBXIOSB(R8)-	;
		ASTADR=W^FAL$MBX_RCV_AST-
		ASTPRM=R8-		; FAL work area address
		P1=FAL$T_MBXBUF(R8)-	; Mailbox buffer address
		P2=#FAL$K_MBXBUF	; Mailbox buffer size
	$CHECK_SS			; Check status code and exit on failure
	RSB				; Exit

	.SBTTL	FAL$MBX_RCV_AST - MAILBOX RECEIVE AST
	.PSECT	FAL$CODE	NOSHR,EXE,RD,NOWRT,BYTE

;++
; Functional Description:
;
;	FAL$MBX_RCV_AST processes an AST for the mailbox associated with the
;	logical link. Depending on the type of message, one of three actions is
;	performed:
;	(1) if it is an interrupt message, a descriptor is formed to point to
;	    the DAP message.
;	(2) if the message indicates that the link has been broken, FAL is
;	    terminated.
;	(3) if it is an extraneous message, it is simply discarded.
;
; Calling Sequence:
;
;	Call	#5,FAL$MBX_RCV_AST	(invoked by VAX/VMS as an AST)
;
; Input Parameters:
;
;	4(AP)	Address of FAL work area
;
; Implicit Inputs:
;
;	None
;
; Output Parameters:
;
;	None
;
; Implicit Outputs:
;
;	FAL$Q_MBX
;	FAL$V_MBXAST
;
; Completion Codes:
;
;	None
;
; Side Effects:
;
;	FAL terminates execution on encountering a mailbox read error.
;
;--

	.ENTRY	FAL$MBX_RCV_AST,^M<R7,R8> ; Entry point from executive
	MOVL	4(AP),R8		; Get address of FAL work area
	$SETBIT	#FAL$V_MBXAST,(R8)	; Signal mailbox AST delivered
	BLBS	FAL$Q_MBXIOSB(R8),10$	; Branch on success
	MOVZWL	FAL$Q_RCVIOSB(R8),R0	; Save error code
	$CHECK_SS			; Fatal error--terminate image

;
; Log the mailbox message type code.
;

10$:	BBC	#FAL$V_LOG_MSG,(R8),20$	; Branch if logging disabled
	$FAO_S	CTRSTR=W^FAL$GQ_MBXMSG-	; Format the message
		OUTLEN=W^FAL$GW_PRTLEN2-;
		OUTBUF=W^FAL$GQ_PRTBUF2-;
		P1=FAL$T_MBXBUF(R8)	; First four bytes of mailbox message
	$CHECK_STATUS			; Check status code
	BSBW	FAL$PRINT_FAO_ASTLEVEL	; Print message

;
; Take appropriate action depending on the mailbox message type.
;

20$:	MOVAB	FAL$T_MBXBUF(R8),R1	; Get address of mailbox buffer

	ASSUME	MSG$_ABORT EQ 48
	ASSUME	MSG$_CONFIRM EQ 49
	ASSUME	MSG$_CONNECT EQ 50
	ASSUME	MSG$_DISCON EQ 51
	ASSUME	MSG$_EXIT EQ 52
	ASSUME	MSG$_INTMSG EQ 53
	ASSUME	MSG$_PATHLOST EQ 54
	ASSUME	MSG$_PROTOCOL EQ 55
	ASSUME	MSG$_REJECT EQ 56
	ASSUME	MSG$_THIRDPARTY EQ 57
	ASSUME	MSG$_TIMEOUT EQ 58
	ASSUME	MSG$_NETSHUT EQ 59

	$CASEW	SELECTOR=(R1)+,-	; Mailbox message type:
		BASE=#MSG$_ABORT-	;
		DISPL=<-		;
		    FAL$TERMINATE-	;  Disconnect abort
		    IGNORE-		;  Connect confirm
		    IGNORE-		;  Connect initiate
		    FAL$TERMINATE-	;  Synchronous disconnect
		    FAL$TERMINATE-	;  Partner exited prematurely
		    INTERRUPT-		;  Interrupt message
		    FAL$TERMINATE-	;  Partner no longer accessible
		    FAL$TERMINATE-	;  NSP protocol error
		    IGNORE-		;  Connect reject
		    FAL$TERMINATE-	;  Thirdparty disconnect
		    IGNORE-		;  Connect timeout
		    IGNORE-		;  Network shutting down
		>			;

;
; Ignore unexpected mailbox message.
;

IGNORE:					; Discard junk mail
	CLRL	R0			; Make this a null message
	MOVQ	R0,FAL$Q_MBX(R8)	; Store descriptor of interrupt data
	RET				; Exit

;
; An interrupt message has been received.
;

INTERRUPT:				;
	TSTW	(R1)+			; Skip over unit #
	MOVZBL	(R1)+,R0		; Skip over device name stored
	ADDL2	R0,R1			;  as a counted ASCII string
	MOVZBL	(R1)+,R0		; Get size of interrupt data field
	MOVQ	R0,FAL$Q_MBX(R8)	; Store descriptor of interrupt data

;
; Update counters and log delivery of mailbox AST containing a DAP interrupt
; message.
;

	MOVAL	FAL$L_STB(R8),R7	; Get address of statistics block
	INCL	FAL$L_RCV_PKT(R7)	; Increment RCV message packet count
	ADDL2	R0,FAL$L_RCV_LNK(R7)	; Update RCV message byte count
	BBC	#FAL$V_LOG_AST,(R8),30$	; Branch if logging disabled
	MOVAB	W^FAL$GT_MBXQIO,R1	; Get address of counted string
	BSBW	FAL$LOG_AST		; Log AST delivered to print file
30$:	RET				; Exit

	.END				; End of module
