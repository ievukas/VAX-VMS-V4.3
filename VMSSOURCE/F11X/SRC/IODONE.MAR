	.TITLE	IODONE - POST REQUEST DONE TO USER
	.IDENT	'V04-000'

;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982, 1984 BY				    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************

;++
;
; FACILITY:  F11ACP STRUCTURE LEVEL 2
;
; ABSTRACT:
;
;	THIS ROUTINE POSTS I/O COMPLETION FOR THE INDICATED FCP REQUEST.
;
; ENVIRONMENT:
;
;	STARLET OPERATING SYSTEM, INCLUDING PRIVILEGED SYSTEM SERVICES
;	AND INTERNAL EXEC ROUTINES. THIS ROUTINE MUST BE CALLED IN
;	KERNEL MODE.
;
;--
;
; AUTHOR:  ANDREW C. GOLDSTEIN, CREATION DATE:  20-DEC-1976  11:25
;
; MODIFIED BY:
;
;	V03-010	CDS0005		Christian D. Saether	21-Aug-1984
;		Call CHECK_DISMOUNT routine before posting i/o
;		completion.
;
;	V03-009	DAS0001		David Solomon		02-May-1984
;		Fix truncation error.
;
;	V03-008	ACG0408		Andrew C. Goldstein,	23-Mar-1984  10:58
;		Make all of global storage based
;
;	V03-007	WMC0001		Wayne Cardoza		14-Mar-1984
;		This routine must call POSTEF because of changes in IOPOST.
;
;	V03-006	CDS0004		Christian D. Saether	15-Jan-1984
;		Call to IOC$_BUFPOST should be IOC$BUFPOST.
;
;	V03-005	CDS0003		Christian D. Saether	 8-Dec-1983
;		For buffered i/o completion, execute iopost as
;		jsb's to appropriate routines rather than actually
;		post a software interrupt.
;
;	V03-004	CDS0002		Christian D. Saether	24-Apr-1983
;		Fix truncation error.
;
;	V03-003	ACG0320		Andrew C. Goldstein,	22-Mar-1983  12:41
;		Change byte count handling to track IOPOST changes
;
;	V03-002 RSH0013		R. Scott Hanna		17-Mar-1983
;		Fix truncation error.
;
;	V03-001	CDS0001		C Saether		31-Jul-1982
;		Make reference to IOC$GL_PSBL pic.
;
;	V02-001	LJK0076		Lawrence J. Kenah	3-Nov-1981
;		Remove check for "queue previously not empty" when making
;		software interrupt request. The request is always made.
;
;**

;
; EQUATED SYMBOLS:
;
PACKET	=4				; ADDRESS OF I/O PACKET ARG

	$ABDDEF				; DEFINE BUFFER PACKET OFFSETS
	$ACBDEF				; AST CONTROL BLOCK OFFSETS
	$FIBDEF				; DEFINE FIB OFFSETS
	$IRPDEF				; DEFINE I/O PACKET OFFSETS
	$UCBDEF				; DEFINE UCB OFFSETS
	$VCBDEF				; DEFINE VCB OFFSETS
	$IPLDEF				; DEFINE IPL SYMBOLS
	$IODEF				; DEFINE I/O FUNCTION CODES
	$PRDEF				; DEFINE PROCESSOR REGISTERS

;++
;
; FUNCTIONAL DESCRIPTION:
;
;	THIS ROUTINE POSTS I/O COMPLETION FOR THE INDICATED FCP REQUEST.
;
; CALLING SEQUENCE:
;	CALL	IODONE (ARG1)
;
; INPUT PARAMETERS:
;	ARG1: ADDRESS OF I/O PACKET
;
; IMPLICIT INPUTS:
;	USER_STATUS: STATUS OF I/O REQUEST
;
; OUTPUT PARAMETERS:
;	NONE
;
; IMPLICIT OUTPUTS:
;	IOC$GL_PSBL: TAIL OF I/O POST QUEUE
;
; ROUTINE VALUE:
;	NONE
;
; SIDE EFFECTS:
;	I/O PACKET PLACED ON I/O POST QUEUE
;	VOLUME CHECKED FOR DISMOUNT
;
;--

	.PSECT	$CODE$,NOWRT,LONG

IO_DONE::
	.WORD	^M<R2,R3,R4,R5,R6,R7>	; SAVE REGISTERS
	MOVL	PACKET(AP),R5		; GET PACKET ADDRESS
	MOVQ	W^USER_STATUS(R10),IRP$L_MEDIA(R5) ; SET STATUS IN PACKET
	EXTZV	#IRP$V_FCODE,#IRP$S_FCODE,-
		IRP$W_FUNC(R5),R7	; GET FUNCTION CODE WITHOUT QUALIFIERS
	CMPB	R7,#IO$_READPBLK	; IF READ PHYSICAL
	BEQL	20$
	CMPB	R7,#IO$_WRITEPBLK	; OR WRITE DO SPECIAL PROCESSING
	BEQL	20$
;
; POST PROCESSING FOR ALL ACP FUNCTIONS: BUMP DOWN THE VOLUME TRANSACTION
; COUNT AND DO THE FIXUPS FOR THE BUFFER PACKET.
;
	MOVL	IRP$L_UCB(R5),R6	; GET UCB ADDRESS
	MOVL	UCB$L_VCB(R6),R6	; TO GET VCB ADDRESS
	DECW	VCB$W_TRANS(R6)		; DEDUCT THIS REQ FROM TRANS COUNT
	BBC	#IRP$V_COMPLX,IRP$W_STS(R5),10$ ; BRANCH IF NO BUFFER PACKET
	MOVL	@IRP$L_SVAPTE(R5),R6	; GET BUFFER DESCRIPTOR ADDRESS
	CLRW	<ABD$C_NAME*ABD$C_LENGTH>+ABD$W_COUNT(R6)
					; INHIBIT WRITE-BACK OF NAME STRING
	MOVAB	<ABD$C_FIB*ABD$C_LENGTH>+ABD$W_TEXT(R6),R2
	MOVZWL	(R2),R3			; GET OFFSET ADDRESS OF FIB IN PACKET
	ADDL	R3,R2			; COMPUTE ABSOLUTE ADDRESS
	MOVC5	#FIB$C_LENGTH,W^LOCAL_FIB(R10),#0,-
		<ABD$C_FIB*ABD$C_LENGTH>+ABD$W_COUNT(R6),1(R2)
					; COPY LOCAL FIB BACK INTO PACKET
	MOVL	PACKET(AP), R5		; RESTORE IRP ADDR TO R5
	BBSS	#IRP$V_FUNC,IRP$W_STS(R5),10$ ; IF READ BIT IS SET, KEEP
	MOVW	#ABD$C_ATTRIB,IRP$W_BCNT(R5) ; ELSE DUMP ATTRIBUTE TEXT

;
; POST COMPLETION WITHOUT ACTUALLY POSTING AN IOPOST SOFTWARE INTERRUPT.
;

10$:	BBC	#IRP$V_BUFIO, IRP$W_STS(R5), 50$ ; THIS HAD BETTER BE A
					; BUFFERED I/O.
	CALLS	#0,L^CHECK_DISMOUNT	; CHECK THE VOLUME FOR DISMOUNT
	TSTL	IRP$L_PID (R5)		; IF NEGATIVE, THIS DOES SPECIAL
	BLSS	30$			; IOPOST PROCESSING.

	MOVL	G^CTL$GL_PCB, R4	; OUR PCB ADDRESS INTO R4.

	SETIPL	#IPL$_ASTDEL		; BLOCK AST DELIVERY
	JSB	G^IOC$BUFPOST		; DO FIRST PART OF IOPOST
	MOVL	IRP$L_PID(R5),R1	; PROCESS IDENTIFICATION
	MOVZBL	IRP$B_EFN(R5),R3	; GET EVENT FLAG NUMBER
	JSB	G^SCH$POSTEF		; AND POST IT
	JSB	@ACB$L_KAST (R5)	; DO SPECIAL KERNEL AST PART OF IOPOST.
	SETIPL	#0			; BACK TO IPL 0.
	RET				; AND EXIT.

;
; FOR READ/WRITE PHYSICAL, KNOCK DOWN THE VIRTUAL BIT IN THE PACKET. ONLY
; ERRORS COME THROUGH HERE, AND WE DON'T WANT TO SEE THEM AGAIN (I/O POST
; RECYCLES VIRTUAL I/O ERRORS FOR ACP ERROR PROCESSING).
;
	ASSUME	IRP$V_VIRTUAL LE 7
20$:	BICB	#IRP$M_VIRTUAL,IRP$W_STS(R5) ; CLEAR THE VIRTUAL BIT

30$:	MOVAB	G^IOC$GL_PSBL, R0	; GET ADDRESS OF BACK LINK
	INSQUE	(R5), @(R0)		; INSERT PACKET INTO QUEUE
	SOFTINT	#IPL$_IOPOST		; SIGNAL I/O POST INTERRUPT
	RET

50$:	BUG_CHECK	XQPERR		; BUFFERED I/O WAS EXPECTED.

	.END
