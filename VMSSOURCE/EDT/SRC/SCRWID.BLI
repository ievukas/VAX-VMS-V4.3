%TITLE 'EDT$SCRWID - set the screen width'
MODULE EDT$SCRWID (				! Set the screen width
		IDENT = 'V04-000'			! File: SCRWID.BLI Edit: JBS1005
		) =
BEGIN
!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982, 1984 BY				    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************
!

!++
! FACILITY:	EDT -- The DEC Standard Editor
!
! ABSTRACT:
!
!	This module outputs an escape sequence to set the terminal width
!
! ENVIRONMENT:	Runs at any access mode - AST reentrant
!
! AUTHOR: Sharon M. Burlingame, CREATION DATE: July 29, 1982
!
! MODIFIED BY:
!
! 1-001	- Original.  SMB 29-Jul-1982.
! 1-002	- Maintain EDT$$G_PRV_COL and EDT$$G_PRV_LN.  JBS 05-Oct-1982
! 1-003 - Remove conditional setting of screen width.  SMB 16-Feb-1983
! 1-004	- Improve the appearance of the listing.  JBS 17-Jun-1983
! 1-005	- Set the width on any kind of terminal, and home the cursor.  JBS 29-Aug-1983
!--


%SBTTL 'Declarations'
!
! TABLE OF CONTENTS:
!

REQUIRE 'EDTSRC:TRAROUNAM';

FORWARD ROUTINE
    EDT$$SC_SETWID;				! Set screen width on VT100's

!
! INCLUDE FILES:
!

REQUIRE 'EDTSRC:EDTREQ';

!
! MACROS:
!
!	NONE
!
! EQUATED SYMBOLS:
!

BIND
    WID_80 = UPLIT (%STRING (%CHAR (ASC_K_ESC), '[H', %CHAR (ASC_K_ESC), '[?3l')),
    WID_132 = UPLIT (%STRING (%CHAR (ASC_K_ESC), '[H', %CHAR (ASC_K_ESC), '[?3h'));

LITERAL
    WID_LEN = 8;

!
! OWN STORAGE:
!
!	NONE
!
! EXTERNAL REFERENCES:
!
!	In the routine

%SBTTL 'EDT$$SC_SETWID - set screen width'

GLOBAL ROUTINE EDT$$SC_SETWID (			! Set screen width
    SIZE					! Desired screen size
    ) =

!++
! FUNCTIONAL DESCRIPTION:
!
!	If this terminal is a VT100, then output an escape sequence
!	to modify the width of the terminal
!
! FORMAL PARAMETERS:
!
!  SIZE			the desired screen size
!
! IMPLICIT INPUTS:
!
!	EDT$$G_TI_WID
!	EDT$$G_TI_TYP
!
! IMPLICIT OUTPUTS:
!
!	EDT$$G_TI_WID
!	EDT$$G_PRV_COL
!	EDT$$G_PRV_LN
!
! ROUTINE VALUE:
!
!	0 = screen size was not changed
!	1 = screen size was changed
!
! SIDE EFFECTS:
!
!	NONE
!
!--

    BEGIN

    EXTERNAL ROUTINE
	EDT$$OUT_FMTBUF,			! Output the format buffer
	EDT$$FMT_LIT : NOVALUE;			! Put a literal string in the format buffer

    EXTERNAL
	EDT$$G_TI_WID,				! Current screen size
	EDT$$G_TI_TYP,				! Terminal type
	EDT$$G_PRV_COL,				! Cursor column number
	EDT$$G_PRV_LN;				! Cursor line number

!+
! Set the width unconditionally in case the user changed it using SET-UP.
!-

    IF (.EDT$$G_TI_TYP EQL TERM_VT100)
    THEN
	BEGIN

	IF (.SIZE LEQ 80) THEN EDT$$FMT_LIT (WID_80, WID_LEN) ELSE EDT$$FMT_LIT (WID_132, WID_LEN);

	EDT$$OUT_FMTBUF ();
	EDT$$G_PRV_COL = 0;
	EDT$$G_PRV_LN = 0;
	END;

    EDT$$G_TI_WID = .SIZE;
    RETURN (1);
    END;					! of routine EDT$$SC_SETWID

!<BLF/PAGE>

END						! of module EDT$SCRWID

ELUDOM
