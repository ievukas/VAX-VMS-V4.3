MODULE PAS$WRITE_INTEGER ( %TITLE 'Write a signed integer'
		IDENT = '1-002'			! File: PASWRIINT.B32 Edit: SBL1002
		) =
BEGIN
!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982, 1984 BY				    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************
!

!++
! FACILITY:	Pascal Language Support
!
! ABSTRACT:
!
!	This module contains a procedure which writes a signed integer
!	to a textfile.
!
! ENVIRONMENT:	User mode - AST reentrant
!
! AUTHOR: Steven B. Lionel, CREATION DATE: 1-April-1981
!
! MODIFIED BY:
!
! 1-001	- Original.  SBL 1-April-1981
! 1-002 - Make total-width a longword.  SBL 30-June-1982
!--


%SBTTL 'Declarations'
!
! PROLOGUE DEFINITIONS:
!

REQUIRE 'RTLIN:PASPROLOG';			! Externals, linkages, PSECTs, structures

!
! TABLE OF CONTENTS:
!

FORWARD ROUTINE
    PAS$WRITE_INTEGER: NOVALUE,			! Write to textfile
    PAS$WRITEV_INTEGER: NOVALUE;		! Write to string

!
! MACROS:
!
!	NONE
!
! EQUATED SYMBOLS:
!
!	NONE
!
! FIELDS:
!
!	NONE
!
! OWN STORAGE:
!
!	NONE
!

%SBTTL 'PAS$WRITE_INTEGER - Write an integer to textfile'
GLOBAL ROUTINE PAS$WRITE_INTEGER (			! Write an integer
	PFV: REF $PAS$PFV_FILE_VARIABLE, 		! File variable
	INTEGER,					! Value to write
	TOTAL_WIDTH: SIGNED,				! Total field width
	ERROR						! Error unwind address
    ): NOVALUE =

!++
! FUNCTIONAL DESCRIPTION:
!
!	This procedure writes a signed integer to the specified textfile.
!
! CALLING SEQUENCE:
!
!	CALL PAS$WRITE_INTEGER (PFV.mr.r, INTEGER.rl.v, TOTAL_WIDTH.rl.v
!				[ERROR.j.r])
!
! FORMAL PARAMETERS:
!
!	PFV		- The Pascal File Variable (PFV) passed by reference.
!			  The structure of the PFV is defined in PASPFV.REQ.
!
!	INTEGER		- The integer to write.
!
!	TOTAL_WIDTH	- Total field width.
!
!	ERROR		- Optional.  Address to unwind to if an error occurs.
!
! IMPLICIT INPUTS:
!
!	NONE
!
! IMPLICIT OUTPUTS:
!
!	NONE
!
! ROUTINE VALUE:
!
!	NONE
!
! SIDE EFFECTS:
!
!	If the file is the standard file INPUT or OUTPUT, it is implicitly opened.
!
! SIGNALLED ERRORS:
!
!	LINTOOLON - line too long
!	NEGWIDDIG - negative width or digits specification not allowed
!
!--

    BEGIN

    LOCAL
	FCB: REF $PAS$FCB_CONTROL_BLOCK,		! File control block
	FIELD_WIDTH,					! Total width
	REMAINING_WIDTH,				! Remaining width
	INT_DIGITS,					! Number of digits
	STRING: VECTOR [12, BYTE],			! String for result
	DESCR: BLOCK [8, BYTE],				! String descriptor
	PFV_ADDR: VOLATILE,				! Enable argument
	UNWIND_ACT: VOLATILE,				! Enable argument
	ERROR_ADDR: VOLATILE;				! Enable argument

    BUILTIN
	ACTUALCOUNT;

    ENABLE
	PAS$$IO_HANDLER (PFV_ADDR, UNWIND_ACT, ERROR_ADDR);	! Enable error handler

    !+
    ! Get ERROR parameter, if present.
    !-

    IF ACTUALCOUNT () GEQU 4
    THEN
	ERROR_ADDR = .ERROR;		! Set unwind address

    PFV_ADDR = PFV [PFV$R_PFV];		! Set PFV address

    !+
    ! Validate PFV and get PFV.
    !-

    PAS$$VALIDATE_PFV (PFV [PFV$R_PFV]; FCB);

    !+
    ! Set unwind action to unlock file.
    !-

    UNWIND_ACT = PAS$K_UNWIND_UNLOCK;

    !+
    ! Do common initialization.
    !-

    PAS$$INIT_WRITE (PFV [PFV$R_PFV], FCB [FCB$R_FCB]; FCB);

    !+
    ! Check for invalid width.
    !-

    IF .TOTAL_WIDTH LSS 0
    THEN
	$PAS$IO_ERROR (PAS$_NEGWIDDIG,0);

    !+
    ! Check for TOTAL_WIDTH exceeding remaining width.
    !-

    REMAINING_WIDTH = .FCB [FCB$A_RECORD_END] - .FCB [FCB$A_RECORD_CUR];
    IF .TOTAL_WIDTH GTRU .REMAINING_WIDTH
    THEN
	$PAS$IO_ERROR (PAS$_LINTOOLON,1,(.TOTAL_WIDTH-.REMAINING_WIDTH));

    !+
    ! Try to convert directly into record buffer.
    !-

    DESCR [DSC$W_LENGTH] = .TOTAL_WIDTH;
    DESCR [DSC$B_CLASS] = 0;
    DESCR [DSC$B_DTYPE] = 0;
    DESCR [DSC$A_POINTER] = .FCB [FCB$A_RECORD_CUR];
    IF NOT OTS$CVT_L_TI (INTEGER, DESCR)
    THEN
	BEGIN

	!+
	! Conversion failed.  Try again with local buffer.
	!-

	!+
	! Convert integer to text
	!-
    
	DESCR [DSC$W_LENGTH] = 12;
	DESCR [DSC$A_POINTER] = STRING;
    
	OTS$CVT_L_TI (INTEGER, DESCR);		! Can't fail
    
	DESCR [DSC$A_POINTER] = CH$FIND_NOT_CH (12, STRING, %C' ');		! Find first non-blank
	DESCR [DSC$W_LENGTH] = STRING [12] - .DESCR [DSC$A_POINTER];	! New length
	
	!+
	! Determine if we need a leading space.
	!-
    
	FIELD_WIDTH = .TOTAL_WIDTH;
	INT_DIGITS = .DESCR [DSC$W_LENGTH];
	IF CH$RCHAR (.DESCR [DSC$A_POINTER]) NEQU %C'-'	! Negative?
	THEN
	    BEGIN
	    !+
	    ! Not negative.  Only if FIELD_WIDTH > INT_DIGITS do we have a leading blank
	    !-
	    IF .FIELD_WIDTH GTR .INT_DIGITS
	    THEN
		BEGIN
		DESCR [DSC$W_LENGTH] = .DESCR [DSC$W_LENGTH] + 1;
		DESCR [DSC$A_POINTER] = .DESCR [DSC$A_POINTER] - 1;
		END;
	    END;
	
	!+
	! See if field will fit in record.
	!-
    
	FIELD_WIDTH = MAX (.FIELD_WIDTH, .DESCR [DSC$W_LENGTH]);
	BEGIN
	LOCAL
	    EXTRA;			! Extra characters past end of line
	EXTRA = .FIELD_WIDTH - .REMAINING_WIDTH;
	IF .EXTRA GTR 0
	THEN
	    $PAS$IO_ERROR (PAS$_LINTOOLON,1,.EXTRA);
	END;
    
	!+
	! Move leading blanks, if any.
	!-
    
	IF .FIELD_WIDTH - .DESCR [DSC$W_LENGTH] GTR 0
	THEN
	    FCB [FCB$A_RECORD_CUR] = CH$FILL (%C' ', .FIELD_WIDTH - .DESCR [DSC$W_LENGTH], .FCB [FCB$A_RECORD_CUR]);
	
	!+
	! Now move value
	!-
    
	FCB [FCB$A_RECORD_CUR] = CH$MOVE (.DESCR [DSC$W_LENGTH], .DESCR [DSC$A_POINTER], .FCB [FCB$A_RECORD_CUR]);

	END

    ELSE

	BEGIN

	!+
	! Update record position
	!-

	FCB [FCB$A_RECORD_CUR] = .FCB [FCB$A_RECORD_CUR] + .TOTAL_WIDTH;
	END;

    !+
    ! Call WRITE epilogue routine to move the last character written to the
    ! user's buffer and to unlock the file variable.
    !-

    PAS$$END_WRITE (PFV [PFV$R_PFV], FCB [FCB$R_FCB]);

    RETURN;

    END;					! End of routine PAS$WRITE_INTEGER

!<BLF/PAGE>

%SBTTL 'PAS$WRITEV_INTEGER - Write integer to string'
GLOBAL ROUTINE PAS$WRITEV_INTEGER (
	MAX_LENGTH: WORD,				! Maximum length of string
	STRING_LINE: REF VECTOR [, WORD],		! String to write to
	INTEGER,					! Value to write
	TOTAL_WIDTH: SIGNED,				! Total field width
	ERROR						! Error unwind address
    ) : NOVALUE =

!++
! FUNCTIONAL DESCRIPTION:
!
!	This procedure writes a signed integer to the specified string.
!
! CALLING SEQUENCE:
!
!	CALL PAS$WRITEV_INTEGER (MAX_LENGTH.rw.v, STRING_LINE.wvt.r,
!	   INTEGER.rl.v, TOTAL_WIDTH.rl.v [, ERROR.j.r])
!
! FORMAL PARAMETERS:
!
!	MAX_LENGTH	- The maximum length of STRING_LINE.
!
!	STRING_LINE	- A varying string to which the output will be appended.
!
!	INTEGER		- The integer to write.
!
!	TOTAL_WIDTH	- The width of the field to write.
!
!	ERROR		- Optional.  If specified, the address to unwind to
!			  in case of an error.
!
! IMPLICIT INPUTS:
!
!	NONE
!
! IMPLICIT OUTPUTS:
!
!	NONE
!
! ROUTINE VALUE:
!
!	NONE
!
! SIDE EFFECTS:
!
!	NONE
!
! SIGNALLED ERRORS:
!
!	See PAS$WRITE_INTEGER
!
!--

    BEGIN

    LOCAL
	PFV: $PAS$PFV_FILE_VARIABLE,	! Pascal File Variable
	ARG_LIST: VECTOR [4, LONG],	! Argument list
	PFV_ADDR: VOLATILE,		! Enable argument
	UNWIND_ACT: VOLATILE,		! Enable argument
	ERROR_ADDR: VOLATILE;		! Enable argument

    BUILTIN
	ACTUALCOUNT;			! Count of arguments

    ENABLE
	PAS$$IO_HANDLER (PFV_ADDR, UNWIND_ACT, ERROR_ADDR);	! Enable error handler

    !+
    ! Get ERROR parameter, if present.
    !-

    IF ACTUALCOUNT () GEQU 5
    THEN
	ERROR_ADDR = .ERROR;		! Set unwind address

    PFV_ADDR = PFV [PFV$R_PFV];		! Set PFV address

    !+
    ! Set up ARG_LIST.
    !-

    ARG_LIST [0] = 3;			! Three arguments
    ARG_LIST [1] = PFV [PFV$R_PFV];	! PFV address
    ARG_LIST [2] = .INTEGER;		! Integer to write
    ARG_LIST [3] = .TOTAL_WIDTH;	! Field width

    !+
    ! Call PAS$$DO_WRITEV to do the work, giving it the address of
    ! PAS$WRITE_INTEGER to call.
    !-

    PAS$$DO_WRITEV (PFV [PFV$R_PFV], .MAX_LENGTH, STRING_LINE [0], ARG_LIST, 
	PAS$WRITE_INTEGER);

    RETURN;

    END;					! End of routine PAS$WRITEV_INTEGER

!<BLF/PAGE>

END						! End of module PAS$WRITE_INTEGER

ELUDOM

