MODULE PAS$RESETK ( %TITLE 'RESETK procedure'
		IDENT = '1-001'			! File: PASRESETK.B32 Edit: SBL1001
		) =
BEGIN
!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982, 1984 BY				    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************
!

!++
! FACILITY:	Pascal Language Support
!
! ABSTRACT:
!
!	This module contains PAS$RESETK, which implements the
!	VAX-11 Pascal RESETK procedure.
!
! ENVIRONMENT:	User mode - AST reentrant
!
! AUTHOR: Steven B. Lionel, CREATION DATE: 16-February-1982
!
! MODIFIED BY:
!
! 1-001	- Original.  SBL 16-February-1982
!--


%SBTTL 'Declarations'
!
! PROLOGUE DEFINITIONS:
!

REQUIRE 'RTLIN:PASPROLOG';			! Externals, linkages, PSECTs, structures

!
! TABLE OF CONTENTS:
!

FORWARD ROUTINE
    PAS$RESETK: NOVALUE;			! Do a RESETK

!
! MACROS:
!
!	NONE
!
! EQUATED SYMBOLS:
!
!	NONE
!
! FIELDS:
!
!	NONE
!
! OWN STORAGE:
!
!	NONE
!

%SBTTL 'PAS$RESETK - RESETK procedure'
GLOBAL ROUTINE PAS$RESETK (
	PFV: REF $PAS$PFV_FILE_VARIABLE, 		! File variable
	KEY_NUMBER: REF VECTOR [, LONG],		! Key number
	ERROR						! Unwind address if error
    ): NOVALUE =

!++
! FUNCTIONAL DESCRIPTION:
!
!	PAS$RESETK implements the VAX-11 Pascal RESETK procedure.  It
!	rewinds the indexed file to the beginning of the specified index.
!
! CALLING SEQUENCE:
!
!	CALL PAS$RESETK (PFV.mr.r, KEY_NUMBER.rl.r [, ERROR.j.r])
!
! FORMAL PARAMETERS:
!
!	PFV		- The Pascal File Variable (PFV) passed by reference.
!			  The structure of the PFV is defined in PASPFV.REQ.
!
!	KEY_NUMBER	- The number of the key to rewind on.
!
!	ERROR		- Optional.  If specified, the address to unwind to
!			  if an error occurs.
!
! IMPLICIT INPUTS:
!
!	NONE
!
! IMPLICIT OUTPUTS:
!
!	NONE
!
! ROUTINE VALUE:
!
!	NONE
!
! SIDE EFFECTS:
!
!	Places file in Inspection mode
!
! SIGNALLED ERRORS:
!
!	FILNOTOPE - File not open
!	FILNOTKEY - File not opened for keyed access
!	KEYNOTDEF - Key "n" is not defined for this file
!	ERRDURRES - Error during RESET or RESETK
!	
!
!--

    BEGIN

    LOCAL
	FCB: REF $PAS$FCB_CONTROL_BLOCK,		! File control block
	STATUS,						! Status from $REWIND
	PFV_ADDR: VOLATILE,				! Enable argument
	UNWIND_ACT: VOLATILE,				! Enable argument
	ERROR_ADDR: VOLATILE;				! Enable argument

    BIND
	RAB = FCB: REF BLOCK [, BYTE];			! RMS RAB

    BUILTIN
	ACTUALCOUNT;

    ENABLE
	PAS$$IO_HANDLER (PFV_ADDR, UNWIND_ACT , ERROR_ADDR);

    IF ACTUALCOUNT () GEQU 3
    THEN
	ERROR_ADDR = .ERROR;				! Set unwind address

    !+
    ! Set PFV address enable argument.
    !-

    PFV_ADDR = PFV [PFV$R_PFV];

    !+
    ! Validate and lock PFV
    !-

    PAS$$VALIDATE_PFV (PFV [PFV$R_PFV]; FCB);

    !+
    ! Set unwind action to unlock file.
    !-

    UNWIND_ACT = PAS$K_UNWIND_UNLOCK;

    !+
    ! Open file if it should be implicitly opened.
    !-

    IF NOT .PFV [PFV$V_VALID]
    THEN
	PAS$$OPEN_IMPLICIT (PFV [PFV$R_PFV], FCB [FCB$R_FCB]; FCB);

    !+
    ! Verify that the file is open.
    !-

    IF NOT .PFV [PFV$V_OPEN]
    THEN
	$PAS$IO_ERROR (PAS$_FILNOTOPE,0);

    !+
    ! Verify that the file is opened for keyed access
    !-

    IF NOT .FCB [FCB$V_KEYED]
    THEN
	$PAS$IO_ERROR (PAS$_FILNOTKEY,0);

    !+
    ! Verify that the key number is valid for the file.
    !-

    IF .KEY_NUMBER [0] GEQU .FCB [FCB$L_NKEYS]
    THEN
	$PAS$IO_ERROR (PAS$_KEYNOTDEF,1,.KEY_NUMBER [0]);

    !+
    ! Set the key number and do a $REWIND.
    !-

    RAB [RAB$B_KRF] = .KEY_NUMBER [0];		! Set key number
    PFV [PFV$V_DFB] = 0;			! Undefine file buffer
    FCB [FCB$V_EOF] = 0;			! Not (yet) at EOF
    
    STATUS = $PAS$RMS_OP ($REWIND (RAB=.RAB));
    IF NOT .STATUS AND (.STATUS NEQ RMS$_BOF) AND (.STATUS NEQ RMS$_EOF)
    THEN
	$PAS$IO_ERROR (PAS$_ERRDURRES);	! Error during RESET or RESETK


    !+
    ! Do a GET.
    !-

    PAS$$GET (PFV [PFV$R_PFV], FCB [FCB$R_FCB]);

    !+
    ! Set Inspection mode
    ! Indicate successful completion
    ! Unlock the file
    !-

    FCB [FCB$V_INSPECTION] = 1;
    FCB [FCB$V_GENERATION] = 0;
    FCB [FCB$L_STATUS] = 0;
    PFV [PFV$V_LOCK] = 0;

    RETURN;

    END;					! End of routine PAS$RESETK

!<BLF/PAGE>

END						! End of module PAS$RESETK

ELUDOM
