%TITLE 'EDT$MCGETLIN - get a text line'
MODULE EDT$MCGETLIN (				! Get a text line
		IDENT = 'V04-000'			! File: MCGETLIN.BLI Edit: JBS1004
		) =
BEGIN
!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982, 1984 BY				    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************
!

!++
! FACILITY:	EDT -- The DEC Standard Editor
!
! ABSTRACT:
!
!	Get a text line.
!
! ENVIRONMENT:	Runs at any access mode - AST reentrant
!
! AUTHOR: Bob Kushlis, CREATION DATE: September 9, 1979
!
! MODIFIED BY:
!
! 1-001	- Original.  DJS 19-FEB-1981.  This module was created by
!	extracting routine EDT$$GET_TXTLN  from module MOVCUR.
! 1-002	- Regularize headers.  JBS 11-Mar-1981
! 1-003 - Put edt$$tst_eob in line. STS 22-Sep-1982
! 1-004	- Improve the appearance of the listing.  JBS 17-Jun-1983
!--


%SBTTL 'Declarations'
!
! TABLE OF CONTENTS:
!

REQUIRE 'EDTSRC:TRAROUNAM';

FORWARD ROUTINE
    EDT$$GET_TXTLN : NOVALUE;

!
! INCLUDE FILES:
!

REQUIRE 'EDTSRC:EDTREQ';

!
! MACROS:
!
!	NONE
!
! EQUATED SYMBOLS:
!
!	NONE
!
! OWN STORAGE:
!
!	NONE
!
! EXTERNAL REFERENCES:
!
!	In the routine

%SBTTL 'EDT$$GET_TXTLN  - get a text line'

GLOBAL ROUTINE EDT$$GET_TXTLN 			! Get a text line
    : NOVALUE =

!++
! FUNCTIONAL DESCRIPTION:
!
!	This routine fetches the current line from the work file into the
!	current line buffer.  The pointer EDT$$A_LN_PTR  is updated to
!	point to the cursor position and EDT$$A_LN_END  and EDT$$G_LN_LEN
!	are intitialized.  A carriage return character is placed after the
!	last character in the buffer to facilitate search operations.
!
! FORMAL PARAMETERS:
!
!	NONE
!
! IMPLICIT INPUTS:
!
!	EDT$$A_CUR_BUF
!	EDT$$T_LN_BUF
!	EDT$$A_WK_LN
!
! IMPLICIT OUTPUTS:
!
!	EDT$$G_LN_CHGD
!	EDT$$A_LN_PTR
!	EDT$$A_LN_END
!	EDT$$G_LN_LEN
!
! ROUTINE VALUE:
!
!	NONE
!
! SIDE EFFECTS:
!
!	NONE
!
!--

    BEGIN

    EXTERNAL ROUTINE
	EDT$$RD_CURLN;

    EXTERNAL
	EDT$$G_LN_CHGD,				! Indicates current line has changed.
	EDT$$A_CUR_BUF : REF TBCB_BLOCK,	! Current text buffer
	EDT$$T_LN_BUF,				! Current line buffer.
	EDT$$A_LN_PTR,				! Current character pointer.
	EDT$$A_LN_END,				! End of current line pointer.
	EDT$$G_LN_LEN,				! Length of current line.
	EDT$$Z_EOB_LN,
	EDT$$A_WK_LN : REF LIN_BLOCK;		! Current line pointer.

!+
! Make sure the current line is accessable.
!-
    EDT$$RD_CURLN ();
!+
! Check for end of buffer.
!-

    IF (.EDT$$A_WK_LN EQLA EDT$$Z_EOB_LN)
    THEN
	EDT$$G_LN_LEN = 0
    ELSE
	BEGIN
!+
! Get the length and move that many characters.
!-
	EDT$$G_LN_LEN = .EDT$$A_WK_LN [LIN_LENGTH];
	EDT$$CPY_MEM (.EDT$$G_LN_LEN, EDT$$A_WK_LN [LIN_TEXT], EDT$$T_LN_BUF);
	END;

!+
! Set up the end of line pointer.
!-
    EDT$$A_LN_END = CH$PTR (EDT$$T_LN_BUF, .EDT$$G_LN_LEN);
!+
! Place a CR at the end of the line.
!-
    CH$WCHAR (ASC_K_CR, .EDT$$A_LN_END);
!+
! Set up the cursor pointer to point to the current character.
!-
    EDT$$A_LN_PTR = CH$PTR (EDT$$T_LN_BUF, .EDT$$A_CUR_BUF [TBCB_CHAR_POS]);
!+
! And mark the current line as unchanged.
!-
    EDT$$G_LN_CHGD = 0;
    END;					! of routine EDT$$GET_TXTLN

!<BLF/PAGE>

END						! of module EDT$MCGETLIN

ELUDOM
