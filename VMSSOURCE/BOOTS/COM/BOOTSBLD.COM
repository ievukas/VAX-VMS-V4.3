$ !	[ B O O T S . C O M ] B O O T S B L D . C O M
$ !
$ !	THIS COMMAND FILE BUILDS THE SYSTEM BOOTSTRAPS.
$ !
$ ON ERROR THEN GOTO FAIL
$ GOTO 'PHASE'
$
$INIT:
$ !
$ !	Create the directories
$ !
$ @SYSBLDCOM$:CREATEDIR
$ EXIT
$
$UPDATE:
$ !
$ !	Create updated sources
$ !
$ SRCUPDATE BOOTDEF.SDL
$ SRCUPDATE BOOTS.MAR
$ SRCUPDATE SYSGENHLP.HLP
$ SRCUPDATE SYSGTTSTR.MSG		! for copying to [SYSHLP.EXAMPLES]
$ EXIT
$
$SRCLIB:
$ !
$ !	Create macro and help libraries.
$ !
$ SDL/LIS=LIS$:BOOTDEF/LANG=(MACRO=LIB$:BOOTDEF)/VMS SRC$:BOOTDEF
$ LIB/MAC/CREATE=(MODULES=10) LIB$:BOOTS SRC$:BOOTS,LIB$:BOOTDEF
$ LIB/HELP/CREATE=(MODULES=16,GLOBALS=0,BLOCKS=4) OBJ$:SYSGEN SRC$:SYSGENHLP
$ EXIT
$
$ASSEM:
$ !
$ !	Purge and re-create the bootstrap objects and listings
$ !
$ PURGE OBJ$:*.OBJ, LIS$:*.LIS
$ @SYSBLDCOM$:FASM 'FACNAM "" N 'UPDATES
$ EXIT
$
$OBJLIB:
$ !
$ !	Create object library from object modules.
$ !
$ COPY OBJ$:*.OBJ OBJ$:SYSBOOT.OBS
$ DELETE LIB$:SYSBOOT.OLB;*
$ ! /NOGLOBALS switch is included to avoid duplicate entry points
$ LIB/CREATE/NOGLOBALS LIB$:SYSBOOT OBJ$:SYSBOOT.OBS
$ DELETE OBJ$:SYSBOOT.OBS;*,OBJ$:*.OBJ;*
$ IF CPYOLB THEN CPYRESOBJ LIB$:SYSBOOT.OLB
$ EXIT
$
$LNKLIB:
$ !
$ ! Null LNKLIB phase
$ !
$ EXIT
$
$LINK:
$ !
$ !	DELETE MAP AND IMAGE FILES, THEN RE-CREATE 
$ !
$ DELETE EXE$:BOOT58.EXE;*, SYSBOOT.EXE;*, SYSGEN.EXE;*, VMB.EXE;*
$ DELETE EXE$:WRITEBOOT.EXE;*, BOOTBLOCK.EXE;*, STASYSGEN.EXE;*
$ DELETE EXE$:CONFIGURE.EXE;*, STACONFIG.EXE;*, STANDCONF.EXE;*
$ DELETE MAP$:BOOT58.MAP;*, SYSBOOT.MAP;*, SYSGEN.MAP;*, VMB.MAP;*
$ DELETE MAP$:WRITEBOOT.MAP;*, BOOTBLOCK.MAP;*, STASYSGEN.MAP;*
$ DELETE MAP$:CONFIGURE.MAP;*, STACONFIG.MAP;*, STANDCONF.MAP;*
$ @COM$:BOOT58LNK
$ @COM$:BOOBLKLNK
$ @COM$:CONFIGLNK
$ @COM$:STACFGLNK
$ @COM$:SYSBOOLNK
$ @COM$:SYSGENLNK
$ @COM$:STASYSLNK
$ @COM$:VMBLNK
$ @COM$:WRBOOTLNK
$ EXIT
$
$RESULT:
$ !
$ !	COPY THE IMAGE AND MAP FILES TO A SYSTEM AREA
$ !
$ CPYRESEXE EXE$:BOOT58.EXE
$ CPYRESMAP MAP$:BOOT58.MAP
$ CPYRESEXE EXE$:BOOTBLOCK.EXE
$ CPYRESMAP MAP$:BOOTBLOCK.MAP
$ CPYRESEXE EXE$:CONFIGURE.EXE
$ CPYRESMAP MAP$:CONFIGURE.MAP
$ CPYRESEXE EXE$:SYSBOOT.EXE
$ CPYRESMAP MAP$:SYSBOOT.MAP
$ CPYRESEXE EXE$:SYSGEN.EXE
$ CPYRESMAP MAP$:SYSGEN.MAP
$ CPYSYSHLP OBJ$:SYSGEN.HLB
$ CPYRESEXE EXE$:STASYSGEN.EXE
$ CPYRESMAP MAP$:STASYSGEN.MAP
$ CPYRESEXE EXE$:STACONFIG.EXE
$ CPYRESMAP MAP$:STACONFIG.MAP
$ CPYRESEXE EXE$:STANDCONF.EXE
$ CPYRESMAP MAP$:STANDCONF.MAP
$ CPYRESEXE EXE$:VMB.EXE
$ CPYRESMAP MAP$:VMB.MAP
$ CPYRESEXE EXE$:WRITEBOOT.EXE
$ CPYRESMAP MAP$:WRITEBOOT.MAP
$ !
$ !	Copy SYSGTTSTR.MSG to [SYSHLP.EXAMPLES]
$ !
$ CPYRES RESD$:[SYSHLP.EXAMPLES] SRC$:SYSGTTSTR.MSG
$ !
$ !	BUILD THE CONCATENATED LISTING 
$ !
$ CPYRESLIS LIS$:*.LIS BOOTS.LSS
$ EXIT
$
$FAIL:
$ ! >>>>>>>>>>>> BOOTS FAILURE <<<<<<<<<<<<<
