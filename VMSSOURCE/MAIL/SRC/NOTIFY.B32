MODULE MAIL$NOTIFY( %TITLE 'NOTIFY LOGGED ON USER OF NEW MAIL'
		IDENT = 'V04-000' ) =
BEGIN

!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982, 1984 BY				    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************
!

!++
! FACILITY:	VAX/VMS MAIL UTILITY
!
! ABSTRACT:	Notify logged on user that new mail exists
!
! ENVIRONMENT:	NATIVE/USER MODE
!
! AUTHOR: Len Kawell, CREATION DATE: 21-APR-1978
!
! MODIFIED BY:
!
!	V03-007	BLS0255		Benn Schreiber		28-Dec-1983
!		Print time in message if enabled in system flags
!
!	V03-006	BLS0241		Benn Schreiber		26-Sep-1983
!		Enable cluster $BRKTHRU.
!
!	V03-005	BLS0229		Benn Schreiber		16-Jul-1983
!		Set CARCON in $BRKTHRU.  Set broadcast class to MAIL
!
!	V03-004	BLS0224		Benn Schreiber		 2-Jun-1983
!		Use new $BRKTHRU service
!
!	V03-003	BLS0211		Benn Schreiber		13-Mar-1983
!		Raise/drop OPER/WORLD privs around this routine.  Pass
!		in as an argument the sender's name.
!
!	V03-002	BLS0197		Benn Schreiber		 9-Dec-1982
!		Use MAIL$Q_FULLUSER to get personal name
!
!	V03-001	BLS0177		Benn Schreiber		21-Jun-1982
!		Use symbolic values for username length.  Tell node name if
!		translation for SYS$NODE exists.
!--

!
! TABLE OF CONTENTS:
!

!
! INCLUDE FILES:
!
	LIBRARY 'SYS$LIBRARY:LIB';
	REQUIRE 'SRC$:MAILREQ';
	LIBRARY	'LIB$:MAILDEF';
!
! MACROS:
!

!
! EQUATED SYMBOLS:
!
LITERAL
	BELL	= 7;				! ASCII bell

EXTERNAL ROUTINE
    MAIL$GETMSG,				!Get message text
    MAIL$UP_PRIV,				!Raise privileges
    MAIL$DOWN_PRIV;				! and lower them
!
! OWN STORAGE:
!
BIND
	BELL_STR = UPLIT(BYTE(1,7));		! ASCIC BELL
!
! EXTERNAL REFERENCES:
!
EXTERNAL
	MAIL$GL_SYSFLAGS : $BBLOCK,		! System control flags
	MAIL$Q_SYSNODE: $BBLOCK;		! Translation of SYS$NODE

GLOBAL ROUTINE MAIL$NOTIFY(USERNAME,FROMDESC) : NOVALUE =

!++
! FUNCTIONAL DESCRIPTION:
!	Loops through all the processes on the system, and if a process's
!	user name matches the specified username, and the process is an
!	interactive process, a new mail message is broadcast to the terminal.
!
! INPUTS:
!
!	USERNAME = Address of username descriptor.
!
!	FROMDESC  = Sender's username descriptor.
!
! OUTPUTS:
!
!	New mail message broadcast to user's terminal, if logged on.
!--
BEGIN
LOCAL
	MSG_BUFF:	VECTOR[MAIL$K_INBUFFSZ],! Message buffer
	MSG_BUFF_DESC:	$BBLOCK[8],		! Message buffer descriptor
	DTIM,
	CLU_FLAG,
	STATUS;					! Routine status

DTIM = .MAIL$GL_SYSFLAGS[SYSF_V_NOTIFY_TIME];
MSG_BUFF_DESC[DSC$W_LENGTH] = MAIL$K_INBUFFSZ;	! Create message buffer desc
MSG_BUFF_DESC[DSC$A_POINTER] = MSG_BUFF;	!
IF .MAIL$Q_SYSNODE[DSC$W_LENGTH] NEQ 0
    THEN $FAO(MAIL$GETMSG((IF .DTIM THEN MAIL$_NOTIFY30
				ELSE MAIL$_NOTIFY10)),
		MSG_BUFF_DESC,
		MSG_BUFF_DESC,
		BELL_STR,MAIL$Q_SYSNODE,
		.FROMDESC,0)
    ELSE $FAO(MAIL$GETMSG((IF .DTIM THEN MAIL$_NOTIFY40
				ELSE MAIL$_NOTIFY20)),
	MSG_BUFF_DESC,
	MSG_BUFF_DESC,
	BELL_STR,.FROMDESC,0);

CLU_FLAG = 0;
IF .MAIL$GL_SYSFLAGS[SYSF_V_CLU_BRKTHRU]
    THEN CLU_FLAG = BRK$M_CLUSTER;

MAIL$UP_PRIV((1^$BITPOSITION(PRV$V_WORLD)	! Enable needed privileges
		OR 1^$BITPOSITION(PRV$V_OPER)),0);
!
! Send the message
!
$BRKTHRUW(MSGBUF=MSG_BUFF_DESC,
	 SENDTO=.USERNAME,
	 SNDTYP=BRK$C_USERNAME,
	 REQID=BRK$C_MAIL,
	 FLAGS=.CLU_FLAG,
	 CARCON=%C' ',
	 TIMOUT=10);

MAIL$DOWN_PRIV();				!Disable privs as needed
END;

END
ELUDOM
