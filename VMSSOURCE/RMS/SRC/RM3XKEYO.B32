MODULE RM3XKEYO (LANGUAGE (BLISS32) ,
		IDENT = 'V04-000'
		) =
BEGIN
!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982, 1984 BY				    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************

!++
!
! FACILITY:	RMS32 index sequential file organization
!
! ABSTRACT:
!		 This routine fills in the KEY XAB from the disk
!
!
! ENVIRONMENT:
!
!		VAX/VMS operating system
!
!--

!
! AUTHOR:        D. M. BOUSQUET
! CREATION DATE: 18-AUG-78  14:19
!
! MODIFIED BY:
!
!	V03-008	DAS0001		David Solomon		25-Mar-1984
!		Fix broken branches.
!
!	V03-007	LJA0099		Laurie J. Anderson	26-Sep-1983
!		Fix bugcheck, where someone will do a $DISPLAY on a
!		file opened with BRO.  In this case, the index descriptors
!		are not allocated.
!
!	V03-006 MCN0002		Maria del C. Nasr	31-Mar-1983
!		More linkages reorganization.
!
!	V03-005 MCN0001		Maria del C. Nasr	01-Mar-1983
!		Reorganize linkages
!
!	V03-004	KBT0289		Keith B. Thompson	23-Aug-1982
!		Reorganize psects
!
!	V03-003	KBT0074		Keith B. Thompson	29-Jun-1982
!		Enable the processing of no-contiguous key descriptors
!
!	V03-002	KBT0017		Keith Thompson		19-Mar-1982
!		Ignore compression bits when filling in xab of a prologue
!		1,2 file
!
!	V03-001	JWH0001		Jeffrey W. Horn		16-March-1982
!		Fix writing into space beyond the old end of XABKEY.
!
!	V02-006	DJD0001		Darrell Duffy		1-March-1982
!		Fix probing in RM$XKEYO3.
!
!	V02-005	KBT0004		K B Thompson		 8-Feb-1982
!		Correct compression bits when filling in key xab from
!		prologue and stuff the prologue version if long key=0 xab
!
!	V02-004	CDS0001		C Saether		 9-Aug-1981
!		Use alternate linkage declaration for RELEASE.
!
!	V02-003 REFORMAT	D M WALP		24-JUL-1980
!
!
! REVISION HISTORY:
!
!   Wendy Koenig, 	24-OCT-78  14:03
!   X0002 - make changes caused by sharing conventions
!
!*****

LIBRARY 'RMSLIB:RMS';
REQUIRE 'RMSSRC:RMSIDXDEF';

! Define default psects for code
!
PSECT
    CODE = RM$RMS3(PSECT_ATTR),
    PLIT = RM$RMS3(PSECT_ATTR);


LINKAGE
    L_CACHE,
    L_CHKSUM,
    L_FABREG_7,
    L_LINK_7_10_11,
    L_RELEASE_FAB;

! External Routines
!
EXTERNAL ROUTINE
    RM$CACHE		: RL$CACHE ADDRESSING_MODE( LONG_RELATIVE ),
    RM$CHKSUM		: RL$CHKSUM,
    RM$GET_NEXT_KEY	: RL$LINK_7_10_11,
    RM$RELEASE		: RL$RELEASE_FAB ADDRESSING_MODE( LONG_RELATIVE );


%SBTTL	'RM$XKEYO3'
GLOBAL ROUTINE RM$XKEYO3 ( XAB : REF BBLOCK ) : RL$FABREG_7 =

!++
!
! FUNCTIONAL DESCRIPTION:
!
!	The KEY XAB is filled in from the KEY descriptor
!
! CALLING SEQUENCE:
!
!	RM$XKEYO3 ( XAB )
!
! INPUT PARAMETERS:
!
!	XAB		-  Pointer to the KEY XAB we are filling in
!
! IMPLICIT INPUTS:
!
!	$XAB
!	    [REF]		-  Key of reference
!	    [KNM]		-  Pointer to key name buffer
!
!	$KEYDEF
!	    [KEY$S_KEYNAM]	-  Size of the keyname buffer, 32
!	    [KEY$T_KEYNAM]	-  Buffer of the keyname (table)
!
!	$IFAB
!	    [IFB$B_ORGCASE]	-  File organization
!	    [IFB$C_IDX]		-  Indexed file organization constant
!	    [IFB$B_MODE]	-  Mode of the key name buffer
!	    [IFB$B_PLG_VER]	-  Index file prologue version
!
!	$IDX_DFN
!	    [all fields in the index descriptor are input, 
!		but specifically reference a few]
!	    [IANUM]		-  Index area number
!	    [SEGMENTS]		-  Number of segments
!	    [FIXED_BLN]		-  Fixed block length analogous to the XAB
!	    [POSITION]		-  Position of first segment
!	    [SIZE]		-  Size of first segment
!
! OUTPUT PARAMETERS:
!
!	XAB			-  Untouched by this routine
!
! IMPLICIT OUTPUTS:
!
!	$XAB
!	    [all fields are filled in from the index descriptor]
!
! ROUTINE VALUE:
!
!	RMSERR
!	    (SUC)  - Success code
!
! SIDE EFFECTS:
!	none
!
!--

    BEGIN

    EXTERNAL REGISTER
	R_IDX_DFN_STR,
	COMMON_FAB_STR;

    LOCAL
	SAV_BDB,
	KEY_DESC	: REF BBLOCK;

    ! If BIO was set in the FAB, then we only want to make sure user knows that
    ! the NUM_KEYS is not filled in
    !
    IF .IFAB [ IFB$B_NUM_KEYS ] EQL 0
    THEN
	RETURN RMSSUC( OK_NOP );

    ! Let's first check to see if this is a indexed file
    !
    IF .IFAB [ IFB$B_ORGCASE ] EQL IFB$C_IDX
    THEN
	BEGIN

	! Now to make sure that the reference input in valid find the internal
	! index descriptor
	!
	! Start with the primary descriptor.  NOTE: We cannot call rm$key_desc
	! here because of register problems but we do know that the first
	! index descriptor off the ifab is the one for key 0 so get it that way
	!
	IDX_DFN = .IFAB [ IFB$L_IDX_PTR ];

	! Make sure that the index descriptors have been allocated before 
	! you use it.  Return success if it's not there, that's okay.
	IF .IDX_DFN EQLU 0
	THEN
	    RETURN RMSSUC( SUC );

	! Loop until we find it
	!
	WHILE .XAB [ XAB$B_REF ] NEQ .IDX_DFN [ IDX$B_KEYREF ]
	DO

	    ! If we ran out of keys there is a problem
	    !
	    IF NOT RM$GET_NEXT_KEY()
	    THEN
		RETURN RMSERR( REF );

	! Now to read in prologue descriptor
	!
	BEGIN
	GLOBAL REGISTER
	    COMMON_IO_STR;

	LOCAL
	    STATUS;

	STATUS = RM$CACHE( .IDX_DFN [ IDX$L_VBN ], 512, 0);

	! If error then return with error code in status
	!
	IF NOT .STATUS
	THEN
	    RETURN .STATUS;

	! Now to release the bucket and check it
	!
	RETURN_ON_ERROR( RM$CHKSUM(),
			 RM$RELEASE(0)
			 );

	! Now point to the key descriptor in the prologue
	!
	KEY_DESC = .BKT_ADDR + .IDX_DFN [ IDX$W_OFFSET ];

	! Now to save the BDB before CH$MOVE clobbers it
	!
	SAV_BDB = .BDB

	END;

	!	Now to do a straight move from the key descriptor to the XAB
	!
	CH$MOVE( $BYTEOFFSET( KEY$T_KEYNAM ) - $BYTEOFFSET ( KEY$B_IANUM ),
	         KEY_DESC [ KEY$B_IANUM ], 
		 XAB [ XAB$B_IAN ]);

	! If this is a prologue 3 file correct the compression bits
	!
	IF .IFAB [ IFB$B_PLG_VER ] GEQU 3
	THEN

	    ! SET in the prologue = CLEAR in the xab
	    !
	    XAB [ XAB$B_FLG ] = .XAB [ XAB$B_FLG ] XOR ( XAB$M_IDX_NCMPR OR
							 XAB$M_KEY_NCMPR OR
							 XAB$M_DAT_NCMPR );

	! If this is a long key xab and it is key-0 (primary)
	! then fill in the prologue version number
	!
	IF ( .XAB [ XAB$B_BLN ] EQLU XAB$K_KEYLEN ) AND
	   ( .XAB [ XAB$B_REF ] EQLU 0 )
	THEN
	    XAB [ XAB$B_PROLOG ] = .IFAB [ IFB$B_PLG_VER ];

	! If the user has a key name buffer fill it in
	!
	IF .XAB [ XAB$L_KNM ] NEQ 0
	THEN
	    BEGIN
	    LOCAL
		KNM_ADDR;

	    KNM_ADDR = .XAB [ XAB$L_KNM ];

	    ! Probe it
	    !
	    IFNOWRT( %REF( KEY$S_KEYNAM ),.KNM_ADDR,IFAB [ IFB$B_MODE ],
			BEGIN
			GLOBAL REGISTER
			    R_BDB_STR;

			BDB = .SAV_BDB;
			RM$RELEASE(0);
			RETURN RMSERR(KNM)
			END);


	    ! Now to move the buffer
	    !
	    CH$MOVE( KEY$S_KEYNAM,KEY_DESC [ KEY$T_KEYNAM ],.KNM_ADDR )

	    END;

	! Now move last long word.
	!
	XAB [ XAB$L_DVB ] = .KEY_DESC [ KEY$L_LDVBN ];

	BEGIN
	GLOBAL REGISTER
	    R_BDB_STR;

	BDB = .SAV_BDB;
	RM$RELEASE(0)
	END

	END;

    ! Now to return the success code if all went well
    !
    RETURN RMSSUC( SUC )

    END;

END

ELUDOM
