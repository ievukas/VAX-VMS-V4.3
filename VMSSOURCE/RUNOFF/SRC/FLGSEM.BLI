MODULE flgsem (	IDENT = 'V04-000'
		%BLISS32[, ADDRESSING_MODE (EXTERNAL    = LONG_RELATIVE,
					    NONEXTERNAL = LONG_RELATIVE)]
		) =
BEGIN
!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982, 1984 BY				    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************
!
!++
! FACILITY:	 DSR (Digital Standard RUNOFF) / DSRPLUS
!
! ABSTRACT:
!
!	Processes the .ENABLE UNDERLINING, .BOLDING, .INDEXING, and 
!	.OVERSTRIKING commands, and the .DISABLE UNDERLINING, BOLDING, 
!	INDEXING, and OVERSTRIKING commands.
!	It also processes .ENABLE TOC and .DISABLE TOC.
!
! ENVIRONMENT:	Transportable
!
! AUTHOR:	R.W.Friday	 CREATION DATE: May, 1978
!


%SBTTL 'Revision History'
!
! MODIFIED BY:
!
!	003	RER00003	Ron Randall	07-Mar-1983
!		Global edit of all modules. Updated module names, idents,
!		copyright dates. Changed require files to BLISS library.
!
!--


%SBTTL 'Module Level Declarations'

!
! TABLE OF CONTENTS:
!
! INCLUDE FILES:
!
LIBRARY 'NXPORT:XPORT';			! XPORT Library
REQUIRE 'REQ:RNODEF';			! RUNOFF variant definitions

%IF DSRPLUS %THEN
LIBRARY 'REQ:DPLLIB';			! DSRPLUS BLISS Library
%ELSE
LIBRARY 'REQ:DSRLIB';			! DSR BLISS Library
%FI

!
! EXTERNAL REFERENCES:
!
EXTERNAL
    GCA : GCA_DEFINITION,
    SCA : SCA_DEFINITION;


GLOBAL ROUTINE FLGSEM (HANDLER_CODE) : NOVALUE =

!++
! FUNCTIONAL DESCRIPTION:
!
!	FLGSEM turns the semantics associated with a flag
!	on and off.
!	It also turns table of contents generation on or off.
!
! FORMAL PARAMETERS:
!
!	HANDLER_CODE indicates which command is to be processed.
!
! IMPLICIT INPUTS:	None
!
! IMPLICIT OUTPUTS:	None
!
! ROUTINE VALUE:
! COMPLETION CODES:	None
!
! SIDE EFFECTS:		None
!--

    BEGIN
    !The following macro definition is to be removed after a few
    !versions of the new RUNOFF have gone out the door.
    MACRO XXXX(X) =
	BEGIN
	EXTERNAL ROUTINE ERM;
	EXTERNAL ROUTINE ERMA;
	EXTERNAL LITERAL
	    RNFMD1,
	    RNFSTR;

	ERM(RNFMD1,
	    CH$PTR ( UPLIT (X)),
	    %CHARCOUNT (X));
	ERMA(RNFSTR, FALSE);
	END %;

    SELECTONE .HANDLER_CODE OF
	SET

	[H_ENABLE_BOLDIN] :
	    BEGIN
	    SCA_DO_BLD = .GCA_CMD_BLD;
	    SCA_WRD_C_BLD = .SCA_BLD AND .GCA_CMD_BLD;
	    END;

	[H_HYPHENATION, H_ENABLE_HYPHEN] :
	    BEGIN
	    SCA_DO_HYP = TRUE;
	    END;

	[H_ENABLE_UNDERL] :
	    BEGIN
	    SCA_DO_UND = .GCA_CMD_UND;
	    SCA_WRD_C_UND = .SCA_UND AND .GCA_CMD_UND;
	    END;

	[H_ENABLE_OVERST] :
	    SCA_DO_OVR = .GCA_CMD_OVR;

	[H_ENABLE_INDEXI] :
	    BEGIN
	    SCA_DO_IND = .GCA_CMD_IND;
	    SCA_INDEX = .GCA_CMD_IND;
	    END;

	[H_DISABLE_BOLDI] :
	    BEGIN
	    SCA_DO_BLD = FALSE;
	    SCA_WRD_C_BLD = FALSE;
	    END;

	[H_NO_HYPHENATIO, H_DISABLE_HYPHE] :
	    BEGIN
	    SCA_DO_HYP = FALSE;
	    END;

	[H_DISABLE_UNDER] :
	    BEGIN
	    SCA_DO_UND = FALSE;
	    SCA_WRD_C_UND = FALSE;
	    END;

	[H_DISABLE_OVERS] :
	    SCA_DO_OVR = FALSE;

	[H_DISABLE_INDEX] :
	    BEGIN
	    SCA_DO_IND = FALSE;
	    SCA_INDEX = FALSE;
	    END;

	[H_ENABLE_TOC] :
	    BEGIN
	    !Turn on table of contents collection if user said /BTC
	    GCA_BTC = .GCA_CMD_BTC
	    END;

	[H_DISABLE_TOC] :
	    BEGIN
	    GCA_BTC = FALSE
	    END;

	TES;

    END;					!End of FLGSEM

END						!End of module
ELUDOM
