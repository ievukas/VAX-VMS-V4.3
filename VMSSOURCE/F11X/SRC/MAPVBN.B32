MODULE MAPVBN (
		LANGUAGE (BLISS32),
		IDENT = 'V04-000'
		) =
BEGIN

!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982, 1984 BY				    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************

!++
!
! FACILITY:  F11ACP Structure Level 1
!
! ABSTRACT:
!
!	This routine maps the specified virtual blocks to their
!	corresponding logical blocks using the supplied window.
!	The window is turned if necessary.
!
! ENVIRONMENT:
!
!	STARLET operating system, including privileged system services
!	and internal exec routines.
!
!--
!
!
! AUTHOR:  Andrew C. Goldstein,	 CREATION DATE:  3-Mar-1977  12:20
!
! MODIFIED BY:
!
!	V03-006	CDS0005		Christian D. Saether	20-Aug-1984
!		Modify test for no lock.
!
!	V03-005	CDS0004		Christian D. Saether	14-Aug-1984
!		Modify handling of fcb rebuilding.
!
!	V03-004	CDS0003		Christian D. Saether	25-Apr-1984
!		Use longword addressing on some routines.
!
!	V03-003	CDS0002		Christian D. Saether	30-Dec-1983
!		Use L_NORM linkage and BIND_COMMON macro.
!
!	V03-002	CDS0001		Christian D. Saether	 2-Feb-1983
!		Changes for distributed file system.  Don't believe
!		FCB$L_FILESIZE anymore, always check the header.
!
!	V03-001	ACG0297		Andrew C. Goldstein,	5-Aug-1982  18:26
!		Fix maintenance of UCB context in updating cathedral windows
!
!	V02-004	ACG0229		Andrew C. Goldstein,	23-Dec-1981  21:08
!		Move updating of PMS$GL_TURN from TURN_WINDOW
!
!	V02-003	LMP0003		L. Mark Pilant,		 9-Dec-1981  14:07
!		Added support for cathedral windows.
!
!	V02-002	ACG0167		Andrew C. Goldstein,	16-Apr-1980  19:25
!		Previous revision history moved to F11A.REV
!**


LIBRARY 'SYS$LIBRARY:LIB.L32';
REQUIRE 'SRC$:FCPDEF.B32';

GLOBAL ROUTINE MAP_VBN (VBN, WINDOW, BLOCK_COUNT, UNMAPPED_BLOCKS) : L_NORM =

!++
!
! FUNCTIONAL DESCRIPTION:
!
!	This routine maps the specified virtual blocks to their
!	corresponding logical blocks using the supplied window.
!	the window is turned if necessary.
!
! CALLING SEQUENCE:
!	MAP_VBN (ARG1, ARG2, ARG3, ARG4)
!
! INPUT PARAMETERS:
!	ARG1: desired VBN
!	ARG2: address of window to use
!	ARG3: number of blocks to map
!		if not present, 1
!
! IMPLICIT INPUTS:
!	CURRENT_VCB: address of VCB in process
!	CURRENT_UCB: address of UCB in process
!
! OUTPUT PARAMETERS:
!	ARG4: if present, addres to store number of unmapped blocks
!
! IMPLICIT OUTPUTS:
!	NONE
!
! ROUTINE VALUE:
!	starting LBN or -1 if no map
!
! SIDE EFFECTS:
!	window may be turned, header may be read, volume may be switched
!
!--

BEGIN

MAP
	WINDOW		: REF BBLOCK;

LOCAL
	COUNT,				! number of blocks to map
	UNMAPPED,			! address to store unmapped block count
	DUMMY,				! place for above by default
	UCB		: REF BBLOCK,	! address of mapping UCB
	FCB		: REF BBLOCK,	! address of FCB of file
	HEADER		: REF BBLOCK,	! address of file header
	LBN;				! resulting LBN of map

EXTERNAL
	CLU$GL_CLUB	: ADDRESSING_MODE (GENERAL),
	PMS$GL_TURN	: ADDRESSING_MODE (ABSOLUTE);
					! system count of window turns

BIND_COMMON;

EXTERNAL ROUTINE
	REBLD_PRIM_FCB	: L_NORM NOVALUE, ! rebuild a primary fcb from header
	BUILD_EXT_FCBS	: L_NORM NOVALUE, ! build extension fcb chain,
	SWITCH_VOLUME	: L_NORM,	! switch context to specified volume
	MAP_WINDOW	: L_NORM,	! scan window map
	READ_HEADER	: L_NORM,	! read file header
	TURN_WINDOW	: L_NORM ADDRESSING_MODE (GENERAL), ! turn window
	REMAP_FILE	: L_NORM;	! remap the file into segmented windows


! Check the VBN for legality - i.e., non-zero
!

FCB = .WINDOW[WCB$L_FCB];

IF .VBN EQL 0
THEN
    RETURN -1;

IF .VBN GTRU .FCB [FCB$L_FILESIZE]
THEN
    BEGIN

    IF .FCB [FCB$B_ACCLKMODE] NEQ 0
    THEN
	BEGIN
	IF NOT .FCB [FCB$V_STALE]
	THEN
	    RETURN -1;
	END
    ELSE
	IF NOT .BBLOCK [CURRENT_UCB [UCB$L_DEVCHAR2], DEV$V_CLU]
	    OR .CLU$GL_CLUB EQL 0
	THEN
	    RETURN -1;

! Either the FCB has been marked stale, or this is a nolock access (which
! means the fcb is always suspect because it cannot be marked stale),
! so rebuild the fcb and extension fcb chain, if there is one.
!

    HEADER = READ_HEADER (0, .FCB);

    REBLD_PRIM_FCB (.FCB, .HEADER);

    IF .HEADER [FH2$W_EX_FIDNUM] NEQ 0
	OR .HEADER [FH2$B_EX_FIDNMX] NEQ 0
    THEN
	BUILD_EXT_FCBS (.HEADER);

    END;

! If an extension was done on a file which was completely mapped, and more
! than one user was accessing it, it is necessary to remap the file to get
! all the blocks correctly mapped.
!

IF .WINDOW[WCB$V_CATHEDRAL] AND NOT .WINDOW[WCB$V_COMPLETE]
THEN REMAP_FILE ();

! Make the filesize test again, in case we did a reconstruction of the
! chain above.  This allows the window to be remapped in that case, if
! necessary.
!

IF .VBN GTRU .FCB [FCB$L_FILESIZE]
THEN
    RETURN -1;

! If the file is multi-header, scan the extension FCB's for the one
! containing the desired VBN. The right FCB is identified by noting that
! there are no more, or that the start VBN of the next one is greater than
! the desired VBN.
!

UNTIL
    (IF .FCB[FCB$L_EXFCB] EQL 0 THEN 1
     ELSE .BBLOCK [.FCB[FCB$L_EXFCB], FCB$L_STVBN] GTRU .VBN
    )
DO FCB = .FCB[FCB$L_EXFCB];

! If chasing extension FCB's took us to another volume, switch the context to
! that volume.
!

SWITCH_VOLUME (.FCB[FCB$W_FID_RVN]);

! Default the optional arguments.
!

COUNT = (IF ACTUALCOUNT GEQ 3
	THEN .BLOCK_COUNT
	ELSE 1
	);
UNMAPPED = (IF ACTUALCOUNT GEQ 4
	THEN .UNMAPPED_BLOCKS
	ELSE DUMMY
	);

! Attempt to map the transfer with the existing window. If the map fails
! completely, turn the window and try once more. When any blocks map,
! return the relevant data.
!

DECR I FROM 2 TO 1 DO
   BEGIN

    LBN = KERNEL_CALL (MAP_WINDOW, .VBN, .WINDOW, .COUNT, .UNMAPPED, UCB);
    IF .LBN NEQ -1 THEN EXITLOOP;

    PMS$GL_TURN = .PMS$GL_TURN + 1;	! count window turn in PMS data base
    HEADER = READ_HEADER (0, .FCB);
    KERNEL_CALL (TURN_WINDOW, .WINDOW, .HEADER, .VBN, .FCB[FCB$L_STVBN]);

    END;

IF .UCB NEQ .CURRENT_UCB
THEN BUG_CHECK (BADRVNWCB, FATAL, 'Inconsistent RVN in window map pointer');
RETURN .LBN;

END;					! end of routine MAP_VBN

END
ELUDOM
