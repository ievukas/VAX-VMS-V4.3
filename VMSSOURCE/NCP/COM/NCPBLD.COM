$ !
$ !	[NCP.COM] N C P B L D . C O M 
$ !
$ !	Assumes [NCP] and definitions for system build
$ !
$ GOTO 'PHASE'
$
$INIT:
$ !
$ !	Create the Directories
$ !
$ @SYSBLDCOM$:CREATEDIR
$ EXIT
$
$UPDATE:
$ !
$ !	Create updated library file sources.
$ !
$ DELETE SRC$:*.*;*
$ SRCUPDATE NCPDEF.SDL
$ SRCUPDATE NMADEF.SDL			! 2.2 definitions
$ SRCUPDATE NCPLIBRY.B32
$ SRCUPDATE NCPERRMSG.MSG
$ SRCUPDATE NCPHLP.HLP
$ SRCUPDATE NMAHEAD.B32
$ SRCUPDATE NMATAIL.B32
$
$ ! NMADEF is needed by VMSLIB in SRCLIB phase
$
$ DELETE LIB$:NMADEF.B32;*, .MAR;*
$ SDL /VMS /LANG=(BLISS=LIB$,MACRO=LIB$) SRC$:NMADEF
$ CPYSHRLIB LIB$:NMADEF.MAR			! For VMSLIB, LIB.MLB
$ CPYSHRLIB LIB$:NMADEF.R32			! For [OPCOM.SRC]SECURITY.B32
$
$ EXIT
$
$SRCLIB:
$ !
$ !	NCPLIBRY
$ !
$ DELETE LIB$:NCPDEF.B32;*, .MAR;*
$ SDL /VMS /LANG=BLISS=LIB$ SRC$:NCPDEF
$ BLISS /LIB=LIB$:NCPLIBRY /LIS=LIS$:NCPLIBRY -
	SRC$:NCPLIBRY+NMAHEAD+LIB$:NCPDEF+SRC$:NMATAIL
$ !
$ !	NMALIBRY
$ !
$ DELSHRLIB LIB$:NMALIBRY.MLB
$ DELSHRLIB LIB$:NMALIBRY.L32
$
$ DELETE LIB$:NMALIBRY.MLB;*
$ BLISS /LIB=LIB$:NMALIBRY /LIS=LIS$:NMALIBRY -
	SRC$:NMAHEAD+LIB$:NMADEF+SRC$:NMATAIL
$ LIB/MACRO/CREATE LIB$:NMALIBRY LIB$:NMADEF
$
$ CPYSHRLIB LIB$:NMALIBRY.MLB
$ CPYSHRLIB LIB$:NMALIBRY.L32
$ !
$ !	NCPHELP
$ !
$ LIB/CREATE=(KEYSIZE:31,MODULE:21)/HELP LIB$:NCPHELP SRC$:NCPHLP
$ EXIT
$
$ASSEM:
$ !
$ !	PURGE and re-create the assembly language objects and listings
$ !
$ PURGE OBJ$:*.OBJ,*.OBB,LIS$:*.LIS
$ @SYSBLDCOM$:FASM 'FACNAM "" NOLIB 'UPDATES'
$ CPYSYSMSG OBJ$:NCPERRMSG.OBB
$ EXIT
$
$OBJLIB:
$ !
$ !	Create object library from object modules.
$ !
$ @SYSBLDCOM$:OBJLIB NCP
$ EXIT
$
$LNKLIB:
$ !
$ ! Null LNKLIB phase
$ !
$ EXIT
$
$LINK:
$ !
$ !	Delete NCP map and image then re-create NCP.EXE and NCP.MAP
$ !
$ @COM$:NCPLNK
$ EXIT
$
$RESULT:
$ !
$ !	Copy the EXE and MAP to a System area
$ !
$ CPYRESEXE EXE$:NCP.EXE
$ CPYRESMAP MAP$:NCP.MAP
$ CPYSYSHLP LIB$:NCPHELP.HLB
$ CPYRESLIS LIS$:*.LIS NCP.LSS
