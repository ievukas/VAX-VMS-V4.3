%TITLE 'Set time to user-specified time'
MODULE SETTIM (
		IDENT = 'V04-000'
%BLISS32[,
	ADDRESSING_MODE(EXTERNAL=LONG_RELATIVE,NONEXTERNAL=LONG_RELATIVE)
	]
		) =
BEGIN
!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982, 1984 BY				    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************
!
!++
! FACILITY:	 DSR (Digital Standard RUNOFF) / DSRPLUS
!
! ABSTRACT: Sets the time to a user-specified time.
!
!
! ENVIRONMENT: Transportable
!
! AUTHOR: R.W.Friday	 CREATION DATE: April, 1979
!

%SBTTL 'Revision History'
!
! MODIFIED BY:
!
!	002	REM00002	Ray Marshall	07-Mar-1983
!		Global edit of all modules. Updated module names, idents,
!		copyright dates. Changed require files to BLISS library.
!
!--

%SBTTL 'Module Level Declarations'
!
! TABLE OF CONTENTS:
!
!
! INCLUDE FILES:
!

LIBRARY 'NXPORT:XPORT';			! XPORT Library
REQUIRE 'REQ:RNODEF';			! RUNOFF variant definitions

%IF DSRPLUS %THEN
LIBRARY 'REQ:DPLLIB';			! DSRPLUS BLISS Library
%ELSE
LIBRARY 'REQ:DSRLIB';			! DSR BLISS Library
%FI

!
! MACROS:
!
MACRO
    DOPARM (P,MAX) =
	!Process this parameter only if it was specified.
	IF					
	    .NMLST_DESCR (P) NEQ NM_NULL
	THEN
	    BEGIN
	    IF					!
		.NMLST_DESCR (P) EQL NM_PLUS
		OR .NMLST_DESCR (P) EQL NM_MINUS
	    THEN
		TEMP_DATIM [2+P] = .TEMP_DATIM [2+P] + .NMLST_VALUE (P)
	    ELSE
		TEMP_DATIM [2+P] = .NMLST_VALUE (P);

	    !Check for negative or GTR MAX value
	    IF					!
		.TEMP_DATIM [2+P] LSS 0	!
		OR .TEMP_DATIM [2+P] GTR MAX	!
	    THEN
		!Illegal number value.
		BEGIN
		ERMA (RNFINM, FALSE);
		RETURN;
		END
	    END			%;
!
! EQUATED SYMBOLS:
!
!
! OWN STORAGE:
!
!
! EXTERNAL REFERENCES:
!

EXTERNAL
    DATTIM : VECTOR [6],			!Date and time.
    NMLST : NUMBER_LIST;			!GLNM returns command parameters here

!
EXTERNAL LITERAL				!Error messages
    RNFINM;

!

EXTERNAL ROUTINE
    ERMA,
    GLNM,
    RNODAT;

!


%SBTTL 'SETTIM  --'
GLOBAL ROUTINE settim (handler_code) : NOVALUE = 	!

!++
! FUNCTIONAL DESCRIPTION:
!
!	SETTIM calls GLNM to pick up the hours, minutes, and seconds, in that order.
!	If the user says nothing, then the time is set to the system time.
!
! FORMAL PARAMETERS:
!
!	HANDLER_CODE is not examined.  It is passed to this routine
!	for reasons of conformity only.  It will always have the
!	value H_SET_TIME.
!
! IMPLICIT INPUTS:	None
!
! IMPLICIT OUTPUTS:	None
!
! ROUTINE value:
! COMPLETION CODES:	None
!
! SIDE EFFECTS:		None
!
!--

    BEGIN

    LOCAL
	temp_datim : VECTOR [6];

    glnm (3);					!Request hours, minutes, and seconds.

    IF	.nmlst_count EQL 0  THEN
	BEGIN
	!Get entire system date/time.
	rnodat (temp_datim);
	!Save just the system time part.
	dattim [3] = .temp_datim [3];		!Hours
	dattim [4] = .temp_datim [4];		!Minutes
	dattim [5] = .temp_datim [5];		!Seconds
	RETURN;
	END;

    !Assume the most complicated case: that the user is
    !adjusting settings by adding/subtracting values.
    !Make a copy of the current time.
    temp_datim [3] = .dattim [3];		!Hours
    temp_datim [4] = .dattim [4];		!Minutes
    temp_datim [5] = .dattim [5];		!Seconds

    !Validate the hours, if given
    doparm (1,23);				!First parameter can have a maximum value of 23

    !Validate the minutes, if given
    IF	.nmlst_count GEQ 2  THEN
	doparm (2,59);				!Second paramater can have a maximum value of 59

    !Validate the seconds, if given
    IF	.nmlst_count GEQ 3  THEN
	doparm (3,59);				!Maixmum of 59 seconds.

    !Make computed time currend
    dattim [3] = .temp_datim [3];		!Hours
    dattim [4] = .temp_datim [4];		!Minutes
    dattim [5] = .temp_datim [5];		!Seconds

    END;					!End of SETTIM

END						!End of module
ELUDOM
