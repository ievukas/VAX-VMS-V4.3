$ IF "''NETSERVER$COMMAND'" .NES. "" THEN NETSERVER$COMMAND
$ IF "''NETSERVER$VERIFY'"  .EQS. "" THEN NETSERVER$VERIFY = 0
$ V = F$VERIFY(NETSERVER$VERIFY)
$ DEFPRV = F$SETPRV("ALL")
$ DEFDIR = F$LOGICAL("SYS$DISK") + F$DIRECTORY()
$ DEFUIC = F$USER()
$ DEFPRIB= F$GETJPI("","PRIB")
$ STARTUP_FLAG = "Y"
$ PERMANENT_NETSERVER = "N"
$LOOP:
$ TEMP = F$SETPRV("ALL")
$ SET PROCESS/PRIORITY='DEFPRIB'
$ IF F$USER() .NES. DEFUIC THEN SET UIC 'DEFUIC'
$ TEMP = F$SETPRV(DEFPRV)
$ RUN SYS$SYSTEM:NETSERVER
$ IF	 = "IF"
$ THEN	 = "THEN"
$ DELETE = "DELETE"
$ IF F$TYPE(SET)      .NES. "" THEN DELETE/SYMBOL/GLOBAL SET
$ IF F$TYPE(RUN)      .NES. "" THEN DELETE/SYMBOL/GLOBAL RUN
$ IF F$TYPE(GOTO)     .NES. "" THEN DELETE/SYMBOL/GLOBAL GOTO
$ IF F$TYPE(DEFINE)   .NES. "" THEN DELETE/SYMBOL/GLOBAL DEFINE
$ IF F$TYPE(WRITE)    .NES. "" THEN DELETE/SYMBOL/GLOBAL WRITE
$ IF F$TYPE(DEASSIGN) .NES. "" THEN DELETE/SYMBOL/GLOBAL DEASSIGN
$ SET DEFAULT 'DEFDIR'
$ IF STARTUP_FLAG .EQS. "Y" THEN GOTO STARTUP_NETSERVER
$ IF .NOT. PERMANENT_NETSERVER THEN GOTO LOOP
$ PERMANENT_NETSERVER_COUNTER = PERMANENT_NETSERVER_COUNTER + 1
$ IF PERMANENT_NETSERVER_COUNTER .LT. 25 THEN GOTO LOOP
$ PERMANENT_NETSERVER_COUNTER = 0
$ SET NOON
$ DEASSIGN SYS$OUTPUT
$ DEFINE SYS$OUTPUT NETSERVER.LOG
$ WRITE SYS$OUTPUT "NETSERVER.LOG - new version created ''F$TIME()'."
$ SET ON
$ GOTO LOOP
$ !
$ ! First time through. Purge old logs.
$ ! Also, see if there are any permanent NETSERVER slots open...
$ !
$STARTUP_NETSERVER:
$ STARTUP_FLAG = "N"
$ IF F$SEARCH("SYS$LOGIN:NETSERVER.LOG;-10") .NES. "" THEN -
	PURGE /KEEP:3 SYS$LOGIN:NETSERVER.LOG
$ USER := 'F$GETJPI("","USERNAME")
$ COUNT = F$LOGICAL("NETSERVER$SERVERS_''USER'")
$ IF COUNT .EQS. "" THEN GOTO LOOP
$ IF F$TYPE(COUNT) .NES. "INTEGER" THEN GOTO LOOP
$ N = 1
$TEST_PERMANENT:
$ NODE = F$LOGICAL("SYS$NODE") - "::" - "_"
$ OPEN/READ/ERROR=CREATE LOCK SYS$LOGIN:NETSERVER_'USER'_'N'_'NODE'.DAT;1
$ READ/END_OF_FILE=RECREATE LOCK PID
$ ON WARNING THEN GOTO RECREATE
$ U := 'F$GETJPI(PID,"USERNAME")
$ IF U .NES. USER THEN GOTO RECREATE
$ ON WARNING THEN CONTINUE
$ CLOSE LOCK
$ N = N + 1
$ IF N .GT. COUNT THEN GOTO LOOP
$ GOTO TEST_PERMANENT
$RECREATE:
$ CLOSE LOCK
$ ON WARNING THEN GOTO LOOP
$ DELETE SYS$LOGIN:NETSERVER_'USER'_'N'_'NODE'.DAT;1
$CREATE:
$ ON WARNING THEN GOTO LOOP
$ OPEN/WRITE LOCK SYS$LOGIN:NETSERVER_'USER'_'N'_'NODE'.DAT;1
$ ON WARNING THEN CONTINUE
$ PID = F$GETJPI("","PID")
$ WRITE LOCK PID
$ CLOSE LOCK
$ PERMANENT_NETSERVER_COUNTER = 0
$ PERMANENT_NETSERVER = "Y"
$ DEFINE NETSERVER$TIMEOUT "9999 23:59:59"
$ WAIT 0:0:20
$ GOTO LOOP
