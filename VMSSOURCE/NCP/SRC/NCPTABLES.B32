%TITLE	'Text Tables'
MODULE NCPTABLES (IDENT = 'V04-000', LIST(NOOBJECT)) =
BEGIN

!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982, 1984 BY				    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************
!

!++
! FACILITY:	Network Control Program (NCP)
!
! ABSTRACT:
!
!	This module contain tables of status codes and text strings
!	used for reporting error messages from NML.
!
! ENVIRONMENT:	VAX/VMS Operating System
!
! AUTHOR:	Darrell Duffy	, CREATION DATE: 30-October-1979
!
! MODIFIED BY:
!
!	V03-030	PRD0091		Paul R. DeStefano	30-Mar-1984
!		Added table for breakdown of Status and Error fields
!		of NICE detail when return status = NMA$C_STS_OPE.
!
!	V03-029	PRD0084		Paul R. DeStefano	26-Mar-1984
!		Added NMA$C_ENT_ARE to DNA entity detail table.
!
!	V03-028	PRD0067		Paul R. DeStefano	14-Feb-1984
!		Change PCXP_MNS parameter to type of LITB.
!
!	V03-027	PRD0043		Paul R. DeStefano	05-Jan-1984
!		Add service node version parameter.
!
!	V03-026	TMH0026		Tim Halvorsen		20-Jul-1983
!		Add executor alias parameter
!
!	V03-025	RPG0025		Bob Grosso		17-May-1983
!		Correct CIRPOL, the Circuit Polling State table.
!
!	V03-024	RPG0024		Bob Grosso		02-May-1983
!		Add decoding for PCCI_SSB and _SPY.
!
!	V03-023	RPG0023		Bob Grosso		14-Mar-1983
!		Change ni addresses from type HEX to NIADR
!
!	V03-022	RPG0022		Bob Grosso		18-Feb-1983
!		Add CTLIN_MBS, _MSN and CTCIR_MNE, _UBU.
!		Add Line parameter Buffer size PCLI_BSZ.
!		Add OBJECT PROXY and NODE DEFAULT PROXY type REQUIRED.
!		Add decoding for RSX and SERVER BASE specific parameters.
!		Clean up Module configurator table
!
!	V03-021	RPG0021		Bob Grosso		15-Dec-1982
!		Add Line parameter Eternet Protocol Type, PCLI_EPT.
!
!	V03-020	RPG0020		Bob Grosso		12-Nov-1982
!		Move area parameters, and fix a few nits.
!		Swap definition of ???TY_X25 and ???TY_LAPB.
!		Change 'maximum broadcast endnodes' to 
!		'max broadcast nonrouters'.  
!
!	V03-019	RPG0019		Bob Grosso		06-Oct-1982
!		Change NI to Ethernet
!		Add new circuit transport types
!
!	V03-018	RPG0018		Bob Grosso		10-Sep-1982
!		Change X-P channels to RNGL type, so that the channel
!		lists can be formatted on a single line.
!		Fix decoding of X25-Access password.
!
!	V03-017	RPG0017		Bob Grosso		08-Sep-1982
!		Alter DTE substate coded values.  Convert some HXPS
!		to HEX.
!
!	V03-016	TMH0016		Tim Halvorsen		19-Aug-1982
!		Shorten some X25-TRACE parameter descriptions so that
!		aren't truncated in the display.
!		Add new values for line protocol to table.
!
!	V03-015	RPG0015		Bob Grosso		02-Aug-1982
!		Add Line mode and Line microcode dump.
!		Add X25-Protocol Max Circuits, Substate, and 
!		Multi-Network Support.
!		Add X25-Server State add Object filespec.
!
!	V03-014	RPG0014		Bob Grosso		14-Jul-1982
!		Add MODULE X25-Trace.  Add NODE TYPEs AREA and
!		ROUTING IV and NONROUTING IV.
!
!	V03-013	RPG0013		Bob Grosso		05-Jul-1982
!		Add NI support for Nodes and Lines.
!		Add CTNOD_ZER to node counter table, which worked
!		by accident before the counters table was split up.
!
!	V03-012	RPG0012		Bob Grosso		23-Jun-1982
!		Add X25 support.
!		Break up tbl_prm_counter into tables by entities.
!		Add new QUERY_STATES_S macro to allow for more flexible
!		prompting with MODULE entity.
!
!	V011	TMH0011		Tim Halvorsen	10-May-1982
!		Add Ethernet circuit parameters, DRT, MRT, RPR.
!		Add EXECUTOR SUBADDRESSES parameter.
!
!	V010	TMH0010		Tim Halvorsen	20-Jan-1982
!		Add VER and XPT circuit parameters.
!
!	V009	TMH0009		Tim Halvorsen	31-Dec-1981
!		Add DMF as a service device.
!		Replace PCL counters with new counter definitions.
!
!	V008	TMH0008		Tim Halvorsen	17-Dec-1981
!		Add text for LINK REMOTE ID parameter.
!		Add executor default proxy, node and object proxy.
!
!	V007	TMH0007		Tim Halvorsen	11-Nov-1981
!		Add circuit source type for logging.
!
!	V006	TMH0006		Tim Halvorsen	22-Oct-1981
!		Fix the spelling on some messages.
!		Add executor default access and node access
!
!	V005	LMK0005		Len Kawell	27-Sep-1981
!		Modify text for V3.0.
!
!	V004	TMH0004		Tim Halvorsen	25-Aug-1981
!		Add new link parameters.
!
!	V003	TMH0003		Tim Halvorsen	11-Aug-1981
!		Add detail table describing VMS entities (NMA$C_SENT_xxx)
!
!	V002	TMH0002		Tim Halvorsen	07-Jul-1981
!		Add LINE CLOCK and rename 'maximum blocks' to 'maximum 
!		transmits'.  Fix numeric values for circuit polling state.
!		Add PCL11-B line counters.
!		Change NODE MAXIMUM LINES to MAXIMUM CIRCUITS.
!		Change NODE SERVICE LINE to SERVICE CIRCUIT.
!		Change NODE LINE (loop line) to CIRCUIT.
!
!	V001	TMH0001		Tim Halvorsen	22-Jun-1981
!		Add parameter text strings for new V2.2 parameters.
!--

%SBTTL	'Definitions'

!
! INCLUDE FILES:
!

LIBRARY 'LIB$:NMALIBRY';
LIBRARY 'LIB$:NCPLIBRY';

%SBTTL	'Macros to Build Tables'

!
!   Macro to build code tables
!

!
!	$NCP_BLDTBL (CLS, COD, TXT ... )
!
!	CLS	Name of table is NCP$GA_TBL_cls
!	COD	Value for code word in search
!	TXT	Text string to associate with COD
!

MACRO
	$NCP_BLDTBL (CLS) =

PSECT	GLOBAL = $NCP_TABLES;		! Build global name for table
GLOBAL
	%NAME ('NCP$GA_TBL_', CLS) : VECTOR [0]
	;

PSECT	GLOBAL = $GLOBAL$;		! Set it back
PSECT	OWN = $NCP_TABLES;		! Build table with own data
SWITCHES UNAMES;			! Object listing we can follow

	$NCP_TBLS (%REMAINING)		! Build table entries

OWN
	TBL_COD : WORD ALIGN (0) INITIAL (-1)	! End table with -1 code word
	;
UNDECLARE TBL_COD;			! Get rid of old symbol
SWITCHES NOUNAMES;			! In general we dont want this
PSECT	OWN = $OWN$;			! Set the psect back for own
	%;

!
!	Build one entry of the table (for all the entries)
!

MACRO
	$NCP_TBLS [COD, TXT] =		! Loop to build all the codes

OWN					! In the own to control placement
	TBL_COD : WORD ALIGN (0) INITIAL (COD),	! The code word
	TBL_TXT : WORD ALIGN (0) 	! Word offset to the text
		INITIAL (UPLIT BYTE (%ASCIC TXT) - TBL_TXT)
	;

UNDECLARE TBL_COD, TBL_TXT;		! Get rid of symbols for reuse
	%;


!
!	Macro to build parameter text tables
!

MACRO
	$NCP_PRMTBL (CLS) =

PSECT	GLOBAL = $NCP_TABLES;			! Define the name of the table
GLOBAL
	%NAME ('NCP$GA_PRM_', CLS) : VECTOR [0]	! Name has no size
	;

PSECT	GLOBAL = $GLOBAL$;			! Set this one back
PSECT	OWN = $NCP_TABLES;			! Build with own for no global
SWITCHES UNAMES;				! Pretty listings

	$NCP_PRMS (%REMAINING)			! Build all the entries

OWN 						! Cap things off
	PRM_ID : WORD ALIGN (0) INITIAL (-1),
	PRM_TYP : BYTE INITIAL (-1)
	;

UNDECLARE PRM_ID, PRM_TYP;			! Remove the names
SWITCHES NOUNAMES;				! Reset pretty listings
PSECT OWN = $OWN$;				! Reset the psect

	%;

MACRO
	$NCP_PRMS [ID, TYP, LST, TXT] =

%IF NOT %NULL (LST)				! If we have a list entry
%THEN
    %IF	NOT %DECLARED (LST)			! If its not declared
    %THEN					! Its coming along later
	    FORWARD LST : VECTOR [0];		! Correct attributes
    %FI
%FI

OWN 						! Build one table entry
	PRM_ID	: WORD ALIGN (0) INITIAL (%NAME ('NMA$C_',ID) ),
	PRM_TYP	: BYTE INITIAL (%NAME ('PBK$K_',TYP) ),
	PRM_LST	: WORD ALIGN (0)
			%IF NOT %NULL (LST)
			%THEN INITIAL ( (LST) - PRM_LST)
			%ELSE INITIAL (0)
			%FI
			,
	PRM_TXT : WORD ALIGN (0)
			INITIAL ( (UPLIT BYTE (%ASCIC TXT) ) - PRM_TXT)
	;

UNDECLARE 					! Remove the symbols
	PRM_ID, PRM_TYP, PRM_LST, PRM_TXT
	;

	%;


!
!	Macro to build counted string lists
!

MACRO

	$NCP_BLDLST (CLS) =		! List is a string of ascic strings

GLOBAL BIND

	%NAME ('NCP$GA_LST_', CLS) =	! Form of the name

	UPLIT BYTE
	(
	$NCP_LST (%REMAINING)
	,BYTE (0)
	)
	;

%,

	$NCP_LST [ITEM] =

	%ASCIC ITEM

%
;

%SBTTL	'Status Code Tables'

!
!	Define the psects to hold the data
!


    PSECT GLOBAL = $NCP_TABLES (NOWRITE, ALIGN (2) );
    PSECT PLIT   = $NCP_TABLE_PLIT (NOWRITE, ALIGN (2) );

!
!	Build NML status tables
!

	$NCP_BLDTBL

	(NMLSTS,


	NMA$C_STS_SUC AND 255, 'Success',
	NMA$C_STS_MOR AND 255, 'Request accepted, more to come',
	NMA$C_STS_PAR AND 255, 'Partial reply',
	NMA$C_STS_DON AND 255, 'Done',
	NMA$C_STS_FUN AND 255, 'Unrecognized function or option',
	NMA$C_STS_INV AND 255, 'Invalid message format',
	NMA$C_STS_PRI AND 255, 'Privilege violation',
	NMA$C_STS_SIZ AND 255, 'Oversized management command message',
	NMA$C_STS_MPR AND 255, 'Network management program error',
	NMA$C_STS_PTY AND 255, 'Unrecognized parameter type',
	NMA$C_STS_MVE AND 255, 'Incompatible management version',
	NMA$C_STS_CMP AND 255, 'Unrecognized component',
	NMA$C_STS_IDE AND 255, 'Invalid identification format',
	NMA$C_STS_LCO AND 255, 'Line communication error',
	NMA$C_STS_STA AND 255, 'Component in wrong state',
	NMA$C_STS_FOP AND 255, 'File open error',
	NMA$C_STS_FCO AND 255, 'Invalid file contents',
	NMA$C_STS_RES AND 255, 'Resource error',
	NMA$C_STS_PVA AND 255, 'Invalid parameter value',
	NMA$C_STS_LPR AND 255, 'Line protocol error',
	NMA$C_STS_FIO AND 255, 'File I/O error',
	NMA$C_STS_MLD AND 255, 'Mirror link disconnected',
	NMA$C_STS_ROO AND 255, 'No room for new entry',
	NMA$C_STS_MCF AND 255, 'Mirror connect failed',
	NMA$C_STS_PNA AND 255, 'Parameter not applicable',
	NMA$C_STS_PLO AND 255, 'Parameter value too long',
	NMA$C_STS_HAR AND 255, 'Hardware failure',
	NMA$C_STS_OPE AND 255, 'Operation failure',
	NMA$C_STS_SYS AND 255, 'System-specific management function not supported',
	NMA$C_STS_PGP AND 255, 'Invalid parameter grouping',
	NMA$C_STS_BLR AND 255, 'Bad loopback response',
	NMA$C_STS_PMS AND 255, 'Parameter missing',
	NMA$C_STS_ALI AND 255, 'Invalid alias identification',
	NMA$C_STS_OBJ AND 255, 'Invalid object identification',
	NMA$C_STS_PRO AND 255, 'Invalid process identification',
	NMA$C_STS_LNK AND 255, 'Invalid link identification',

	)


!
!	Error details
!

!
!	STS_FOP and STS_FIO
!

	$NCP_BLDTBL

	(FOPDTL,

	NMA$C_FOPDTL_PDB, 'Permanent database',
	NMA$C_FOPDTL_LFL, 'Load file',
	NMA$C_FOPDTL_DFL, 'Dump file',
	NMA$C_FOPDTL_SLF, 'Secondary loader',
	NMA$C_FOPDTL_TLF, 'Tertiary loader',
	NMA$C_FOPDTL_SDF, 'Secondary dumper',

	)

!
!	STS_MLD, STS_MCF
!

	$NCP_BLDTBL

	(NCEDTL,

	NMA$C_NCEDTL_NNA, 'No node name set',
	NMA$C_NCEDTL_INN, 'Invalid node name format',
	NMA$C_NCEDTL_UNA, 'Unrecognized node name',
	NMA$C_NCEDTL_UNR, 'Node unreachable',
	NMA$C_NCEDTL_RSC, 'Network resources',
	NMA$C_NCEDTL_RJC, 'Rejected by object',
	NMA$C_NCEDTL_ONA, 'Invalid object name format',
	NMA$C_NCEDTL_OBJ, 'Unrecognized object',
	NMA$C_NCEDTL_ACC, 'Access control rejected',
	NMA$C_NCEDTL_BSY, 'Object too busy',
	NMA$C_NCEDTL_NRS, 'No response from object',
	NMA$C_NCEDTL_NSD, 'Node shut down',
	NMA$C_NCEDTL_DIE, 'Node or object failed',
	NMA$C_NCEDTL_DIS, 'Disconnect by object',
	NMA$C_NCEDTL_ABO, 'Abort by object',
	NMA$C_NCEDTL_ABM, 'Abort by management',

	)


!
!	Details for STS_CMP, STS_IDE, STS_STA
!

	$NCP_BLDTBL

	(ENTDTL,

	NMA$C_ENT_NOD, 'Node',
	NMA$C_ENT_LIN, 'Line',
	NMA$C_ENT_LOG, 'Logging',
	NMA$C_ENT_CIR, 'Circuit',
	NMA$C_ENT_MOD, 'Module',
	NMA$C_ENT_ARE, 'Area',
	)

	$NCP_BLDTBL

	(VMSENTDTL,

	NMA$C_ENT_NOD, 'Node',
	NMA$C_ENT_LIN, 'Line',
	NMA$C_ENT_LOG, 'Logging',
	NMA$C_SENT_OBJ, 'Object',
	NMA$C_SENT_LNK, 'Link'
	)

!
!	Details STS_OPE
!

	$NCP_BLDTBL

	(OPEDTL,

	NMA$C_OPEDTL_DCH, 'data check',			! Status field bits
	NMA$C_OPEDTL_TIM, 'timeout',
	NMA$C_OPEDTL_ORN, 'data overrun',
	NMA$C_OPEDTL_ACT, 'unit is active',
	NMA$C_OPEDTL_BAF, 'buffer allocation failure',
	NMA$C_OPEDTL_RUN, 'protocol running',
	NMA$C_OPEDTL_DSC, 'line disconnected',
	NMA$C_OPEDTL_FTL, 'fatal hardware error',	! Error field bits
	NMA$C_OPEDTL_MNT, 'DDCMP maintainance message received',
	NMA$C_OPEDTL_LST, 'data lost due to buffer size mismatch',
	NMA$C_OPEDTL_THR, 'threshold error',
	NMA$C_OPEDTL_TRB, 'tributary malfunction',
	NMA$C_OPEDTL_STA, 'DDCMP start message received',
	)



%SBTTL	'Parameter Text Tables'

!
!    Area
!

	$NCP_PRMTBL
	(ARE,

	PCAR_STA, LITB, NCP$GA_TBL_NODSTA, 	'State',
	PCAR_COS, NUMW,		, 		'Cost',
	PCAR_HOP, NUMB, 	, 		'Hops',
	PCAR_CIR, TKN,	 	, 		'Circuit',
	PCAR_NND, NADR, 	, 		'Next node to area',

	)

!
!    circuit
!

	$NCP_PRMTBL
	(CIR,

	PCCI_STA, LITB,	NCP$GA_TBL_LINSTA,	'State',
	PCCI_SUB, LITB, NCP$GA_TBL_LINSUB,	'Substate',
	PCCI_SER, LITB, NCP$GA_TBL_LINSVC,	'Service',
	PCCI_LCT, NUMW,		,		'Counter timer',
	PCCI_CNO, NADR,		,		'Connected node',
!	PCCI_COB, ????,		,		'Connected object',
	PCCI_LOO, TKN,		,		'Loopback name',
	PCCI_ADJ, NADR,		,		'Adjacent node',
	PCCI_DRT, NADR,		,		'Designated router',
	PCCI_BLO, NUMW,		,		'Block size',
	PCCI_COS, NUMB,		,		'Cost',
	PCCI_MRT, NUMB,		,		'Maximum routers allowed',
	PCCI_RPR, NUMB,		,		'Router priority',
	PCCI_HET, NUMW,		,		'Hello timer',
	PCCI_LIT, NUMW,		,		'Listen timer',
	PCCI_BLK, LITB,	NCP$GA_TBL_CIRBLK,	'Blocking',
	PCCI_MRC, NUMB,		,		'Maximum recalls',
	PCCI_RCT, NUMW,		,		'Recall timer',
	PCCI_NUM, TKN,		,		'Number',
	PCCI_USR, ENT,		,		'User',
	PCCI_POL, LITB, NCP$GA_TBL_CIRPOL,	'Polling state',
	PCCI_PLS, LITB, NCP$GA_TBL_CIRPLS,	'Polling substate',
	PCCI_OWN, ENT,		,		'Owner',
	PCCI_LIN, TKN,		,		'Line',
	PCCI_USE, LITB, NCP$GA_TBL_CIRUSE,	'Usage',
	PCCI_TYP, LITB, NCP$GA_TBL_CIRTYP,	'Type',
	PCCI_DTE, TKN,		,		'DTE',
	PCCI_CHN, NUMW,		,		'Channel',
	PCCI_MBL, NUMW,		,		'Maximum data',
	PCCI_MWI, NUMB,		,		'Maximum window',
	PCCI_TRI, NUMB,		,		'Tributary',
	PCCI_BBT, NUMW, 	,		'Babble timer',
	PCCI_TRT, NUMW, 	,		'Transmit timer',
	PCCI_MRB, NUMB,		,		'Maximum buffers',
	PCCI_MTR, NUMB,		,		'Maximum transmits',
	PCCI_ACB, NUMB, 	,		'Active base',
	PCCI_ACI, NUMB, 	,		'Active increment',
	PCCI_IAB, NUMB, 	,		'Inactive base',
	PCCI_IAI, NUMB, 	,		'Inactive increment',
	PCCI_IAT, NUMB, 	,		'Inactive threshold',
	PCCI_DYB, NUMB, 	,		'Dying base',
	PCCI_DYI, NUMB, 	,		'Dying increment',
	PCCI_DYT, NUMB, 	,		'Dying threshold',
	PCCI_DTH, NUMB, 	,		'Dead threshold',

	PCCI_VER, LITB,	NCP$GA_TBL_CIRVER,	'Verification',
	PCCI_XPT, LITB, NCP$GA_TBL_CIRXPT,	'Transport type',
	PCCI_SSB, LITB, NCP$GA_TBL_LINSUB,	'Service substate',
	PCCI_SPY, NIADR, 	,		'Service physical address',

	!
	! RSX-specific circuit parameters
	!
	PCCI_RSX_MAC, HIGH,	,		'Multipoint active ratio',
	PCCI_RSX_LOG, HIGH,	,		'Logical name',
	PCCI_RSX_DLG, HIGH,	,		'Designated name',
	PCCI_RSX_ACT, HIGH,	,		'Actual name',

	! 
	! Server Base specific Circuit parameters
	!
	PCCI_SRV_LOG, HIGH,	,		'Logical name',
	PCCI_SRV_DLG, HIGH,	,		'Designated name',
	PCCI_SRV_ACT, HIGH,	,		'Actual name',

	)


!
!    line
!

	$NCP_PRMTBL
	(LIN,

	PCLI_STA, LITB, NCP$GA_TBL_LINSTA, 	'State',
	PCLI_SUB, LITB, NCP$GA_TBL_LINSUB, 	'Substate',
	PCLI_SER, LITB, NCP$GA_TBL_LINSVC, 	'Service',
	PCLI_LCT, NUMW, 	,		'Counter timer',
	PCLI_LOO, TKN,		, 		'Loopback name',
	PCLI_ADJ, NADR, 	, 		'Adjacent node',
	PCLI_BLO, NUMW, 	, 		'Block size',
	PCLI_COS, NUMB, 	, 		'Cost',
	PCLI_DEV, TKN, 		,		'Device',
	PCLI_PRO, LITB,	NCP$GA_TBL_LINPRO,	'Protocol',
	PCLI_DUP, LITB, NCP$GA_TBL_LINDPX, 	'Duplex',
	PCLI_CON, LITB, NCP$GA_TBL_LINCTL, 	'Controller',
	PCLI_CLO, LITB, NCP$GA_TBL_LINCLO,	'Clock',
!	PCLI_LTY, LITB, NCP$GA_TBL_LINTYP, 	'Type',
	PCLI_STI, NUMW, 	, 		'Service timer',
	PCLI_NTI, NUMW, 	, 		'Retransmit timer',
	PCLI_HTI, NUMW,		,		'Holdback timer',
	PCLI_MBL, NUMW,		,		'Maximum block',
	PCLI_MRT, NUMB,		,		'Maximum retransmits',
	PCLI_MWI, NUMB,		,		'Maximum window',
	PCLI_TRI, NUMB, 	, 		'Tributary address',
	PCLI_SLT, NUMW,		,		'Scheduling timer',
	PCLI_DDT, NUMW,		,		'Dead timer',
	PCLI_DLT, NUMW,		,		'Delay timer',
	PCLI_SRT, NUMW,		,		'Stream timer',
	PCLI_HWA, NIADR,	,		'Hardware address',
	PCLI_BFN, NUMW, 	, 		'Receive buffers',

	PCLI_MCD, TKN,		,		'Micro-code dump file',
	PCLI_XMD, LITB,	NCP$GA_TBL_LINMOD,	'X25 line mode',
	PCLI_EPT, NUMW,		,		'Ethernet protocol type',
	PCLI_BSZ, NUMW,		,		'Buffer size',

	!
	! RSX-specific line parameters
	!
	PCLI_RSX_OWN, HIGH,	,		'Owner',
	PCLI_RSX_CCS, HIGH,	,		'Controller CSR',
	PCLI_RSX_UCS, HIGH,	,		'Unit CSR',
	PCLI_RSX_VEC, HIGH,	,		'Vector',
	PCLI_RSX_PRI, HIGH,	,		'Priority',
	PCLI_RSX_MDE, HIGH,	,		'Dead polling ratio',
	PCLI_RSX_LLO, LITB, NCP$GA_TBL_LINLOC,	'Location',
	PCLI_RSX_LOG, HIGH,	,		'Logical name',
	PCLI_RSX_DLG, HIGH,	,		'Designated name',
	PCLI_RSX_ACT, HIGH,	,		'Actual name',

	!
	!    Server Base specific line parameters
	!
	PCLI_SRV_OWN, HIGH,	,		'Owner',
	PCLI_SRV_UCS, HIGH,	,		'Unit CSR',
	PCLI_SRV_VEC, HIGH,	,		'Vector',
	PCLI_SRV_PRI, HIGH,	,		'Priority',
	PCLI_SRV_LOG, HIGH,	,		'Logical name',
	PCLI_SRV_DLG, HIGH,	,		'Designated name',
	PCLI_SRV_ACT, HIGH,	,		'Actual name',

	)


!
!    logging
!

	$NCP_PRMTBL
	(LOG,

	PCLO_STA, LITB, NCP$GA_TBL_LOGSTA, 	'State',
	PCLO_LNA, TKN, 		, 		'Name',
	PCLO_SIN, NADR, 	, 		'Sink node',
	PCLO_EVE, ESET, 	, 		'Events',

	)


!
!    module X25-Access
!

	$NCP_PRMTBL
	(MODACC,

	PCXA_NET, TKN,		,		'Network',
	PCXA_NOD, NADR,		,		'Node',
	PCXA_USR, TKN,		,		'User',
	PCXA_PSW, LITB, NCP$GA_TBL_MSEPAS,	'Password',
	PCXA_ACC, TKN,		,		'Account',

	!
	! RSX-specific X.25-Access module parameters
	!
	PCXA_RSX_ADS, HIGH,	,		'Destination',
	PCXA_RSX_ANB, HIGH,	,		'Number',
	PCXA_RSX_ASC, HIGH,	,		'Scope',

	!
	! Server Base specific X.25-Access module parameters
	!
	PCXA_SRV_ADS, HIGH,	,		'Destination',
	PCXA_SRV_ANB, HIGH,	,		'Number',
	PCXA_SRV_ASC, HIGH,	,		'Scope',

	)


!
!    module X25 Protocol
!

	$NCP_PRMTBL
	(MODPRO,

	PCXP_STA, LITB, NCP$GA_TBL_MPRSTA,	'State',
	PCXP_CTM, NUMW,		,		'Counter timer',
	PCXP_ACH, NUMW,		,		'Active channels',
	PCXP_ASW, NUMW,		,		'Active switched',
	PCXP_DTE, TKN,		,		'DTE',
	PCXP_GRP, TKN,		,		'Group',
	PCXP_NET, TKN,		,		'Network',
	PCXP_LIN, TKN,		,		'Line',
	PCXP_CHN, RNGL,		,		'Channels',
	PCXP_MCH, NUMW,		,		'Maximum channels',
	PCXP_DBL, NUMW,		,		'Default data',
	PCXP_DWI, NUMB,		,		'Default window',
	PCXP_MBL, NUMW,		,		'Maximum data',
	PCXP_MWI, NUMB,		,		'Maximum window',
	PCXP_MCL, NUMB,		,		'Maximum clears',
	PCXP_MRS, NUMB,		,		'Maximum resets',
	PCXP_MST, NUMB,		,		'Maximum restarts',
	PCXP_CAT, NUMB,		,		'Call timer',
	PCXP_CLT, NUMB,		,		'Clear timer',
	PCXP_RST, NUMB,		,		'Reset timer',
	PCXP_STT, NUMB,		,		'Restart timer',
	PCXP_GDT, TKN,		,		'DTE',
	PCXP_GNM, NUMW,		,		'Number',
	PCXP_GTY, LITB, NCP$GA_TBL_MPRTYP,	'Type',
 
	PCXP_MNS, LITB, NCP$GA_TBL_MPRMNS,	'Multi-Network support',
	PCXP_MCI, NUMW,		,		'Maximum circuits',
 
	!
	!      RSX-specific X.25-Protocol Module parameters
	!
	PCXP_RSX_PMC, HIGH,	,		'Maximum circuits',

	!
	! Server Base specific X.25-Protocol Module parameters
	!
	PCXP_SRV_PMC, HIGH,	,		'Maximum circuits',
	PCXP_SBS, LITB, NCP$GA_TBL_MPRSBS,	'Substate',

	)


!
!    module X25 Server
!    module X29-Server
!

	$NCP_PRMTBL
	(MODSER,

	PCXS_CTM, NUMW,		,		'Counter timer',
	PCXS_ACI, NUMW,		,		'Active circuits',
	PCXS_DST, TKN,		,		'Destination',
	PCXS_MCI, NUMW,		,		'Maximum circuits',
	PCXS_NOD, NADR,		,		'Node',
	PCXS_USR, TKN,		,		'User',
	PCXS_SPW, TKN,		,		'Password',
	PCXS_RPW, LITB, NCP$GA_TBL_MSEPAS,	'Password',
	PCXS_ACC, TKN,		,		'Account',
	PCXS_OBJ, OBJ,		,		'Object',
	PCXS_PRI, NUMB,		,		'Priority',
	PCXS_CMK, HEX,		,		'Call mask',
	PCXS_CVL, HEX,		,		'Call value',
	PCXS_GRP, TKN,		,		'Group',
	PCXS_NUM, TKN,		,		'Number',
	PCXS_SAD, SAD,		,		'Subaddresses',

	PCXS_STA, LITB, NCP$GA_TBL_MPRSTA,	'State',
	PCXS_FIL, TKN,		,		'Object filespec',

	!
	! RSX-specific X.25-Server Module parameters
	!
	PCXS_RSX_5ST, LITB, NCP$GA_TBL_LINSTA,	'State',
 
	!
	! Server Base specific X.25-Server Module parameters
	!
	PCXS_SRV_5ST, LITB, NCP$GA_TBL_LINSTA,	'State',

	)


!
!    module X25-Trace (VMS specific)
!

	$NCP_PRMTBL
	(MODTRC,

	PCXT_STA, LITB, NCP$GA_TBL_NODSTA, 	'State',
	PCXT_BSZ, NUMW,		,		'Buffer size',
	PCXT_MBK, NUMW,		,		'Maximum blocks',
	PCXT_FNM, TKN,		,		'Filename',
	PCXT_MBF, NUMW,		,		'Maximum number of buffers',
	PCXT_CPL, NUMW,		,		'Global data capture size',
	PCXT_MVR, NUMW,		,		'Maximum file version',
	PCXT_TPT, TKN,		,		'Trace point name',
	PCXT_CPS, NUMW,		,		'Per-trace capture size',
	PCXT_TST, LITB, NCP$GA_TBL_NODSTA, 	'Per-trace state',

	)


!
!    Console module
!

	$NCP_PRMTBL
	(MODCON,

	PCCO_RTR, NUMW,		,		'Reservation timer',

	)


!
!    Loader module
!

	$NCP_PRMTBL
	(MODLOA,

	PCLD_ASS, LITB,	NCP$GA_TBL_MLOASS,	'Assistance flag',

	)


!
!    Looper module
!

	$NCP_PRMTBL
	(MODLOO,

	PCLP_ASS, LITB, NCP$GA_TBL_MLOASS,	'Assistance flag',

	)


!
!    Configurator module
!

	$NCP_PRMTBL
	(MODCNF,

	PCCN_CIR, TKN,		,		'Circuit name',
	PCCN_SUR, LITB,	NCP$GA_TBL_MLOASS,	'Surveillance flag',
	PCCN_ELT, DELTIM, 	,		'Elapsed time',
	PCCN_PHA, NIADR,	,		'Physical address',
	PCCN_LRP, DAYTIM, NCP$GA_TBL_MON,	'Time of last report',
	PCCN_MVR, TRIPL,	,		'Maintenance version',
	PCCN_FCT, LITLST, NCP$GA_TBL_CNFFCT,	'Function list',
	PCCN_CUS, NADR,		,		'Current console user',
	PCCN_RTR, NUMW,		,		'Reservation timer',
	PCCN_CSZ, NUMW,		,		'Command buffer size',
	PCCN_RSZ, NUMW,		,		'Response buffer size',
	PCCN_HWA, NIADR,	,		'Hardware address',
	PCCN_DTY, LITB, NCP$GA_TBL_SVCDEV,	'Device type',
!!	PCCN_SFI, ???		,		'Software ID',
	PCCN_SPR, LITB,	NCP$GA_TBL_MCOPRC,	'System processor',
	PCCN_DLK, LITB,	NCP$GA_TBL_MCODAT,	'Data link type',

	)


!
!    node
!

	$NCP_PRMTBL
	(NOD,

	PCNO_STA, LITB, NCP$GA_TBL_NODSTA, 	'State',
	PCNO_PHA, NIADR,	,		'Physical address',
	PCNO_IDE, TKN, 		, 		'Identification',
	PCNO_MVE, TRIPL, 	, 		'Management version',
	PCNO_SLI, TKN, 		, 		'Service circuit',
	PCNO_SNV, LITB, NCP$GA_TBL_NODSNV,	'Service node version',
	PCNO_SPA, HXPS, 	, 		'Service password',
	PCNO_SDV, LITB, NCP$GA_TBL_SVCDEV, 	'Service device',
	PCNO_CPU, LITB, NCP$GA_TBL_CPUTYP, 	'CPU type',
	PCNO_HWA, NIADR,	,		'Hardware address',
	PCNO_LOA, TKN, 		, 		'Load file',
	PCNO_SLO, TKN, 		, 		'Secondary loader',
	PCNO_TLO, TKN, 		, 		'Tertiary loader',
	PCNO_DFL, TKN,		,		'Diagnostic loader',
	PCNO_STY, LITB, NCP$GA_TBL_SOFTYP, 	'Software type',
	PCNO_SID, TKN, 		, 		'Software identification',
	PCNO_DUM, TKN, 		, 		'Dump file',
	PCNO_SDU, TKN, 		, 		'Secondary dumper',
	PCNO_DAD, NUML, 	, 		'Dump address',
	PCNO_DCT, NUML, 	, 		'Dump count',
	PCNO_OHO, NADR, 	, 		'Host',
	PCNO_IHO, NADR, 	, 		'Host',
	PCNO_LPC, NUMW, 	, 		'Loop count',
	PCNO_LPL, NUMW, 	, 		'Loop length',
	PCNO_LPD, LITB, NCP$GA_TBL_LOODAT, 	'Loop Data type',
	PCNO_LPA, NIADR,	,		'Loop assistant physical address',
	PCNO_LPH, LITB, NCP$GA_TBL_NODHLP,	'Loop help type',
	PCNO_CTI, NUMW, 	, 		'Counter timer',
	PCNO_NNA, TKN, 		, 		'Name',
	PCNO_NLI, TKN, 		, 		'Circuit',
	PCNO_ADD, NUMW, 	, 		'Address',
	PCNO_ITI, NUMW, 	, 		'Incoming timer',
	PCNO_OTI, NUMW, 	, 		'Outgoing timer',
	PCNO_ACL, NUMW, 	, 		'Active links',
	PCNO_DEL, NUMW, 	, 		'Delay',
	PCNO_NVE, TRIPL, 	, 		'NSP version',
	PCNO_MLK, NUMW, 	, 		'Maximum links',
	PCNO_DFA, NUMB, 	, 		'Delay factor',
	PCNO_DWE, NUMB, 	, 		'Delay weight',
	PCNO_IAT, NUMW, 	, 		'Inactivity timer',
	PCNO_RFA, NUMW, 	, 		'Retransmit factor',
	PCNO_DTY, LITB, NCP$GA_TBL_NODTYP,	'Type',
	PCNO_DCO, NUMW, 	, 		'Cost',
	PCNO_DHO, NUMB, 	, 		'Hops',
	PCNO_DLI, TKN, 		, 		'Circuit',
	PCNO_NND, NADR,		,		'Next node to destination',
	PCNO_RVE, TRIPL, 	, 		'Routing version',
	PCNO_ETY, LITB, NCP$GA_TBL_NODTYP, 	'Type',
	PCNO_RTI, NUMW, 	, 		'Routing timer',
	PCNO_SAD, SAD,		,		'Subaddresses',
	PCNO_BRT, NUMW,		,		'Broadcast routing timer',
	PCNO_MAD, NUMW, 	, 		'Maximum address',
	PCNO_MLN, NUMW, 	, 		'Maximum circuits',
	PCNO_MCO, NUMW, 	, 		'Maximum cost',
	PCNO_MHO, NUMB, 	, 		'Maximum hops',
	PCNO_MVI, NUMB, 	, 		'Maximum visits',
	PCNO_MAR, NUMB,		,		'Maximum area',
	PCNO_MBE, NUMW,		,		'Max broadcast nonrouters',
	PCNO_MBR, NUMW,		,		'Max broadcast routers',
	PCNO_AMC, NUMW,		,		'Area maximum cost',
	PCNO_AMH, NUMB,		,		'Area maximum hops',
	PCNO_MBU, NUMW, 	, 		'Maximum buffers',
	PCNO_BUS, NUMW, 	, 		'Buffer size',
	PCNO_SBS, NUMW,		,		'Segment buffer size',
	PCNO_RPA, TKN, 		, 		'Receive password',
	PCNO_TPA, TKN, 		, 		'Transmit password',

	PCNO_PUS, TKN, 		, 		'Privileged user id',
	PCNO_PAC, TKN, 		, 		'Privileged account',
	PCNO_PPW, TKN, 		, 		'Privileged password',
	PCNO_NUS, TKN, 		, 		'Nonprivileged user id',
	PCNO_NAC, TKN, 		, 		'Nonprivileged account',
	PCNO_NPW, TKN, 		, 		'Nonprivileged password',
	PCNO_ACC, LITB,	NCP$GA_TBL_NODACC,	'Access',
	PCNO_DAC, LITB,	NCP$GA_TBL_NODACC,	'Default access',
	PCNO_PRX, LITB, NCP$GA_TBL_NODACC,	'Proxy access',
	PCNO_DPX, LITB, NCP$GA_TBL_NODACC,	'Default proxy access',
	PCNO_PIQ, NUMW,		,		'Pipeline quota',
	PCNO_ALI, NADR,		,		'Alias address',

	!
	! RSX-Specific Node (Executor) parameters
	!
	PCNO_RSX_RPA, LITB, NCP$GA_TBL_MSEPAS,	'Receive password',
	PCNO_RSX_TPA, LITB, NCP$GA_TBL_MSEPAS,	'Transmit password',
	PCNO_RSX_VER, LITB, NCP$GA_TBL_LINSTA,	'Verification state',

	!
	! Server Base specific Node (Executor) parameters
	!
	PCNO_SRV_RPA, LITB, NCP$GA_TBL_MSEPAS,	'Receive password',
	PCNO_SRV_TPA, LITB, NCP$GA_TBL_MSEPAS,	'Transmit password',
	PCNO_SRV_VER, LITB, NCP$GA_TBL_LINSTA,	'Verification state',
 	PCNO_SRV_ACB, HIGH,	,		'Active control buffers',
	PCNO_SRV_ASB, HIGH,	,		'Active small buffers',
	PCNO_SRV_ALB, HIGH,	,		'Active large buffers',
	PCNO_SRV_MCB, HIGH,	,		'Maximum control buffers',
	PCNO_SRV_MSB, HIGH,	,		'Maximum small buffers',
	PCNO_SRV_MLB, HIGH,	,		'Maximum large buffers',
	PCNO_SRV_LBS, HIGH,	,		'Large buffer size',
	PCNO_SRV_NRB, HIGH,	,		'Minimum receive buffers',
	PCNO_SRV_CPT, HIGH,	,		'CEX pool: total bytes',
	PCNO_SRV_CPF, HIGH,	,		'CEX pool: number of segments',
	PCNO_SRV_CPL, HIGH,	,		'CEX pool: largest segment',
	PCNO_SRV_XPT, HIGH,	,		'Extended pool: total bytes',
	PCNO_SRV_XPF, HIGH,	,		'Extended pool: number of segments',
	PCNO_SRV_XPL, HIGH,	,		'Extended pool: largest segment',

	)


!
!	object
!

	$NCP_PRMTBL
	(OBJ,

!	PCOB_OAN, *, 		, 		'Active name',
!	PCOB_OAC, *, 		, 		'Active links',
!	PCOB_ONA, *, 		, 		'Name',
!	PCOB_OCO, *, 		, 		'Copies',
!	PCOB_OUS, *, 		, 		'User',
!	PCOB_OVE, *, 		, 		'Verification',

	PCOB_NAM, TKN, 		, 		'Name',
	PCOB_FID, TKN, 		, 		'File id',
	PCOB_PID, TKN, 		, 		'Process id',
	PCOB_NUM, NUMB, 	, 		'Number',
	PCOB_PRV, PRVL, 	, 		'Privilege list',
	PCOB_USR, TKN, 		, 		'User id',
	PCOB_ACC, TKN, 		, 		'Account',
	PCOB_PSW, TKN, 		, 		'Password',
	PCOB_PRX, LITB,	NCP$GA_TBL_NODACC,	'Proxy access',

	)

!
!	links
!

	$NCP_PRMTBL
	(LNK,

	PCLK_STA, LITB,	NCP$GA_TBL_LNKSTA,	'State',
	PCLK_PID, HXPS,		,		'PID',
	PCLK_NID, NADR,		,		'Remote node',
	PCLK_LAD, NUMW, 	, 		'Link address, PID',
	PCLK_DLY, NUMW,		,		'Delay time',
	PCLK_RLN, NUMW,		,		'Remote link',
	PCLK_RID, TKN,		,		'Remote user',
	PCLK_USR, TKN,		,		'Username',
	PCLK_PRC, TKN,		,		'Process name',

	)


!
!	Bitmap tables for counters
!

	$NCP_BLDLST
	(BMDEI,				! Data errors inbound

	'NAKs sent, header block check error',
	'NAKs sent, data field block check error',
	'NAKs sent, REP response',
	'Block too long',
	'Block check error',
	'REJ sent',

	)

	$NCP_BLDLST
	(BMDEO,				! Data errors outbound

	'NAKs received, header block check error',
	'NAKs received, data field block check error',
	'NAKs received, REP response',
	'REJ received',

	)

	$NCP_BLDLST
	(BMRBE,				! Remote buffer errors

	'NAKs received, buffer unavailable',
	'NAKs received, buffer too small',
	'RNR received, buffer unavailable',

	)

	$NCP_BLDLST
	(BMLBE,				! Local buffer errors

	'NAKs sent, buffer unavailable',
	'NAKs sent, buffer too small',
	'RNR sent, buffer unavailable',

	)

	$NCP_BLDLST
	(BMSTO,				! Selection timeouts

	'No reply to select',
	'Incomplete reply to select',

	)

	$NCP_BLDLST
	(BMRPE,				! Remote process errors

	'NAKs received, receive overrun',
	'NAKs sent, header format error',
	'Selection address errors',
	'Streaming tributaries',
	'Invalid N(R) received',
	'FRMR sent, header format error',

	)

	$NCP_BLDLST
	(BMLPE,				! Local process errors

	'NAKs sent, receive overrun',
	'Receive overruns, NAK not sent',
	'Transmit underruns',
	'NAKs received, header format error',
	'Receive overrun',
	'FRMR received, header format error',

	)

	$NCP_BLDLST
	(BMRME,				! PCL-11B remote errors

	'TDM bus busy',
	'Message rejected',
	'Message truncated',
	'Receiver offline',
	'Receiver busy',
	'Transmitter offline',
	)

	$NCP_BLDLST
	(BMLCE,				! PCL-11B local errors

	'Transmitter overrun',
	'Transmit CRC failure',
	'Receive CRC failure',
	'Timeouts',
	'Non-existant memory transmit',
	'Non-existant memory receive',
	'Buffer too small',
	'Failed to open channel',
	'Memory overflow',
	)

	$NCP_BLDLST
	(BMMSE,				! PCL-11B master/secondary errors

	'',
	'Master down',
	'Now master',
	)

	$NCP_BLDLST
	(BMERI,				! PCL-11B inbound errors

	'Receive CRC failure',
	)

	$NCP_BLDLST
	(BMERO,				! PCL-11B outbound errors

	'Transmit CRC failure',
	'Timeout on word',
	)

	$NCP_BLDLST
	(BMRTO,				! PCL-11B remote timeouts

	'Receiver busy',
	'Transmitter offline',
	'Receiver offline',
	)

	$NCP_BLDLST
	(BMBER,				! PCL-11B remote buffer errors

	'Message truncated',
	'Message reject',
	)

	$NCP_BLDLST
	(BMSF,				! NI Send failure errors

	'Excessive collisions',
	'Carrier check failed',
	'Short circuit',
	'Open cicuit',
	'Frame too long',
	'Remote failure to defer',
	)

	$NCP_BLDLST
	(BMRF,				! NI Receive failure errors

	'Block check error',
	'Framing error',
	'Frame too long',
	)


!
!	Counters
!

!
!	Circuit counters
!
	$NCP_PRMTBL
	(CIRCNTR,

	CTCIR_ZER, LITB,	,		'Seconds since last zeroed',
	CTCIR_APR, LITB, 	, 		'Terminating packets received',
	CTCIR_DPS, LITB, 	, 		'Originating packets sent',
	CTCIR_ACL, LITB, 	, 		'Terminating congestion loss',
	CTCIR_CRL, LITB,	,		'Corruption loss',
	CTCIR_TPR, LITB, 	, 		'Transit packets received',
	CTCIR_TPS, LITB, 	, 		'Transit packets sent',
	CTCIR_TCL, LITB, 	, 		'Transit congestion loss',
	CTCIR_LDN, LITB, 	, 		'Circuit down',
	CTCIR_IFL, LITB, 	, 		'Initialization failure',
	CTCIR_BRC, LITB, 	, 		'Bytes received',
	CTCIR_BSN, LITB, 	, 		'Bytes sent',
	CTCIR_DBR, LITB, 	, 		'Data blocks received',
	CTCIR_DBS, LITB, 	, 		'Data blocks sent',
	CTCIR_DEI, LITB, NCP$GA_LST_BMDEI, 	'Data errors inbound',
	CTCIR_DEO, LITB, NCP$GA_LST_BMDEO, 	'Data errors outbound',
	CTCIR_RRT, LITB, 	, 		'Remote reply timeouts',
	CTCIR_LRT, LITB, 	, 		'Local reply timeouts',
	CTCIR_RBE, LITB, NCP$GA_LST_BMRBE, 	'Remote buffer errors',
	CTCIR_LBE, LITB, NCP$GA_LST_BMLBE, 	'Local buffer errors',
	CTCIR_SIE, LITB, 	, 		'Selection intervals elapsed',
	CTCIR_SLT, LITB, NCP$GA_LST_BMSTO, 	'Selection timeouts',
	CTCIR_LIR, LITB,	,		'Locally initiated resets',
	CTCIR_RIR, LITB,	,		'Remotely initiated resets',
	CTCIR_NIR, LITB,	,		'Network initiated resets',
	CTCIR_ERI, LITB, NCP$GA_LST_BMERI,	'Inbound PCL errors',
	CTCIR_ERO, LITB, NCP$GA_LST_BMERO,	'Outbound PCL errors',
	CTCIR_RTO, LITB, NCP$GA_LST_BMRTO,	'Remote PCL timeouts',
	CTCIR_LTO, LITB, 	,		'Local PCL timeouts',
	CTCIR_BER, LITB, NCP$GA_LST_BMBER,	'Remote PCL buffer errors',
	CTCIR_BEL, LITB, 	,		'Local PCL buffer errors',

	CTCIR_MNE, LITB,	,		'Unrecognized frame destination',
	CTCIR_UBU, LITB,	,		'User buffer unavailable',

	)

!
!	Line counters
!
	$NCP_PRMTBL
	(LINCNTR,

	CTLIN_ZER, LITB, 	, 		'Seconds since last zeroed',
	CTLIN_APR, LITB, 	, 		'Arriving packets received',
	CTLIN_DPS, LITB, 	, 		'Departing packets sent',
	CTLIN_ACL, LITB, 	, 		'Arriving congestion loss',
	CTLIN_TPR, LITB, 	, 		'Transit packets received',
	CTLIN_TPS, LITB, 	, 		'Transit packets sent',
	CTLIN_TCL, LITB, 	, 		'Transit congestion loss',
	CTLIN_LDN, LITB, 	, 		'Line down',
	CTLIN_IFL, LITB, 	, 		'Initialization failure',
	CTLIN_BRC, LITB, 	, 		'Bytes received',
	CTLIN_BSN, LITB, 	, 		'Bytes sent',
	CTLIN_MBY, LITB,	,		'Multicast bytes received',		! 1002
	CTLIN_DBR, LITB, 	, 		'Data blocks received',
	CTLIN_DBS, LITB, 	, 		'Data blocks sent',
	CTLIN_MBL, LITB,	,		'Multicast blocks received',		! 1012
	CTLIN_BID, LITB,	,		'Blocks sent, initially deferred',	! 1013
	CTLIN_BS1, LITB,	,		'Blocks sent, single collision',	! 1014
	CTLIN_BSM, LITB,	,		'Blocks sent, multiple collision',	! 1015
	CTLIN_DEI, LITB, NCP$GA_LST_BMDEI, 	'Data errors inbound',
	CTLIN_DEO, LITB, NCP$GA_LST_BMDEO, 	'Data errors outbound',
	CTLIN_RRT, LITB, 	, 		'Remote reply timeouts',
	CTLIN_LRT, LITB, 	, 		'Local reply timeouts',
	CTLIN_RBE, LITB, NCP$GA_LST_BMRBE, 	'Remote buffer errors',
	CTLIN_LBE, LITB, NCP$GA_LST_BMLBE, 	'Local buffer errors',
	CTLIN_SIE, LITB, 	, 		'Selection intervals elapsed',
	CTLIN_SLT, LITB, NCP$GA_LST_BMSTO, 	'Selection timeouts',
	CTLIN_SFL, LITB, NCP$GA_LST_BMSF,	'Send failure',				! 1060
	CTLIN_CDC, LITB,	,		'Collision detect check failure',	! 1061
	CTLIN_RFL, LITB, NCP$GA_LST_BMRF,	'Receive failure',			! 1062
	CTLIN_UFD, LITB,	,		'Unrecognized frame destination',	! 1063
	CTLIN_OVR, LITB,	,		'Data overrun',				! 1064
	CTLIN_SBU, LITB,	,		'System buffer unavailable',		! 1065
	CTLIN_UBU, LITB,	,		'User buffer unavailable',		! 1066
	CTLIN_RPE, LITB, NCP$GA_LST_BMRPE, 	'Remote process errors',
	CTLIN_LPE, LITB, NCP$GA_LST_BMLPE, 	'Local process errors',
	CTLIN_RME, LITB, NCP$GA_LST_BMRME,	'Remote PCL errors',
	CTLIN_LCE, LITB, NCP$GA_LST_BMLCE,	'Local PCL errors',
	CTLIN_MSE, LITB, NCP$GA_LST_BMMSE,	'PCL master/secondary errors',

	CTLIN_MBS, LITB, 	,		'Multicast blocks sent',
	CTLIN_MSN, LITB,	,		'Multicast bytes sent',

	)

!
!	Node counters
!
	$NCP_PRMTBL
	(NODCNTR,

	CTNOD_ZER, LITB, 	, 		'Seconds since last zeroed',
	CTNOD_BRC, LITB, 	, 		'Bytes received',
	CTNOD_BSN, LITB, 	, 		'Bytes sent',
	CTNOD_MRC, LITB, 	, 		'Messages received',
	CTNOD_MSN, LITB, 	, 		'Messages sent',
	CTNOD_CRC, LITB, 	, 		'Connects received',
	CTNOD_CSN, LITB, 	, 		'Connects sent',
	CTNOD_RTO, LITB, 	, 		'Response timeouts',
	CTNOD_RSE, LITB, 	, 		'Received connect resource errors',
	CTNOD_MLL, LITB, 	, 		'Maximum logical links active',
	CTNOD_APL, LITB, 	, 		'Aged packet loss',
	CTNOD_NUL, LITB, 	, 		'Node unreachable packet loss',
	CTNOD_NOL, LITB, 	, 		'Node out-of-range packet loss',
	CTNOD_OPL, LITB, 	, 		'Oversized packet loss',
	CTNOD_PFE, LITB, 	, 		'Packet format error',
	CTNOD_RUL, LITB, 	, 		'Partial routing update loss',
	CTNOD_VER, LITB, 	, 		'Verification reject',

	!
	! Server Base Specific Executor Node Counters
	!
	CTNOD_SRV_SYC, HIGH,	,		'Control buffer failures',
	CTNOD_SRV_SYS, HIGH,	,		'Small buffer failures',
	CTNOD_SRV_SYL, HIGH,	,		'Large buffer failures',
	CTNOD_SRV_SYR, HIGH,	,		'Receive buffer failures',

	)

!
!	Module X25-Protocol counters
!
	$NCP_PRMTBL
	(MPRCNTR,


	CTXP_ZER, LITB,		,	'Seconds since last zeroed',
	CTXP_BRC, LITB,		,	'Bytes received',
	CTXP_BSN, LITB,		,	'Bytes sent',
	CTXP_BLR, LITB,		,	'Data blocks received',
	CTXP_BLS, LITB,		,	'Data blocks sent',
	CTXP_CRC, LITB,		,	'Calls received',
	CTXP_CSN, LITB,		,	'Calls sent',
	CTXP_FSR, LITB,		,	'Fast selects received',
	CTXP_FSS, LITB,		,	'Fast selects sent',
	CTXP_MSA, LITB,		,	'Maximum switched circuits active',
	CTXP_MCA, LITB,		,	'Maximum channels active',
	CTXP_RSE, LITB,		,	'Received call resource errors',
	CTXP_LIR, LITB,		,	'Locally initiated resets',
	CTXP_RIR, LITB,		,	'Remotely initiated resets',
	CTXP_NIR, LITB,		,	'Network initiated resets',
	CTXP_RST, LITB,		,	'Restarts',

	)

!
!	Module X25-Server counters
!
	$NCP_PRMTBL
	(MSECNTR,


	CTXS_ZER, LITB,		,		'Seconds since last zeroed',
	CTXS_MCA, LITB,		,		'Maximum circuits active',
	CTXS_ICR, LITB,		,		'Incoming calls rejected, no resources',
	CTXS_LLR, LITB,		,		'Logical links rejected, no resources',

	)



!
!	Parameter keyword tables
!


	$NCP_BLDTBL

	(CIRPOL,			! Circuit polling state

	1,	'automatic',
	2,	'active',
	3,	'inactive',
	4,	'dying',
	5,	'dead',

	)

	$NCP_BLDTBL

	(CIRPLS,			! Circuit polling substate

	1,	'active',
	2,	'inactive',
	3,	'dying',
	4,	'dead',

	)

	$NCP_BLDTBL

	(CIRBLK,			! Circuit blocking

	0,	'enabled',
	1,	'disabled',

	)

	$NCP_BLDTBL

	(CIRUSE,			! Circuit usage

	0,	'permanent',
	1,	'incoming',
	2,	'outgoing',

	)

	$NCP_BLDTBL

	(CIRTYP,			! Circuit type

	0,	'DDCMP point',
	1,	'DDCMP control',
	2,	'DDCMP tributary',
	3,	'X.25',
	4,	'DDCMP DMC compatible',
	5,	'LAPB',
	6,	'Ethernet',

	)

	$NCP_BLDTBL

	(CIRVER,			! Circuit verification state

	0,	'enabled',
	1,	'disabled',
	)

	$NCP_BLDTBL

	(CIRXPT,			! Circuit (desired) transport type

	1,	'Z-node',
	2,	'Phase II',		! Force Phase II on this circuit
	3,	'Routing III',
	4,	'Phase IV',		! Nonrouting Phase IV

	)

	$NCP_BLDTBL

	(LINSTA,			! Line state

	0,	'on',
	1,	'off',
	2,	'service',
	3,	'cleared',

	)

	$NCP_BLDTBL

	(LINSUB,			! Line substate

	0,	'-starting',
	1,	'-reflecting',
	2,	'-looping',
	3,	'-loading',
	4,	'-dumping',
	5,	'-triggering',
	6,	'-autoservice',
	7,	'-autoloading',
	8,	'-autodumping',
	9,	'-autotriggering',
	10,	'-synchronizing',
	11,	'-failed',

	)

	$NCP_BLDTBL

	(LINSVC,			! Service state

	0,	'enabled',
	1,	'disabled',

	)

	$NCP_BLDTBL

	(LINCTL,			! Controller mode

	0,	'normal',
	1,	'loopback',

	)


	$NCP_BLDTBL

	(LINDPX,			! Line duplex mode

	0,	'full',
	1,	'half',

	)

	$NCP_BLDTBL

	(LINCLO,			! Line clock mode

	0,	'external',
	1,	'internal',

	)

	$NCP_BLDTBL

	(LINTYP,			! Line type

	0,	'point',
	1,	'control',
	2,	'tributary',
	3,	'DMC compatible mode',

	)

	$NCP_BLDTBL

	(LINPRO,			! Line protocol

	0,	'DDCMP point',
	1,	'DDCMP control',
	2,	'DDCMP tributary',
	3,	'X.25',
	4,	'DDCMP DMC compatible',
	5,	'LAPB',
	6,	'Ethernet',

	)

	$NCP_BLDTBL

	(LINMOD,			! X25 line mode

	1,	'DTE',
	2,	'DCE',
	3,	'DTE loopback',
	4,	'DCE loopback',

	)

	$NCP_BLDTBL

	(LINLOC,			! Line Location /RSX parameter

	0,	'firstfit',
	1,	'topdown',

	)

	$NCP_BLDTBL

	(LOGSTA,			! Logging state

	0,	'on',
	1,	'off',
	2,	'hold',

	)

	$NCP_BLDTBL

	(LNKSTA,			! Link state

	0,	'closed',
	1,	'CI sending',
	2,	'CI ACKed',
	3,	'CI received',
	4,	'CC sending',
	5,	'run',
	6,	'DI received',
	7,	'DI sending',

	)

	$NCP_BLDTBL

	(MPRSTA,			! X25-Protocol state

	0,	'on',
	1,	'off',
	2,	'shut',

	)

	$NCP_BLDTBL

	(MPRTYP,			! X25-Protocol type

	1,	'bilateral',

	)

	$NCP_BLDTBL

	(MPRMNS,			! Multi-Network Support

	0,	'enabled',
	1,	'disabled',

	)

	$NCP_BLDTBL

	(MPRSBS,			! X25-Protocol Substate

	12,	'-running',
	10,	'-synchronizing',
	13,	'-unsynchronized',

	)

	$NCP_BLDTBL

	(MSEPAS,			! X25-Server password

	0,	'(password set)',

	)


	$NCP_BLDTBL

	(MLOASS,			! Module loader assistance flag,
					!  Module looper assistance flag and
					!  Module configurator surveillance flag
	0,	'enabled',
	1,	'disabled',

	)

	$NCP_BLDTBL

	(MCOPRC,			! Module configurator system processor

	1,	'PDP-11',
	2,	'Pluto',

	)

	$NCP_BLDTBL

	(MCODAT,			! Module configurator data link type

	1,	'Ethernet',		! (DEC ethernet)
	2,	'DDCMP',
	3,	'LAPB',			! (frame level of X.25)

	)

	$NCP_BLDTBL

	(NODSTA,			! Node state

	0,	'on',
	1,	'off',
	2,	'shut',
	3,	'restricted',
	4,	'reachable',
	5,	'unreachable',

	)

	$NCP_BLDTBL

	(NODHLP,			! Node loop help

	0,	'transmit',
	1,	'receive',
	2,	'full',

	)

	$NCP_BLDTBL

	(NODTYP,			! Node type

	0,	'routing III',
	1,	'nonrouting III',
	2,	'phase II',
	3,	'area',
	4,	'routing IV',
	5,	'nonrouting IV',

	)

	$NCP_BLDTBL

	(NODACC,			! Node access

	0,	'none',
	1,	'incoming',
	2,	'outgoing',
	3,	'incoming and outgoing',
	4,	'required',

	)

	$NCP_BLDTBL

	(SVCDEV,			! Service device type

	0,	'DP11',
	1,	'UNA',
	2,	'DU11',
	4,	'DL11',
	6,	'DQ11',
	8,	'DA11',
	10,	'DUP11',
	12,	'DMC11',
	18,	'DMP11',
	20,	'DTE20',
	32,	'KL8J',
	34,	'DMV11',
	36,	'DPV11',
	38,	'DMF32',

	)

	$NCP_BLDTBL

	(NODSNV,			! Service node version

	0,	'Phase III',
	1,	'Phase IV',

	)

	$NCP_BLDTBL

	(CPUTYP,			! CPU type

	0,	'PDP-8',
	1,	'PDP-11',
	2,	'DECsystem 10/20',
	3,	'VAX-11',

	)

	$NCP_BLDTBL

	(SOFTYP,			! Software type

	0,	'secondary loader',
	1,	'tertiary loader',
	2,	'system',

	)

	$NCP_BLDTBL

	(LOODAT,			! Loop data type

	0,	'zeroes',
	1,	'ones',
	2,	'mixed',

	)

	$NCP_BLDTBL

	(EVESOUR,			! Event Source type

	0,	'Source node = ',
	1,	'Source line = ',
	3,	'Source circuit = ',
	%X'FF',	' ',			! No source
	)

	$NCP_BLDTBL

	(CNFFCT,			! Module Configurator Functions

	0,	'Loop',
	1,	'Dump',
	2,	'Primary loader',
	3,	'Multi-block loader',
	4,	'Boot',
	5,	'Console carrier',
	6,	'Data link counters',
	7,	'Console carrier reservation',

	)


	$NCP_BLDTBL

	(MON,

	1,	'Jan',
	2,	'Feb',
	3,	'Mar',
	4,	'Apr',
	5,	'May',
	6,	'Jun',
	7,	'Jul',
	8,	'Aug',
	9,	'Sep',
	10,	'Oct',
	11,	'Nov',
	12,	'Dec',

	)

%SBTTL	'Tables for Phase II Communication'

$NCP_BLDTBL			! Error status table
(PH2STS,

(-1) AND 255,	'invalid function or option',
(-2) AND 255,	'invalid message format',
(-3) AND 255,	'insufficient status',
(-4) AND 255,	'dialogue message out of order',
(-5) AND 255,	'NICE process program error',
(-6) AND 255,	'illegal error code',
(-7) AND 255,	'invalid error code',
(-8) AND 255,	'invalid line identification',
(-9) AND 255,	'line in wrong state',
(-10) AND 255,	'line communications error',
(-11) AND 255,	'invalid node identification',
(-12) AND 255,	'invalid server identification',
(-13) AND 255,	'invalid file',
(-14) AND 255,	'configuration database error',
(-15) AND 255,	'resource error',
(-16) AND 255,	'invalid parameter value',
(-17) AND 255,	'line protocol error',
(-18) AND 255,	'file i/o error',
(-19) AND 255,	'network communications error',
(-20) AND 255,	'no room for new entry',
(-21) AND 255,	'remote NICE process not available',

)


$NCP_BLDTBL		! Executor counters
(PH2EXECOU,

0,	'Seconds since last zeroed',
1,	'User messages sent',
2,	'User messages received',
3,	'Messages routed through',
4,	'Extraneous messages received',
5,	'Negative acknowledgements sent',
6,	'Messages timed out',
7,	'Unknown destination node',
8,	'Format errors in routing data',
9,	'Format errors other than routing',
10,	'Resource errors',
11,	'Successful neighbor initializations',
12,	'Unsuccessful neighbor initializations',
13,	'Currently active logical links',
14,	'Maximum logical links active',
15,	'Single messages send',
16,	'Single messages received',
17,	'Connects initiated',
18,	'Connects received',
64,	'Number of currently connected remotes',
65,	'Number of bytes transmitted',
66,	'Number of bytes received',
67,	'Number of bytes allocated for buffers',
68,	'Number of successful allocation attempts',
69,	'Number of unsuccessful buffer allocation attempts',

)


$NCP_BLDTBL			! Line counters
(PH2LINCOU,

0,	'Seconds since last zeroed',
1,	'Blocks received',
2,	'Blocks sent',
3,	'Retransmissions, line errors',
4,	'Received line errors',
5,	'Retransmissions, other than line errors and parity',
6,	'Receive timeouts',
7,	'Receive selection errors',
8,	'Resource errors',
64,	'Naks received--no buffer available',
65,	'Naks received--bad buffer bcc',
66,	'Naks received--bad data bcc',
67,	'Naks send--no buffers available',
68,	'Naks sent--bad header bcc',
69,	'Naks sent--bad data bcc',
70,	'Reps sent',
71,	'Reps received',

)


$NCP_BLDTBL		! Line type codes
(PH2LINES,

0,	'DP',
2,	'DU',
6,	'DQ',
8,	'DA',
10,	'DUP',
12,	'DMC',
14,	'DLV',
18,	'KL',
20,	'DT',
22,	'DV',
24,	'DZ',
28,	'KDP',
30,	'KDZ',

)

%SBTTL 'Object Listing of Tables'

END				!End of module
ELUDOM
