MODULE MCHK_DISP
(%TITLE 'Machine check dispatcher'
IDENT = 'V04-000') =
	
BEGIN
	
!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982, 1984 BY				    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************

!++
! FACILITY:  ERLRPT, Error Log Report Generator
!
! ABSTRACT:
!
! 	This module contains the machine check dispatching routine.
!
! ENVIRONMENT:
!
!	VAX/VMS operating system, user mode.
!
! AUTHOR:  Sharon A. Reynolds,	CREATION DATE: 10-Apr-1984
!
! Modified by:
!
!	V04-003	SAR0290		Sharon A. Reynolds	17-Jul-1984
!		Changed the way that option is referenced due to change
!		in the way it is passed.
!
!	V04-002	SAR0256		Sharon A. Reynolds	25-apr-1984
!		Put in check to determine whether to output text.
!
!	V04-001	EAD0139		Elliott A. Drayton		12-Apr-1984
!		Removed reference to LIB.REQ.
!
!--

REQUIRE 'SRC$:ERFDEF.REQ' ;

MACRO
    Cpu_fld = 0,24,8,0 % ;

FORWARD ROUTINE
    Mchk_dispatcher:	Novalue ;

EXTERNAL ROUTINE
    Frctof,
    Header,
    Logger,
    Mcheck_7ZZ,
    Mcheck_750,
    Mcheck_780,
    Mcheck_UV1 ;

EXTERNAL
    Emb:	$BBLOCK PSECT (emb) ;


GLOBAL Routine MCHK_DISPATCHER (lun,option) : NOVALUE = 
Begin

!++
!
! Functional Description:
!
!    This routine outputs the machine check header, then via the
!    cpu-type determines which cpu dependent routine to invoke and
!    invokes the routine.
!
! Calling Sequence:
!
!    MCHK_DISPATCHER (lun,option)
!
! Input Parameters:
!
!    Lun = logical unit number of output device.
!    Option = address of descriptor that contains the option.
!
! Output Parameters:
!
!    None.
!
!--

LOCAL
    Cpu_type:	REF $BBLOCK ;

Map
    Option:	REF $BBLOCK ;

Bind start_of_cpu_specific_stuff = emb[emb$b_mc_sumcod] ;

!
! Determine whether to output any text.
!
If ..option[dsc$a_pointer] NEQ 'S'
Then
    Return ;

!
! Force a top of form to occur, and output the entry header.
!
FRCTOF (.lun) ;
HEADER (.lun) ;
LOGGER (.lun,%ASCID 'MACHINE CHECK') ;

!
! Get the cpu type field from the system id register.
!
Cpu_type = emb[emb$l_hd_sid] ;
Cpu_type = .cpu_type[cpu_fld] ;

!
! Determine if the cpu type is an 11/780 without the cpu field
! in the system id set.
!
If .cpu_type EQLU 255
Then
    ! Yes, call the 11/780 machine check module to output
    ! the rest of the report.
    !
    MCHECK_780 (.lun,start_of_cpu_specific_stuff) ;

!
! Determine if the cpu type is any other valid type. If a valid/supported
! cpu type is found then call the cpu dependent machine check
! module.
!
Case .cpu_type from 1 to 7 of
    Set
    [1]: MCHECK_780 (.lun,start_of_cpu_specific_stuff) ;

    [2]: MCHECK_750 (.lun,start_of_cpu_specific_stuff) ;

    [3]: MCHECK_7ZZ (.lun,start_of_cpu_specific_stuff) ;

    [7]: MCHECK_UV1 (.lun,start_of_cpu_specific_stuff) ;

    [INRANGE]:	Return ;
    Tes ;

End ;	! Routine

End 	! Module
ELUDOM
