MODULE  exch$cld_table

!
! Version:	'V04-000'
!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982, 1984 BY				    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************

!++
! FACILITY:	EXCHANGE - Foreign volume interchange facility
!
! ABSTRACT:	Internal DCL command definition tables
!
! ENVIRONMENT:	VAX/VMS User mode
!
! AUTHOR:	CW Hobbs		CREATION DATE: 17-July-1982
!
! MODIFIED BY:
!
!	V03-003	CWH3003		CW Hobbs		12-Apr-1984
!		Add dummy P2 for COPY /BOOT
!
!	V03-002	CWH3002		CW Hobbs		10-Sep-1983
!		Add missing "PLACEMENT=LOCAL" to /VOLUME for MOUNT
!
!
!--

! Types are non-negatable by default


DEFINE TYPE  data_check_type

	KEYWORD	read		NEGATABLE
	KEYWORD write		NEGATABLE, DEFAULT


DEFINE TYPE  density_type

	KEYWORD 800
	KEYWORD 1600
	KEYWORD 6250


DEFINE TYPE  carriage_control_type

	KEYWORD carriage_return	DEFAULT
	KEYWORD fortran
	KEYWORD none


DEFINE TYPE  record_format_type

	KEYWORD binary
	KEYWORD fixed		VALUE (DEFAULT=512,TYPE=$NUMBER)
	KEYWORD pad		VALUE (DEFAULT=0)
	KEYWORD stream
	KEYWORD variable


!\ DEFINE TYPE  retain_type
!\
!\	KEYWORD retain


DEFINE TYPE  transfer_mode_type

	KEYWORD automatic	DEFAULT
	KEYWORD block
	KEYWORD record


DEFINE TYPE  volume_format_type

	KEYWORD dos11
	KEYWORD files11
	KEYWORD rt11

DEFINE SYNTAX  boot_copy

	ROUTINE exch$rtnam_copy_boot

	PARAMETER P1	LABEL=input
			PROMPT="Monitor file"
			VALUE (REQUIRED, TYPE=$INFILE)

	PARAMETER P2	LABEL=output			! Dummy, only here so that normal RT-11 syntax is allowed
			VALUE (TYPE=$OUTFILE)

	QUALIFIER boot		NONNEGATABLE
				VALUE
	QUALIFIER log
	QUALIFIER volume_format	NONNEGATABLE
				VALUE (REQUIRED, TYPE=volume_format_type)
				PLACEMENT=LOCAL


DEFINE VERB  copy

	ROUTINE exch$copy_copy

	PARAMETER P1	LABEL=input
			PROMPT="Input file(s)"
			VALUE (REQUIRED, LIST, TYPE=$INFILE)

	PARAMETER P2	LABEL=output
			PROMPT="Output file"
			VALUE (REQUIRED, TYPE=$OUTFILE)

	QUALIFIER allocation	NONNEGATABLE
				VALUE (REQUIRED, TYPE=$NUMBER)
				PLACEMENT=POSITIONAL
	QUALIFIER best_try_contiguous
				NONNEGATABLE
				PLACEMENT=POSITIONAL
	QUALIFIER boot		NONNEGATABLE
				SYNTAX=boot_copy
	QUALIFIER carriage_control
				DEFAULT
				VALUE (TYPE=carriage_control_type)
				PLACEMENT=LOCAL
!\	QUALIFIER confirm
	QUALIFIER contiguous	PLACEMENT=POSITIONAL
	QUALIFIER delete	DEFAULT
	QUALIFIER extension	NONNEGATABLE
				VALUE (REQUIRED, TYPE=$NUMBER)
				PLACEMENT=POSITIONAL
!\	QUALIFIER fdl		NONNEGATABLE
!\				VALUE (REQUIRED, TYPE=$INFILE)
!\				PLACEMENT=POSITIONAL
	QUALIFIER log
	QUALIFIER protect
	QUALIFIER record_format	NONNEGATABLE
				VALUE (REQUIRED, LIST, TYPE=record_format_type)
				PLACEMENT=LOCAL
	QUALIFIER replace
	QUALIFIER rewind	DEFAULT
				PLACEMENT=POSITIONAL
	QUALIFIER start_block	NONNEGATABLE
				VALUE (REQUIRED, TYPE=$NUMBER)
	QUALIFIER system
	QUALIFIER transfer_mode	NONNEGATABLE
				VALUE (REQUIRED, TYPE=transfer_mode_type)
				PLACEMENT=LOCAL
	QUALIFIER truncate	PLACEMENT=POSITIONAL
	QUALIFIER volume_format	NONNEGATABLE
				VALUE (REQUIRED, TYPE=volume_format_type)
				PLACEMENT=LOCAL


DEFINE VERB  delete

	ROUTINE exch$rtnam_delete

	PARAMETER P1	LABEL=input
			PROMPT="Filename(s)"
			VALUE (REQUIRED, LIST, TYPE=$INFILE)

!\	QUALIFIER confirm
	QUALIFIER log
	QUALIFIER system
	QUALIFIER volume_format	NONNEGATABLE
				VALUE (REQUIRED, TYPE=volume_format_type)
				PLACEMENT=LOCAL


DEFINE VERB  directory

	ROUTINE exch$dire_directory

	PARAMETER P1	LABEL=filename
			VALUE (LIST, TYPE=$INFILE, DEFAULT=SYS$DISK)

	QUALIFIER all
!\	QUALIFIER badblocks	NONNEGATABLE
	QUALIFIER blocks
	QUALIFIER brief
	QUALIFIER columns	DEFAULT
				NONNEGATABLE
				VALUE (DEFAULT=0, TYPE=$NUMBER)
	QUALIFIER date		DEFAULT
	QUALIFIER deleted
	QUALIFIER free
	QUALIFIER full
	QUALIFIER octal
	QUALIFIER output	VALUE (REQUIRED, TYPE=$OUTFILE)
	QUALIFIER owner
	QUALIFIER printer
	QUALIFIER size		DEFAULT
	QUALIFIER summary
	QUALIFIER volume_format	NONNEGATABLE
				VALUE (REQUIRED, TYPE=volume_format_type)
				PLACEMENT=LOCAL


DEFINE VERB  dismount

	ROUTINE exch$moun_dismount

	PARAMETER P1	LABEL=devicename
			PROMPT="Device"
			VALUE (REQUIRED, TYPE=$INFILE)

	QUALIFIER message


DEFINE VERB  exit

	ROUTINE exch$main_exit


DEFINE VERB  help

	ROUTINE exch$main_help

	PARAMETER P1	LABEL=topic, VALUE (TYPE=$REST_OF_LINE)


DEFINE SYNTAX  create_init

	PARAMETER P1	LABEL=devicename
			PROMPT="Filename"
			VALUE (REQUIRED, TYPE=$INFILE)

	PARAMETER P2	LABEL=volumelabel
			VALUE (DEFAULT="VMS Exchange")


DEFINE VERB  initialize

	ROUTINE exch$init_initialize

	PARAMETER P1	LABEL=devicename
			PROMPT="Device"
			VALUE (REQUIRED, TYPE=$INFILE)

	PARAMETER P2	LABEL=volumelabel
			VALUE (DEFAULT="VMS Exchange")

	QUALIFIER allocation	NONNEGATABLE
				VALUE (REQUIRED, TYPE=$NUMBER)
!\	QUALIFIER badblocks	VALUE (TYPE=retain_type)
	QUALIFIER create	NONNEGATABLE
				SYNTAX=create_init
	QUALIFIER density	NONNEGATABLE
				VALUE (REQUIRED, TYPE=density_type)
	QUALIFIER extra_words	VALUE (REQUIRED, TYPE=$NUMBER)
	QUALIFIER message
!\	QUALIFIER replace	VALUE (TYPE=retain_type)
	QUALIFIER segments	NONNEGATABLE
				VALUE (REQUIRED, TYPE=$NUMBER)
	QUALIFIER volume_format	NONNEGATABLE
				VALUE (REQUIRED, TYPE=volume_format_type)
				PLACEMENT=LOCAL


DEFINE SYNTAX  virtual_mount

	PARAMETER P1	LABEL=devicename
			PROMPT="Virtual device"
			VALUE (REQUIRED, TYPE=$INFILE)

	PARAMETER P2	LABEL=filename
			PROMPT="Filename"
			VALUE (REQUIRED, TYPE=$INFILE)


DEFINE VERB  mount

	ROUTINE exch$moun_mount

	PARAMETER P1	LABEL=devicename
			PROMPT="Device"
			VALUE (REQUIRED, TYPE=$INFILE)

	QUALIFIER data_check	VALUE (LIST, TYPE=data_check_type)
	QUALIFIER foreign	DEFAULT
	QUALIFIER message
	QUALIFIER virtual	NONNEGATABLE
				SYNTAX=virtual_mount
	QUALIFIER volume_format	NONNEGATABLE
				VALUE (REQUIRED, TYPE=volume_format_type)	! only 1 parm, no placement
				PLACEMENT=LOCAL
	QUALIFIER write		DEFAULT


DEFINE VERB  rename

	ROUTINE exch$rtnam_rename

	PARAMETER P1	LABEL=input
			PROMPT="From"
			VALUE (REQUIRED, TYPE=$INFILE)

	PARAMETER P2	LABEL=output
			PROMPT="To"
			VALUE (REQUIRED, TYPE=$OUTFILE)

!\	QUALIFIER confirm
	QUALIFIER log
	QUALIFIER protect
	QUALIFIER system
	QUALIFIER volume_format	NONNEGATABLE
				VALUE (REQUIRED, TYPE=volume_format_type)
				PLACEMENT=LOCAL


DEFINE VERB  show

	ROUTINE exch$show_show


!\DEFINE VERB  squeeze
!\
!\	ROUTINE exch$sque_squeeze
!\
!\	PARAMETER P1	LABEL=device_name
!\			PROMPT="Device"
!\			VALUE (REQUIRED, TYPE=$OUTFILE)
!\
!\	PARAMETER P2	LABEL=output_device
!\			PROMPT="Output device"
!\			VALUE (TYPE=$OUTFILE)
!\
!\	QUALIFIER volume_format	NONNEGATABLE
!\				VALUE (REQUIRED, TYPE=volume_format_type)
!\				PLACEMENT=LOCAL


DEFINE VERB  type

	ROUTINE exch$copy_type

	PARAMETER P1	LABEL=input
			PROMPT="Filename(s)"
			VALUE (REQUIRED, LIST, TYPE=$INFILE)

!\	QUALIFIER confirm
	QUALIFIER log
	QUALIFIER record_format	NONNEGATABLE
				VALUE (REQUIRED, LIST, TYPE=record_format_type)
				PLACEMENT=LOCAL
	QUALIFIER rewind	DEFAULT
				PLACEMENT=POSITIONAL
	QUALIFIER volume_format	NONNEGATABLE
				VALUE (REQUIRED, TYPE=volume_format_type)
				PLACEMENT=LOCAL
