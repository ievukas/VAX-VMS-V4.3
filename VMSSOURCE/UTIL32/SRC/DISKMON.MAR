	.TITLE	DISKMON - DISK I/O MONITOR
	.IDENT	'V04-000'

;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982, 1984 BY				    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************

;++
; FACILITY: Performance Monitoring
;
; ABSTRACT:
;	Solicits time interval and calls I/O data collection routine.
;
; ENVIRONMENT:
;	MODE = USER
;
; AUTHOR: S. S. AMWAY, CREATION DATE: 24-Oct-83
;
; MODIFIED BY:
;
;--


	.SBTTL	DECLARATIONS
;
; INCLUDE FILES:
;

;
; MACROS:
;

;
; EQUATED SYMBOLS:
;

;
; OWN STORAGE:
;
	.PSECT	$PDATA,  PIC,CON,REL,LCL,  SHR,NOEXE,  RD,NOWRT,LONG
BLANK:
	.ASCID	\ \
	.ALIGN	LONG
ANNOUNCE:
	.ASCID	\VAX/VMS Disk I/O Monitor (Unsupported/Experimental Utility)\
	.ALIGN	LONG
PROMPT:
	.ASCID	\Data collection duration (minutes): \

	.PSECT	$LOCAL,  PIC,CON,REL,LCL,NOSHR,NOEXE,  RD,  WRT,QUAD
LEGAL_FUNCTIONS:
	.LONG	-1,-1
COLLECT_ARG:
	.LONG	7
	.LONG	BUFFER_COUNT
	.LONG	SECONDS
	.LONG	DEV_CLASS
	.LONG	DEV_TYPE
	.LONG	AND_MASK
	.LONG	OR_MASK
	.LONG	LEGAL_FUNCTIONS
BUFFER_COUNT:
	.LONG	^X7FFFFFFF
SECONDS:
	.LONG	0
MINUTES:
	.LONG	0
AND_MASK:
	.WORD	0
OR_MASK:
	.WORD	0
DEV_CLASS:
	.BYTE	1
DEV_TYPE:
	.BYTE	-1
	.ALIGN	LONG
CNV_STR:
	.LONG	0
	.LONG	GET_STR+8
GET_STR:
	.ASCID	\          \


	.SBTTL	MAIN PROGRAM
;++
; FUNCTIONAL DESCRIPTION:
;	Solicits monitoring interval and calls collection routine.
;
; CALLING SEQUENCE:
;	CALLG/CALLS DISKMON
;
; INPUT PARAMETERS:
;	NONE
;
; IMPLICIT INPUTS:
;	NONE
;
; OUTPUT PARAMETERS:
;	NONE
;
; IMPLICIT OUTPUTS:
;	I/O DATA RECORDED TO FILE
;
; COMPLETION CODES:
;	SS$_NORMAL
;	Numerous errors returned by other facilities
;
; SIDE EFFECTS:
;	NONE
;
;--
	.PSECT	$CODE ,  PIC,CON,REL,LCL,  SHR,  EXE,  RD,NOWRT,LONG

	.ENTRY	DISKMON,^M<>

	PUSHAL	ANNOUNCE		; Announce program
	CALLS	#1,G^LIB$PUT_OUTPUT
	BLBC	R0,9999$
	PUSHAL	BLANK
	CALLS	#1,G^LIB$PUT_OUTPUT
	BLBC	R0,9999$
10$:	PUSHAW	CNV_STR			; Solicit monitoring interval
	PUSHAL	PROMPT			; (in minutes)
	PUSHAL	GET_STR
	CALLS	#3,G^LIB$GET_INPUT
	BLBC	R0,9999$
	PUSHL	#^X11			; Convert ASCII to longword
	PUSHL	#4
	PUSHAL	MINUTES
	PUSHAL	CNV_STR
	CALLS	#4,G^OTS$CVT_TI_L
	BLBC	R0,10$
	TSTL	MINUTES			; Must be >= 0
	BLEQ	10$
	MULL3	#60,MINUTES,SECONDS	; Convert minutes to seconds
	CALLG	COLLECT_ARG,IOCOLLECT	; Call data collection routine
9999$:	RET

	.END	DISKMON
