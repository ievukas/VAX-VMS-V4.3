	.TITLE	PARSE		Parse tables for SDA commands
	.SBTTL	COPYRIGHT NOTICE
	.IDENT	'V04-000'
;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982, 1984 BY				    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;
	.PAGE
	.SBTTL	PROGRAM DESCRIPTION
;++
;   FACILITY
;
;	SYSTEM DUMP ANALYZER
;
;   ABSTRACT
;
;	This module contains the parse tables which
;	are used to parse SDA commands.
;
;   ENVIRONMENT
;
;	NATIVE MODE, USER MODE
;
;   AUTHOR
;
;	TIM HALVORSEN, JULY 1978
;
;   MODIFIED BY
;
;	V03-015 EMB0109 	Ellen M. Batbouta	24-Jul-1984
;		Fix SHOW DEVICE/ADDRESS so that it will be consistent
;		with other sda commands with the qualifier /address
;		such as SHOW PORTS/ADDRESS.
;
;	V03-014 EMB0102		Ellen M. Batbouta       30-May-1984
;		Add support for SHOW CLUSTER, SHOW CONNECTIONS, SHOW
;		RSPID, and SHOW PORTS.
;
;	V03-013	PRB0302		Paul Beck	28-Dec-1983  17:50
;		Add support for EXAMINE/INST/NOSKIP
;
;	V03-012	ROW0260		Ralph O. Weber		21-NOV-1983
;		Changes parse tables to allow SHOW RESOURCES, SHOW LOCKS, and 
;		SHOW PROCESS /LOCKS.
;
;	V03-011	ROW0237		Ralph O. Weber		10-NOV-1983
;		Add TPARSE table entries to support the SHOW DEVICE/ADDR 
;		command.
;
;	V03-010	WMC0002		Wayne Cardoza		30-Sep-1983
;		Bad transition after page table range
;
;	V03-009	BLS0230		Benn Schreiber		24-Jul-1983
;		Correct end of line / comment handling
;
;	V03-008	WMC0001		Wayne cardoza		11-Jul-1983
;		Support for separate display of P0 and P1 process page tables.
;
;	V03-007 JLV0259		Jake VanNoy		23-MAY-1983
;		ADD DEFINE/KEY.
;
;	V03-006	TMK0001		Todd M. Katz		21-Mar-1983
;		ADD SET LOG and SET NOLOG syntax.
;
;	V03-005	CWH1002		CW Hobbs		 2-Mar-1983
;		Store only the low bits of proc index (as determined by
;		the global PIX_WIDTH so that we can interchange internal
;		and extended pids.  Change syntax of SHOW SUMMARY to add
;		SHOW SUMMARY /IMAGE.
;
;	V03-004	RAS0123		Ron Schaefer		 8-Feb-1983
;		Add SHOW PROC/RMS=RJB syntax.
;
;	V03-003	JLV0224		Jake VanNoy		21-JAN-1983
;		Add Examine/PSL Examine/CONDITION_VLAUE and Examine/TIME 
;		parsing. Add VALIDATE QUEUE syntax.
;
;	V03-002	LMP0031		L. Mark Pilant,		17-Jun-1982  13:15
;		Add support for SHOW PROCESS/CHANNELS.  Also, change the parsing
;		to allow a process name along with qualifiers.
;
;	V03-001 MSH0001		Maryann Hinden		10-Jun-1982
;		Add SHOW LOCK, SHOW RES, SHOW PROC/LOCK commands.
;
;--

	.SBTTL	DECLARATIONS
;
;	SYMBOL DEFINTIONS
;
	$TPADEF			; TPARSE DEFINITIONS
	$OPTDEF			; DEFINE BITS IN OPTIONS WORD
	$SMGDEF			; SCREEN INPUT DEFINITIONS

SEMI	= ^A';'
COLON	= ^A':'

	.SBTTL	DATA STORAGE
;
;	STORAGE DEFINITIONS
;

	.PSECT	SDADATA,WRT,NOEXE

PREV_BUFFER:
	.BLKB	80		; HOLDS PREVIOUS COMMAND
PREV_COMMAND:
	.LONG	0
	.ADDRESS PREV_BUFFER

KEY_ATTR:
	.LONG	SMG$M_KEY_TERMINATE	; KEYPAD ATTRIBUTE FOR "REPEAT KEY"

	.SBTTL	PARSER STATE TABLE
;
;	STATE TABLE FOR DUMP ANALYZER COMMANDS
;

	$INIT_STATE SDA_STATE,SDA_KEY

	$STATE	START
	$TRAN	'READ',READ_CMD
	$TRAN	'REPEAT',START,REPEAT_COMMAND
	$TRAN	TPA$_LAMBDA,,SAVE_COMMAND
	$STATE
	$TRAN	'@',INDIRECT_CMD
	$TRAN	'COPY',COPY_CMD
	$TRAN	'DEFINE',DEFINE
	$TRAN	'EXAMINE',EXAMINE
	$TRAN	'FORMAT',FORMAT_CMD
	$TRAN	'HELP',HELP_CMD
	$TRAN	'SET',SET
	$TRAN	'SEARCH',SEARCH
	$TRAN	'SHOW',SHOW
	$TRAN	'EVALUATE',EVALUATE
	$TRAN	'EXIT',EXIT_CMD
	$TRAN	'VALIDATE',VALIDATE
	$TRAN	TPA$_LAMBDA,CK_EOS

	$STATE	CK_EOS
	$TRAN	'!',TPA$_EXIT
	$TRAN	TPA$_EOS,TPA$_EXIT

;
;	SEARCH start:end = object
;	       start;length = object
;

	$STATE	SEARCH
	$TRAN	!EXPRESSION
	$STATE
	$TRAN	SEMI,,,OPT$M_LENGTH,OPTIONS
	$TRAN	COLON,,,OPT$M_RANGE,OPTIONS
	$STATE
	$TRAN	!EXPRESSION
	$STATE
	$TRAN	'='
	$TRAN	TPA$_LAMBDA
	$STATE
	$TRAN	!EXPRESSION
	$STATE
	$TRAN	TPA$_LAMBDA,TPA$_EXIT,SEARCH_MEMORY

;
;	@filespec
;

	$STATE	INDIRECT_CMD
	$TRAN	!COLLECT,TPA$_EXIT,INDIRECT_COMMAND,,FILE_DESC

;
;	SHOW PROCESS name /options
;	SHOW SUMMARY
;	SHOW PAGE_TABLE
;	SHOW PFN_DATA
;	SHOW POOL /IRP /NONPAGED /ALL
;	SHOW CRASH
;	SHOW STACK /mode   (default is current stack)
;	SHOW SYMBOL name /ALL
;	SHOW DEVICE device /ALL
;	SHOW RMS
;	SHOW LOCKS
;	SHOW RESOURCES
;	SHOW CLUSTER /CSID=n /SCS
;	SHOW CONNECTIONS /ADDRESS=n
;	SHOW PORTS /ADDRESS=n
;	SHOW RSPID /CONNECTION=n
;

	$STATE	SHOW
	$TRAN	'CRASH',TPA$_EXIT,DISPLAY_CRASH
	$TRAN	'DEVICE',DEVICE
	$TRAN	'HEADER',TPA$_EXIT,PRINT_HEADER
	$TRAN	'LOCKS',LOCK
	$TRAN	'PAGE_TABLE',PAGE_TABLE
	$TRAN	'PFN_DATA',PFN_DATA
	$TRAN	'POOL',POOL
	$TRAN	'PROCESS',PROCESS
	$TRAN	'RESOURCES',RES
	$TRAN	'RMS',SHOW_RMS
	$TRAN	'STACKS',STACK
	$TRAN	'SUMMARY',SHOW_SUMMARY
	$TRAN	'SYMBOLS',SYMBOL
    	$TRAN	'CLUSTER',VAXCLUSTER
    	$TRAN	'CONNECTIONS',SCS_CONNECT
    	$TRAN	'RSPID',SCS_RDT
    	$TRAN	'PORTS',SCS_PORTS
;
;	SHOW LOCK lockid
;		  [/ALL]
;

	$STATE	LOCK
	$TRAN	!LOCK_OPTS,TPA$_EXIT
	$TRAN	TPA$_HEX,,,,LOCKID

	$STATE
	$TRAN	!CK_EOS,TPA$_EXIT,SHOW_ONE_LOCK

	$STATE	LOCK_OPTS
	$TRAN	!CK_EOS,TPA$_EXIT,SHOW_ALL_LOCKS
	$TRAN	'/'

	$STATE	
	$TRAN	'ALL'

	$STATE
	$TRAN	!CK_EOS,TPA$_EXIT,SHOW_ALL_LOCKS

;
;	SHOW RES [/ALL] 
;		 /LOCKID = xxxx
;
	
	$STATE	RES
	$TRAN	!CK_EOS,TPA$_EXIT,SHOW_ALL_RES
	$TRAN	'/'

	$STATE
	$TRAN	'ALL',TPA$_EXIT,SHOW_ALL_RES
	$TRAN	'LOCKID'

	$STATE
	$TRAN	':'
	$TRAN	'='

	$STATE
	$TRAN	TPA$_HEX,,,,LOCKID
	
	$STATE	
	$TRAN	!CK_EOS,TPA$_EXIT,SHOW_ONE_RES

;
;	SHOW RMS
;

	$STATE	SHOW_RMS
	 $TRAN	!CK_EOS,TPA$_EXIT,SHOW_RMS_OPT

;	SHOW CLUSTER /CSID=n /SCS

    	$STATE	VAXCLUSTER
    	$TRAN	!CK_EOS,TPA$_EXIT,SHOW_CLUSTER
    	$TRAN	'/'

    	$STATE
    	$TRAN	'CSID'
    	$TRAN   'SCS',TPA$_EXIT,SHOW_SCS

    	$STATE
    	$TRAN	'='

    	$STATE
    	$TRAN	TPA$_HEX,,,,CSID	

    	$STATE	
    	$TRAN	!CK_EOS,TPA$_EXIT,SHOW_CLUSTER

;
;	SHOW CONNECTIONS /ADDR=n
;

    	$STATE	SCS_CONNECT
    	$TRAN	!CK_EOS,TPA$_EXIT,SHOW_CONNECTIONS
    	$TRAN	'/'

    	$STATE
    	$TRAN	'ADDRESS'

    	$STATE
    	$TRAN	'='

    	$STATE
    	$TRAN	!EXPRESSION,TPA$_EXIT,CDT_BYADDR
;
;	SHOW PORTS /ADDR=n
;

    	$STATE	SCS_PORTS
    	$TRAN	!CK_EOS,TPA$_EXIT,SHOW_PORTS
    	$TRAN	'/'

    	$STATE
    	$TRAN	'ADDRESS'

    	$STATE
    	$TRAN	'='

    	$STATE
    	$TRAN	!EXPRESSION,TPA$_EXIT,PDT_BYADDR
;
;	SHOW RSPID /CONNECTION=n
;

    	$STATE	SCS_RDT
    	$TRAN	!CK_EOS,TPA$_EXIT,SHOW_RSPID
    	$TRAN	'/'

   	$STATE
    	$TRAN	'CONNECTION'

    	$STATE
    	$TRAN	'='

    	$STATE
    	$TRAN	!EXPRESSION,,,1,CDT_SPCFY

    	$STATE
	$TRAN	TPA$_LAMBDA,TPA$_EXIT,SHOW_RSPID


;
;	SET LOG filespec
;	SET NOLOG
;	SET OUTPUT filespec
;	SET PROCESS name /INDEX=n
;	SET RMS/DISPLAY=(opt1,opt2,...,optn)
;

	$STATE	SET
	$TRAN	'LOG',SET_LOG
	$TRAN	'NOLOG',TPA$_EXIT,CLOSE_LOG
	$TRAN	'OUTPUT',SET_OUTPUT
	$TRAN	'PROCESS',SET_PROC
	$TRAN	'RMS',SET_RMS

;
;	SET LOG filespec
;

	$STATE	SET_LOG
	$TRAN	!COLLECT,,,,LOG_FILE
	$STATE
	$TRAN	TPA$_LAMBDA,TPA$_EXIT,OPEN_LOG

;
;	SET OUTPUT filespec
;

	$STATE	SET_OUTPUT
	$TRAN	!COLLECT,,,,OUTPUT_FILE
	$STATE
	$TRAN	TPA$_LAMBDA,TPA$_EXIT,OPEN_OUTPUT

;
;	SUB-EXPRESSION TO COLLECT ALL REMAINING CHARACTERS
;

	$STATE	COLLECT
	$TRAN	TPA$_ANY		; MUST HAVE AT LEAST 1 CHAR.
	$STATE	COLLECT_LOOP
	$TRAN	!SNARF,COLLECT_LOOP
	$TRAN	TPA$_LAMBDA,TPA$_EXIT
	$STATE	SNARF
	$TRAN	!CK_EOS,TPA$_FAIL
	$TRAN	TPA$_ANY,TPA$_EXIT

;
;	SET RMS=opt
;	        (opt1,opt2,...,optn)
;

	$STATE	SET_RMS
	 $TRAN	!DISPLAY,TPA$_EXIT,END_SET_RMS

	$STATE	DISPLAY
	 $TRAN	TPA$_LAMBDA,,SETUP_RMS_TMP
	$STATE
	 $TRAN	':'
	 $TRAN	'='
	$STATE
	 $TRAN	!DIS_OPT,TPA$_EXIT
	 $TRAN	'('
	$STATE	NXT_OPT
	 $TRAN	!DIS_OPT
	$STATE
	 $TRAN	<','>,NXT_OPT
	 $TRAN	')',TPA$_EXIT

	$STATE	DIS_OPT
	 $TRAN	TPA$_LAMBDA,,SETUP_DIS_OPT
	$STATE
	 $TRAN	!ATM_OPT,DIS_OPT_EXIT
	 $TRAN	'N'
	$STATE
	 $TRAN	'O'
	$STATE
	 $TRAN	!ATM_OPT,,,OPT$M_NO,RMS_DIS_TMP1
	$STATE	DIS_OPT_EXIT
	 $TRAN	TPA$_LAMBDA,TPA$_EXIT,STORE_TMP1_OPT

	$STATE	ATM_OPT
	 $TRAN	'ALL',IFI,,OPT$M_RMSALL,RMS_DIS_TMP1
	 $TRAN	'ASB',TPA$_EXIT,,OPT$M_ASB,RMS_DIS_TMP1
	 $TRAN	'BDB',TPA$_EXIT,,OPT$M_BDB,RMS_DIS_TMP1
	 $TRAN	'BDBSUM',TPA$_EXIT,,OPT$M_BDBSUM,RMS_DIS_TMP1
	 $TRAN	'BLB',TPA$_EXIT,,OPT$M_BLB,RMS_DIS_TMP1
	 $TRAN	'BLBSUM',TPA$_EXIT,,OPT$M_BLBSUM,RMS_DIS_TMP1
	 $TRAN	'CCB',TPA$_EXIT,,OPT$M_CCB,RMS_DIS_TMP1
	 $TRAN	'FAB',TPA$_EXIT,,OPT$M_FAB,RMS_DIS_TMP1
	 $TRAN	'FCB',TPA$_EXIT,,OPT$M_FCB,RMS_DIS_TMP1
	 $TRAN	'FWA',TPA$_EXIT,,OPT$M_FWA,RMS_DIS_TMP1
	 $TRAN	'GBD',TPA$_EXIT,,OPT$M_GBD,RMS_DIS_TMP1
	 $TRAN	'GBDSUM',TPA$_EXIT,,OPT$M_GBDSUM,RMS_DIS_TMP1
	 $TRAN	'GBH',TPA$_EXIT,,OPT$M_GBH,RMS_DIS_TMP1
	 $TRAN	'IDX',TPA$_EXIT,,OPT$M_IDX,RMS_DIS_TMP1
	 $TRAN	'IFAB',IFI,,OPT$M_IFB,RMS_DIS_TMP1
	 $TRAN	'IFB',IFI,,OPT$M_IFB,RMS_DIS_TMP1
	 $TRAN	'IRAB',TPA$_EXIT,,OPT$M_IRB,RMS_DIS_TMP1
	 $TRAN	'IRB',TPA$_EXIT,,OPT$M_IRB,RMS_DIS_TMP1
	 $TRAN	'NAM',TPA$_EXIT,,OPT$M_NAM,RMS_DIS_TMP1
	 $TRAN	'RAB',TPA$_EXIT,,OPT$M_RAB,RMS_DIS_TMP1
	 $TRAN	'RJB',TPA$_EXIT,,OPT$M_RJB,RMS_DIS_TMP1
	 $TRAN	'RLB',TPA$_EXIT,,OPT$M_RLB,RMS_DIS_TMP1
	 $TRAN	'TRC',TPA$_EXIT,,OPT$M_TRC,RMS_DIS_TMP1
	 $TRAN	'WCB',TPA$_EXIT,,OPT$M_WCB,RMS_DIS_TMP1
	 $TRAN	'XAB',TPA$_EXIT,,OPT$M_XAB,RMS_DIS_TMP1
	 $TRAN	'*',TPA$_EXIT,STORE_STAR

	$STATE	IFI
	 $TRAN	':'
	 $TRAN	'='
	 $TRAN	TPA$_LAMBDA,TPA$_EXIT
	$STATE	IFI2
	 $TRAN	'ALL',TPA$_EXIT,CLR_IFI_TMP
	 $TRAN	!EXPRESSION,TPA$_EXIT,STORE_IFI_TMP

;
;	EVALUATE expression
;

	$STATE	EVALUATE
	$TRAN	'/'
	$TRAN	TPA$_LAMBDA,EVAL_EXP
	$STATE	
	$TRAN	'CONDITION_VALUE',,,OPT$M_COND,OPTIONS
	$STATE	EVAL_EXP
	$TRAN	!EXPRESSION
	$STATE
	$TRAN	'/',EVAL_QUAL
	$TRAN	TPA$_LAMBDA,TPA$_EXIT,SHOW_EXPR
	$STATE	EVAL_QUAL
	$TRAN	'CONDITION_VALUE',,,OPT$M_COND,OPTIONS
	$STATE
	$TRAN	TPA$_LAMBDA,TPA$_EXIT,SHOW_EXPR

;
;	EXAMINE start:end /P0 /P1 /SYSTEM /ALL /INSTRUCTION /PSL /TIME /CONDITION
;	EXAMINE start;length
;

	$STATE	EXAMINE
	$TRAN	'/'
	$TRAN	TPA$_LAMBDA,EXAMINE_EXPRESSION
	$STATE
	$TRAN	'P0',EXAM_OK,,OPT$M_P0,OPTIONS
	$TRAN	'P1',EXAM_OK,,OPT$M_P1,OPTIONS
	$TRAN	'SYSTEM',EXAM_OK,,OPT$M_SYSTEM,OPTIONS
	$TRAN	'ALL',EXAM_OK,,OPT$M_ALL,OPTIONS
	$TRAN	'INSTRUCTION',EXAM_INS,,OPT$M_INST,OPTIONS
	$TRAN	'PSL',,,OPT$M_PSL,OPTIONS
	$TRAN	'TIME',,,OPT$M_TIME,OPTIONS
	$TRAN	'CONDITION_VALUE',,,OPT$M_COND,OPTIONS

	$STATE	EXAMINE_EXPRESSION
	$TRAN	!CK_EOS,EXAM_OK
	$TRAN	!EXPRESSION
	$STATE
	$TRAN	!CK_EOS,EXAM_OK
	$TRAN	'/',EXAM_QUALS
	$TRAN	SEMI,,,OPT$M_LENGTH,OPTIONS
	$TRAN	COLON,,,OPT$M_RANGE,OPTIONS
	$STATE
	$TRAN	!EXPRESSION
	$STATE
	$TRAN	'/',EXAM_QUALS
	$TRAN	TPA$_LAMBDA,EXAM_OK
	$STATE	EXAM_QUAL_LOOP
	$TRAN	!CK_EOS,EXAM_OK
	$TRAN	'/'
	$STATE	EXAM_QUALS
	$TRAN	'INSTRUCTION',EXAM_QUAL_LOOP,,OPT$M_INST,OPTIONS
	$TRAN	'NOSKIP',EXAM_QUAL_LOOP,,OPT$M_NOSKIP,OPTIONS
	$TRAN	'PSL',EXAM_QUAL_LOOP,,OPT$M_PSL,OPTIONS
	$TRAN	'TIME',EXAM_QUAL_LOOP,,OPT$M_TIME,OPTIONS
	$TRAN	'CONDITION_VALUE',EXAM_QUAL_LOOP,,OPT$M_COND,OPTIONS
	$STATE	EXAM_OK
	$TRAN	TPA$_LAMBDA,TPA$_EXIT,EXAM_MEMORY
	$STATE	EXAM_INS
	$TRAN	'/'
	$TRAN	TPA$_LAMBDA,EXAMINE_EXPRESSION
	$STATE
	$TRAN	'NOSKIP',EXAM_INS,,OPT$M_NOSKIP,OPTIONS
;
; VALIDATE QUEUE expression
;
	$STATE	VALIDATE
	$TRAN	'QUEUE',VAL_QUEUE

	$STATE	VAL_QUEUE
	$TRAN	!CK_EOS,VALIDATE_Q_OK
	$TRAN	!VAL_QUEUE_OPTS,TPA$_EXIT

	$STATE	VAL_QUEUE_OPTS
	$TRAN	'/',VAL_QUE_QUAL
    	$TRAN	!EXPRESSION,VAL_QUEUE

	$STATE	VAL_QUE_QUAL
	$TRAN	'SELF_RELATIVE',VAL_QUEUE,,OPT$M_SELF,OPTIONS
	$TRAN	'MAXIMUM_LINKS'
	$STATE
	$TRAN	':'
	$TRAN	'='
	$STATE
	$TRAN	!DECIMAL,VAL_QUEUE,VAL_SET_MAX

	$STATE	VALIDATE_Q_OK
	$TRAN	TPA$_LAMBDA,TPA$_EXIT,VALIDATE_QUEUE	

;
;	EXPRESSION ANALYSIS FOR EXAMINE ARGUMENTS
;
;	THIS REDUCTION TABLE USES A SEPARATE STACK FOR PARSING
;	TOKENS AS THEY ENCOUNTERED.  THE RESULT OF THE LAST
;	OPERATION IS ALWAYS PLACED IN TPA$L_NUMBER TO RETURN
;	TO THE CALLING TRANSITION ROUTINE.
;

	$STATE	EXPRESSION
	$TRAN	!TERM
	$STATE	EXPOP
	$TRAN	'+',ADDITION
	$TRAN	'-',SUBTRACTION
	$TRAN	TPA$_LAMBDA,TPA$_EXIT
	$STATE	ADDITION
	$TRAN	!EXPRESSION,TPA$_EXIT,ADD2
	$STATE	SUBTRACTION
	$TRAN	!TERM, EXPOP, SUB2
;	$TRAN	!EXPRESSION,TPA$_EXIT,SUB2

	$STATE	TERM
	$TRAN	!FACTOR
	$STATE
	$TRAN	'@',ARITH_SHIFT
	$TRAN	'*',MULTIPLICATION
	$TRAN	!DIVISION,TPA$_EXIT
	$TRAN	TPA$_LAMBDA,TPA$_EXIT
	$STATE	ARITH_SHIFT
	$TRAN	!TERM,TPA$_EXIT,SHIFT2
	$STATE	MULTIPLICATION
	$TRAN	!TERM,TPA$_EXIT,MULT2
	$STATE	DIVISION
	$TRAN	'/'
	$STATE
	$TRAN	!TERM,TPA$_EXIT,DIV2

	$STATE	FACTOR
	$TRAN	'-',NEGATE
	$TRAN	'(',PARENS
	$TRAN	'@',INDIRECT
	$TRAN	'.',TPA$_EXIT,EVAL_DOT
	$TRAN	TPA$_SYMBOL,TPA$_EXIT,EVAL_SYMBOL
	$TRAN	!CONSTANT,TPA$_EXIT,EVAL_CONSTANT
	$STATE	NEGATE
	$TRAN	!FACTOR,TPA$_EXIT,NEG1
	$STATE	INDIRECT
	$TRAN	!FACTOR,TPA$_EXIT,EVAL_CONTENTS
	$STATE	PARENS
	$TRAN	!EXPRESSION
	$STATE
	$TRAN	')',TPA$_EXIT

	$STATE	CONSTANT
	$TRAN	'+',CONSTANT
	$TRAN	'^',RADIX
	$TRAN	!HEX,TPA$_EXIT

	$STATE	RADIX
	$TRAN	'O',OCTAL
	$TRAN	'X',HEX
	$TRAN	'D',DECIMAL

	$STATE	OCTAL
	$TRAN	TPA$_OCTAL,TPA$_EXIT

	$STATE	DECIMAL
	$TRAN	TPA$_DECIMAL,TPA$_EXIT

	$STATE	HEX
	$TRAN	'G',SYSREG
	$TRAN	'H',CTLREG
	$TRAN	TPA$_HEX,TPA$_EXIT
	$STATE	SYSREG
	$TRAN	TPA$_HEX,TPA$_EXIT,ADD8000
	$STATE	CTLREG
	$TRAN	TPA$_HEX,TPA$_EXIT,ADD7FFE

;
;	SHOW PROCESS [name/ALL] [/INDEX=n] [/SYSTEM]
;		/WORKING_SET_LIST		[or WSL]
;		/PROCESS_SECTION_TABLE		[or PST]
;		/PAGE_TABLES			[or PPT]
;		    start:end
;		    start;length
;		    /P0
;		    /P1
;		/REGISTERS
;		/PCB
;		/PHD
;		/ALL
;		/RMS [=opt]
;		     [=(opt1,opt2,...,optn)]
;		/LOCKS
;		/CHANNELS
;

	$STATE	PROCESS
	$TRAN	!CK_EOS,TPA$_EXIT,SHOW_PROCESS
	$TRAN	'/'
	$TRAN	TPA$_SYMBOL,PROCESS,,,PROC_NAME
	$STATE	PROCESS1
	$TRAN	'INDEX',PROC_PIX
	$TRAN	'SYSTEM',PROCESS,,OPT$M_SYSPROC,OPTIONS
	$TRAN	'WORKING_SET_LIST',PROCESS,,OPT$M_WSL,OPTIONS
	$TRAN	'WSL',PROCESS,,OPT$M_WSL,OPTIONS
	$TRAN	'PROCESS_SECTION_TABLE',PROCESS,,OPT$M_PST,OPTIONS
	$TRAN	'PST',PROCESS,,OPT$M_PST,OPTIONS
	$TRAN	'PAGE_TABLES',PROC_PPT,,OPT$M_PPT,OPTIONS
	$TRAN	'PPT',PROC_PPT,,OPT$M_PPT,OPTIONS
	$TRAN	'P0',PROCESS,,<OPT$M_PPT!OPT$M_P0_PPT>,OPTIONS
	$TRAN	'P1',PROCESS,,<OPT$M_PPT!OPT$M_P1_PPT>,OPTIONS
	$TRAN	'REGISTERS',PROCESS,,OPT$M_REGS,OPTIONS
	$TRAN	'PCB',PROCESS,,OPT$M_PCB,OPTIONS
	$TRAN	'PHD',PROCESS,,OPT$M_PHD,OPTIONS
	$TRAN	'ALL',PROCESS,,<OPT$M_PCB!OPT$M_PHD!OPT$M_REGS!-
OPT$M_WSL!OPT$M_PST!OPT$M_PPT!OPT$M_CHAN>,OPTIONS
	$TRAN	'LOCKS',PROCESS,,OPT$M_LCK,OPTIONS
	$TRAN	'CHANNELS',PROCESS,,OPT$M_CHAN,OPTIONS
	$TRAN	!RMS,PROCESS,,OPT$M_RMS,OPTIONS
	$STATE	PROC_PIX
	$TRAN	':'
	$TRAN	'='
	$STATE
	$TRAN	TPA$_HEX,PROCESS,STORE_PROC_INDEX
	$STATE	PROC_PPT
	$TRAN	!CK_EOS,TPA$_EXIT,SHOW_PROCESS
	$TRAN	'/',PROCESS1
	$TRAN	!EXPRESSION
	$STATE
	$TRAN	SEMI,,,OPT$M_PPT_LEN,OPTIONS
	$TRAN	COLON,,,OPT$M_PPT_RNG,OPTIONS
	$STATE
	$TRAN	!EXPRESSION,PROCESS
	
;
;	SHOW PROCESS/RMS [=opt]
;			 [=(opt1,opt2,...,optn)]
;

	$STATE	RMS
	 $TRAN	'RMS'
	$STATE
	 $TRAN	!DISPLAY,TPA$_EXIT,,OPT$M_RMSD,OPTIONS
	 $TRAN	TPA$_LAMBDA,TPA$_EXIT

;
;	SET PROCESS name /INDEX=n /SYSTEM
;

	$STATE	SET_PROC
	$TRAN	!INDEX
	$TRAN	TPA$_SYMBOL,,,,PROC_NAME
	$STATE
	$TRAN	TPA$_LAMBDA,TPA$_EXIT,SET_PROCESS

	$STATE	INDEX
	$TRAN	'/'
	$STATE
	$TRAN	'INDEX'
	$TRAN	'SYSTEM',TPA$_EXIT,,OPT$M_SYSPROC,OPTIONS
	$STATE
	$TRAN	':'
	$TRAN	'='
	$STATE
	$TRAN	TPA$_HEX,TPA$_EXIT,STORE_PROC_INDEX

;
;	SHOW POOL /IRP /NONPAGED /PAGED /ALL /SUMMARY /HEADER
;

	$STATE	POOL
	$TRAN	!CK_EOS,TPA$_EXIT,SHOW_POOL
	$TRAN	'/'
	$TRAN	TPA$_LAMBDA,POOL_RANGE
	$STATE
	$TRAN	'IRP',POOL,,OPT$M_IRP,OPTIONS
	$TRAN	'LRP',POOL,,OPT$M_LRP,OPTIONS
	$TRAN	'SRP',POOL,,OPT$M_SRP,OPTIONS
	$TRAN	'NONPAGED',POOL,,OPT$M_NONPAGED,OPTIONS
	$TRAN	'PAGED',POOL,,OPT$M_PAGED,OPTIONS
	$TRAN	'ALL',POOL,,OPT$M_IRP!OPT$M_NONPAGED!OPT$M_PAGED,OPTIONS
	$TRAN	'SUMMARY',POOL,,OPT$M_SUMMARY,OPTIONS
	$TRAN	'HEADER',POOL,,OPT$M_HEADER,OPTIONS
	$TRAN	'FREE',POOL,,OPT$M_FREE,OPTIONS
	$TRAN	'TYPE',,,OPT$M_TYPE,OPTIONS
	$STATE
	$TRAN	'='
	$TRAN	':'
	$STATE
	$TRAN	TPA$_SYMBOL,POOL,,,STRUCTURE

;
;	SHOW POOL  start:end
;		   start;length
;

	$STATE	POOL_RANGE
	$TRAN	!EXPRESSION
	$STATE
	$TRAN	SEMI,,,OPT$M_LENGTH,OPTIONS
	$TRAN	COLON,,,OPT$M_RANGE,OPTIONS
	$STATE
	$TRAN	!EXPRESSION
	$STATE
	$TRAN	!CK_EOS,TPA$_EXIT,SHOW_POOL_RANGE

;
;	SHOW STACK /mode
;

	$STATE	STACK
	$TRAN	!CK_EOS,TPA$_EXIT,PRINT_STACKS
	$TRAN	'/'
	$TRAN	TPA$_LAMBDA,STACK_RANGE
	$STATE
	$TRAN	'INTERRUPT',STACK,,OPT$M_ISP,OPTIONS
	$TRAN	'KERNEL',STACK,,OPT$M_KSP,OPTIONS
	$TRAN	'EXECUTIVE',STACK,,OPT$M_ESP,OPTIONS
	$TRAN	'SUPERVISOR',STACK,,OPT$M_SSP,OPTIONS
	$TRAN	'USER',STACK,,OPT$M_USP,OPTIONS
	$TRAN	'ALL',STACK,,OPT$M_ALL,OPTIONS

;
;	SHOW STACK start:end
;		   start;length
;

	$STATE	STACK_RANGE
	$TRAN	!EXPRESSION
	$STATE
	$TRAN	SEMI,,,OPT$M_LENGTH,OPTIONS
	$TRAN	COLON,,,OPT$M_RANGE,OPTIONS
	$STATE
	$TRAN	!EXPRESSION
	$STATE
	$TRAN	!CK_EOS,TPA$_EXIT,PRINT_ANY_STACK

;
;	SHOW PFN_DATA <optional pfn>
;

	$STATE	PFN_DATA
	$TRAN	'/',PFN_OPTION
	$TRAN	!EXPRESSION,,,OPT$M_SINGLEPFN,OPTIONS
	$TRAN	TPA$_LAMBDA,,,OPT$M_FREE!OPT$M_MODIFIED!OPT$M_BAD!OPT$M_WHOLEPFN,OPTIONS
	$STATE
	$TRAN	TPA$_LAMBDA,TPA$_EXIT,DISPLAY_PFN

	$STATE	PFN_OPTS
	$TRAN	'/'
	$TRAN	TPA$_LAMBDA,TPA$_EXIT,DISPLAY_PFN
	$STATE	PFN_OPTION
	$TRAN	'FREE',PFN_OPTS,,OPT$M_FREE,OPTIONS
	$TRAN	'MODIFIED',PFN_OPTS,,OPT$M_MODIFIED,OPTIONS
	$TRAN	'BAD',PFN_OPTS,,OPT$M_BAD,OPTIONS
	$TRAN	'SYSTEM',PFN_OPTS,,OPT$M_WHOLEPFN,OPTIONS
	$TRAN	'ALL',PFN_OPTS,,OPT$M_FREE!OPT$M_MODIFIED!OPT$M_BAD!OPT$M_WHOLEPFN,OPTIONS

;
;	SHOW PAGE_TABLE/SYSTEM/GLOBAL/ALL
;

	$STATE	PAGE_TABLE
	$TRAN	'/',PAGE_OPTION
	$TRAN	TPA$_LAMBDA,,,OPT$M_SYSTEM!OPT$M_GLOBAL,OPTIONS
	$STATE
	$TRAN	!CK_EOS,TPA$_EXIT,DISPLAY_SPT
	$TRAN	TPA$_LAMBDA,PAGE_RANGE

	$STATE	PAGE_OPTS
	$TRAN	'/'
	$TRAN	!CK_EOS,TPA$_EXIT,DISPLAY_SPT
	$TRAN	TPA$_LAMBDA,PAGE_RANGE
	$STATE	PAGE_OPTION
	$TRAN	'SYSTEM',PAGE_OPTS,,OPT$M_SYSTEM,OPTIONS
	$TRAN	'GLOBAL',PAGE_OPTS,,OPT$M_GLOBAL,OPTIONS
	$TRAN	'ALL',PAGE_OPTS,,OPT$M_ALL,OPTIONS


;
;	SHOW PAGE  start:end
;		   start;length
;

	$STATE	PAGE_RANGE
	$TRAN	!EXPRESSION
	$STATE
	$TRAN	SEMI,,,OPT$M_LENGTH,OPTIONS
	$TRAN	COLON,,,OPT$M_RANGE,OPTIONS
	$STATE
	$TRAN	!EXPRESSION
	$STATE
	$TRAN	!CK_EOS,TPA$_EXIT,DISPLAY_SPT_RANGE

;
;	SHOW SUMMARY /IMAGE
;

	$STATE	SHOW_SUMMARY
	$TRAN	!CK_EOS,TPA$_EXIT,PROCESS_SUMMARY
	$TRAN	'/'
	$STATE
	$TRAN	'IMAGE',SHOW_SUMMARY,,OPT$M_IMAGE,OPTIONS

;
;	SHOW SYMBOL[/ALL] [name] [/ALL]
;

	$STATE	SYMBOL
	$TRAN	TPA$_LAMBDA,,CLEAR_SYMBOL_NAME
	$STATE	SYMBOL_QUALS
	$TRAN	'/',SYMBOL_OPTS
	$TRAN	TPA$_SYMBOL,SYM,,,SYMBOL_NAME
	$TRAN	!CK_EOS,TPA$_EXIT,SHOW_SYMBOL
	$STATE	SYM
	$TRAN	'/'
	$TRAN	!CK_EOS,TPA$_EXIT,SHOW_SYMBOL
	$STATE	SYMBOL_OPTS
	$TRAN	'ALL',SYMBOL_QUALS,,OPT$M_ALL,OPTIONS

;
;	SHOW DEVICE device
;

	$STATE	DEVICE
	$TRAN	'/',DEVICE_OPTS
	$TRAN	TPA$_SYMBOL,TPA$_EXIT,DISPLAY_DEVICE
	$TRAN	TPA$_LAMBDA,TPA$_EXIT,DISPLAY_DEVICE

	$STATE	DEVICE_OPTS
	$TRAN	'ADDRESS'
    	$STATE
    	$TRAN	'='    	
	$STATE
	$TRAN	!EXPRESSION
	$STATE
	$TRAN	TPA$_LAMBDA,TPA$_EXIT,DISPLAY_DEVBYADDR

;
;	DEFINE symbol = value
;

	$STATE	DEFINE
	$TRAN	'/',TPA$_EXIT,DEFINE_KEY
	$TRAN	TPA$_SYMBOL,,,,SYMBOL_DESC
	$STATE
	$TRAN	'='
	$TRAN	TPA$_LAMBDA
	$STATE
	$TRAN	!EXPRESSION
	$STATE
	$TRAN	TPA$_LAMBDA,TPA$_EXIT,DEFINE_SYMBOL

;
;	HELP COMMAND 
;

	$STATE	HELP_CMD
	$TRAN	!CK_EOS,TPA$_EXIT,DISPLAY_HELP
	$TRAN	!COLLECT,TPA$_EXIT,DISPLAY_HELP

;
;	EXIT COMMAND
;

	$STATE	EXIT_CMD
	$TRAN	!CK_EOS,TPA$_EXIT,EXIT_COMMAND
	$TRAN	TPA$_STRING,TPA$_EXIT,EXIT_COMMAND

;
;	READ filespec
;

	$STATE	READ_CMD
	$TRAN	!READ_QUALS
	$TRAN	TPA$_LAMBDA
	$STATE
	$TRAN	!COLLECT,,,,FILE_DESC
	$STATE
	$TRAN	TPA$_LAMBDA,TPA$_EXIT,READ_SYMFILE

	$STATE	READ_QUALS
	$TRAN	'/'
	$STATE
	$TRAN	'RELOCATE'
	$STATE
	$TRAN	'='
	$STATE
	$TRAN	!EXPRESSION,TPA$_EXIT,STORE_RELOCATION

;
;	COPY filespec
;

	$STATE	COPY_CMD
	$TRAN	!COLLECT,,,,FILE_DESC
	$STATE
	$TRAN	TPA$_LAMBDA,TPA$_EXIT,SAVE_DUMP

;
;	FORMAT address /TYPE=struct
;

	$STATE	FORMAT_CMD
	$TRAN	!FORMAT_OPTS
	$STATE
	$TRAN	!EXPRESSION
	$STATE
	$TRAN	!FORMAT_OPTS
	$STATE
	$TRAN	TPA$_LAMBDA,TPA$_EXIT,FORMAT

	$STATE	FORMAT_OPTS
	$TRAN	'/'
	$TRAN	TPA$_LAMBDA,TPA$_EXIT
	$STATE
	$TRAN	'TYPE'
	$STATE
	$TRAN	'='
	$TRAN	':'
	$STATE
	$TRAN	TPA$_SYMBOL,,,,STRUCTURE
	$STATE
	$TRAN	TPA$_LAMBDA,TPA$_EXIT,,1,OPTIONS


	$END_STATE

	.PSECT	PARSE,EXE,NOWRT

	.SBTTL	EXPRESSION ANALYSIS ACTION ROUTINES
;---
;
;	EXPRESSION ANALYSIS ACTION ROUTINES
;
;	THESE ROUTINES ARE CALLED FROM THE EXPRESSION ANALYSIS
;	PARSING TRANSITIONS TO EVALUATE ARGUMENTS USING A SEPARATE
;	STACK SPACE.
;
;   INPUTS:
;
;	AP = ADDRESS OF TPARSE PARAMETER BLOCK
;
;---

ADD2:	.WORD	0
	BSBB	EVAL_SETUP
	ADDL2	(R1)+,(R1)		; ADD 2 ARGUMENTS, RESULT ON TOP
	BRB	STORE

MULT2:	.WORD	0
	BSBB	EVAL_SETUP
	MULL2	(R1)+,(R1)		; MULTIPLY 2 ARGUMENTS, RESULT ON TOP
	BRB	STORE

SUB2:	.WORD	0
	BSBB	EVAL_SETUP
	SUBL2	(R1)+,(R1)		; SUBTRACT 2 ARGUMENTS, RESULT ON TOP
	BRB	STORE

DIV2:	.WORD	0
	BSBB	EVAL_SETUP
	TSTL	(R1)			; DO NOT ALLOW DIVIDE BY ZERO
	BEQL	FAILURE
	DIVL2	(R1)+,(R1)		; DIVIDE 2 ARGUMENTS, RESULT ON TOP
	BRB	STORE

SHIFT2:	.WORD	0
	BSBB	EVAL_SETUP
	MOVL	(R1)+,R0		; GET SHIFT COUNT
	ASHL	R0,(R1),(R1)		; SHIFT LEFT BY COUNT
	BRB	STORE

NEG1:	.WORD	0
	BSBB	EVAL_SETUP
	MNEGL	(R1),(R1)		; NEGATE 1 ARGUMENT, RESULT ON TOP
	BRB	STORE

EVAL_CONSTANT:
	.WORD	0
	BSBB	EVAL_SETUP
	MOVL	TPA$L_NUMBER(AP),-(R1)	; PUSH RESULT FROM TPARSE
	BRB	STORE

EVAL_SETUP:
	MOVL	ESP,R1			; R1 = STACK POINTER
	RSB

STORE:
	MOVL	(R1),TPA$L_NUMBER(AP)	; SAVE RESULT FOR CALLER
	MOVL	R1,ESP			; STORE STACK POINTER
	MOVL	#1,R0
	RET

FAILURE:
	CLRL	R0
	RET

EVAL_SYMBOL:
	.WORD	0
	PUSHAQ	TPA$L_TOKENCNT(AP)	; DESCRIPTOR OF SYMBOL NAME
	CALLS	#1,SYMBOL_VALUE		; TRY TO FIND IN SYMBOL TABLE
	BLBC	R0,FAILURE
	PUSHL	R1
	BSBB	EVAL_SETUP
	POPL	-(R1)			; PUSH RESULT ONTO STACK
	BRB	STORE

EVAL_DOT:
	.WORD	0
	BSBB	EVAL_SETUP
	MOVL	ADDRESS,-(R1)
	BRB	STORE

EVAL_CONTENTS:
	.WORD	0
	BSBB	EVAL_SETUP
	REQMEM	@(R1)			; GET CONTENTS OF LOCATION
	PUSHL	R1
	BSBB	EVAL_SETUP
	POPL	(R1)			; AND REPLACE ARG. ON TOP OF STACK
	BRB	STORE

ADD8000:				; ADDRESS IN SYSTEM REGION
	.WORD	0
	ADDL	#^X80000000,TPA$L_NUMBER(AP) ; ADD BASE OF SYSTEM REGION
	RET

ADD7FFE:				; ADDRESS IN CONTROL REGION
	.WORD	0
	ADDL	#^X7FFE0000,TPA$L_NUMBER(AP) ; CONTROL REGION
	RET

	.SBTTL	STORE_RELOCATION, STORE READ/RELOCATE VALUE
;---
;
;	STORE THE VALUE OF THE READ/RELOCATE QUALIFIER.
;
; INPUTS:
;
;	TPA$_L_NUMBER(AP) = RELOCATION BASE
;
; OUTPUTS:
;
;	RELOCATE_BASE = RELOCATION BASE
;---

STORE_RELOCATION:
	.WORD	0

	MOVL	TPA$L_NUMBER(AP),RELOCATE_BASE
	RET

	.SBTTL	STORE_PROC_INDEX, STORE PROCESS INDEX LOW BITS
;---
;
;	STORE THE LOW ORDER BITS (AS DETERMINED BY PIX_WIDTH) OF THE PROCESS
;	INDEX.  THIS LETS US INTERCHANGE INTERNAL AND EXTENDED PIDS
;
; INPUTS:
;
;	TPA$_L_NUMBER(AP) = USER-SPECIFIED PROC INDEX
;
; OUTPUTS:
;
;	PROC_INDEX = LOW ORDER BITS
;---

STORE_PROC_INDEX:
	.WORD	0

	EXTZV	#0, PIX_WIDTH, TPA$L_NUMBER(AP), PROC_INDEX
	MOVL	#1, R0

	RET

	.SBTTL	SAVE_COMMAND, SAVE THE CURRENT COMMAND
;---
;
;	SAVE THE CURRENT COMMAND IN THE COMMAND BUFFER
;	SO THAT A LATER 'REPEAT' COMMAND CAN RECALL IT.
;
; INPUTS:
;
;	TPA$L_STRINGCNT(AP) = DESCRIPTOR OF STRING
;
; OUTPUTS:
;
;	PREV_COMMAND = DESCRIPTOR OF SAVED STRING
;
;---

SAVE_COMMAND::
	.WORD	^M<R2,R3,R4,R5>

	MOVW	TPA$L_STRINGCNT(AP),PREV_COMMAND
	MOVC	TPA$L_STRINGCNT(AP),@TPA$L_STRINGPTR(AP),-
		@PREV_COMMAND+4
	;
	; Tell the SMG$ level how to repeat this command
	;
	CLRL	-(SP)			; State string null
	PUSHAB	PREV_COMMAND		; Last command
	PUSHAB	KEY_ATTR		; attributes
	CLRL	-(SP)			; No IF_STATE
	PUSHAB	REPEAT_KEY		; Repeat key name (KP0 defaults)
	PUSHAB	KEYTABLE		; key table
	CALLS	#6,G^SMG$ADD_KEY_DEF	; Change key definition
	SIGNAL
	RET

	.SBTTL	REPEAT_COMMAND, REPEAT THE PREVIOUS COMMAND
;---
;
;	STORE THE PREVIOUS COMMAND IN THE COMMAND BUFFER
;	AND RESTART THE PARSE.
;
; INPUTS:
;
;	PREV_COMMAND = DESCRIPTOR OF PREVIOUS COMMAND
;
; OUTPUTS:
;
;	TPA$L_STRINGCNT(AP) = THE PREVIOUS COMMAND IS RELOADED
;
;---

REPEAT_COMMAND::
	.WORD	0

	MOVQ	PREV_COMMAND,TPA$L_STRINGCNT(AP)
	RET

	.SBTTL	DEFINE_KEY - DEFINE KEY
;+++
;
; 	Use input buffer as input, calls SMG$ to parse DEF/KEY.
;
;---

.ENTRY	DEFINE_KEY,^M<R2>

	PUSHAL	INPUT_BUFFER		; address
	MOVZWL	INPUT_LEN,-(SP)		; length
	PUSHL	SP			; address of desc
	PUSHAB	KEYTABLE		; table id
	CALLS	#2,G^SMG$DEFINE_KEY	; Define key...
	ADDL	#8,SP			; pop
	SIGNAL
	STATUS	SUCCESS			; ok
	RET


	.SBTTL	Action Routines for SET|SHOW RMS/DISPLAY=optionspec
;+++
; SETUP_RMS_TMP -- Setup the temporary RMS options word.
;
; Inputs:
;
;	None.
;
; Outputs:
;
;	RMS_DIS_TMP = 0
;	RMS_IFI_TMP = 0
;---

SETUP_RMS_TMP:	.WORD	0
	CLRL	RMS_DIS_TMP
	CLRW	RMS_IFI_TMP
	MOVL	#1,R0
	RET

;+++
; STORE_TMP1_OPT -- Merge this particular option into temporary option word.
;
; Inputs:
;
;	RMS_DIS_TMP1 = Outcome of !DIS_OPT state (Possibly containing OPT$M_NO)
;
; Outputs:
;
;	RMS_DIS_TMP is set or cleared with value of RMS_DIS_TMP1 depending
;		    on value of OPT$M_NO in RMS_DIS_TMP1.
;---

STORE_TMP1_OPT:	.WORD	0
	BITL	#OPT$M_NO,RMS_DIS_TMP1	; was this display option "no'ed"?
	BNEQ	10$			; if neq yes
	BISL	RMS_DIS_TMP1,RMS_DIS_TMP
	MOVL	#1,R0
	RET
10$:	BICL	RMS_DIS_TMP1,RMS_DIS_TMP
	MOVL	#1,R0
	RET

;+++
; END_SET_RMS -- Finish the SET RMS Command.
;
; Inputs:
;
;	Final outputs of the SET RMS command:
;		RMS_DIS_TMP
;		RMS_IFI_TMP
;
; Outputs:
;
;	Permanent display options set:
;		RMS_DIS_OPT = RMS_DIS_TMP
;		RMS_IFI	    = RMS_IFI_TMP
;---

END_SET_RMS:.WORD	0
	MOVL	RMS_DIS_TMP,RMS_DIS_OPT
	MOVW	RMS_IFI_TMP,RMS_IFI
	MOVL	#1,R0
	RET

;+++
; STORE_IFI_TMP -- Set the temporary IFI from the results of !EXPRESSION.
;
; Inputs:
;
;	TPA$L_NUMBER(AP) = Results of !EXPRESSION
;
; Outputs:
;
;	RMS_IFI_TMP contains above value.
;---

STORE_IFI_TMP:.WORD	0
	MOVW	TPA$L_NUMBER(AP),RMS_IFI_TMP
	MOVL	#1,R0
	RET

;+++
; CLR_IFI_TMP -- Clear RMS_IFI_TMP.
;
; Inputs:
;
;	None.
;
; Outputs:
;
;	RMS_IFI_TMP = 0
;---

CLR_IFI_TMP:.WORD 0
	CLRW	RMS_IFI_TMP
	MOVL	#1,R0
	RET

;+++
; SETUP_DIS_OPT -- Setup for !DIS_OPT state.
;
; Inputs:
;
;	None.
;
; Outputs:
;
;	RMS_DIS_TMP1 = 0
;---

SETUP_DIS_OPT:.WORD	0
	CLRL	RMS_DIS_TMP1
	MOVL	#1,R0
	RET

;+++
; STORE_STAR -- Include into temporary options previous perm options.
;
; Inputs:
;
;	RMS_DIS_OPT = Permanent options.
;	RMS_IFI = Permanent IFI.
;
; Outputs:
;
;	RMS_DIS_TMP1 = RMS_DIS_OPT - OPT$M_NO
;	RMS_IFI_TMP = RMS_IFI
;---

STORE_STAR:.WORD 0
	PUSHL	RMS_DIS_OPT
	BICL	#OPT$M_NO,(SP)
	POPL	RMS_DIS_TMP1
	MOVW	RMS_IFI,RMS_IFI_TMP
	MOVL	#1,R0
	RET
;+++
; CLEAR_SYMBOL_NAME -- Clear SYMBOL_NAME prior to parsing SHOW/SYMBOL
;
; Inputs:
;
;	None.
;
; Outputs:
;
;	SYMBOL_NAME = 0
;---

CLEAR_SYMBOL_NAME: 
	.WORD 0
	CLRQ	SYMBOL_NAME
	MOVL	#1,R0
	RET


	.END
