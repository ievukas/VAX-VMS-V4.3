	$BEGIN	RMS0MISC,000,RM$RMS,<$FREE,$RELEASE,$FLUSH>

;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982, 1984 BY				    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;
;++
; Facility: RMS32
;
; Abstract:
;
;	This module provides the high level control for the
;	$FREE, $RELEASE, and $FLUSH RMS services.
;
; Environment:
;		Star processor running Starlet exec.
;
; Author: L. F. Laverdure	 Creation Date: 25-OCT-1977
;
; Modified By:
;
;	V03-013	RAS0310		Ron Schaefer		14-Jun-1984
;		Fix a broken branch.
;
;	V03-012	DGB0013		Donald G. Blair		01-Mar-1984
;		Make changes related to ACP calls as part of the 
;		restructuring necessary to implement access mode 
;		protected files.
;
;	V03-011	JWT0159		Jim Teague		28-Feb-1983
;		Add an alternate entry point to RM$FLUSH.  If the 
;		caller desires to invalidate the cache, then the
;		entry point should be RM$FLUSH_ALT.  Otherwise,
;		buffers are not invalidated.
;
;	V03-010	RAS0206		Ron Schaefer		26-Oct-1983
;		Change $FLUSH to invalidate all buffers.  This is a
;		temporary measure to make RU cancel/rollback work properly
;		until RU cancel can be modified to have a private version 
;		of $FLUSH.
;
;	V03-009	SHZ0007		Stephen H. Zalewski,	25-Mar-1983
;		Do not attempt to write the header attributes of a file to
;		disk if the file is not write accessed.
;
;	V03-008	SHZ0006		Stephen H. Zalewski,	21-Jan-1983
;		Fixed undefined symbol FHCLEN.
;
;	V03-007	SHZ0005		Stephen H. Zalewski,	17-Jan-1983
;		Check ifb$v_rw_attr bit in ifb to see if $FLUSH should
;		write the file header out to disk.  This is only helpful
;		for sequential files where the FFB field in the file header
;		changed, but the hbk and ebk fields did not.
;
;	V03-006	SHZ0004		Stephen H. Zalewski,	17-Jan-1983  15:58
;		Modified $FLUSH so that a file header is only written if
;		the hbk or ebk of the file has changed.  This prevents
;		unnecessary writes of the file header to disk.
;
;	V03-005	SHZ0003		Stephan H. Zalewski, 	11-Dec-1983
;		Fix bug that caused ORGCASE to be picked up from IRB
;		instead of IFB when doing a $FLUSH.
;
;	V03-004	SHZ0002		Stephen H. Zalewski,	5-Dec-1982
;		Change entry point RM$WRITE_ATTR:: to WRITE_ATTR:.  Allow
;		flush to write out the header for any file (used to only
;		flush nonshared sequential files).
;
;	V03-003	SHZ0001		Stephen H. Zalewski,	16-Dec-1982  5:07
;		Remove assume statements for locations of hbk and ebk in ifb.
;
;	V03-002	KBT0185		Keith B. Thompson	23-Aug-1982
;		Reorganize psects
;
;	V03-001	KBT0103		Keith B. Thompson	13-Jul-1982
;		Clean up psects
;
;--
;


	.SBTTL	DECLARATIONS

;
; Include Files:
;

;
; Macros:
;

	$FABDEF
	$RABDEF
	$FIBDEF
	$DEVDEF
	$BDBDEF
	$IFBDEF
	$IRBDEF
	$IODEF
	$ATRDEF
	$RLSDEF
	$RMSDEF

;
; Equated Symbols:
;

	BKP	= IRB$L_BKPBITS*8		; bit offset to flags
	LSTMSK	= <1@<IRB$V_FIND_LAST-BKP>>-
		 !<1@<IRB$V_PUTS_LAST-BKP>> 	; mask for resetting irab status
	FHCLEN	= IFB$C_FHAEND - IFB$B_RFMORG	; file header characteristics length

;
; Own Storage:
;


	.SBTTL	MISCELLANEOUS RECORD SERVICE ENTRY POINTS

;++
; Functional Description:
;
;	This module provides the high level control routines
;	for the miscellaneous rab functions $free, $release, and
;	$flush. see individual descriptions.
;
; Calling Sequence:
;
;	Entered as a result of user's calling SYS$FREE,
;	SYS$RELEASE, SYS$FLUSH.
;
; Input Parameters:
;
;	AP	user's argument list
;
; Implicit Inputs:
;
;	The contents of the rab.
;
; Output Parameters:
;
;	R0	status code
;	R1	destroyed
;
; Implicit Outputs:
;
;	The sts and stv fields of the rab are output by all of
;	these services. See individual service descriptions
;	for additional rab outputs.
;
; Completion Codes:
;
;	Standard rms
;
; Side Effects:
;
;	none
;
;--



;++
;
; Entry point for $free.
;
; Call RM$UNLOCKALL to release locks on all records.
;
;--

	$ENTRY	RMS$FREE
	$TSTPT	FREE
	$RABSET				; set up stream
	BBS	#IFB$V_DAP,(R10),NTFREE	; branch if network operation
	BSBW	RM$UNLOCKALL		; free all locked records
	BRB	EXIT			; all set

;++
;
; Entry point for $release.
;
; Call RM$UNLOCK to release lock on record specified by
; the rfa field of the rab.
;
;--

	$ENTRY	RMS$RELEASE
	$TSTPT	RELEASE0
	$RABSET				; set up stream
	BBS	#IFB$V_DAP,(R10),NTREL	; branch if network operation
	MOVL	RAB$W_RFA(R8),R1	; get rfa
	MOVZWL	RAB$W_RFA+4(R8),R2	; and zero extend last word
	BSBW	RM$UNLOCK		; release lock on record
	BRB	EXIT

;++
;
; Entry point for $FLUSH.
;
; Call internal flush routine.
;
;--

	$ENTRY	RMS$FLUSH
	$TSTPT	FLUSH
	$RABSET				; set up stream
	BBS	#IFB$V_DAP,(R10),NTFLUSH; branch if network operation
	BSBB	RM$FLUSH		; do flush
EXIT:	BRW	RM$EXRMS

;++
;
; Perform network functions.
;
;--

NTFREE:
	BSBW	NT$FREE			; perform function via
	BRB	EXIT			;  remote fal

NTREL:
	BSBW	NT$RELEASE		; perform function via
	BRB	EXIT			;  remote fal

NTFLUSH:
	BSBW	NT$FLUSH		; perform function via
	BRB	EXIT			;  remote fal



	.SBTTL	RM$FLUSH - COMPLETE ACTIVITY ON STREAM

;++
; RM$FLUSH -- Complete Activity on Stream.
;
;	This routine performs the following steps to guarantee
; 	that all modified i/o buffers are written back to the
; 	file and the files attributes are written back to the
;	file header.
;
;	1.  If sequential file org, call RM$WTLST1.
;
;	2.  Get the bdb list head.
;
;	3.  Get next bdb, if no more call RM$WRITE_ATTR, clear the
;	    FIND_LAST, PUTS_LAST, and BIO_LAST IRAB bookkeeping
;	    bits and exit.
;
;	4.  If bdb not dirty, go to 3.
;
;	5.  Call cache to get exclusive access (lock) to the buffer.
;
;	6.  Release the buffer with write through.
;
;	7.  Go to step 2.
;
;    RM$FLUSH_ALT does basically the same except it invalidates all
;    buffers as it steps through the BDBs.
;
; Calling Sequence:
;
;	BSBW	RM$FLUSH or RM$FLUSH_ALT
;
; Input Parameters:
;
;	R11	impure area address
;	R10	ifab address
;	R9	irab address
;	R8	rab address
;
; Implicit Inputs:
;
;	The contents of the ifab, irab, and rab.
;
; Output Parameters:
;
;	R0		status code
;	R1-R7,AP 	destroyed
;
; Implicit Outputs:
;
;	none.
;
; Side Effects:
;
; 	For the sequential file organization, guarantees that any
;	write behinds have terminated.
;
;	For the other organizations, $flush has a possibility of
;	never terminating if other streams continually leave
;	around dirty buffers. If this is a problem for a given
;	application, some other mechanism is required in addition
;	to flush to guarantee a quiet point. Note that these
;	problems only occur with the deferred write option.
;--


RM$FLUSH_ALT::
	MOVL 	#1,R6		; Set flag to invalidate buffers
	BRB	FLUSH		;   and proceed

RM$FLUSH::
	CLRL	R6		; don't invalidate buffers
FLUSH:	BICB	#LSTMSK,IRB$L_BKPBITS(R9); reset irab flags

	ASSUME	FAB$C_SEQ EQ 0

	TSTB	IFB$B_ORGCASE(R10)	; seq. file org?
	BNEQ	SCAN_BDB_LIST		; branch if not
	BBS	#IFB$V_BIO,IFB$B_FAC(R10),10$; branch if block i/o
	BBSC	#IRB$V_BIO_LAST,(R9),10$; or last op was block i/o
					;  (i.e., mixed block and record op)
	JSB	RM$WTLST1		; write out partial buffer
	CLRW	IRB$W_CSIZ(R9)		; say no current record
	BLBC	R0,FLXIT		; get out on error
10$:

;
; Scan bdb list for any dirty buffers and, if found, write them
; out. If any have a write in progress, wait for it to finish.
; Cache is called to access the buffer so that all necessary
; interlocking is done correctly there.
;

SCAN_BDB_LIST:
	MOVAL	IFB$L_BDB_FLNK(R10),R4	; get bdb list head addr

	ASSUME	BDB$L_FLINK EQ 0

NXTBDB:	MOVL	(R4),R4			; get next bdb addr
	CMPL	(R4),IFB$L_BDB_FLNK(R10); is this the list head?
	BEQL	WRITE_ATTR		; branch if yes
CHKDRT:	BBC	#BDB$V_DRT,BDB$B_FLGS(R4),CLEAR_VAL; branch if buffer not dirty

;
; Buffer is dirty.  Deferred write may be in use or another stream may
; currently have the buffer accessed.  At any rate, get exclusive access
; to the buffer, then release with write-through.
;

	$CSHFLAGS	LOCK		; say we want lock
	MOVL	BDB$L_VBN(R4), R1	; get vbn for this bdb
	MOVZWL	BDB$W_NUMB(R4), R2	; get size of this buffer.
	BSBW	RM$CACHE		; access the bucket.
	BLBC	R0, ERRX		; and go write it.
	BLBC 	R6,15$			; If no invalidate, don't do it
	MOVL	#RLS$M_DEQ,R3		; flag to write and invalidate
	BRB	20$
15$:	MOVL	#RLS$M_WRT_THRU,R3	; flag to cause write only
20$:	JSB	RM$RELEASE		; and write bucket out
	BLBS	R0,SCAN_BDB_LIST	; and branch if o.k.
ERRX:	RSB				; get out on error
CLEAR_VAL:
	BLBC	R6,NXTBDB		; If no invalidate, get next BDB
	BICB2	#BDB$M_VAL,BDB$B_FLGS(R4) ;  else clear valid bit, then
	BRB	NXTBDB			  ;    get next BDB


;
;  Clear nrp offset on magtape flush and avoid rewrite of file attributes.
;

CLRNRP:	CLRW	IRB$W_NRP_OFF(R9)	; get start of next block

SUCXIT:	RMSSUC				; show success
FLXIT:	RSB

	.SBTTL	WRITE_ATTR - REWRITE FILE ATTRIBUTES

;++
; WRITE_ATTR -- Rewrite File Attributes.
;
; This subroutine examines the current hbk and ebk marks for the file,
; and if either of them has changed,  rewrites the file attributes.
;
; Acp write through is forced by setting FIB$V_WRITETHRU.
;
; Inputs:
;
;	R11		impure area address
;	R10		ifab address
;	R9		ifab or irab address
;
; Outputs:
;
;	R0		status code
;	R1-R6,AP	destroyed
;--

WRITE_ATTR:
	BBS	#DEV$V_SQD,IFB$L_PRIM_DEV(R10),CLRNRP; branch if magtape
	BBC	#DEV$V_DIR,IFB$L_PRIM_DEV(R10),CLRNRP; or if non-directory device
	BBC	#IFB$V_WRTACC,(R10),SUCXIT	; Exit with success if no write access
	BBSC	#IFB$V_RW_ATTR,(R10),5$		; Write header if this bit set.
	ROTL	#16,IFB$L_EBK_DISK(R10),R0	; Get old ebk mark
	CMPL	R0,IFB$L_EBK(R10)		; Has it changed?
	BNEQ	5$				; Yes, write the header
	ROTL	#16,IFB$L_HBK_DISK(R10),R0	; Get old hbk mark
	CMPL	R0,IFB$L_HBK(R10)		; Has this changed?
	BEQL	SUCXIT				; No, then exit with success

;
; Build attribute list to rewrite record attributes.
; Allocate space for the record attributes and move them there from 
; the ifab, converting them to their on disk format.
;

5$:	MOVZBL	#FHCLEN+FIB$C_LENGTH,R2 ; size of rec. attr. + fib
	BSBW	RM$GETSPC1		; get the space
	BLBC	R0,DONE			; get out on error
	CLRL	-(SP)			; end of attribute list
	PUSHL	R1			; push addr of allocated space
	PUSHL	#<ATR$C_RECATTR@16>+FHCLEN; and set attr. code and length

	ASSUME	IFB$B_RAT EQ IFB$B_RFMORG+1
	ASSUME	IFB$W_LRL EQ IFB$B_RFMORG+2
	ASSUME	<IFB$C_SEQ + 1> EQ IFB$C_REL

	MOVL	IFB$B_RFMORG(R10),(R1)	; copy rfm, rat, lrl
	MOVB	IFB$B_ORGCASE(R10), R0	; move orgcase into R0
	BBC	#IFB$V_SEQFIL,(R10),10$	; if this is really a sequential
	DECB	R0			; file then turn it back into one.
10$:	INSV	R0,#IFB$V_ORG,#IFB$S_ORG,(R1); insert org
	TSTL	(R1)+			; move to next field

	ASSUME	IFB$L_HBK_DISK EQ IFB$B_RFMORG+4
	ASSUME	IFB$L_EBK_DISK EQ IFB$B_RFMORG+8

	ROTL	#16,IFB$L_HBK(R10),IFB$L_HBK_DISK(R10) 	; switch words of hbk
	MOVL	IFB$L_HBK_DISK(R10),(R1)+		; copy disk structured hbk
	ROTL	#16,IFB$L_EBK(R10),IFB$L_EBK_DISK(R10) 	; switch words of ebk
	MOVL	IFB$L_EBK_DISK(R10),(R1)+		; copy disk-structured ebk

	ASSUME	<IFB$W_GBC-IFB$W_FFB> EQ 8
	ASSUME	IFB$W_FFB EQ IFB$L_EBK_DISK+4
	ASSUME	IFB$C_FHAEND EQ <IFB$W_GBC+2>

	MOVQ	IFB$W_FFB(R10),(R1)+	; copy record attributes to GBC
	MOVW	IFB$W_GBC(R10), (R1)+	; copy GBC

	ASSUME	FIB$L_ACCTL EQ 0

	SSB	#FIB$V_WRITETHRU,(R1)	; force write thru
	PUSHL	R1			; push addr of fib
	MOVZBL	#FIB$C_LENGTH,-(SP)	; and length of fib
	MOVZBL	#IO$_MODIFY,R0		; specify modify i/o function
	PUSHL	#0			; p6 = 0 for qio
	PUSHAL	12(SP)			; p5 = address of attribute list
	BSBW	RM$FCPFNC_P4		; call acp to write attributes
	ADDL2	#12,SP			; clean fib descriptor off stack

;
; And rest of stack clean.
;

	MOVQ	(SP)+,R4		; addr of space to deallocate
	MOVZBL	#FHCLEN+FIB$C_LENGTH,R2	; length of space to deallocate
	PUSHL	R0			; save status from write attr
	BSBW	RM$RETSPC1		; deallocate the space
	POPR	#^M<R0>			; restore status code
	BLBC	R0,RWERR		; branch on error
DONE:	RSB

;
; Map error code from qio to rms code.
;

RWERR:
	RMSERR	WER,R1			; default error code
	BRW	RM$MAPERR		; map the error code

	.END
