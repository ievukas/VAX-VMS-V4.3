MODULE PAS$READ_BOOLEAN ( %TITLE 'Read a boolean'
		IDENT = '1-001'			! File: PASREABOO.B32 Edit: SBL1001
		) =
BEGIN
!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982, 1984 BY				    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************
!

!++
! FACILITY:	Pascal Language Support
!
! ABSTRACT:
!
!	This module contains procedures which read a boolean
!	from a textfile or a string.
!
! ENVIRONMENT:	User mode - AST reentrant
!
! AUTHOR: Steven B. Lionel, CREATION DATE: 1-April-1981
!
! MODIFIED BY:
!
! 1-001	- Original.  SBL 1-April-1981
!--


%SBTTL 'Declarations'
!
! PROLOGUE DEFINITIONS:
!

REQUIRE 'RTLIN:PASPROLOG';			! Externals, linkages, PSECTs, structures

!
! TABLE OF CONTENTS:
!

FORWARD ROUTINE
    PAS$READ_BOOLEAN,				! Read from textfile
    PAS$READV_BOOLEAN;				! Read from string

!
! MACROS:
!
!	NONE
!
! EQUATED SYMBOLS:
!
!	NONE
!
! FIELDS:
!
!	NONE
!
! OWN STORAGE:
!

!+
! Declare our own Pascal Enumerated Type Descriptor (PETD) for the BOOLEAN
! type.  This will be passed to PAS$READ_ENUMERATED.
!-

GLOBAL
    PAS$$GR_BOOLEAN_PETD: VECTOR [4, LONG] PSECT (_PAS$CODE) INITIAL (

	(UPLIT BYTE (%ASCIC'BOOLEAN') - PAS$$GR_BOOLEAN_PETD),	! Name of type
	2,							! Count of values
	(UPLIT BYTE (%ASCIC'FALSE') - PAS$$GR_BOOLEAN_PETD),	! Value FALSE = 0
	(UPLIT BYTE (%ASCIC'TRUE') - PAS$$GR_BOOLEAN_PETD));	! Value TRUE = 1


%SBTTL 'PAS$READ_BOOLEAN - Read a boolean from textfile'
GLOBAL ROUTINE PAS$READ_BOOLEAN (
	PFV: REF $PAS$PFV_FILE_VARIABLE, 		! File variable
	ERROR						! Error unwind address
    ) =

!++
! FUNCTIONAL DESCRIPTION:
!
!	This function reads a boolean from the specified textfile
!	and returns it as the function value.
!
! CALLING SEQUENCE:
!
!	BOOLEAN.wv.v = PAS$READ_BOOLEAN (PFV.mr.r [, ERROR.ja.r])
!
! FORMAL PARAMETERS:
!
!	PFV		- The Pascal File Variable (PFV) passed by reference.
!			  The structure of the PFV is defined in PASPFV.REQ.
!
!	ERROR		- Optional.  If specified, the address to unwind to
!			  in case of an error.
!
! IMPLICIT INPUTS:
!
!	NONE
!
! IMPLICIT OUTPUTS:
!
!	NONE
!
! ROUTINE VALUE:
!
!	The value of the boolean read.
!
! SIDE EFFECTS:
!
!	If the file is the standard file INPUT or OUTPUT, it is implicitly opened.
!
! SIGNALLED ERRORS:
!
!
!--

    BEGIN

    LOCAL
	PFV_ADDR: VOLATILE,		! Enable argument
	UNWIND_ACT: VOLATILE,		! Enable argument
	ERROR_ADDR: VOLATILE;		! Enable argument

    BUILTIN
	ACTUALCOUNT;			! Count of arguments

    ENABLE
	PAS$$IO_HANDLER (PFV_ADDR, UNWIND_ACT, ERROR_ADDR);	! Enable error handler

    !+
    ! Get ERROR parameter, if present.
    !-

    IF ACTUALCOUNT () GEQU 2
    THEN
	ERROR_ADDR = .ERROR;		! Set unwind address

    !+
    ! Set PFV address.
    !-

    PFV_ADDR = PFV [PFV$R_PFV];

    !+
    ! Call READ_ENUMERATED to do the work.
    !-

    RETURN (PAS$READ_ENUMERATED (
		PFV [PFV$R_PFV],
		PAS$$GR_BOOLEAN_PETD));


    END;					! End of routine PAS$READ_BOOLEAN

!<BLF/PAGE>

%SBTTL 'PAS$READV_BOOLEAN - Read a boolean from a string'
GLOBAL ROUTINE PAS$READV_BOOLEAN (			! Read an integer
	STRING: REF BLOCK [, BYTE],			! String descriptor
	ERROR						! Error unwind address
    ) =

!++
! FUNCTIONAL DESCRIPTION:
!
!	This function reads a boolean from the specified string
!	and returns it as the function value.
!
! CALLING SEQUENCE:
!
!	Boolean.wv.v = PAS$READV_BOOLEAN (STRING.mt.ds [, ERROR.ja.r])
!
! FORMAL PARAMETERS:
!
!	STRING		- The string to read from, passed as a class S
!			  (assumed) descriptor.  The length and pointer
!			  are updated to reflect the unread string.
!
!	ERROR		- Optional.  If specified, the address to unwind to
!			  in case of an error.
!
! IMPLICIT INPUTS:
!
!	NONE
!
! IMPLICIT OUTPUTS:
!
!	NONE
!
! ROUTINE VALUE:
!
!	The value of the boolean read.
!
! SIDE EFFECTS:
!
!	NONE
!
! SIGNALLED ERRORS:
!
!	NONE
!
!--

    BEGIN

    LOCAL
	PFV: $PAS$PFV_FILE_VARIABLE,	! Pascal File Variable
	RESULT,				! Result value
	ARG_LIST: VECTOR [2, LONG],	! Argument list
	PFV_ADDR: VOLATILE,		! Enable argument
	UNWIND_ACT: VOLATILE,		! Enable argument
	ERROR_ADDR: VOLATILE;		! Enable argument

    BUILTIN
	ACTUALCOUNT;			! Count of arguments

    ENABLE
	PAS$$IO_HANDLER (PFV_ADDR, UNWIND_ACT, ERROR_ADDR);	! Enable error handler

    !+
    ! Get ERROR parameter, if present.
    !-

    IF ACTUALCOUNT () GEQU 2
    THEN
	ERROR_ADDR = .ERROR;		! Set unwind address

    PFV_ADDR = PFV [PFV$R_PFV];		! Set PFV address

    !+
    ! Set up ARG_LIST.
    !-

    ARG_LIST [0] = 1;			! One argument
    ARG_LIST [1] = PFV [PFV$R_PFV];	! PFV address

    !+
    ! Call PAS$$DO_READV to do the work, giving it the address of
    ! PAS$READ_BOOLEAN to call.
    !-

    PAS$$DO_READV (PFV [PFV$R_PFV], .STRING, ARG_LIST, PAS$READ_BOOLEAN; 
	RESULT);

    RETURN .RESULT;

    END;					! End of routine PAS$READV_BOOLEAN

!<BLF/PAGE>

END						! End of module PAS$READ_BOOLEAN

ELUDOM
