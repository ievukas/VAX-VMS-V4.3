%TITLE	'VAX-11 CONVERT'
MODULE	CONV$FSTIO	( IDENT='V04-000',
			OPTLEVEL=3
			) =

BEGIN

!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982, 1984 BY				    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************

!++
!
! Facility:	VAX-11 CONVERT
!
! Abstract:	CONVERT fast load I/O and bucket routines
!
! Contents:
!		WRITE_BUCKET
!		SET_NXTBKT
!		SWAP_BUCKET
!		GET_BUCKET
!		INIT_BUCKET
!		EXTEND_AREA
!		CONVERT_VBN_ID
!
! Environment:
!
!		VAX/VMS Operating System
!
!--

!
! Author:	Keith B Thompson	Creation date:        	August-1980
!
!
! Modified by:
!
!	V03-005	RAS0331		Ron Schaefer		31-Jul-1984
!		Accumulate total area allocation.
!
!	V03-004	KBT0478		Keith B. Thompson	29-Jan-1983
!		Make extend_area global
!
!	V03-003	KBT0385		Keith B. Thompson	27-Oct-1982
!		Make changes to support prologue 3 sidrs
!
!	V03-002	KBT0350		Keith B. Thompson	4-Oct-1982
!		Use new linkage definitions
!
!	V03-001	KBT0024		Keith Thompson		25-Mar-1982
!		Change the linkage to get_bucket and remove some useless code
!
!****


PSECT
	OWN	= _CONV$FAST_D	(PIC),
	GLOBAL	= _CONV$FAST_D	(PIC),
	PLIT	= _CONV$PLIT	(SHARE,PIC),
	CODE	= _CONV$FAST_S	(SHARE,PIC);

LIBRARY	'SYS$LIBRARY:LIB.L32';
LIBRARY	'SRC$:CONVERT';

DEFINE_ERROR_CODES;

EXTERNAL ROUTINE
	CONV$$GET_TEMP_VM	: CL$GET_TEMP_VM,
	CONV$$RMS_ERROR		: NOVALUE,
	CONV$$RMS_READ_ERROR	: NOVALUE;

FORWARD ROUTINE
	SET_NXTBKT		: CL$JSB_REG_9	NOVALUE,
	SWAP_BUCKET		: CL$JSB_REG_9	NOVALUE,
	CONV$$INIT_BUCKET	: CL$JSB_REG_9	NOVALUE,
	CONV$$EXTEND_AREA	: CL$EXTEND_AREA	NOVALUE;

EXTERNAL
	CONV$GL_RFA_BUFFER,

	CONV$AB_OUT_FAB		: $FAB_DECL,
	CONV$AB_OUT_RAB		: $RAB_DECL,

	CONV$GL_CTX_BLOCK,

	CONV$GL_EOF_VBN,
	CONV$GB_PROL_V3		: BYTE,
	CONV$AR_AREA_BLOCK	: REF BLOCKVECTOR [ ,AREA$C_BLN,BYTE ],
	CONV$GW_AREA_SIZE	: WORD;

MACRO

!	These macros make the code look a little better
!
	BKT$W_VBNFS	= .CONV$GW_VBN_FS_PTR,0,16,0%,	! VBN Freespace Pointer in index level
	BKT$W_VBNFS0	= .CONV$GW_VBN_FS_PTR0,0,16,0%,	! VBN Freespace Pointer at the data level
	BKT$L_LCBPTR	= .CONV$GW_LCB_PTR,0,32,0%;	! Last Contuation Bucket Pointer

EXTERNAL
	CONV$GW_VBN_FS_PTR	: WORD,
	CONV$GW_VBN_FS_PTR0	: WORD,
	CONV$GW_LCB_PTR		: WORD;

GLOBAL	CONV$GL_CONT_VBN	: LONG;


%SBTTL	'WRITE_BUCKET'
GLOBAL ROUTINE	CONV$$WRITE_BUCKET : CL$JSB_REG_9 NOVALUE =
!++
!
! Functional Description:
!
!	Writes the current bucket into the output file
!
! Calling Sequence:
!
!	CONV$$WRITE_BUCKET()
!
! Input Parameters:
!	none
!
! Implicit Inputs:
!	none
!
! Output Parameters:
!	none
!
! Implicit Outputs:
!	none
!
! Routine Value:
!	none
!
! Routines Called:
!
!	SET_NXTBKT
!	SWAP_BUCKET
!	$WRITE
!	CONV$$RMS_ERROR	- By RMS as an AST
!
! Side Effects:
!	none
!
!--

    BEGIN

    DEFINE_CTX;
    DEFINE_BUCKET;
    DEFINE_KEY_DESC;

    ! Set the next bucket pointer to the bucket
    !
    SET_NXTBKT();

    ! Point RMS to the bucket.  NOTE: This is where OUT_RAB is changed
    !
    CONV$AB_OUT_RAB [ RAB$L_RBF ] = .BUCKET;
    CONV$AB_OUT_RAB [ RAB$W_RSZ ] = .CTX [ CTX$L_SIZ ];
    CONV$AB_OUT_RAB [ RAB$L_BKT ] = .CTX [ CTX$L_CURRENT_VBN ];

    ! If we are doing double buffering on this bucket
    ! then swap pointers to the buckets and set for asyc. operation
    !
    IF .CTX [ CTX$V_DBF ]
    THEN
	BEGIN

	! Switch the buffer
	!
	SWAP_BUCKET();

	! Set the asynchronous bit
	!
	CONV$AB_OUT_RAB [ RAB$V_ASY ] = _SET

	END
    ELSE

	! If not then we need a syncrous call
	!
	CONV$AB_OUT_RAB [ RAB$V_ASY ] = _CLEAR;

    ! Wait on the last IO if necessary
    !
    $WAIT ( RAB=CONV$AB_OUT_RAB );

    ! Write The Bucket
    !
    $WRITE ( RAB=CONV$AB_OUT_RAB,ERR=CONV$$RMS_ERROR );

    RETURN

    END;


%SBTTL	'SET_NXTBKT'
ROUTINE	SET_NXTBKT : CL$JSB_REG_9 NOVALUE =
!++
!
! Functional Description:
!
!	Writes the next bucket vbn field in the current bucket
!
! Calling Sequence:
!
!	SET_NXTBKT()
!
! Input Parameters:
!	none
!
! Implicit Inputs:
!	none
!
! Output Parameters:
!	none
!
! Implicit Outputs:
!	none
!
! Routine Value:
!	none
!
! Routines Called:
!	none
!
! Side Effects:
!	none
!
!--

    BEGIN

    DEFINE_CTX;
    DEFINE_BUCKET;

    LOCAL	AREA;

    ! Get the area that the bucket is in
    !
    AREA = .CTX [ CTX$B_AREA ];

    ! If this is the last bucket in a horz. chain
    ! then write back pointers to the beginning of the chain
    ! else write next bucket pointers
    !
    IF .BUCKET [ BKT$V_LASTBKT ]
    THEN

	! Next bucket pointer points to the first bucket in this chain
	!
	BUCKET [ BKT$L_NXTBKT ] = .CTX [ CTX$L_FIRST_VBN ]
    ELSE

	! First see if the next bucket will fit in the current extent. If it dosen't
	! then set the next bkt. ptr to EOF else set it to next bucket
	!
	IF ( .CONV$AR_AREA_BLOCK [ .AREA,AREA$B_ARBKTSZ ]  GTR
		( .CONV$AR_AREA_BLOCK [ .AREA,AREA$L_CNBLK ] -
			.CONV$AR_AREA_BLOCK [ .AREA,AREA$L_USED ] ) )
	THEN

	    ! Next bucket pointer points to end of file where the next extend
	    ! will come from. (in INIT_BUCKET)
	    !
	    BUCKET [ BKT$L_NXTBKT ] = .CONV$GL_EOF_VBN
	ELSE

	    ! Next bucket pointer points to the next bucket VBN in this area
	    !
	    BUCKET [ BKT$L_NXTBKT ] = .CONV$AR_AREA_BLOCK [ .AREA,AREA$L_NXTVBN ];

    RETURN

    END;

%SBTTL	'SWAP_BUCKET'
ROUTINE	SWAP_BUCKET :  CL$JSB_REG_9 NOVALUE =
!++
!
! Functional Description:
!
!	Swaps the current bucket with the secondary bucket for
!	double buffering
!
! Calling Sequence:
!
!	SWAP_BUCKET()
!
! Input Parameters:
!	none
!
! Implicit Inputs:
!	none
!
! Output Parameters:
!	none
!
! Implicit Outputs:
!	none
!
! Routine Value:
!	none
!
! Routines Called:
!	none
!
! Side Effects:
!	none
!
!--

    BEGIN

    DEFINE_CTX;
    DEFINE_BUCKET;

    IF .CTX [ CTX$V_DBX ]
    THEN
	BEGIN
	BUCKET = .CTX [ CTX$L_PT0 ];
	CTX [ CTX$L_CURRENT_BUFFER ] = .CTX [ CTX$L_PT0 ];
	CTX [ CTX$L_END ] = .CTX [ CTX$L_EN0 ]
	END
    ELSE
	BEGIN
	BUCKET = .CTX [ CTX$L_PT1 ];
	CTX [ CTX$L_CURRENT_BUFFER ] = .CTX [ CTX$L_PT1 ];
	CTX [ CTX$L_END ] = .CTX [ CTX$L_EN1 ]
	END;

    CTX [ CTX$V_DBX ] = NOT .CTX [ CTX$V_DBX ];

    RETURN

    END;

%SBTTL	'GET_BUCKET'
GLOBAL ROUTINE	CONV$$GET_BUCKET ( AREA ) :  CL$JSB_REG_9 NOVALUE =
!++
!
! Functional Description:
!
!	Allocates and formats a pair of buckets in memory
!
! Calling Sequence:
!
!	CONV$$GET_BUCKET( area )
!
! Input Parameters:
!
!	area	- Area from witch the bucket is to be allocated
!
! Implicit Inputs:
!	none
!
! Output Parameters:
!	none
!
! Implicit Outputs:
!	none
!
! Routine Value:
!	none
!
! Routines Called:
!
!	CONV$$GET_TEMP_VM
!	INIT_BUCKET
!
! Side Effects:
!	none
!
!--

    BEGIN

    DEFINE_CTX;
    DEFINE_BUCKET;
    DEFINE_KEY_DESC;

    LOCAL
	BYTES;

    ! Get the number of bytes per bucket for that area:
    ! ( bytes = blocks * block_size * 2 ) NOTE: *2 for double buffering
    !
    BYTES = ( .CONV$AR_AREA_BLOCK [ .AREA,AREA$B_ARBKTSZ ] * BLOCK_SIZE * 2 );

    ! Get the space.
    !
    BUCKET = CONV$$GET_TEMP_VM ( .BYTES );

    ! For double buffering then hide the extra buffer for later
    !
    BYTES = .BYTES / 2;

    ! Initialize some static values
    !
    CTX [ CTX$L_CURRENT_BUFFER ] = .BUCKET;
    CTX [ CTX$L_SIZ ] = .BYTES;

    ! CTX$L_END points at the first free byte BEFORE the check byte
    !
    CTX [ CTX$L_END ] = .BUCKET + .BYTES - 2;
    CTX [ CTX$B_AREA ] = .AREA;

    !++
    !
    ! Init. static fields in the bucket
    !
    !--

    ! Level (all prologues)
    !
    BUCKET [ BKT$B_LEVEL ] = .CTX [ CTX$B_LEVEL ];

    ! Prologue dependent fields and some pointers into the
    ! bucket and some sizes
    !
    IF .CONV$GB_PROL_V3
    THEN
	BEGIN		! Prologue 3

	! Bucket key of ref
	!
	BUCKET [ BKT$B_INDEXNO ] = .KEY_DESC [ KEY$B_KEYREF ];

	! For level 0 (data) buckets we can have a LCB pointer
	!
	IF .CTX [ CTX$B_LEVEL ] EQL 0
	THEN

	    IF ( .KEY_DESC [ KEY$B_KEYREF ] EQL 0 ) AND
						.KEY_DESC [ KEY$V_DUPKEYS ]
	    THEN
		BEGIN

		! Only primary data bucket have a LCB pointer
		!
		CONV$GW_LCB_PTR = .BYTES - 6;

		CTX [ CTX$W_FREE ] = .BYTES - BKT$C_OVERHDSZ - 6

		END
	    ELSE
		CTX [ CTX$W_FREE ] = .BYTES - BKT$C_OVERHDSZ - 2

	ELSE
	    BEGIN

	    ! Index buckets only have a VBN freespace pointer
	    !
	    CONV$GW_VBN_FS_PTR = .BYTES - 4;

	    CTX [ CTX$W_FREE ] = .BYTES - BKT$C_OVERHDSZ - 4

	    END
	END		! Prologue 3
    ELSE
	BEGIN		! Prologue 1,2

	! Bucket area number
	!
	BUCKET [ BKT$B_AREANO ] = .AREA;

	! Highest record id
	!
	BUCKET [ BKT$B_LSTRECID ] = 255;

	! The space avail. is bytes - overhead - check byte
	!
	CTX [ CTX$W_FREE ] = .BYTES - BKT$C_OVERHDSZ - 1

	END;		! Prologue 1,2

    ! For double buffering set up the pointers to the buffers and init
    ! the extra buffer
    !
    ! Set up the various pointers
    !
    CTX [ CTX$L_PT0 ] = .BUCKET;
    CTX [ CTX$L_PT1 ] = .BUCKET + .BYTES;
    CTX [ CTX$L_EN0 ] = .CTX [ CTX$L_END ];
    CTX [ CTX$L_EN1 ] = .CTX [ CTX$L_END ] + .BYTES;

    ! Init the second buffer by coping the static overhead into it
    !
    CH$MOVE( BKT$K_OVERHDSZ + 1,.CTX [ CTX$L_PT0 ],.CTX [ CTX$L_PT1 ] );

    ! Tell everyone that we are doind double buffering and which bucket
    ! are pointing to
    !
    CTX [ CTX$V_DBF ] = _SET;
    CTX [ CTX$V_DBX ] = _CLEAR;		! Clear = bucket 0, Set = bucket 1

    ! Initialize dynamic values and update area descriptor
    !
    CONV$$INIT_BUCKET();

    ! Set the pointer for this level
    !
    CTX [ CTX$L_FIRST_VBN ] = .CTX [ CTX$L_CURRENT_VBN ];

    ! Say that the bucket is ready and that will have the first record
    !
    CTX [ CTX$V_RDY ] = _SET;
    CTX [ CTX$V_FST ] = _SET;

    RETURN

    END;

%SBTTL	'INIT_BUCKET'
GLOBAL ROUTINE	CONV$$INIT_BUCKET :  CL$JSB_REG_9 NOVALUE =
!++
!
! Functional Description:
!
!	Gets a new VBN for a bucket in the proper area and initializes
!	all of the dynamic fields in the bucket
!
! Calling Sequence:
!
!	CONV$$INIT_BUCKET()
!
! Input Parameters:
!	none
!
! Implicit Inputs:
!	none
!
! Output Parameters:
!	none
!
! Implicit Outputs:
!	none
!
! Routine Value:
!	none
!
! Routines Called:
!
!	CONV$$EXTEND_AREA
!
! Side Effects:
!
!	Could extend the allocation of the output file
!
!--

    BEGIN

    DEFINE_CTX;
    DEFINE_BUCKET;
    DEFINE_KEY_DESC;

    LOCAL	AREA;

    AREA = .CTX [ CTX$B_AREA ];

    ! See if the bucket will fit in the current extent if it doesent extend the
    ! file.
    !
    IF ( .CONV$AR_AREA_BLOCK [ .AREA,AREA$B_ARBKTSZ ] GTR
	( .CONV$AR_AREA_BLOCK [ .AREA,AREA$L_CNBLK ] -
				.CONV$AR_AREA_BLOCK [ .AREA,AREA$L_USED ] ) )
    THEN

	! Extend area
	!
	CONV$$EXTEND_AREA ( .AREA );

    ! Set the VBN of the bucket and determine the size of the VBN pointers
    !
    BEGIN	! VBN local

    LOCAL	VBN;

    ! Get the next VBN of this bucket
    !
    VBN = .CONV$AR_AREA_BLOCK [ .AREA,AREA$L_NXTVBN ];

    CTX [ CTX$L_CURRENT_VBN ] = .VBN;

    ! Determine the pointer size needed for this VBN
    !
    IF .VBN LSS 65536
    THEN
	CTX [ CTX$V_VBN ] = 0		! 2 byte pointer
    ELSE
	IF .VBN LSS 1048576
	THEN
	    CTX [ CTX$V_VBN ] = 1	! 3 byte
	ELSE
	    CTX [ CTX$V_VBN ] = 2	! 4 byte

    END;	! VBN local

    !++
    !
    ! Update the area descriptor to account for the new bucket
    !
    !--

    ! Correct the pointers and counters in the prologue area descriptor
    !
    CONV$AR_AREA_BLOCK [ .AREA,AREA$L_USED ] =
				.CONV$AR_AREA_BLOCK [ .AREA,AREA$L_USED ] +
				.CONV$AR_AREA_BLOCK [ .AREA,AREA$B_ARBKTSZ ];

    CONV$AR_AREA_BLOCK [ .AREA,AREA$L_NXTVBN ] =
				.CONV$AR_AREA_BLOCK [ .AREA,AREA$L_NXTVBN ] +
				.CONV$AR_AREA_BLOCK [ .AREA,AREA$B_ARBKTSZ ];

    CONV$AR_AREA_BLOCK [ .AREA,AREA$L_TOTAL_ALLOC ] =
				.CONV$AR_AREA_BLOCK [ .AREA,AREA$L_TOTAL_ALLOC ] +
				.CONV$AR_AREA_BLOCK [ .AREA,AREA$B_ARBKTSZ ];

    !++
    !
    ! Init. dynamic fields in bucket
    !
    !--

    ! Bucket control byte (all prologues)
    !
    BUCKET [ BKT$B_BKTCB ] = _CLEAR;

    ! The freespace always points just past the bucket overhead (all prologues)
    !
    BUCKET [ BKT$W_FREESPACE ] = BKT$C_OVERHDSZ;

    ! Set address sample (all prologues)
    !
    BUCKET [ BKT$W_ADRSAMPLE ] = .CTX [ CTX$L_CURRENT_VBN ];

    ! Prologue dependent fields
    !
    IF .CONV$GB_PROL_V3
    THEN
	BEGIN		! Prologue 3

	! Index buckets have VBN freespace pointers
	!
	IF .CTX [ CTX$B_LEVEL ] NEQ 0
	THEN
	    BEGIN

	    LOCAL CTX_M1 : REF BLOCK [ ,BYTE ];

	    ! The VBN of the bucket one level down determines size
	    ! of the VBN pointers in this bucket
	    !
	    CTX_M1 = .CTX - CTX$K_BLN;

	    BUCKET [ BKT$V_PTR_SZ ] = .CTX_M1 [ CTX$V_VBN ];

	    ! The vbn freespace points to the byte just before the pointer
	    !
	    BUCKET [ BKT$W_VBNFS ] = .CONV$GW_VBN_FS_PTR - 1

	    END;

	! Reset the next record ID
	!
	BUCKET [ BKT$W_NXTRECID ] = 1

	END		! Prologue 3
    ELSE
	BEGIN		! Prologue 1,2

	! Reset the record ID
	!
	BUCKET [ BKT$B_NXTRECID ] = 1

	END;		! Prologue 1,2

    ! Reset the avaiable space in the bucket
    !
    CTX [ CTX$W_SPC ] = .CTX [ CTX$W_FREE ];

    ! Indicate that the bucket has not been used yet
    !
    CTX [ CTX$W_USE ] = 0;

    RETURN

    END;

%SBTTL	'EXTEND_AREA'
GLOBAL ROUTINE	CONV$$EXTEND_AREA ( AREA ) :  CL$EXTEND_AREA NOVALUE =
!++
!
! Functional Description:
!
!	Extens the disk allocation of a specified area
!
! Calling Sequence:
!
!	EXTEND_AREA ( .area )
!
! Input Parameters:
!
!	AREA - 	 Area to be extended
!
! Implicit Inputs:
!	none
!
! Output Parameters:
!	none
!
! Implicit Outputs:
!	none
!
! Routine Value:
!	none
!
! Routines Called:
!
!	$EXTEND
!	CONV$$RMS_ERROR	- By RMS as an AST
!
! Side Effects:
!	none
!
!--

    BEGIN

    ! Set the allocation to the largest size. (To aviod bad parameters)
    !
    CONV$AB_OUT_FAB [ FAB$L_ALQ ] =
		MAX( .CONV$AR_AREA_BLOCK [ .AREA,AREA$W_DEQ ],
				.CONV$AR_AREA_BLOCK [ .AREA,AREA$B_ARBKTSZ ] );

    ! Wait on the rab in case we have a asyinc operation going on
    !
    $WAIT( RAB=CONV$AB_OUT_RAB );

    ! Stuff the error if we get one
    !
    CONV$AB_OUT_FAB [ FAB$L_CTX ] = CONV$_EXTN_ERR;

    ! Do the extend
    !
    $EXTEND ( FAB=CONV$AB_OUT_FAB,ERR=CONV$$RMS_ERROR );

    ! Reset some pointers in the prologue
    !
    CONV$AR_AREA_BLOCK [ .AREA,AREA$L_CVBN ] = .CONV$GL_EOF_VBN;
    CONV$AR_AREA_BLOCK [ .AREA,AREA$L_NXTVBN ] = .CONV$GL_EOF_VBN;
    CONV$AR_AREA_BLOCK [ .AREA,AREA$L_CNBLK ] = .CONV$AB_OUT_FAB [ FAB$L_ALQ ];
    CONV$AR_AREA_BLOCK [ .AREA,AREA$L_USED ] = 0;
    CONV$GL_EOF_VBN = .CONV$GL_EOF_VBN + .CONV$AB_OUT_FAB [ FAB$L_ALQ ];

    RETURN

    END;

%SBTTL	'CONVERT_VBN_ID'
GLOBAL ROUTINE	CONV$$CONVERT_VBN_ID	: CL$CONVERT_VBN_ID NOVALUE =
!++
!
! Functional Description:
!
!	Converts the rfa created by the sort of the output file
!	into a VBN and ID to be used as an alturnate index pointer
!
! Calling Sequence:
!
!	CONV$$CONVERT_VBN_ID()
!
! Input Parameters:
!	none
!
! Implicit Inputs:
!	none
!
! Output Parameters:
!	none
!
! Implicit Outputs:
!
!	SORT_VBN - R6	VBN of the primary data record for this key
!	SORT_ID  - R7	ID of the primary data record for this key
!
! Routine Value:
!	none
!
! Routines Called:
!	none
!
! Side Effects:
!	none
!
!--

    BEGIN

    EXTERNAL REGISTER
	SORT_VBN,
	SORT_ID;

    !   Get the VBN an offset returned by SORT by RFA
    !
    LOCAL	SORT_RFA : REF BLOCK [ ,BYTE ];

    SORT_RFA = .CONV$GL_RFA_BUFFER;

    SORT_VBN = .SORT_RFA [ 0,0,32,0 ];
    SORT_ID = .SORT_RFA [ 4,0,16,0 ];

    RETURN

    END;

END	ELUDOM
