MODULE PAS$WRITE_UNSIGNED ( %TITLE 'Write an unsigned integer'
		IDENT = '1-002'			! File: PASWRIUNS.B32 Edit: SBL1002
		) =
BEGIN
!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982, 1984 BY				    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************
!

!++
! FACILITY:	Pascal Language Support
!
! ABSTRACT:
!
!	This module contains a procedure which writes an unsigned integer
!	to a textfile.
!
! ENVIRONMENT:	User mode - AST reentrant
!
! AUTHOR: Steven B. Lionel, CREATION DATE: 1-April-1981
!
! MODIFIED BY:
!
! 1-001	- Original.  SBL 1-April-1981
! 1-002 - Make total-width a longword.  SBL 30-June-1982
!--


%SBTTL 'Declarations'
!
! PROLOGUE DEFINITIONS:
!

REQUIRE 'RTLIN:PASPROLOG';			! Externals, linkages, PSECTs, structures

!
! TABLE OF CONTENTS:
!

FORWARD ROUTINE
    PAS$WRITE_UNSIGNED: NOVALUE,		! Write to textfile
    PAS$WRITEV_UNSIGNED: NOVALUE;		! Write to string

!
! MACROS:
!
!	NONE
!
! EQUATED SYMBOLS:
!
!	NONE
!
! FIELDS:
!
!	NONE
!
! OWN STORAGE:
!
!	NONE
!

%SBTTL 'PAS$WRITE_UNSIGNED - Write unsigned integer to textfile'
GLOBAL ROUTINE PAS$WRITE_UNSIGNED (
	PFV: REF $PAS$PFV_FILE_VARIABLE, 		! File variable
	INTEGER,					! Value to write
	TOTAL_WIDTH: SIGNED,				! Total field width
	ERROR						! Error unwind address
    ): NOVALUE =

!++
! FUNCTIONAL DESCRIPTION:
!
!	This procedure writes an unsigned integer to the specified textfile.
!
! CALLING SEQUENCE:
!
!	CALL PAS$WRITE_UNSIGNED (PFV.mr.r, INTEGER.rlu.v, TOTAL_WIDTH.rl.v
!				[ERROR.j.r])
!
! FORMAL PARAMETERS:
!
!	PFV		- The Pascal File Variable (PFV) passed by reference.
!			  The structure of the PFV is defined in PASPFV.REQ.
!
!	INTEGER		- The integer to write.
!
!	TOTAL_WIDTH	- Optional.  Total field width.
!
!	ERROR		- Optional.  Address to unwind to if an error occurs.
!
! IMPLICIT INPUTS:
!
!	NONE
!
! IMPLICIT OUTPUTS:
!
!	NONE
!
! ROUTINE VALUE:
!
!	NONE
!
! SIDE EFFECTS:
!
!	If the file is the standard file INPUT or OUTPUT, it is implicitly opened.
!
! SIGNALLED ERRORS:
!
!	LINTOOLON - line too long
!	NEGWIDDIG - negative width or digits specification not allowed
!
!--

    BEGIN

    LOCAL
	FCB: REF $PAS$FCB_CONTROL_BLOCK,		! File control block
	FIELD_WIDTH,					! Total width
	INT_DIGITS,					! Number of digits
	STRING: VECTOR [12, BYTE],			! String for result
	DESCR: BLOCK [8, BYTE],				! String descriptor
	CTRSTR_DESCR: BLOCK [8, BYTE],			! FAO Control string descriptor
	PFV_ADDR: VOLATILE,				! Enable argument
	UNWIND_ACT: VOLATILE,				! Enable argument
	ERROR_ADDR: VOLATILE;				! Enable argument

    BUILTIN
	ACTUALCOUNT;

    ENABLE
	PAS$$IO_HANDLER (PFV_ADDR, UNWIND_ACT, ERROR_ADDR);	! Enable error handler

    !+
    ! Get ERROR parameter, if present.
    !-

    IF ACTUALCOUNT () GEQU 4
    THEN
	ERROR_ADDR = .ERROR;		! Set unwind address

    PFV_ADDR = PFV [PFV$R_PFV];		! Set PFV address

    !+
    ! Validate PFV and get PFV.
    !-

    PAS$$VALIDATE_PFV (PFV [PFV$R_PFV]; FCB);

    !+
    ! Set unwind action to unlock file.
    !-

    UNWIND_ACT = PAS$K_UNWIND_UNLOCK;

    !+
    ! Do common initialization.
    !-

    PAS$$INIT_WRITE (PFV [PFV$R_PFV], FCB [FCB$R_FCB]; FCB);

    !+
    ! Check for invalid width.
    !-

    IF .TOTAL_WIDTH LSS 0
    THEN
	$PAS$IO_ERROR (PAS$_NEGWIDDIG,0);

    !+
    ! Convert integer to text
    !-

    DESCR [DSC$W_LENGTH] = 12;
    DESCR [DSC$B_CLASS] = DSC$K_CLASS_S;
    DESCR [DSC$B_DTYPE] = DSC$K_DTYPE_T;
    DESCR [DSC$A_POINTER] = STRING;
    CTRSTR_DESCR [DSC$B_CLASS] = DSC$K_CLASS_S;
    CTRSTR_DESCR [DSC$B_DTYPE] = DSC$K_DTYPE_T;
    CTRSTR_DESCR [DSC$W_LENGTH] = %CHARCOUNT ('!UL');
    CTRSTR_DESCR [DSC$A_POINTER] = UPLIT BYTE ('!UL');

    $FAO (CTRSTR_DESCR,		! Control string descriptor
    	  DESCR [DSC$W_LENGTH],	! Returned length
    	  DESCR,		! Result descriptor
          .INTEGER);		! Value to convert (Conversion can't fail)

    
    !+
    ! Get desired field width and width of converted string.
    !-

    FIELD_WIDTH = .TOTAL_WIDTH;
    INT_DIGITS = .DESCR [DSC$W_LENGTH];
    
    !+
    ! See if field will fit in record.
    !-

    FIELD_WIDTH = MAX (.FIELD_WIDTH, .DESCR [DSC$W_LENGTH]);
    BEGIN
    LOCAL
	EXTRA;			! Extra characters past end of line
    EXTRA = (.FCB [FCB$A_RECORD_CUR] + .FIELD_WIDTH) - .FCB [FCB$A_RECORD_END];
    IF .EXTRA GTR 0
    THEN
	$PAS$IO_ERROR (PAS$_LINTOOLON,1,.EXTRA);
    END;

    !+
    ! Move leading blanks, if any.
    !-

    IF .FIELD_WIDTH - .DESCR [DSC$W_LENGTH] GTR 0
    THEN
	FCB [FCB$A_RECORD_CUR] = CH$FILL (%C' ', .FIELD_WIDTH - .DESCR [DSC$W_LENGTH], .FCB [FCB$A_RECORD_CUR]);
    
    !+
    ! Now move value
    !-

    FCB [FCB$A_RECORD_CUR] = CH$MOVE (.DESCR [DSC$W_LENGTH], .DESCR [DSC$A_POINTER], .FCB [FCB$A_RECORD_CUR]);

    !+
    ! Call WRITE epilogue routine to move the last character written to the
    ! user's buffer and to unlock the file variable.
    !-

    PAS$$END_WRITE (PFV [PFV$R_PFV], FCB [FCB$R_FCB]);

    RETURN;

    END;					! End of routine PAS$WRITE_UNSIGNED

!<BLF/PAGE>

%SBTTL 'PAS$WRITEV_UNSIGNED - Write unsigned to string'
GLOBAL ROUTINE PAS$WRITEV_UNSIGNED (
	MAX_LENGTH: WORD,				! Maximum length of string
	STRING_LINE: REF VECTOR [, WORD],		! String to write to
	VALUE,						! Value to write
	TOTAL_WIDTH: SIGNED,				! Total field width
	ERROR						! Error unwind address
    ) : NOVALUE =

!++
! FUNCTIONAL DESCRIPTION:
!
!	This procedure writes an un signed integer to the specified string.
!
! CALLING SEQUENCE:
!
!	CALL PAS$WRITEV_UNSIGNED (MAX_LENGTH.rw.v, STRING_LINE.wvt.r,
!	   VALUE.rlu.v, TOTAL_WIDTH.rl.v [, ERROR.j.r])
!
! FORMAL PARAMETERS:
!
!	MAX_LENGTH	- The maximum length of STRING_LINE.
!
!	STRING_LINE	- A varying string to which the output will be appended.
!
!	VALUE		- The unsigned integer to write.
!
!	TOTAL_WIDTH	- The width of the field to write.
!
!	ERROR		- Optional.  If specified, the address to unwind to
!			  in case of an error.
!
! IMPLICIT INPUTS:
!
!	NONE
!
! IMPLICIT OUTPUTS:
!
!	NONE
!
! ROUTINE VALUE:
!
!	NONE
!
! SIDE EFFECTS:
!
!	NONE
!
! SIGNALLED ERRORS:
!
!	See PAS$WRITE_UNSIGNED
!
!--

    BEGIN

    LOCAL
	PFV: $PAS$PFV_FILE_VARIABLE,	! Pascal File Variable
	ARG_LIST: VECTOR [4, LONG],	! Argument list
	PFV_ADDR: VOLATILE,		! Enable argument
	UNWIND_ACT: VOLATILE,		! Enable argument
	ERROR_ADDR: VOLATILE;		! Enable argument

    BUILTIN
	ACTUALCOUNT;			! Count of arguments

    ENABLE
	PAS$$IO_HANDLER (PFV_ADDR, UNWIND_ACT, ERROR_ADDR);	! Enable error handler

    !+
    ! Get ERROR parameter, if present.
    !-

    IF ACTUALCOUNT () GEQU 5
    THEN
	ERROR_ADDR = .ERROR;		! Set unwind address

    PFV_ADDR = PFV [PFV$R_PFV];		! Set PFV address

    !+
    ! Set up ARG_LIST.
    !-

    ARG_LIST [0] = 3;			! Three arguments
    ARG_LIST [1] = PFV [PFV$R_PFV];	! PFV address
    ARG_LIST [2] = .VALUE;		! Unsigned integer to write
    ARG_LIST [3] = .TOTAL_WIDTH;	! Field width

    !+
    ! Call PAS$$DO_WRITEV to do the work, giving it the address of
    ! PAS$WRITE_UNSIGNED to call.
    !-

    PAS$$DO_WRITEV (PFV [PFV$R_PFV], .MAX_LENGTH, STRING_LINE [0], ARG_LIST, 
	PAS$WRITE_UNSIGNED);

    RETURN;

    END;					! End of routine PAS$WRITEV_UNSIGNED

!<BLF/PAGE>

END						! End of module PAS$WRITE_UNSIGNED

ELUDOM

