MODULE AED$CLEANUP (
		 LANGUAGE (BLISS32),
		 IDENT = 'V04-000'
		 ) =
BEGIN

!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982, 1984 BY				    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************

!++
!
! FACILITY:	Miscellaneous utilities
!
! ABSTRACT:
!
!	This module contains routines to restore the user's environment to
!	the same as it was before the editing session began (unneeded files
!	are deleted, terminal characteristics reset, etc).
!
! ENVIRONMENT:
!
!	VAX/VMS operating system, user mode utilities.
!
!--
!
!
! AUTHOR:	L. Mark Pilant		CREATION DATE:	12-Nov-1982  9:50
!
! MODIFIED BY:
!
!	V03-004	LMP0213		L. Mark Pilant,		24-Mar-1984  12:23
!		Add support for locking and unlocking the object's ACL.
!
!	V03-003	LMP0172		L. Mark Pilant,		28-Nov-1983  12:11
!		Numerous bug fixes, support for VT2xx terminals, and a
!		session keystroke logger.
!
!	V03-002	LMP0144		L. Mark Pilant,		25-Aug-1983  10:18
!		Leave keypad application mode on if it was on initially.
!
!	V03-001	LMP0076		L. Mark Pilant,		10-Feb-1983  15:36
!		Reset the scrolling region upon leaving the editor.
!
!**

LIBRARY 'SYS$LIBRARY:LIB.L32';
LIBRARY 'SYS$LIBRARY:TPAMAC.L32';
REQUIRE 'SRC$:ACLEDTDEF';

FORWARD ROUTINE
	AED_CLEANUP	: NOVALUE;		! Reset screen characteristics

EXTERNAL ROUTINE
	AED_SET_CURSOR,				! Set cursor position
	AED_PUTOUTPUT;				! Do terminal output

GLOBAL ROUTINE AED_CLEANUP : NOVALUE =

!++
!
! FUNCTIONAL DESCRIPTION:
!
!	This routine sets the scope characteristics back to normal.  This
!	means: (1) the normal keypad and (2) no scrolling region if any.
!	If the terminal is not a scope, this routine is a no-op.
!
! CALLING SEQUENCE:
!	AED_CLEANUP ()
!
! INPUT PARAMETERS:
!	none
!
! IMPLICIT INPUTS:
!	AED_W_TERMIN: terminal input channel
!	AED_W_TERMOUT: terminal output channel
!
! OUTPUT PARAMETERS:
!	none
!
! IMPLICIT OUTPUTS:
!	none
!
! ROUTINE VALUE:
!	none
!
! SIDE EFFECTS:
!	1) Keypad is set back to its normal functions
!	2) Scrolling region, if any, is set to include the entire screen
!
!--

BEGIN

LOCAL
	TERM_CHAR	: VECTOR [3],		! Terminal characteristics
	LOCAL_IOSB	: VECTOR [4,WORD],	! I/O Status block
	LOCAL_STATUS;				! Routine exit status

! Check to make sure the terminal is a scope.

IF NOT .AED_L_FLAGS[AED_V_SCOPE] THEN RETURN 1;

! Set terminal wrapping if necessary.

IF .AED_L_FLAGS[AED_V_WRAP]
THEN
    BEGIN
    LOCAL_STATUS = $QIOW (CHAN = .AED_W_TERMOUT,
			  FUNC = IO$_SENSEMODE,
			  IOSB = LOCAL_IOSB,
			  P1 = TERM_CHAR);
    IF .LOCAL_STATUS THEN LOCAL_STATUS = .LOCAL_IOSB[0];
    IF NOT .LOCAL_STATUS THEN SIGNAL (.LOCAL_STATUS);
    TERM_CHAR[1] = .TERM_CHAR[1] OR TT$M_WRAP;
    LOCAL_STATUS = $QIOW (CHAN = .AED_W_TERMOUT,
			  FUNC = IO$_SETMODE,
			  IOSB = LOCAL_IOSB,
			  P1 = TERM_CHAR);
    IF .LOCAL_STATUS THEN LOCAL_STATUS = .LOCAL_IOSB[0];
    IF NOT .LOCAL_STATUS THEN SIGNAL (.LOCAL_STATUS);
    END;

! Reset the keypad, if necessary.

IF NOT .AED_L_FLAGS[AED_V_APPLICAT]
THEN
    BEGIN
    LOCAL_STATUS = AED_PUTOUTPUT ($DESCRIPTOR (%CHAR (AED_C_CHAR_ESC), '>'));
    IF NOT .LOCAL_STATUS THEN SIGNAL (.LOCAL_STATUS);
    END;

! Reset the scrolling region.

SCR$SET_SCROLL (1, 24);

AED_SET_CURSOR (23, 1);

RETURN 1;

END;						! End of routine AED_CLEANUP

END
ELUDOM
