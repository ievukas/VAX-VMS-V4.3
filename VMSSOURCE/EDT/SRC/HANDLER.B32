%TITLE 'EDT$HANDLER - EDT condition handler'
MODULE EDT$HANDLER (				! EDT condition hanndler
		IDENT = 'V04-000'			! File: HANDLER.BLI Edit: JBS1004
		) =
BEGIN
!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982, 1984 BY				    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************
!

!++
! FACILITY:	EDT -- The DEC Standard Editor
!
! ABSTRACT:
!
!	Condition handler enabled for callable EDT
!
! ENVIRONMENT:	Runs on Vax/Vms
!
! AUTHOR: Shelly T. Solomon, CREATION DATE: 09-Mar-1982
!
! MODIFIED BY:
!
! 1-001	- Original.  STS 09-Mar-1982
! 1-002 - Clear re-entry flag before continueing signal. STS 11-Mar-1982
! 1-003	- Ignore severity bits so we won't miss signal and change file name.  JBS 23-Feb-1983
! 1-004	- Improve the appearance of the listing.  JBS 14-Jun-1983
!--


%SBTTL 'Declarations'
!
! TABLE OF CONTENTS:
!

REQUIRE 'EDTSRC:TRAROUNAM';

FORWARD ROUTINE
    EDT$$HANDLER;

!
! INCLUDE FILES:
!

REQUIRE 'EDTSRC:EDTREQ';

REQUIRE 'EDTSRC:SYSSYM';

!
! MACROS:
!
!	NONE
!
! EQUATED SYMBOLS:
!
!	NONE
!
! OWN STORAGE:
!
!	NONE
!
! EXTERNAL REFERENCES:
!

%SBTTL 'EDT$$HANDLER - condition handler for callable EDT'

GLOBAL ROUTINE EDT$$HANDLER (			! Condition handler for callable EDT
    SIG, 					! Signal arguments
    MECH, 					! Mechanism arguments
    ENBL					! ENABLE arguments (none)
    ) =

!++
! FUNCTIONAL DESCRIPTION:
!
!    this is the condition handler for all callable EDT. It examines
! the error received.  If the error is to pass the status to caller then
! the following error is returned to the caller via a setunwind.
! This handler is enabled in EDT$EDIT, the highest routine in callable
! EDT.
!
! FORMAL PARAMETERS:
!
!	SIG			An array of the signal arguments
!	MECH			An array of the mechanism arguments
!	ENBL			The arguments to the ENABLE expression, none provided
!
! IMPLICIT INPUTS:
!
!	NONE
!
! IMPLICIT OUTPUTS:
!
!	EDT$$G_REENTRY
!
! COMPLETION STATUS:
!
!	Always SS$_RESIGNAL, we never try to continue execution
!
! SIDE EFFECTS:
!
!	May do an unwind to the caller of EDT$EDIT
!
!--

    BEGIN

    EXTERNAL
	EDT$$G_REENTRY;

    MAP
	SIG : REF VECTOR,
	MECH : REF VECTOR,
	ENBL : REF VECTOR;

    MESSAGES ((PASSTATUS));

    IF ((.SIG [1] AND ( NOT 7)) EQL (EDT$_PASSTATUS AND ( NOT 7)))
    THEN
	BEGIN
	MECH [3] = .SIG [3];			! Value to return in R0
	EDT$$G_REENTRY = 67;
	SETUNWIND ();				! Pass status to caller
	END;

    RETURN (SS$_RESIGNAL);			! continue signal
    END;					! of routine EDT$$HANDLER

!<BLF/PAGE>

END						! of module EDT$HANDLER

ELUDOM
