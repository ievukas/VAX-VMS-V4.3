!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982, 1984 BY				    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************

!+
! This file, ERRMSG.REQ, contains definitions of EDT's messages.
!
! Edit history:
!
! 1-001	- Original, from EDT.REQ.  JBS 20-Jul-1981
! 1-002	- Add a short global name for PDP-11 MACRO modules.  JBS 20-Jul-1981
! 1-003	- Add a message for "work file failed to open".  Module RSTIOI was
!	   using code 0 for this condition!  JBS 20-Jul-1981
! 1-004	- Change "." to "_" in PDP-11 names.  JBS 21-Jul-1981
! 1-005	- Don't define the short global names, let EDT$MESSAGE do it.  JBS 21-Jul-1981
! 1-006	- Use an iterative macro.  JBS 21-Jul-1981
! 1-007	- Use the specified increment rather than 1 between messages.  JBS 27-Jul-1981
! 1-008	- Correct a couple of typos based on the MDL file.  JBS 28-Jul-1981
! 1-009	- Update a message based on the PDP-11 messages.  JBS 28-Jul-1981
! 1-010	- Change the form of the mnemonics to EDT$_..., limit the names to nine characters,
!	   and let them be defined as globals on VAX-11.  JBS 04-Aug-1981
! 1-011 - Add new error msg INVSTR for bad string passed to set command.  STS 20-Oct-1981
! 1-012	- Make the INVSTR message more general so it can be used in more cases.  JBS 22-Oct-1981
! 1-013 - Correct a typo in NOFILSPC.  SMB 03-Nov-1981
! 1-014 - Revise messages FORHLPANO & TOEXITHLP.  SMB 19-Nov-1981
! 1-015 - Add a new message NOKEYHLP for no help on a key in change mode.  SMB 20-Nov-1981
! 1-016 - Add new messages for line number & sequence numbers out of range.  SMB 3-Feb-1982
! 1-017 - Revise existing error messages related to line numbers.  SMB 05-Feb-1982
! 1-018	- Add a new message for numeric value out of range.  JBS 10-Feb-1982
! 1-019	- Add a new message for ASCII character out of range.  JBS 10-Feb-1982
! 1-020 - Add a new message for internal EDT error. STS 19-Feb-1982
! 1-021 - Add new message warning that DEFK is not allowed in Nokeypad mode. SMB 1-Mar-1982
! 1-022 - Add message for passing back status. STS 09-Mar-1982
! 1-023 - Add message for re-entry of EDT. STS 11-Mar-1982
! 1-024	- Put the messages in alphabetical order.  JBS 15-Mar-1982
! 1-025	- Add a message for a non-standard input file.  JBS 26-Mar-1982
! 1-026	- Make the PDP-11 messages be status codes -- good ones are odd, bad are even.  JBS 26-Mar-1982
! 1-027	- Add messages about closing files.  JBS 12-Apr-1982
! 1-028	- Add second control C message.  JBS 24-May-1982
! 1-029 - Add an error message for Help File initialization.  SMB 28-May-1982
! 1-030	- Put the messages in the same order as the manual, to simplify
!	   verifying one against the other.  JBS 09-Jun-1982
! 1-031	- Add "Press return to continue".  JBS 17-Jun-1982
! 1-032	- Add "Working".  JBS 18-Jun-1982
! 1-033 - Add a new select range error message.  SMB 01-Jul-1982
! 1-034	- Do some miscellaneous improvements based on today's review.  JBS 12-Jul-1982
! 1-035	- Fix duplicated mnemonic.  JBS 13-Jul-1982
! 1-036 - Move ER_OUT and ER_INP to reflect the real meaning of the messages.  SMB 13-Jul-1982
! 1-037 - Add an error message for no output file written and invalid
!	   input from terminal. STS 05-Aug-1982
! 1-038 - Take spaces out of the working message.  SMB 18-Aug-1982
! 1-039 - Remove unused messages and add one new one.  SMB 13-Dec-1982
! 1-040 - Add two new error messages for terminal opening. STS 15-Dec-1982
! 1-041 - Change the severity of terminal open errors to fatal. STS 16-Dec-1982
! 1-042	- Remove references to ASCII, since EDT uses the DEC Multinational
!	   character set.  JBS 20-Jan-1983
!--

!+
! Maintenance note: the messages should be kept in alphabetical order
! by text, so that they can be matched against the manual.
!-

!+
! Error messages: name, severity and text.
!-

MACRO
    ERROR_MESSAGES =
 
          ERR (COLONREQ, W, ''':'' required',, 
		ABOBYCC, W,   'Aborted by CTRL/C',, 
		BOTOFBUF, W,  'Advance past bottom of buffer',, 
		ASREQ, W,   '''AS'' required',, 
		ATTCUTAPP, W, 'Attempt to CUT or APPEND to current buffer',, 
		ATTPASCUR, W, 'Attempt to PASTE current buffer',, 
		REENTRY, F,   'Attempt to re-enter EDT',,
		TOPOFBUF, W,  'Backup past top of buffer',, 
		NOSETTRM, W,  'Cannot set terminal type from change mode',,
		CHGMODTER, W, 'Change mode can be entered only from a terminal',, 
		COMBUFEXH, W, 'Command buffer exhausted',, 
		COMEXHXLA, W, 'Command buffer exhausted during XLATE command processing',,
		COMFILCLO, W, 'Command file could not be closed',,
		COMFILNOP, W, 'Command file could not be opened',, 
		COMFILNEX, W, 'Command file does not exist',, 
		CONCHKFLD, W, 'Consistency check failed, please check your file',, 
		CLDNOTALN, W, 'Could not align tabs with cursor',, 
		CTRC__IGN, W, 'CTRL/C ignored',,
		DSTMOVCOP, W, 'Destination for MOVE or COPY not found',, 
		EDITORABO, F, 'Editor aborted',, 
		ENTMUSTBE, W, 'Entity must be WORD, SENTENCE, PARAGRAPH or PAGE',, 
		ERRCOMOPT, W, 'Error in command option',, 
		ERRRANSPC, W, 'Error in range specification',, 
    		OPNINTRM,  F,  'Error opening terminal for input',,
    		OPNOUTTRM, F, 'Error opening terminal for output',,
		ERRINPFIL, W, 'Error reading from input file', ER_INP, 
    		ERRINPTRM, W, 'Error reading from terminal',,
		ERROUTFIL, W, 'Error writing to output file', ER_OUT, 
		BADFILATR, W, 'File attributes error', ER_TYP, 
		FILNAM, W,    'File name:',, 
		NOFILSPC, W,  'File specification required',, 
		FORHLPANO, W, 'For help on any other keypad key, press the key',, 
		HLPFILCLO, W, 'Help file could not be closed',,
		NOHLPAVL, W,  'Help file could not be opened',, 
		NOHLPINI, W,  'Help File Index could not be initialized',,
		INCFILCLO, W, 'Include file could not be closed',,
		INCFILOPN, W, 'Include file could not be opened',, 
		INCFILNEX, W, 'Include file does not exist',, 
		INPFILCLO, W, 'Input file could not be closed',,
		INPFILOPN, W, 'Input file could not be opened',, 
		INPFILNEX, W, 'Input file does not exist',, 
		NONSTDFIL, I, 'Input file does not have standard text file format', ER_NST,
		INSMEM, W,    'Insufficient memory',, 
		INTERERR, F,  'Internal software error - please submit an SPR',,
		INVBUFNAM, W, 'Invalid buffer name',, 
		INVASCCHR, W, 'Invalid character code',,
		INVENT, W,    'Invalid entity',, 
		INVOPTCOM, W, 'Invalid option for that command',, 
		INVPARFOR, W, 'Invalid parameter for SET or SHOW',, 
		INVSTR, W,    'Invalid string',,
		INVSUBCOM, W, 'Invalid subcommand',, 
		INVVALSET, W, 'Invalid value in SET command',, 
		IOERRWRK, F,  'I/O error on work file', ER_WF, 
		JOUFILCLO, W, 'Journal file could not be closed',,
		NOJNLFIL, W,  'Journal file could not be opened',, 
		BADDEFK, W,   'Keys cannot be defined in Nokeypad mode',,
		LINEXC255, W, 'Line exceeded 255 characters, truncated',, 
		MACKEYREQ, W, 'MACRO or KEY required',, 
		MAXINPLIN, F, 'Max input line of 2814749767 exceeded, file input terminated',,
		MAXLINNUM, F, 'Max line number exceeded; lines no longer ascending; resequence recommended',,
		MAXLINVAL, F, 'Max number of lines for this buffer exceeded',,
		NODEFN, W,    'No definition',, 
		NOKEYHLP, W,  'No help available for that key',,
		BADRANGE, F,  'No more than 65535 lines can be processed in a single command',,
    		NOFILWRT, W,  'No output file written',,
		NOSELRAN, W,  'No select range active',, 
		NOSUCHLIN, W, 'No such line',, 
		NOWENTDEF, W, 'Now enter the definition terminated by ENTER',, 
		NUMVALILL, W, 'Numeric value illegal',,
		NUMVALREQ, W, 'Numeric value required',, 
		NOORIGNUM, F, 'ORIGINAL line numbers no longer an EDT feature',,
		OUTFILCLO, W, 'Output file could not be closed',,
		OUTFILCRE, W, 'Output file could not be created',, 
		PARENMIS, W,  'Parenthesis mismatch',, 
		PARSTKOVF, W, 'Parsing stack overflow',, 
		PLSANSYNQ, W, 'Please answer Y(es), N(o), Q(uit), or A(ll)',, 
	  	PASSTATUS, W, 'Pass bad status to caller',,
		PRERETCON, W, 'Press return to continue ',,
		PRSKEYDEF, W, 'Press the key you wish to define',, 
		PRIFILCLO, W, 'Print file could not be closed',,
		PRIFILCRE, W, 'Print file could not be created',, 
		QUOSTRREQ, W, 'Quoted string required',, 
		RANNONCON, W, 'Range for RESEQUENCE must be contiguous',, 
		RANSPCSEQ, W, 'Range specified by /SEQUENCE would cause duplicate or non-sequential numbers',, 
		RECTOOBIG, W, 'Record too big, truncated to 255 characters', ER_RTB, 
		SUBSTRNUL, W, 'Search string cannot be null',, 
		INVSRAN, W,   'Select complete lines only',,
		SELALRACT, W, 'Select range is already active',, 
		SEQINCROV, F, 'Sequence increment must be less than 65536',,
		SEQNUMOV, F,  'Sequence number must be less than 65536',,
		NONALPNUM, W, 'String delimiter must be non-alphanumeric',, 
		STRNOTFND, W, 'String was not found',, 
		KEYNOTDEF, W, 'That key is not definable',, 
		TOEXITHLP, W, 'To exit from HELP, press the spacebar',, 
		TORETKEY, W,  'To return to the keypad diagram, press the return key',, 
		UNXCHRAFT, W, 'Unexpected characters after end of command',, 
		UNRCOM, W,    'Unrecognized command',, 
		UNRCOMOPT, W, 'Unrecognized command option',, 
		WORFILCLO, W, 'Work file failed to close',,
		WORFILFAI, F, 'Work file failed to open', ER_WFO,
		WRKFILOVF, F, 'Work file overflow',,
		WORKING, F,   'Working',,
		WRIFILCLO, W, 'Write file could not be closed',,
		WRIFILCRE, W, 'Write file could not be created',, 
		)
%;

!+
! Define the base and offset for the message codes.
! The offset is used to distinguish EDT message codes from system-specific
! message codes.  On VAX/VMS, the codes are defined by the MESSAGE compiler.
!-

%IF %BLISS (BLISS16)
%THEN

LITERAL
    W_BASE = 256,
    F_BASE = 256,
    I_BASE = 257,
    E_INC = 2;

!+
! Define the error codes.
!-

MACRO
    ERR [NAME, SEVERITY, TEXT, ENAME] =
 
	%NAME ('ERR_', NAME) = 
		%NAME (SEVERITY,'_BASE') + (E_INC * ERROR_CODE) 
	%ASSIGN (ERROR_CODE, ERROR_CODE + 1) 
    %;

COMPILETIME
    ERROR_CODE = 1;

LITERAL
    ERROR_MESSAGES;

UNDECLARE %QUOTE
    ERR;

%FI

!+
! The modules EDT$MESSAGE and EDT$MSGTXT use macro ERROR_MESSAGES to
! generate the text of each message.
!-
!+
! Define the MESSAGES macro, which defines EDT$_mnemonic properly for
! either BLISS16 or BLISS32.  On BLISS16 it is defined as a literal,
! equal to the ERR_mnemonic name.  On BLISS32 it is defined as external.
!-

MACRO
    MESSAGES (MNEMONIC_LIST) =
 
    %IF %BLISS(BLISS16) %THEN 
	MACRO MSG [MNEMONIC] = 
	%NAME ('EDT$_', MNEMONIC) = %NAME ('ERR_', MNEMONIC) %QUOTE %; 
	LITERAL MSG (%REMOVE(MNEMONIC_LIST)); 
	UNDECLARE %QUOTE %QUOTE MSG; 
    %ELSE 
	MACRO MSG [MNEMONIC] = 
	%NAME('EDT$_', MNEMONIC) %QUOTE %; 
	EXTERNAL LITERAL MSG (%REMOVE(MNEMONIC_LIST)); 
	UNDECLARE %QUOTE %QUOTE MSG; 
    %FI; 
    %;

!
!	End of file ERRMSG.REQ
