MODULE KDEDEF IDENT "1-003";	{ Screen Management Key Definition Entry (KDE)
				{ File: SMGKDE.SDL, Edit: SBL1003

{****************************************************************************
{*									    *
{*  COPYRIGHT (c) 1978, 1980, 1982, 1984 BY				    *
{*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
{*  ALL RIGHTS RESERVED.						    *
{* 									    *
{*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
{*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
{*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
{*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
{*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
{*  TRANSFERRED.							    *
{* 									    *
{*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
{*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
{*  CORPORATION.							    *
{* 									    *
{*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
{*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
{* 									    *
{*									    *
{****************************************************************************
{
{ Author: Steven B. Lionel, 10-February-1983
{
{ 1-001 - Original.  SBL 10-Feb-1983
{ 1-002 - Add NEXT field.  SBL 27-May-1983
{ 1-003 - Move SETSTATE flag to user portion of attributes so that
{	  GET_KEY_DEF and LIST_KEY_DEFS can return it.  SBL 26-Aug-1983
{--

{+
{ The Key Definition Entry associates a key (and state) name with
{ an equivalence string and other values.  The tables are maintained
{ by LIB$INSERT_TREE, called from SMG$ADD_KEY_DEF.
{-

AGGREGATE KDE_STRUCT STRUCTURE PREFIX KDE_ MARKER KDE_;

    HEADER BYTE DIMENSION 10 FILL;	{ Header used by LIB$INSERT_TREE
    IF_STATE_LENGTH WORD UNSIGNED;	{ Length of IF_STATE in TKEY
    TKEY_DESC STRUCTURE;		{ Descriptor for TKEY
	TKEY_LENGTH WORD UNSIGNED;	{ Length of TKEY
	TKEY_DTYPE BYTE;		{ Datatype
	TKEY_CLASS BYTE;		{ Class
	TKEY_POINTER ADDRESS;		{ Pointer to TKEY
	END TKEY_DESC;
    EQUIV_DESC STRUCTURE;		{ Descriptor for EQUIV_STRING
	EQUIV_LENGTH WORD UNSIGNED;	{ Length of EQUIV
	EQUIV_DTYPE BYTE;		{ Datatype
	EQUIV_CLASS BYTE;		{ Class
	EQUIV_POINTER ADDRESS;		{ Pointer to EQUIV
	END EQUIV_DESC;
    STATE_DESC STRUCTURE;		{ Descriptor for STATE_STRING
	STATE_LENGTH WORD UNSIGNED;	{ Length of STATE
	STATE_DTYPE BYTE;		{ Datatype
	STATE_CLASS BYTE;		{ Class
	STATE_POINTER ADDRESS;		{ Pointer to STATE
	END STATE_DESC;
    PREFIX_DESC STRUCTURE;		{ Descriptor for PREFIX
	PREFIX_LENGTH WORD UNSIGNED;	{ Length of PREFIX
	PREFIX_DTYPE BYTE;		{ Datatype
	PREFIX_CLASS BYTE;		{ Class
	PREFIX_POINTER ADDRESS;		{ Pointer to PREFIX
	END PREFIX_DESC;
    NEXT ADDRESS;			{ Pointer to next KDE
    ATTR STRUCTURE LONGWORD;		{ Attributes
	ATTR_UNION UNION;
	    ATTR_BITS STRUCTURE;
		NOECHO BITFIELD MASK;	{ Don't echo strings
		TERMINATE BITFIELD MASK; { This key terminates read
		LOCK BITFIELD MASK;	{ Lock new state
		PROTECTED BITFIELD MASK; { Definition is protected
		SETSTATE BITFIELD MASK;	{ Sets a new state
		RESERVED BITFIELD LENGTH 3 MASK; { Reserved bits
		dummy BITFIELD LENGTH 23 FILL;
		DEFINED BITFIELD MASK;	{ Key is defined
		END ATTR_BITS;
	    ATTR_FIELDS STRUCTURE;
		USER_ATTR BITFIELD LENGTH 8 MASK; { User bits
		SMG_ATTR BITFIELD LENGTH 24 MASK; { SMG routine bits
		END ATTR_FIELDS;
	    END ATTR_UNION;
	END ATTR_STRUCTURE;

    TERM_MASK LONGWORD;			{ Terminator bit mask
    END KDE_STRUCT;
END_MODULE KDEDEF;
