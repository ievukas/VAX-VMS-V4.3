
	.TITLE	LCDRIVER - LINE PRINTER DRIVER FOR LP-730
	.IDENT	'V04-000'

;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982, 1984 BY				    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;
; ABSTRACT:
;
;	LINE PRINTER DRIVER FOR LP-730 CONTROLLER
;
; AUTHOR:
;
;	E. A. DRAYTON 4-NOV-80
;
; MODIFIED BY:
;
;	V03-008 EMD0091		Ellen M. Dusseault	30-Apr-1984
;		Add DEV$M_NNM characteristic to DEVCHAR2 so that these
;		devices will have the prefix "node$".
;
;	V03-007	EAD0113		Elliott A. Drayton	 9-Mar-1984
;		Added code to restore CRB address to R0.
;
;	V03-006 EMD0048		Ellen M. Dusseault	20-Feb-1984
;		Add new feature, fallback.  The ability to convert
;		multinational characters (8-bit) to a 7-bit equivalent
;		representation if possible.  Otherwise an underscore
;		replaces the multinational character.
;
;	V03-005	WHM0001		Bill Matthews		19-Dec-1983
;		Added code to support new IDB fields IDB$B_COMBO_VECTOR
;		and IDB$B_COMBO_CSR_OFFSET for determining the main CSR
;		address and loading the soft vector for the combo device.
;
;	V03-004 EAD0004		Elliott A. Drayton	26-Jul-1983
;		Add code to restore R2 after call to RELMPR, also
;		increase the number of preallocated map registers.
;
;	V03-003	EAD0003		Elliott A. Drayton	25-Apr-1983
;		Changed code to cause indirect registers BUFADR thru
;		LINES to be loaded during a write physical operation.
;		Also changed DIPL to 21 and default lines per page
;		to 66 and do not allow a zero for lines per page.
;
;	V03-002	KDM0002		Kathleen D. Morse	28-Jun-1982
;		Added $DYNDEF, $DCDEF, and $PRDEF.
;
;--
	.PAGE
	.SBTTL	Declarations

;
; MACRO LIBRARY CALLS
;
 
	$CRBDEF				; CRB offsets
	$DCDEF				; Define device types
	$DDBDEF				; DDB offsets
	$DPTDEF				; DPT offsets
	$DYNDEF				; Dynamic data structure types
	$EMBDEF				; Error msg buffer
	$IDBDEF				; IDB offsets
	$IODEF				; I/O function codes
	$IRPDEF				; IRP offsets
	$JIBDEF				; JIB offsets
	$LPDEF				; Line printer characteristics
	$MSGDEF				; System message types
    	$PCBDEF				; Process Control Block
	$PRDEF				; Processor register numbers
	$SSDEF				; System status codes
	$UCBDEF				; UCB offsets
	$VECDEF				; VEC offsets
    	$VADEF				; Virtual address fields 
;
; LOCAL SYMBOLS
;
; ARGUMENT LIST OFFSET DEFINITIONS
;
 
P1=0					; First function dependent parameter
P2=4					; Second function dependent parameter
P3=8					; Third function dependent parameter
P4=12					; Fourth function dependend parameter
P5=16					; Fifth function dependent parameter
P6=20					; Sixth function dependent parameter
 
;
; CHARACTER CODE DEFINITIONS
;
 
DMF_CSR		= -20			; DMF CSR offset from LP CSR
RESET		= 2			; Value for LP master reset
M_STDCSR	= <2 * 256> + 4		; Set indirect reg. addr. to 2 and format on 

C_INDREG4	= 4 * 256		; Address of indirect reg. number 5
C_REGCNT	= 6			; Number of indirect registers to init
C_TIM		= 15			; Initial value for LPTIMEOUT

LC_MAPREG	= 5			; Number of map reg. to alloc
LP_HRCNT	= 1920			; Timeout value for one hour
LP_NUM_REGS	= 9			; Total number of controller indirect reg.
PAGSIZ		= 512			; Memory page size(bytes)
LINPAG		= 66			; Default lines per page for printer paper

LP_ERRBUF_LEN	= <LP_NUM_REGS*4>+<<EMB$L_DV_REGSAV>+4> ; Size of errorlog buffer

	.PAGE 
;
; Local macros
;

	.MACRO	LOADUBAA
		JSB	G^IOC$LOADUBAMAPA
	.ENDM	LOADUBAA

	.PAGE
;
; DEVICE REGISTER OFFSET DEFINITIONS
;
 
	$DEFINI	LP
 
$DEF	LP_CSR		.BLKW	1	;CONTROL STATUS REGISTER
	$VIELD	LP_CSR,0,<-
		<PRIENA,,M>,-		; Print enable
		<RESET,,M>,-		; Line printer controller reset
		<FMTCTL,,M>,-		; Format enable
		<UNUSED,2,M>,-		; Unused bits
		<MAINT,,M>,-		; Maintance mode enable
		<IE,,M>,-		; Interrupt enable
		<RDY,,M>,-		; Line printer ready ( done )
		<INDADD,3,M>,-		; Indirect register address
		<UNUSED,,M>,-		; Unused bit
		<CVERF,,M>,-		; Cable connected verification
		<DAVERR,,M>,-		; Direct access vertical forms unit error
		<LPERR,,M>,-		; Line printer error
		<NEX,,M>,-		; Non existant memory error
	>

	$VIELD	LP_CSR2,8,<-
		<AUTOCR,,M>,-		; Automatic CR insert enable
		<MECHFF,,M>,-		; Mech. form feed to multiple line feed enable
		<NPC,,M>,-		; Nonprintable character enable
		<RESERVED,,M>,-		; RESERVED
		<WRAP,,M>,-		; Enable wrapping of lines > 132 characters in length
		<UNUSED,2,M>,-		; Unused bits
		<CASE,,M>,-		; Upper and lower case enable
	>

$DEF	LP_DBR		.BLKW	1	;DATA BUFFER REGISTER
 
	$DEFEND	LP

LP_CSR$M_ERR=<LP_CSR$M_NEX!LP_CSR$M_CVERF>;*** LP_CSR$M_DAVERR!***
	.PAGE
;
; DEFINE DEVICE DEPENDENT UNIT CONTROL BLOCK OFFSETS
;
 
	$DEFINI	UCB
 
.=UCB$L_DPC+4
$DEF	UCB$W_CSR	.BLKW	1	; Image of line printer CSR
$DEF	UCB$W_BYTXFR	.BLKW	1	; Bytes transfered by controller
$DEF	UCB$W_LINCNT	.BLKW	1	; Number of lines printed 
$DEF	UCB$W_PREFIX	.BLKW	1	; Prefix char and count(indirect reg. 2 )
$DEF	UCB$W_SUFFIX	.BLKW	1	; Suffix char and count(indirect reg. 3 )
$DEF	UCB$W_BUFADR	.BLKW	1	; Lower addr bits of buff addr(indirect reg. 4 )
$DEF	UCB$W_BYTECNT	.BLKW	1	; DMA byte count (indirect reg. 5)
$DEF	UCB$B_HIBUFADR	.BLKB	1	; Upper addr bits of buff addr(indirect reg. 6)
$DEF	UCB$B_CSR2	.BLKB	1	; Control flags for LP attributes
$DEF	UCB$W_LINES	.BLKW	1	; Number of lines per page (indirect reg. 7)
$DEF	UCB$W_LPTIMEOUT	.BLKW	1	; Offline message timeout value
$DEF	UCB$W_LP_OFLCNT	.BLKW	1	; Offline time counter
$DEF	UCB$W_MAXBYT	.BLKW	1	; Number of map registers to alloc.
$DEF	UCB$L_REGSAVE	.BLKL	2	; Save R1 and R2 here
$DEF	UCB$L_MAPREG	.BLKL	1	; Save permanent map reg. info.

UCB$K_SIZE=. 

	$DEFEND	UCB
	.PAGE
	.SBTTL	Driver prologue table and driver dispatch table

;
; LOCAL DATA
;
; DRIVER PROLOGUE TABLE
;
 
	DPTAB	-			;DEFINE DRIVER PROLOGUE TABLE
		END=LP_END,-		; End of driver
		ADAPTER=UBA,-		; Adapter type
		UCBSIZE=UCB$K_SIZE,-	; UCB size
		NAME=LCDRIVER		; Driver name
	DPT_STORE INIT			; Control block init values
	DPT_STORE UCB,UCB$B_FIPL,B,8	; Fork IPL
	DPT_STORE UCB,UCB$L_DEVCHAR,L,-	;Device characteristics
		<DEV$M_REC-		; Record oriented
		!DEV$M_AVL-		; Available
		!DEV$M_CCL-		; Carriage control device
		!DEV$M_ODV-		; Output device
		!DEV$M_ELG-		; Error logging
		>
    	DPT_STORE UCB,UCB$L_DEVCHAR2,L,- ; Device Characteristics
    		<DEV$M_NNM>		 ; prefix with "node$"
	DPT_STORE UCB,UCB$B_DEVCLASS,B,DC$_LP ; Device class
	DPT_STORE UCB,UCB$B_DEVTYPE,B,DT$_DMF32 ; Device type
	DPT_STORE UCB,UCB$W_DEVBUFSIZ,W,132 ; Default buffer size
	DPT_STORE UCB,UCB$L_DEVDEPEND,L,<66@24+LP$M_MECHFORM> ; Printer parameters
	DPT_STORE UCB,UCB$B_DIPL,B,21	; Device IPL
	DPT_STORE REINIT		; Control block re-init values
	DPT_STORE CRB,CRB$L_INTD+4,D,LP$INT ; Interrupt service routine address
	DPT_STORE CRB,CRB$L_INTD+VEC$L_INITIAL,D,LC_CINIT ; Controller init
	DPT_STORE CRB,CRB$L_INTD+VEC$L_UNITINIT,D,LC_INIT ; Unit init
	DPT_STORE DDB,DDB$L_DDT,D,LP$DDT ; DDT address
	DPT_STORE END			;
 
;
; DRIVER DISPATCH TABLE
;
 
	DDTAB	LP,-			;DRIVER DISPATCH TABLE
		STARTIO,-		; Start I/O operation
		0,-			; Unsolicited interrupt
		FUNCTABLE,-		; Function table
		+IOC$CANCELIO,-		; Cancel I/O
		LP_REGDUMP,-		; Register dump routine
		0,-			; Size of diagnostic buffer
		LP_ERRBUF_LEN		; Size of error log buffer
	.PAGE
	.SBTTL	Function decision table
;
; FUNCTION DECISION TABLE
;
 
FUNCTABLE:				;FUNCTION DECISION TABLE
	FUNCTAB	,-			; Legal functions
		<SENSECHAR,-		; Sense characteristics
		 SETCHAR,-		; Set characteristics
		 SENSEMODE,-		; Sense mode
		 SETMODE,-		; Set mode
		 WRITELBLK,-		; Write logical block
		 WRITEPBLK,-		; Write physical block
		 WRITEVBLK>		; Write virtual block
		
	FUNCTAB	,			;NO BUFFERED FUNCTIONS
	FUNCTAB LP_WRITE,<WRITELBLK,-	; Write functions
			  WRITEPBLK,-
			  WRITEVBLK>
	FUNCTAB	+EXE$SETMODE,-
		<SETCHAR,SETMODE>	; Set characteristics functions
	FUNCTAB	+EXE$SENSEMODE,-	;
		<SENSECHAR,-		; Sense characteristics
		 SENSEMODE>		; Sense mode
	.PAGE
	.SBTTL	FDT - Write function processing
;+
; LP_WRITE - WRITE FUNCTION PROCESSING
;
; THIS ROUTINE IS CALLED FROM THE FUNCTION DECISION TABLE DISPATCHER TO PROCESS
; A WRITE PHYSICAL, WRITE LOGICAL, OR WRITE VIRTUAL FUNCTION TO A LINE PRINTER.
;
; INPUTS:
;
;	R0  = SCRATCH.
;	R1  = SCRATCH.
;	R2  = SCRATCH.
;	R3  = ADDRESS OF I/O REQUEST PACKET.
;	R4  = CURRENT PROCESS PCB ADDRESS.
;	R5  = ASSIGNED DEVICE UCB ADDRESS.
;	R6  = ADDRESS OF CCB.
;	R7  = I/O FUNCTION CODE.
;	R8  = FUNCTION DECISION TABLE DISPATCH ADDRESS.
;	R9  = SCRATCH.
;	R10 = SCRATCH.
;	R11 = SCRATCH.
;	AP  = ADDRESS OF FIRST FUNCTION DEPENDENT PARAMETER.
;
; OUTPUTS:
;
;	 FOR PROCESSING BY THE LINE PRINTER DRIVER.
;-

LP_WRITE:
	MOVL	P4(AP),IRP$B_CARCON(R3)		; Insert carriage control byte
	MOVZWL	P2(AP),R1			; Number of bytes to transfer
	BEQL	10$				; If EQL none
	MOVL	P1(AP),R0			; Get starting virtual addr. of transfer
    	BBS	#LP$V_FALLBACK,UCB$L_DEVDEPEND(R5),20$   ;If set, fallback
5$:	JSB	G^EXE$WRITELOCK			; Check buffer and lock in memory
10$:	JMP	G^EXE$QIODRVPKT			; Queue I/O packet to driver

;
; Convert IRP to a buffered i/o packet since fallback is specified.
; Contigent upon the fact that passall is not set (since passall would take
; precedence) and upon the fact that this is not a physical write operation
; which again has precedence.
;
20$:	BBS	#LP$V_PASSALL,UCB$L_DEVDEPEND(R5),5$ ; passall has precedence
    	CMPB	R7,#IO$_WRITEPBLK 		; physical write
    	BEQL	5$				; EQl, yes so don't convert
	DECW	PCB$W_BIOCNT(R4)		; decrement buffered io count
    	BLSS	30$				; if negative, quota exceeded
25$:   	INCW	PCB$W_DIOCNT(R4)		; readjust direct io count
 	BISW    #IRP$M_BUFIO,IRP$W_STS(R3)   	; set irp$m_bufio equal to 1 
    	PUSHR	#^M<R0,R1>			; save buffer address and length
    	JSB	G^EXE$WRITECHK			; do we have access to user data
    	ADDL	#12,R1				; add 12 bytes for buffer header
	PUSHL	R3				; save irp address
    	JSB 	G^EXE$BUFFRQUOTA		; ensure user has sufficient resources
    	BLBC	R0,40$				; if clr, no
    	JSB	G^EXE$ALLOCBUF			; allocate system buffer
    	BLBC	R0,40$				; if clr, no buffer allocated
    	POPL	R3				; restore irp address    	
    	MOVL	R2,IRP$L_SVAPTE(R3)		; save address of buffered i/o packet
    	MOVL	PCB$L_JIB(R4),R0		; get jib address
    	SUBL	R1, JIB$L_BYTCNT(R0)		; adjust buffered i/o quota
    	MOVW	R1,IRP$W_BOFF(R3)		; set number of bytes charged to quota
    	POPR	#^M<R0,R1>			; restore to user buf addr and length
    	MOVAB	12(R2),(R2)			; set up system buffer header
    	MOVL	R0,4(R2)			; insert address of user data
    	MOVAB	12(R2),R2			; point at data area in buffer
    	PUSHR	#^M<R3,R4,R5>			; save registers that movtc destroys
    	MOVTC   R1,(R0),#^X20,@TRANS_TAB,R1,(R2)  ; move char. into system buffer
    	POPR	#^M<R3,R4,R5>			; restore registers
    	BRB	10$				;
    	

30$:	INCW	PCB$W_BIOCNT(R4)		; Backout charge
    	MOVAW	PCB$W_BIOCNT(R4),R2		; store pcb buffer count address
    	PUSHR	#^M<R0,R1,R3>			; save registers before destroyed
    	JSB	G^EXE$SNGLEQUOTA		; check unit quota of io function type
    	BLBC	R0,50$				; Clr, abort io
    	POPR	#^M<R0,R1,R3>			; restore registers  
   	DECW	PCB$W_BIOCNT(R4)		; charge for i/o of type
    	BRB	25$				; all set, continue on

40$:	POPL	R3				; restore irp address
	JMP	G^EXE$ABORTIO			;

50$:	POPR	#^M<R1,R2,R3>			; restore registers
    	JMP	G^EXE$ABORTIO			;
   
	.PAGE 
	.SBTTL	Line printer driver
;+
; STARTIO - START I/O OPERATION ON LINE PRINTERS
;
; THIS ROUTINE IS ENTERED WHEN THE ASSOCIATED UNIT IS IDLE AND A PACKET
; IS AVAILABLE.
;
; INPUTS:
;
;	R3 = ADDRESS OF I/O REQUEST PACKET.
;	R5 = UCB ADDRESS FOR IDLE UNIT.
;
; OUTPUTS:	
;
;	NO EXPLICIT OUTPUTS - THE UNIT IS IN WAITING FOR INTERRPUT STATE
;				OR THE I/O IS COMPLETE.
;-
 
STARTIO:
	CLRW	UCB$W_CSR(R5)		; Clr storage area

;	Retrieve the address of the device CSR

	MOVL	UCB$L_CRB(R5),R4	; Get address of CRB
	MOVL	@CRB$L_INTD+VEC$L_IDB(R4),R4 ; Get device CSR address
 
;	What is the function?

	CMPZV	#IRP$V_FCODE,-		; WRITELBLK function?
		#IRP$S_FCODE,-
		IRP$W_FUNC(R3),-
		#IO$_WRITELBLK
	BNEQ	10$			; If NEQ, some other function
	BRW	WRITE			; It is EQL, it's a write function

10$:	CMPZV	#IRP$V_FCODE,-		; Write physical block function?
		#IRP$S_FCODE,-
		IRP$W_FUNC(R3),-
		#IO$_WRITEPBLK
	BNEQ	15$			;If NEQ no.
	BRW	WRITEP			;Else go do it.

15$:	CMPZV	#IRP$V_FCODE,-		;Set mode function?
		#IRP$S_FCODE,-
		IRP$W_FUNC(R3),-
		#IO$_SETMODE
	BEQL	30$			;If EQL yes.

	.PAGE

;
; THE SET MODE AND SET CHARACTERISTICS INFORMATION IS NOT MOVED
; TO THE BOARDS REGISTERS UNTIL IO IS STARTED.
;

;
; Set characteristics
;

20$:	MOVW	IRP$L_MEDIA(R3),-	;
		UCB$B_DEVCLASS(R5)

;
; Set mode
;

30$:	MOVB	IRP$L_MEDIA+2(R3),-	; Set Page Width - DMF only has a BYTE
		UCB$W_DEVBUFSIZ(R5)

	MOVZWL	#SS$_BADPARAM,R0	; Set BADPARM error status
	TSTB	IRP$L_MEDIA+7(R3)	; Lines per page EQL zero
	BEQL	35$			; Yes, then branch

	MOVL	IRP$L_MEDIA+4(R3),-	; Set device dependent flags
		UCB$L_DEVDEPEND(R5)

	MOVB	IRP$L_MEDIA+7(R3),-	; Set lines per page
		UCB$W_LINES(R5)

	MOVB	IRP$L_MEDIA+2(R3),-	; Set Page Width BYTE for DMF32
		UCB$W_LINES+1(R5)

	MOVB	UCB$L_DEVDEPEND(R5),-	; Set device dependant controller bits
		UCB$B_CSR2(R5)

	MOVZWL	#SS$_NORMAL,R0		; Set normal return status.
35$:	CLRL	R1

	BRW	EXIT
	.PAGE
;
; Is PASSALL set?
;

WRITE:	BBS	#LP$V_PASSALL,-		; If set do physical I/O.
		UCB$L_DEVDEPEND(R5),-
		WRITEP
	BRB	DECODE			; Else decode carriage control info

	.ENABL	LSB
;
; Perform write physical block
;

WRITEP:	
	MOVW	#C_INDREG4,-		; Load the controller CSR
		LP_CSR(R4)		;...and set indirect register address
	MOVAW	UCB$W_BUFADR(R5),R1	; Address of indirect reg. data 
	MOVL	#C_REGCNT-2,R2		; Number of indirect registers to copy
	BRB	COMMON			; Ok, do what everyone else would

;
; Decode carriage control info.
;
DECODE:
	JSB	G^EXE$CARRIAGE		; Returns carr. control in IRP$B_CARCON
	ASSUME	UCB$W_PREFIX+2 EQ UCB$W_SUFFIX
	MOVL	IRP$B_CARCON(R3),-	; Set prefix char and cnt in cont. image
		UCB$W_PREFIX(R5)
	MOVW	#M_STDCSR,LP_CSR(R4)	; Setup for typical transfer
	MOVL	#C_REGCNT,R2		; Number of reg. to init. in controller
	MOVAW	UCB$W_PREFIX(R5),R1	; Get address of reg. images
	.PAGE
COMMON:

	TSTW	UCB$W_BCNT(R5)		; Is this a zero length transfer?
	BNEQ	10$			; If NEQ, no
	CLRB	UCB$B_HIBUFADR(R5)	; High bits of buffer address
	CLRW	UCB$W_BYTECNT(R5)	; Byte count
	BRB	30$
;
; Allocate UBA map registers
;

10$:

	MOVQ	R1,UCB$L_REGSAVE(R5)	; Save registers R1 and R2
	CMPW	UCB$W_MAXBYT(R5),-	; If bcnt is > max bytes then get more map registers
		UCB$W_BCNT(R5)
	BGEQ	20$			;  ...else load UBA map registers
	MOVL	UCB$L_CRB(R5),R2	; Get the CRB address
	MOVL	CRB$L_INTD+VEC$W_MAPREG(R2),- ; Save pre alloc. map register info
		UCB$L_MAPREG(R5)

	BICW	#VEC$M_MAPLOCK,-	; Clear map register lock bit
		CRB$L_INTD+VEC$W_MAPREG(R2)

	REQMPR				; Request UNIBUS map registers
20$:
;
; If buffered flag set, irp is buffered io which first must be
; converted to a direct io before proceeding further.  
;
    	BBC	#IRP$V_BUFIO,IRP$W_STS(R3),25$	; IF Set, buffered io
    	MOVL	IRP$L_SVAPTE(R3), R0		; Get system buffer address
    	EXTZV 	#VA$V_VPN,#VA$S_VPN,(R0),R2	; Extract vpn from system buffer address
    	MOVL	G^MMG$GL_SPTBASE,R1		; Get address of base of system page table
    	MOVAL   (R1)[R2],UCB$L_SVAPTE(R5)	; Store address of pte in ucb field
    	EXTZV	#VA$V_BYTE,#VA$S_BYTE,(R0),R2   ; Byte offset in system page
       	MOVW	R2, UCB$W_BOFF(R5)		; Store this offset in ucb field
25$:
    	LOADUBAA				; load uba map registers
;
; Load byte count and calculate UNIBUS transfer address
;

	MNEGW	UCB$W_BCNT(R5),-	; Setup byte count
		UCB$W_BYTECNT(R5)
	MOVZWL	UCB$W_BOFF(R5),R1	; Fetch byte offset
	MOVL	UCB$L_CRB(R5),R2	; Get CRB address
	INSV 	CRB$L_INTD+VEC$W_MAPREG(R2),-
		#9,#9,R1
	EXTZV	#16,#2,R1,R2		;Get memory extension bits
	MOVW	R1,UCB$W_BUFADR(R5)	;Setup 16 bit buffer address
	MOVB	R2,UCB$B_HIBUFADR(R5)	; Set upper address bits 
	MOVQ	UCB$L_REGSAVE(R5),R1	;Restore registers R1 and R2

;
; Load controller and start transfer
;

30$:	MOVW	(R1)+,LP_DBR(R4)	; Copy indirect reg. to controller
	SOBGTR	R2,30$			; Branch if more to do.

40$:	DSBINT				; Raise IPL
	BBS	#UCB$V_POWER,-		; If set, power has failed.
		UCB$W_STS(R5),41$
	BISW	#LP_CSR$M_PRIENA!LP_CSR$M_IE,-
		LP_CSR(R4)		; Start xfer
	WFIKPCH	140$,#12		; Wait for xfer to complete
	IOFORK				; Fork to lower IPL
	.PAGE
;
; Save status and check for errors
;

	MOVW	LP_CSR(R4),UCB$W_CSR(R5); Save controller state
	BBS	#LP_CSR$V_LPERR,-	; If set printer problem
		UCB$W_CSR(R5),140$
41$:	CLRW	LP_CSR(R4)		; Set indirect register addr. to zero
	MOVW	LP_DBR(R4),UCB$W_BYTXFR(R5) ; Save the number of bytes transfered
	MOVW	LP_DBR(R4),UCB$W_LINCNT(R5) ; Save the number of lines printed
	PURDPR				; Purge the data paths
	MOVL	UCB$L_CRB(R5),R2	; Get the address of the CRB
	BBS	#VEC$V_MAPLOCK,-	; If SET do not release map registers
		CRB$L_INTD+VEC$W_MAPREG(R2),-
		42$
	RELMPR				; Release map registers
	MOVL	UCB$L_CRB(R5),R2	; Restore the address of the CRB
	MOVL	UCB$L_MAPREG(R5),-	; Restore pre-allocated map information
		CRB$L_INTD+VEC$W_MAPREG(R2)
42$:
	BBSC	#UCB$V_POWER,-		; If set, power has failed.
		UCB$W_STS(R5),90$

	BITW	#LP_CSR$M_ERR,UCB$W_CSR(R5) ; Any errors?
	BEQL	44$			; If EQL, no errors.
	JSB	G^ERL$DEVICERR		; Report the error
	BRW	46$

44$:	BITW	#UCB$M_CANCEL,UCB$W_STS(R5) ; Is the cancel bit set?
	BEQL	48$

46$:	MOVZWL	#SS$_ABORT,R0		; Set operation aborted status
	BRW	50$
	

;
; I/O OPERATION SUCCESSFULLY COMPLETED
;

48$:	MOVZWL	#SS$_NORMAL,R0		; Set normal completion status
50$:
	MOVL	UCB$L_IRP(R5),R3	; Retrieve address of i/o packet
	MOVZWL	UCB$W_LINCNT(R5),R1	; Get number of lines printed
	INSV	UCB$W_BYTXFR(R5),-
		#16,#16,R0		; Insert number of characters in status
EXIT:
60$:	REQCOM				; COMPLETE I/O REQUEST

90$:	ENBINT
	BRW	STARTIO
	.PAGE
;
; PRINTER IS NOT READY OR HAS PAPER PROBLEM
;
 
140$:	CLRW	UCB$W_LP_OFLCNT(R5)	; Clear offline counter
	BICW	#LP_CSR$M_IE,LP_CSR(R4)	; Disable printer interrupt
150$:	SETIPL	UCB$B_FIPL(R5)		; Lower to fork level
	BITW	#LP_CSR$M_LPERR,LP_CSR(R4) ; Printer still have paper problem?
	BNEQ	155$			; If NEQ, yes it does
	MOVW	#C_TIM,UCB$W_LPTIMEOUT(R5) ; Reset timeout value
	BRW	40$			; And continue transfer.

155$:	BITW	#UCB$M_CANCEL,UCB$W_STS(R5) ; Is the cancel bit set?
	BEQL	156$			; If EQL, no cancel
	BRW	41$

156$:	ACBW	UCB$W_LPTIMEOUT(R5),#1,-
		UCB$W_LP_OFLCNT(R5),160$; Skip until timeout

	CLRW	UCB$W_LP_OFLCNT(R5)	; Reset counter

	CMPW	#LP_HRCNT,-		; Are we at our max. (1 HR.)
		UCB$W_LPTIMEOUT(R5)

	BLEQU	157$			; If LEQ, yes
	MULW2	#2,UCB$W_LPTIMEOUT(R5)	; Double message timeout value
157$:	PUSHR	#^M<R3,R4>		; Save registers
	MOVZBL	#MSG$_DEVOFFLIN,R4	; Set up message type
	MOVAB	G^SYS$GL_OPRMBX,R3	; Address target mailbox
	JSB	G^EXE$SNDEVMSG		; Send message ignore error
	POPR	#^M<R3,R4>		; Restore registers
160$:	DSBINT	UCB$B_DIPL(R5)		; Disable interrupts
	WFIKPCH	150$,#2			; Wait for an interrupt or timeout
	IOFORK				; Create for process
	BRB	150$			;
	.DSABL	LSB
 
	.PAGE
	.SBTTL	Line printer interrupt dispatcher
;+
; LP$INT - LINE PRINTER INTERRUPT DISPATCHER.
;
; THIS ROUTINE IS ENTERED VIA A JSB INSTRUCTION WHEN AN INTERRUPT OCCURS ON
; THE LINE PRINTER CONTROLLER. THE STATE OF THE STACK ON ENTRY IS:
;
;	00(SP) = ADDRESS OF IDB ADDRESS.
;	04(SP) = SAVED R3.
;	08(SP) = SAVED R4.
;	12(SP) = SAVED R5.
;	16(SP) = INTERRUPT PC.
;	20(SP) = INTERRUPT PSL.
;
; INTERRUPT DISPATCHING OCCURS AS FOLLOWS:
;
;	IF THE INTERRUPT IS EXPECTED, THEN THE DRIVER IS CALLED AT ITS INTERRUPT
;	WAIT ADDRESS. ELSE THE INTERRUPT IS DISMISSED.
;-
 
LP$INT::				; Entry from dispatch
	MOVL	@(SP)+,R3		; Get address of IDB
	MOVQ	IDB$L_CSR(R3),R4	; Get controller CSR and owner UCB addr
	BBCC	#UCB$V_INT,UCB$W_STS(R5),10$ ;If clr, interrupt not expected
	BICW	#LP_CSR$M_IE,LP_CSR(R4)	; Disable output interrupts
	MOVL	UCB$L_FR3(R5),R3	; Restore remainder of driver context
	JSB	@UCB$L_FPC(R5)		; Call driver at interrupt wait address
10$:	MOVQ	(SP)+,R0	 	; Restore registers
	MOVQ	(SP)+,R2		;
	MOVQ	(SP)+,R4		;
	REI				;
	.PAGE
	.SBTTL	Line printer unit initialization
;+
; LC_INIT - LINE PRINTER UNIT INITIALIZATION
;
; THIS ROUTINE IS CALLED AT SYSTEM STARTUP AND AFTER A POWER FAILURE. THE
; ONLINE BIT IS SET FOR THE SPECIFIED UNIT.
;
; INPUTS:
;
;	R5 = ADDRESS OF DEVICE UCB.
;
; OUTPUTS:
;
;	THE ONLINE BIT IS SET IN THE DEVICE UCB AND THE ADDRESS OF THE UCB
;	IS FILLED INTO THE IDB OWNER FIELD.
;-
 
LC_INIT:				; LINE PRINTER UNIT INITIALIZATION

	BISW	#UCB$M_ONLINE,-		; Set unit online
		UCB$W_STS(R5)

	BBS	#UCB$V_POWER,-		; If set power has failed
		UCB$W_STS(R5),10$

	MOVL	#LC_MAPREG,R3		; Number of mapping reg. to pre-alloc
	JSB	G^IOC$ALOUBAMAPN	; Request map registers
	BLBC	R0,10$			; If LBC, request failed
	SUBW3	#2,R3,UCB$W_MAXBYT(R5)	; One less reg. for buf. near page boundry
	MULW2	#PAGSIZ,UCB$W_MAXBYT(R5); Save max byte count
	MOVL	UCB$L_CRB(R5),R0	; Get address of CRB
	BISW	#VEC$M_MAPLOCK,-	; Set maplock bit
		CRB$L_INTD+VEC$W_MAPREG(R0)
	BRW 20$

10$:	MOVL	UCB$L_CRB(R5),R0	; Get address of CRB
	CLRW	UCB$W_MAXBYT(R5)	; Clear max byte cnt. since no map alloc

20$:	MOVB	UCB$W_DEVBUFSIZ(R5),-	    ; Set default buffer size
		UCB$W_LINES+1(R5)

	BISB	#LP$M_LOWER,UCB$B_CSR2(R5)  ; Set device dependant bits
	MOVB	#LINPAG,UCB$W_LINES(R5)	    ; Set lines per page
	MOVL	CRB$L_INTD+VEC$L_IDB(R0),R0 ; Get address of IDB
	MOVL	R5,IDB$L_OWNER(R0)	    ; Set address of device UCB
	RSB
	.PAGE
	.SBTTL	Line printer unit initialization
;+
; LC_CINIT - LINE PRINTER CONTROLLER INITIALIZATION
;
; THIS ROUTINE IS CALLED AT SYSTEM STARTUP AND AFTER A POWER FAILURE. THE
; ONLINE BIT IS SET FOR THE SPECIFIED UNIT.
;
; INPUTS:
;
;	R4 = ADDRESS OF DEVICE CSR.
;	R5 = ADDRESS OF DEVICE UCB.
;
; OUTPUTS:
;
;	THE LINE PRINTER OFFLINE MESSAGE COUNT IS INITIALIZED AFTER A
;	RESET HAS BEEN DONE ON THE LP CONTROLLER AND THE INTERRUPT
;	VECTOR OF THE LP IS LOADED INTO THE CONTROLLER.
;-

LC_CINIT:
	MOVW	#RESET,LP_CSR(R4)		; Master reset
	MOVL	IDB$L_UCBLST(R5),R0		; Get UCB address
	MOVW	#C_TIM,UCB$W_LPTIMEOUT(R0) 	; Initialize LP offline message time
	CVTBL	IDB$B_COMBO_CSR_OFFSET(R5),R0	; GET OFFSET TO MAIN DMF CSR
    	SUBB3	IDB$B_COMBO_VECTOR_OFFSET(R5),-
    		IDB$B_VECTOR(R5),(R4)[R0]	; LOAD THE VECTOR ADDRESS
    	MOVAL	FALLTAB,TRANS_TAB		; Get address of fallback tables
	RSB					;
	.PAGE
	.SBTTL  Register dump routine
;++
; LP_REGDUMP - REGISTER DUMP ROUTINE
;
; FUNCTIONAL DESCRIPTION:
;
; THIS ROUTINE IS CALLED TO SAVE THE DEVICE REGISTERS IN A SPECIFIED 
; BUFFER. IT IS CALLED FROM THE DEVICE ERROR LOGGING ROUTINE.
;
; INPUTS:
;	R0 = ADDRESS OF REGISTER SAVE BUFFER
;	R4 = ADDRESS OF DEVICE CONTROL STATUS REGISTER
;	R5= ADDRESS OF UNIT CONTROL BLOCK
; OUTPUTS:
;	DEVICE REGISTERS ARE MOVED INTO THE SPECIFIED BUFFER.
;	R0 CONTAINS THE ADDRESS OF THE NEXT EMPTY LONGWORD IN THE BUFFER.
;	ALL REGISTERS EXCEPT R1 AND R2 ARE PRESERVED.
;
;--
LP_REGDUMP:
	MOVL	#LP_NUM_REGS,R2		; Store number of registers
	MOVL	R2,(R0)+		; Insert # of longwords dumped
	MOVAW	UCB$W_CSR(R5),R1	; Get address of first reg.
10$:	MOVZWL	(R1)+,(R0)+		; Dump registers into buffer
	SOBGTR	R2,10$			; If GTR - more to go
	RSB				; Return

    .PAGE
    .SBTTL    FALLBACK - Macros to create character table for fallback

;
;	Pointer to the fallback tables
;
TRANS_TAB:
    .LONG  FALLTAB


;++
;FALLBACK - TABLE TO ALLOW THE TERMINAL TO DO FALLBACK PRESENTATION OF
;           8BIT CHARACTERS on 7 bit terminals
;
; Description:
;	The following macros generate 1 table.  The table is a 256 byte
; table with the single character fallback representation of all the
; characters that can be represented by a single character, those with
; no fallback presentation at all are represented by the _ character,
;
;--
	.macro	$fallini
$$=0
.repeat	256
.IF LE $$-<^X9F>	; EVERYTHING BUT THE MULTINATIONAL SET SHOULD
			; ECHO AS ITSELF.
	.byte $$
.IFF
	.BYTE ^A/_/
.ENDC
$$=$$+1
.endr
$$$=.
	.endm 	$fallini

;++
; $FALL - generates the table entry for a given character
;
; Inputs:
;
;	CHARH - COLUMN IN THE ASCII TABLE.
;	CHARL - ROW IN THE ASCII TABLE.
;	FALLBACK - String that is the fallback representation
;	COUNT - Number of times to repeat this character
;--
	.MACRO	$FALL	CHARH,CHARL,FALLBACK,COUNT=1
.=FALLTAB+<CHARH*16>+CHARL
.REPEAT	COUNT
.NCHR	SLEN,^\FALLBACK\
.IF EQ	SLEN-1
	.BYTE	^A/FALLBACK/
.ENDR
	.ENDM	$FALL

;++
; $FALLEND - GENERATES END CONDITIONS FOR THE FALLBACK TABLE
;
; Description:
;
;	Resets the . to the end of the fallback table
;
; Inputs:
;
;	None
;--
	.MACRO	$FALLEND
.=$$$
	.ENDM	$FALLEND


FALLTAB::
	$FALLINI
	$FALL	10,1,!
	$FALL	10,2,c
	$FALL	10,3,L
	$FALL	10,5,Y
	$FALL	10,8,O
	$FALL	10,10,a
	$FALL	11,0,o
	$FALL	11,1,+
	$FALL	11,2,2
	$FALL	11,3,3
	$FALL	11,5,u
	$FALL	11,7,.
	$FALL	11,9,1
	$FALL	11,10,o
	$FALL	11,15,?
	$FALL	12,0,A,6
	$FALL	12,7,C
	$FALL	12,8,E,4
	$FALL	12,12,I,4
	$FALL	13,1,N
	$FALL	13,2,O,5
	$FALL	13,8,O
	$FALL	13,9,U,4
	$FALL	13,13,Y
	$FALL	14,0,a,6
	$FALL	14,7,c
	$FALL	14,8,e,4
	$FALL	14,12,i,4
	$FALL	15,1,n
	$FALL	15,2,o,5
	$FALL	15,8,o
	$FALL	15,9,u,4
	$FALL	15,13,y
	$FALLEND


LP_END:					; ADDRESS OF LAST LOCATION IN DRIVER
 
	.END
