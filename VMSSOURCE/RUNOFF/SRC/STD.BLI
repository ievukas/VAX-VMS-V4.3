%TITLE 'Processes .STANDARD command'
MODULE STD (IDENT = 'V04-000'
    	    %BLISS32[,ADDRESSING_MODE (	EXTERNAL	= LONG_RELATIVE,
    					NONEXTERNAL	= LONG_RELATIVE)]
		) =
BEGIN
!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982, 1984 BY				    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************
!
!++
! FACILITY:	 DSR (Digital Standard RUNOFF) / DSRPLUS
!
! ABSTRACT: Processes the .STANDARD command
!
! ENVIRONMENT: Transportable
!
! AUTHOR: R.W.Friday	 CREATION DATE: August, 1978
!

%SBTTL 'Revision History'
!
! MODIFIED BY:
!
!	005	KFA00005	Ken Alden	5-Jul-1983
!		Fixed sca_justify so it works correctly with gca_autojust.    
!
!	004	KFA00004	Ken Alden	28-Mar-1983
!		In SDXY the right margin is no longer reset(to gca_lwidth).
!
!	003	REM00003	Ray Marshall	07-Mar-1983
!		Global edit of all modules. Updated module names, idents,
!		copyright dates. Changed require files to BLISS library.
!
!--

%SBTTL 'Module Level Declarations'
!
! TABLE OF CONTENTS:
!

FORWARD ROUTINE
    SDXY : NOVALUE;

!
! INCLUDE FILES:
!

LIBRARY 'NXPORT:XPORT';			! XPORT Library
REQUIRE 'REQ:RNODEF';			! RUNOFF variant definitions

%IF DSRPLUS %THEN
LIBRARY 'REQ:DPLLIB';			! DSRPLUS BLISS Library
%ELSE
LIBRARY 'REQ:DSRLIB';			! DSR BLISS Library
%FI

!
! MACROS:
!
!
! EQUATED SYMBOLS:
!
!
! OWN STORAGE:
!
!
! EXTERNAL REFERENCES:
!

EXTERNAL
    GCA : GCA_DEFINITION,
    IRA : FIXED_STRING,
    NUMPRM : NUMPRM_DEFINE,
    PDT : REF PDT_DEFINITION,
    PHAN : PHAN_DEFINITION,
    SCA : SCA_DEFINITION;

EXTERNAL LITERAL 				!Error messages
    RNFINM;

EXTERNAL ROUTINE
    ERMA,
    REMNEG,
    REMMRG,
    SETCAS,
    TSTBLK;


%SBTTL 'STD  --'
GLOBAL ROUTINE std (handler_code) : NOVALUE =

!++
! FUNCTIONAL DESCRIPTION:
!
!	See the ABSTRACT, above.
!
! FORMAL PARAMETERS:
!
!	HANDLER_CODE is passed for conformance only.
!
! IMPLICIT INPUTS:
!
!	NUMPRM contains a number as processed by GETNUM.
!
! IMPLICIT OUTPUTS:	None
!
! ROUTINE VALUE:
! COMPLETION CODES:	None
!
! SIDE EFFECTS:		None
!
!--

    BEGIN

    tstblk (0);					!Force .LISTs and .NOTEs to end, and
    !issue error messages if applicable.
    remmrg ();					!Report unreported bad margins.
    remneg ();					!Report unreported bad backdents.

    IF	NOT .num_result  THEN		! If no argument given by user,
	num_value = .gca_lwidth		!  use current page width.

    ELSE				! Otherwise:
	BEGIN
	IF .num_sign NEQ 0  THEN	!   If a signed number, Try adjusting
	    num_value = .num_value + .gca_lwidth  !	current page width.
	ELSE
	    IF .num_length EQL 0 THEN	!   Otherwise, if user said nothing,
		num_value = .gca_lwidth; !     ! use current page width
	IF .num_value LEQ 0  THEN	!   If negative number,
	    BEGIN
	    erma (rnfinm, false);	!	report error and
	    num_value = .gca_lwidth;	!	retain current line width.
	    END;
	END;

    gca_lwidth = .num_value;

    !Set lines per page based on page width.

    IF	.gca_lwidth EQL 60  THEN
	phan_llines = 58
    ELSE

	IF  .gca_lwidth EQL 70
	THEN
	    phan_llines = 74;

    !Check for top of page condition so that footnotes
    !get taken care of ok.

    IF	.phan_lines_tp GEQ .phan_llines  THEN
	phan_top_page = true;

    pdt_indent = 0;
    sdxy ();					!Finish resetting.
    END;					!End of STD


%SBTTL 'SDXY  --'
GLOBAL ROUTINE sdxy : NOVALUE =

!++
! FUNCTIONAL DESCRIPTION:
!
!	Part of .STANDARD command common to .CHAPTER and .APPENDIX
!
! FORMAL PARAMETERS:	None
!
! IMPLICIT INPUTS:	None
!
! IMPLICIT OUTPUTS:	None
!
! ROUTINE VALUE:
! COMPLETION CODES:	None
!
! SIDE EFFECTS:		None
!
!--

    BEGIN
    phan_paging = .phan_cmd_paging;		!Turn on paging if not /NOPAGING
    sca_fill = true;
    sca_justify = .gca_autojust or .sca_justify;!Turn on justification if the user didn't
    sca_crock = .gca_autojust;			!say .NO AUTOJUSTIFY.
    sca_spacing = 1;
    sca_lm = 0;
    
    setcas (.gca_case);				! Set case rules.

    END;					!End of SDXY

END						!End of module
ELUDOM
