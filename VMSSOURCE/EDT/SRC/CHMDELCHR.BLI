%TITLE 'EDT$CHMDELCHR - delete characters'
MODULE EDT$CHMDELCHR (				! Delete characters
		IDENT = 'V04-000'			! File: CHMDELCHR.BLI Edit: JBS1006
		) =
BEGIN
!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982, 1984 BY				    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************
!

!++
! FACILITY:	EDT -- The DEC Standard Editor
!
! ABSTRACT:
!
!	This module deletes text on the current line (change mode).
!
! ENVIRONMENT:	Runs at any access mode - AST reentrant
!
! AUTHOR: Bob Kushlis, CREATION DATE: Unknown
!
! MODIFIED BY:
!
! 1-001	- Original.  DJS 04-Feb-1981.  This module was created by
!	extracting the routine DELETE_CHARS from module CHANGE.BLI.
! 1-002	- Regularize headers.  JBS 27-Feb-1981
! 1-003	- Fix module name.  JBS 03-Mar-1981
! 1-004	- Improve some comments.  JBS 02-Jun-1981
! 1-005	- New screen update logic.  JBS 13-Sep-1982
! 1-006	- Change the call to EDT$$MRK_LCNHG.  JBS 27-Dec-1982
!--


%SBTTL 'Declarations'
!
! TABLE OF CONTENTS:
!

REQUIRE 'EDTSRC:TRAROUNAM';

FORWARD ROUTINE
    EDT$$DEL_CHS : NOVALUE;			! Delete part of the current line

!
! INCLUDE FILES:
!

REQUIRE 'EDTSRC:EDTREQ';

!
! MACROS:
!
!	NONE
!
! EQUATED SYMBOLS:
!
!	NONE
!
! OWN STORAGE:
!
!	NONE
!
! EXTERNAL REFERENCES:
!
!	In the routine

%SBTTL 'EDT$$DEL_CHS  - delete characters'

GLOBAL ROUTINE EDT$$DEL_CHS (			! Delete characters
    FC, 					! Position of first character to delete
    LC						! Position of last character to delete
    ) : NOVALUE =

!++
! FUNCTIONAL DESCRIPTION:
!
!	This routine deletes text on the current line between the
!	two pointers FC and LC.
!
! FORMAL PARAMETERS:
!
!  FC			Position of the first character to delete
!
!  LC			Position of the last character to delete
!
! IMPLICIT INPUTS:
!
!	EDT$$A_SEL_POS
!	EDT$$A_LN_END
!
! IMPLICIT OUTPUTS:
!
!	EDT$$A_SEL_POS
!	EDT$$A_LN_PTR
!
! ROUTINE VALUE:
!
!	NONE
!
! SIDE EFFECTS:
!
!	NONE
!
!--

    BEGIN

    EXTERNAL ROUTINE
	EDT$$MRK_LNCHG : NOVALUE,		! Track line updates
	EDT$$UPD_LNLEN : NOVALUE,		! Update the length of the current line
	EDT$$SAV_LN : NOVALUE,			! Save a line in the auxiliary buffer
	EDT$$SEL_RNGPOS;			! Compare the select line with the current line

    EXTERNAL
	EDT$$A_SEL_POS,				! select position.
	EDT$$A_LN_END,				! End of current line pointer
	EDT$$A_LN_PTR,				! Current character pointer
	EDT$$T_LN_BUF;				! Line buffer

    LOCAL
	LEN;

!+
! Compute length of the delete, and quit if zero.
!-
    LEN = CH$DIFF (.LC, .FC);
    EDT$$SAV_LN (.FC, .LEN);

    IF (.LEN EQL 0) THEN RETURN;

!+
! Watch out for select range, adjust as necessary.
!-

    IF (EDT$$SEL_RNGPOS () EQL 0)
    THEN
!+
! This is the selected line
!-

	IF CH$PTR_GTR (.EDT$$A_SEL_POS, .FC)
	THEN

	    IF CH$PTR_GTR (.EDT$$A_SEL_POS, .LC)
	    THEN
		EDT$$A_SEL_POS = CH$PLUS (.EDT$$A_SEL_POS, -.LEN)
	    ELSE
		EDT$$A_SEL_POS = .FC;

!+
! Now, adjust the line.
!-
    EDT$$CPY_MEM (CH$DIFF (.EDT$$A_LN_END, .LC), .LC, .FC);
    EDT$$UPD_LNLEN (-.LEN);
    EDT$$MRK_LNCHG (SCR_EDIT_MODIFY, .FC - EDT$$T_LN_BUF);
    EDT$$A_LN_PTR = .FC;
    END;					! of routine EDT$$DEL_CHS

!<BLF/PAGE>

END						! of module EDT$CHMDELCHR

ELUDOM
