%TITLE 'EDT$WFCLEAR - empty the current buffer'
MODULE EDT$WFCLEAR (				! Empty the current buffer
		IDENT = 'V04-000'			! File: WFCLEAR.BLI Edit: JBS1003
		) =
BEGIN
!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982, 1984 BY				    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************
!

!++
! FACILITY:	EDT -- The DEC Standard Editor
!
! ABSTRACT:
!
!	Empty the current buffer.
!
! ENVIRONMENT:	Runs at any access mode - AST reentrant
!
! AUTHOR: Bob Kushlis, CREATION DATE: October 16, 1978
!
! MODIFIED BY:
!
! 1-001	- Original.  DJS 23-Feb-1981.  This module was created by
!	extracting routine EDT$$WF_CLRBUF  from module EDTWF.
! 1-002	- Regularize headers.  JBS 16-Mar-1981
! 1-003	- Improve the appearance of the listing.  JBS 20-Jun-1983
!--


%SBTTL 'Declarations'
!
! TABLE OF CONTENTS:
!

REQUIRE 'EDTSRC:TRAROUNAM';

FORWARD ROUTINE
    EDT$$WF_CLRBUF : NOVALUE;

!
! INCLUDE FILES:
!

REQUIRE 'EDTSRC:EDTREQ';

!
! MACROS:
!
!	NONE
!
! EQUATED SYMBOLS:
!
!	NONE
!
! OWN STORAGE:
!
!	NONE
!
! EXTERNAL REFERENCES:
!
!	In the routine

%SBTTL 'EDT$$WF_CLRBUF  - empty the current buffer'

GLOBAL ROUTINE EDT$$WF_CLRBUF 			! Empty the current buffer
    : NOVALUE =

!++
! FUNCTIONAL DESCRIPTION:
!
!	Clear the entire current buffer.  The first bucket of the buffer is
!	updated to be empty, and, if there is more than one, the rest of the
!	bucket is placed on the available bucket list.  Note that since the
!	buckets are already linked together, we need only link the last bucket
!	in the buffer to the current available bucket, then make avail the
!	first bucket we are releasing.
!
! FORMAL PARAMETERS:
!
!	NONE
!
! IMPLICIT INPUTS:
!
!	EDT$$A_CUR_BUF
!	EDT$$G_WK_AVAIL
!	EDT$$A_WK_BUK
!	EDT$$G_WK_CURBUK
!	EDT$$L_LNO_ZERO
!
! IMPLICIT OUTPUTS:
!
!	EDT$$G_WK_AVAIL
!	EDT$$A_WK_BUK
!	EDT$$A_CUR_BUF
!	EDT$$G_WK_MODFD
!
! ROUTINE VALUE:
!
!	NONE
!
! SIDE EFFECTS:
!
!	NONE
!
!--

    BEGIN

    EXTERNAL ROUTINE
	EDT$$WF_BOT : NOVALUE,
	EDT$$TOP_BUF : NOVALUE;

    EXTERNAL
	EDT$$A_CUR_BUF : REF TBCB_BLOCK,	! Current text buffer control block
	EDT$$G_WK_AVAIL,			! Pointer to next available deleted bucket
	EDT$$A_WK_BUK : 			! Pointer to current bucket
	    REF BLOCK [WF_BUKT_SIZE, BYTE] FIELD (WFB_FIELDS),
	EDT$$G_WK_CURBUK,			! Number of the current bucket
	EDT$$G_WK_MODFD,			! Flag indicating bucket was modified
	EDT$$L_LNO_ZERO : LN_BLOCK;

    EDT$$TOP_BUF ();
!+
! Release remaining buckets if there are more than one.
!-

    IF (.EDT$$A_WK_BUK [WFB_NEXT_BUKT] NEQ 0)
    THEN
	BEGIN
	EDT$$WF_BOT ();
	EDT$$A_WK_BUK [WFB_NEXT_BUKT] = .EDT$$G_WK_AVAIL;
	EDT$$G_WK_MODFD = 1;
	EDT$$TOP_BUF ();
	EDT$$G_WK_AVAIL = .EDT$$A_WK_BUK [WFB_NEXT_BUKT];
	END;

    EDT$$A_CUR_BUF [TBCB_LINE_ADDR] = WFB_FIXED_SIZE;
    EDT$$A_WK_BUK [WFB_END] = WFB_FIXED_SIZE;
    EDT$$A_WK_BUK [WFB_NEXT_BUKT] = 0;
    EDT$$G_WK_MODFD = 1;
    MOVELINE (EDT$$L_LNO_ZERO, EDT$$A_CUR_BUF [TBCB_LINE_COUNT]);
    EDT$$A_CUR_BUF [TBCB_CHAR_COUNT] = 0;
    EDT$$A_CUR_BUF [TBCB_LAST_BUKT] = .EDT$$G_WK_CURBUK;
    END;					! of routine EDT$$WF_CLRBUF

!<BLF/PAGE>

END						! of module EDT$WFCLEAR

ELUDOM
