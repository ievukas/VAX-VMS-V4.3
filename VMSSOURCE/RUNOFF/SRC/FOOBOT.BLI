MODULE foobot (	IDENT = 'V04-000'
		%BLISS32[, ADDRESSING_MODE (EXTERNAL    = LONG_RELATIVE,
					    NONEXTERNAL = LONG_RELATIVE)]
		) =
BEGIN
!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982, 1984 BY				    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************
!

!++
! FACILITY:	 DSR (Digital Standard RUNOFF) / DSRPLUS
!
! ABSTRACT:
!
!	Puts as many footnotes as possible at the bottom of the current page.
!
! ENVIRONMENT:	Transportable
!
! AUTHOR:	R.W.Friday	 CREATION DATE: November, 1981


%SBTTL 'Revision History'
! MODIFIED BY:
!
!	004	RER00004	Ron Randall	05-Apr-1983
!		For DSRPLUS:  Added code for footnote numbering.
!
!	003	RER00003	Ron Randall	07-Mar-1983
!		Global edit of all modules. Updated module names, idents,
!		copyright dates. Changed require files to BLISS library.
!--


%SBTTL 'Module Level Declarations'
!
! TABLE OF CONTENTS:
!
FORWARD ROUTINE
    FOOBOT : NOVALUE;
!
! INCLUDE FILES:
!
LIBRARY 'NXPORT:XPORT';			! XPORT Library
REQUIRE 'REQ:RNODEF';			! RUNOFF variant definitions

%IF DSRPLUS %THEN
LIBRARY 'REQ:DPLLIB';			! DSRPLUS BLISS Library
%ELSE
LIBRARY 'REQ:DSRLIB';			! DSR BLISS Library
%FI

!
! MACROS:
!
! This macro makes certain that when blank lines at the top or bottom 
! of a page are skipped change bars don't get output.
!
MACRO
    NO_BAR_SKIP (N) =
	BEGIN
	LOCAL
	    HOLD_BARS;

	HOLD_BARS = .TSF_BARS;
	TSF_BARS  = FALSE;
	USKIPL (N);
	TSF_BARS  = .HOLD_BARS;
	END %;
!
! EXTERNAL REFERENCES:
!
EXTERNAL
    FNCT	: FNCT_DEFINITION,		! Footnote control table.
    FNESIZ	: FN_EXT_SIZE_DEFINITION,
    HCT		: HCT_DEFINITION,
    PHAN	: PHAN_DEFINITION,
    TSF		: TSF_DEFINITION;

EXTERNAL LITERAL
    RNFFWF;

EXTERNAL ROUTINE
    ERML,
    FOOOUT,
    TPFFIT,
    TPFSKP,
    USKIPL;


GLOBAL ROUTINE foobot : NOVALUE =

!++
! FUNCTIONAL DESCRIPTION:
!
!	See ABSTRACT, above.
!
! FORMAL PARAMETERS:	None
!
! IMPLICIT INPUTS:	None
!
! IMPLICIT OUTPUTS:	None
!
! ROUTINE VALUE:
! COMPLETION CODES:	None
!
! SIDE EFFECTS:		None
!--

    BEGIN

	!
	! See if there are any footnotes to be output.
	!
	IF  (.FNCT_READY NEQ 0)
	THEN

	    !
	    ! See if these footnotes should be output now.
	    ! Check for two cases: 
	    ! one in which there's a footnote that's too big to fit on the page,
	    ! and the other in which there's no such error.
	    !
	    ! Skip over old footnotes, consider the first ready footnote.
	    !
	    IF 	.FNESIZ [.FNCT_OLD] GTR 
		(.PHAN_LLINES + .HCT_LAYOUTN -
		(IF	.HCT_HEADERS
		 THEN			! 3 is title line + 2 blank lines.
			3 + (IF .HCT_SUBTITLE THEN 1 ELSE 0)
		 ELSE
			0)

%IF DSRPLUS %THEN
		- (IF .FNCT_NUMBERING THEN 1 ELSE 0)
%FI

		)
	    THEN
		!
		! The first ready footnote is so lengthy that it requires 
		! more space than allowed per page of the document, 
		! so force it out now and complain to the user.
		!
		BEGIN
		ERML (RNFFWF);
		FOOOUT (1)		! Dump one footnote, namely the bad one.
		END
	    ELSE
		!
		! These footnotes are not too lengthy, so see if there is
		! space at the bottom of the page to output them now.
		!
		BEGIN
		LOCAL
		    HOW_MANY_FIT;

		!
		! Find out how many footnotes of the ready ones can fit.
		!
		HOW_MANY_FIT = TPFFIT ();

		IF  (.HOW_MANY_FIT NEQ 0)
		THEN
		    !
		    ! There are some footnotes that will fit.  Output them.
		    !
		    BEGIN
		    LOCAL
			HOLD_PAGING;

		    HOLD_PAGING = .PHAN_PAGING;
		    PHAN_PAGING = FALSE;
		    !
		    ! Position to proper location on page.
		    !
		    NO_BAR_SKIP (TPFSKP (.HOW_MANY_FIT));
		    FOOOUT (.HOW_MANY_FIT);	! Generate the footnotes now.
		    PHAN_PAGING = .HOLD_PAGING;
		    END

		END;

    END;					! End of routine FOOBOT

END						! End of module
ELUDOM
