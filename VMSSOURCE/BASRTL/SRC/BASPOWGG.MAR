	.TITLE	BAS$POWGG		; BASIC gfloat ** gfloat routine
	.IDENT	/1-001/			; File: BASPOWGG.MAR Edit:PLL1001

;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982, 1984 BY				    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;

;++
; FACILITY: Basic Support Library
;
; ABSTRACT:
;
;	This module contains entry points to support exponentiation 
;	(** or ^) in BASIC-PLUS-2 for DOUBLE ** DOUBLE.
;
; ENVIRONMENT: User Mode, AST Reentrant
;
;--
; AUTHOR: P. Levesque	, CREATION DATE: 5-Oct-81
;
; MODIFIED BY:
;
; 1-001	- Original 

	.SBTTL	DECLARATIONS
;
; INCLUDE FILES:
;

;
; EXTERNAL DECLARATIONS:
;
	.DSABL	GBL			; Prevent undeclared
					; symbols from being
					; automatically global.

	.EXTRN	OTS$POWGG		; OTS$ gfloat ** gfloat exponentiation
	.EXTRN	OTS$POWGJ		; OTS$ gfloat ** int exponentation
	.EXTRN	BAS$K_DIVBY_ZER		; Divide by Zero
	.EXTRN	BAS$K_ILLARGLOG		; Illegal argument in LOG
	.EXTRN	BAS$$STOP		; Error reporting routine

;
; MACROS:
;

;
; EQUATED SYMBOLS:
;

;
; OWN STORAGE:
;

;
; PSECT DECLARATIONS:
;
	.PSECT _BAS$CODE PIC, USR, CON, REL, LCL, SHR, -
			EXE, RD, NOWRT, LONG


	.SBTTL	BAS$POWGG  - BASIC gfloat ** gfloat
;++
; FUNCTIONAL DESCRIPTION:
;
;	This routine takes BASE ** EXP, using the following table
;	for unusual cases:
;
;	BASE > 0			Call OTS$POWGG, normal case.
;	BASE = 0, EXP > 0		Return 0.0.
;	BASE = 0, EXP = 0		Return 1.0.
;	BASE = 0, EXP < 0		Error: divide by zero
;	BASE < 0, EXP even integer	Call OTS$POWGJ with -BASE
;	BASE < 0, EXP odd integer	Call OTS$POWGJ with -BASE, negate result
;	BASE < 0, EXP not integer	Error: illegal argument in LOG.
;
; CALLING SEQUENCE:
;
;	CALL result.wg.v = BAS$POWGG (base.rg.v, exponent.rg.v)
;
; INPUT PARAMETERS:
;
	base = 4
	exponent = 12
;
; IMPLICIT INPUTS:
;
;	NONE
;
; OUTPUT PARAMETERS:
;
;	NONE
;
; IMPLICIT OUTPUTS:
;
;	NONE
;
; FUNCTION VALUE:
; COMPLETION CODES:
;
;	gfloat result of exponentiation
;
; SIDE EFFECTS:
;
;	Will signal Divide By Zero or Illegal argument in LOG if its
;	arguments are bad, and OTS$POWGG and OTS$POWGJ may also signal.
;
;--

BAS$POWGG::	.MASK OTS$POWGG		; Entry point
					; Since this routine uses no 
					; registers and usually transfers
					; control to OTS$POWGG, we copy
					; its register save mask and then
					; JMP past its save mask and only
					; save the registers once
	TSTG	base(AP)		; Test base relationship to zero
	BLEQ	1$			; If base leq 0, do case analysis
	JMP	G^OTS$POWGG+2		; Transfer control to the OTS$
					; routine to do exponentiation
;+
; Come here if the base is less than or equal to zero.  We must filter
; several special cases, as described above.
;-
1$:	BEQL	4$			; Branch if base = 0
	EMODG	exponent(AP), #0, #1, R0, R0
	BNEQ	3$			; Branch if exponent is not integer
;+
; The base is less than zero and the exponent is an integer.
; BASIC defines this as working the same way as if an integer was
; in the expression (making a double variable which happens to
; contain an integer value equivalent to an integer variable).
;-
	CVTGL	exponent(AP), R0	; Convert exponent to integer
	PUSHL	R0			; Save for even/odd test
	PUSHL	R0			; Stack as parameter to OTS$POWGJ
	MNEGG	base(AP), -(SP)		; Stack -base also
	CALLS	#3, G^OTS$POWGJ		; Call integer power routines
	BLBC	(SP)+,2$		; Branch if exponent even
	MNEGG	R0, R0			; Exponent odd, negate the result
2$:	RET				; and return with it.
;+
; Come here if the base is less than zero but the exponent is not
; an integer.  BASIC defines this as an error.
;-
3$:	MOVZBL	#BAS$K_ILLARGLOG, -(SP)	; Illegal Argument in LOG
	CALLS	#1, G^BAS$$STOP		; Never return.
;+
; Come here if the base is equal to zero.  The value we return depends
; upon the sign of the exponent.
;-
4$:	TSTG	exponent(AP)		; Test the exponent against zero
	BLSS	6$			; Branch if exponent lss 0
	BEQL	5$			; Branch if exponent is 0
;+
; Come here if the base is zero and the exponent is greater than zero.
; BASIC defines this as 0.0.
;-
	CLRG	R0			; R0, R1 = 0.0
	RET				; Return to caller
;+
; Come here if the base is zero and the exponent is zero.  BASIC defines
; this as 1.0.
;-
5$:	MOVG	#1, R0			; R0, R1 = 1.0
	RET				; Return to caller.
;+
; Come here if the base is zero and the exponent is less than zero.
; BASIC defines this as an error.
;-
6$:	MOVZBL	#BAS$K_DIVBY_ZER, -(SP)	; Divide by zero
	CALLS	#1, G^BAS$$STOP		; Report error, never return.
;
	.END
