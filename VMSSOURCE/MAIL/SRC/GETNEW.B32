MODULE MAIL$GETNEW (
	IDENT = 'V04-000'
		) =
BEGIN

!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982, 1984 BY				    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************
!

!++
! FACILITY:	VAX/VMS MAIL UTILITY
!
! ABSTRACT:	Find user's new mail count.  Used by LOGINOUT.
!
! ENVIRONMENT:	NATIVE/USER MODE
!
! AUTHOR: Benn Schreiber, CREATION DATE: 10-Jul-1983
!
! MODIFIED BY:
!
!	V03-004	BLS0266		Benn Schreiber		26-Jan-1984
!		Correct username offset reference.
!
!	V03-003	BLS0260		Benn Schreiber		12-Jan-1984
!		Account for UAF$S_USERNAME being 32 bytes now.
!
!	V03-002	BLS0246		Benn Schreiber		 8-Nov-1983
!		Move new mail count into VMSMAIL.DAT
!--

!
! INCLUDE FILES
!
    LIBRARY	'SYS$LIBRARY:LIB';
    REQUIRE	'SRC$:MAILREQ';
    LIBRARY	'LIB$:MAILDEF';

EXTERNAL ROUTINE
    UTIL$REPORT_IO_ERROR;		!Report io error

BIND
    VMSMAIL_DESC = $DESCRIPTOR('VMSMAIL') : $BBLOCK,
    SYSEXE_DESC = $DESCRIPTOR('SYS$SYSTEM:.DAT') : $BBLOCK;

GLOBAL ROUTINE MAIL$GET_NEW_COUNT(NEWCOUNT,UAFREC,SYSMAIREC) =
!++
! FUNCTIONAL DESCRIPTION:
!
!	Return the count of new mail messages.
!
! Inputs:
!
!	newcount = address of a longword to receive the new message count.
!		returned as -1 if the mail file is sequential, rather than isam
!	uafrec = address of buffer containing SYSUAF record for user
!	sysmairec = (optional) address of buffer containing users' vmsmail rec.
!
! Outputs:
!
!	longword pointed to by newcount contains number of new messages.
!
! Routine value:
!
!	1 if success, failure code if error
!
! All errors signalled.
!
!--
BEGIN
MAP
    NEWCOUNT : REF VECTOR[,LONG],
    UAFREC : REF $BBLOCK,
    SYSMAIREC : REF $BBLOCK;

BUILTIN
    NULLPARAMETER;

LOCAL
    SYSMAIFAB : $FAB_DECL,
    SYSMAIRAB : $RAB_DECL,
    STATUS,
    USEBUF : REF $BBLOCK,
    SYSMAIBUF : $BBLOCK[VMD_C_USERNAME+3*NAM$C_MAXRSS];

NEWCOUNT[0] = 0;
STATUS = TRUE;

IF NOT NULLPARAMETER(3)
THEN USEBUF = .SYSMAIREC
ELSE BEGIN
    USEBUF = SYSMAIBUF;
    !
    ! Need to open VMSMAIL to find  new mail count
    !
    $FAB_INIT(FAB=SYSMAIFAB,
	FAC=GET,
	FNS=.VMSMAIL_DESC[DSC$W_LENGTH],
	FNA=.VMSMAIL_DESC[DSC$A_POINTER],
	DNS=.SYSEXE_DESC[DSC$W_LENGTH],
	DNA=.SYSEXE_DESC[DSC$A_POINTER],
	SHR=(GET,PUT,DEL,UPD));

    SYSMAIFAB[FAB$V_LNM_MODE] = PSL$C_EXEC;
    STATUS = $OPEN(FAB=SYSMAIFAB);

    $RAB_INIT(RAB=SYSMAIRAB,
	FAB=SYSMAIFAB,
	RAC=KEY,
	ROP=(RLK,NLK),
	KRF=0,
	KSZ=VMD_C_USERNAME,
	KBF=SYSMAIBUF,
	RBF=SYSMAIBUF,
	USZ=VMD_C_LENGTH+3*NAM$C_MAXRSS,
	UBF=SYSMAIBUF);

    IF .STATUS
	THEN STATUS = $CONNECT(RAB=SYSMAIRAB);

    IF NOT .STATUS
	AND  .STATUS EQL RMS$_CRMP
	THEN STATUS = (SYSMAIFAB[FAB$W_GBC] = 0;
					$CONNECT(RAB=SYSMAIRAB));
	!
	! Read the user's VMSMAIL record
	!
    CH$COPY(MINU(UAF$S_USERNAME,VMD_C_USERNAME),UAFREC[UAF$T_USERNAME],
				' ',VMD_C_USERNAME,SYSMAIBUF);
    SYSMAIBUF[VMD_B_DIRLNG] = 0;
    IF .STATUS
	THEN STATUS = $GET(RAB=SYSMAIRAB);
    $CLOSE(FAB=SYSMAIFAB);
    END;

IF .STATUS
    THEN NEWCOUNT[0] = .SYSMAIBUF[VMD_W_MAIL];

RETURN .STATUS
END;

END ELUDOM
