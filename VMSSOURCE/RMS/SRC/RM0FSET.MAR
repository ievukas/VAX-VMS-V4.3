	$BEGIN	RM0FSET,000,RM$RMS0,<SETUP FOR A FAB FUNCTION>

;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982, 1984 BY				    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;

;++
; Facility: RMS32
;
; Abstract:
;		routine to perform common setup for a fab function
;
; Environment:
;		star processor running starlet exec.
;
; Author: L F Laverdure,	 creation date: 4-JAN-1977
;
; Modified By:
;
;	V03-005	DAS0001		David Solomon		2-Feb-1984
;		Don't call RM$RAISE_LOCK unless sharing.
;
;	V03-004	KBT0319		Keith B. Thompson	8-Sep-1982
;		Remove all S0 sharing code
;
;	V03-003	KBT0208		Keith B. Thompson	23-Aug-1982
;		Reorganize psects
;
;	V03-002	TMK0002		Todd M. Katz		02-Aug-1982
;		Add a comment emphasizing (spelling ?) that the field FAB$W_IFI
;		must have a value if performing an indirect ppf operation.
;
;	V03-001	TMK0001		Todd M. Katz		27-Jul-1982
;		Add the alternate entry point RM$FSET_ALT1. By transfering
;		control to this entry point, the call to RM$FABCHK is bypassed
;		but the retrieval of the IFAB's address is not.
;
;	V02-010	REFORMAT	Keith B. Thompson	29-Jul-1980
;
;	V009	CDS0077		C D Saether	23-JAN-1980	14:00
;		clear busy bit in ifab if irab is busy (act error)
;
;	V008	RAN0003		R A Newell	9-NOV-1978	15:22
;		file sharing code enhancements
;
; Revision History:
;
;				L F Laverdure	10-OCT-1978	13:30
;		copy of CHK_IDLE from RM0FILFNC
;
;--


	.SBTTL	DECLARATIONS

;
; Include Files:
;

;
; Macros:
;

	$IRBDEF				; irab data defintions
	$IMPDEF
	$FABDEF
	$IFBDEF
	$RMSDEF

;
; Equated Symbols:
;

;
; Own Storage:
;


	.SBTTL	RM$FSET - COMMON SETUP FOR FAB FUNCTION ROUTINE

;++
;
;  RMS$FSET	 - Set up for a FAB function call
;  RMS$FSET_ALT1 - Bypass RMS$FABCHK call but obtain IFAB's address
;  RMS$FSET_ALT	 - Bypass RMS$FABCHK call
;
;  this routine performs common setup for a fab function call
;  including the following:
;
;	1. call RM$FABCHK to check arglist, set base regs,
;	   and zero sts and stv fields in fab
;	2. check for valid ifi and set ifab addr
;	3. check for stream idle and set to busy
;	4. store the arglist addr and caller's mode in the ifab
;	5. save sp entry value in IMP$L_SAVED_SP
;
;
; Calling sequence:
;
;	BSBW	RM$FSET
;
;  alternate entry at RM$FSET_ALT to perform functions 3, 4, & 5 only
;	R7, R8, R9, and R11 must be set as per output prior to call.
;
;  alternate entry at RM$FSET_ALT1 to perform functions 2, 3, 4, & 5 only
;	R7, R8, R9, and R11 must be set as per output prior to call.
;
;
; Input Parameters:
;
;	R9	IFI of IFAB if enter at RM$FSET_ALT1
;	SP	stack pointer
;	AP	argument list addr
;
; Implicit Inputs:
;
;	The contents of the FAB. 
;
;	NOTE: If performing an indirect PPF operation, the field FAB$W_IFI
;	      must have a value.
;
; Output Parameters:
;
;	R11	impure area address
;	R10	ifab address
;	R9	ifab address
;	R8	fab address
;	R7	caller's mode
;	R0 thru R5 destroyed
;
; Implicit Outputs:
;
;	IMP$L_SAVED_SP is set to value of SP+4
;
; Completion Codes:
;
;	none.  if an error is detected returns to user (not caller)
;	after appropriate cleanup.  the user receives a standard
;	rms error code, in particular, ifi and those returned by
;	RM$FABCHK.
;
; Side Effects:
;
;	none
;
;--


RM$FSET::
	BSBW	RM$FABCHK		; valid fab?
					; returns only if o.k.
					;NOTE:
					; note: sets R11 to impure addr
					;     R9 to ifi
					;     R8 to fab addr
					;     R7 to caller's mode

;
;  Alternate entry point.
;
;  Get the IFAB address and check for a valid IFAB.
;

RM$FSET_ALT1::
	MOVL	#IMP$L_IFABTBL/4,R0	; ifab table offset divided by 4
	BSBW	RM$GTIADR		; get ifab addr
	BEQL	ERRIFI			; branch if bad
	ASSUME	IFB$B_BID EQ IFB$B_BLN-1
	.IF	NE $$RMSTEST&$$RMS_TBUGCHK
	CMPW	IFB$B_BID(R9),#IFB$C_BID+<<IFB$C_BLN/4>*256>
	BNEQ	ERRBUG			; branch if not a valid ifab
	.ENDC

;
;  alternate entry from fseti here
;
;  set busy, checking if already active
;  store caller's mode and arglist addr in ifab
;

RM$FSET_ALT::
	BBSS	#IFB$V_BUSY,(R9),ERRACT
	BBS	#FAB$V_PPF_IND+<FAB$W_IFI*8>,(R8),CHKIND; branch if indirect ppf
	CSB	#IFB$V_PPF_IMAGE,(R9)	; make sure indirect bit off
SETMOD:	MOVB	R7,IFB$B_MODE(R9)	; save caller's mode
	MOVL	AP,IFB$L_ARGLST(R9)	; save pointer to arglist
	MOVL	R9,R10			; copy ifab addr
	ADDL3	#4,SP,IMP$L_SAVED_SP(R11); save stack entry value

;
; (less return pc)
;

	.IF	NE $$RMSTEST&$$RMS_TBUGCHK
	MOVL	R8,IFB$L_LAST_FAB(R9)	; save addr this fab
	.ENDC

;
;   check that all irabs connected to an ifab are not busy.
;

	MOVL	R10,R0			; ifab addr to temp reg
	BRB	20$			; go check if any irabs linked
10$:	BBS	#IRB$V_BUSY,(R0),ERRACT0; error if busy
	ASSUME	IRB$L_IRAB_LNK EQ IFB$L_IRAB_LNK
20$:	MOVL	IRB$L_IRAB_LNK(R0),R0	; get next irab
	BNEQ	10$			; branch if we got one
	TSTL	IFB$L_SFSB_PTR(R10)	; are we sharing?
	BEQL	30$			; no, no need to lock file
	BSBW	RM$RAISE_LOCK		; take lock on file
30$:	RSB

;
;  the ifi value indicates indirect processing of a process permanent file
;
;  set PPF_IMAGE flag
;

CHKIND:	SSB	#IFB$V_PPF_IMAGE,(R9)	; set indirect operation flag
	BRB	SETMOD			; and continue


;
;  error returns
;

ERRIFI:
	RMSERR	IFI			; invalid ifi value
	BRB	ERROR

ERRACT0:
	CSB	#IFB$V_BUSY,(R10)	; don't leave ifab busy on irab busy

ERRACT:
	RMSERR	ACT			; stream already active
ERROR:	BRW	RM$EX_NOSTR

;
;  internal rms problem  -  ifab table pointed to an invalid ifab!
;

ERRBUG:	RMSTBUG	FTL$_BADIFAB


	.END
