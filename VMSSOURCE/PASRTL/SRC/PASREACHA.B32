MODULE PAS$READ_CHAR ( %TITLE 'Read a character'
		IDENT = '1-002'			! File: PASREACHA.B32 Edit: SBL1002
		) =
BEGIN
!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982, 1984 BY				    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************
!

!++
! FACILITY:	Pascal Language Support
!
! ABSTRACT:
!
!	This module contains procedures to read a character from a
!	textfile and a string.
!
! ENVIRONMENT:	User mode - AST reentrant
!
! AUTHOR: Steven B. Lionel, CREATION DATE: 1-April-1981
!
! MODIFIED BY:
!
! 1-001	- Original.  SBL 1-April-1981
! 1-002 - Use PAS$$END_READ.  SBL 26-May-1982
!--


%SBTTL 'Declarations'
!
! PROLOGUE DEFINITIONS:
!

REQUIRE 'RTLIN:PASPROLOG';			! Externals, linkages, PSECTs, structures

!
! TABLE OF CONTENTS:
!

FORWARD ROUTINE
    PAS$READ_CHAR,				! Read from textfile
    PAS$READV_CHAR;				! Read from string

!
! MACROS:
!
!	NONE
!
! EQUATED SYMBOLS:
!
!	NONE
!
! FIELDS:
!
!	NONE
!
! OWN STORAGE:
!
!	NONE
!

%SBTTL 'PAS$READ_CHAR - Read a character from textfile'
GLOBAL ROUTINE PAS$READ_CHAR (
	PFV: REF $PAS$PFV_FILE_VARIABLE, 		! File variable
	ERROR						! Error unwind address
    ) =

!++
! FUNCTIONAL DESCRIPTION:
!
!	This function reads a single character from the specified textfile
!	and returns that character as its function value.
!
! CALLING SEQUENCE:
!
!	char.wc.v = PAS$READ_CHAR (PFV.mr.r [, ERROR.j.r])
!
! FORMAL PARAMETERS:
!
!	PFV		- The Pascal File Variable (PFV) passed by reference.
!			  The structure of the PFV is defined in PASPFV.REQ.
!
!	ERROR		- Optional.  If specified, the address to unwind to
!			  in case of an error.
!
! IMPLICIT INPUTS:
!
!	NONE
!
! IMPLICIT OUTPUTS:
!
!	NONE
!
! ROUTINE VALUE:
!
!	The character read from the file
!
! SIDE EFFECTS:
!
!	If the file is the standard file INPUT or OUTPUT, it is implicitly opened.
!
! SIGNALLED ERRORS:
!
!
!--

    BEGIN

    LOCAL
	CHAR_READ,			! Character read
	FCB: REF $PAS$FCB_CONTROL_BLOCK,! File Control block
	PFV_ADDR: VOLATILE,		! Enable argument
	UNWIND_ACT: VOLATILE,		! Enable argument
	ERROR_ADDR: VOLATILE;		! Enable argument

    BUILTIN
	ACTUALCOUNT;			! Count of arguments

    ENABLE
	PAS$$IO_HANDLER (PFV_ADDR, UNWIND_ACT, ERROR_ADDR);	! Enable error handler

    !+
    ! Get ERROR parameter, if present.
    !-

    IF ACTUALCOUNT () GEQU 2
    THEN
	ERROR_ADDR = .ERROR;		! Set unwind address

    PFV_ADDR - PFV [PFV$R_PFV];		! Set PFV address

    !+
    ! Validate PFV and get PFV.
    !-

    PAS$$VALIDATE_PFV (PFV [PFV$R_PFV]; FCB);

    !+
    ! Set unwind action to unlock file.
    !-

    UNWIND_ACT = PAS$K_UNWIND_UNLOCK;

    !+
    ! Do common initialization.
    !-

    PAS$$INIT_READ (PFV [PFV$R_PFV], FCB [FCB$R_FCB]; FCB);

    !+
    ! Get the character to return and advance the record pointer.
    !-

    IF .FCB [FCB$A_RECORD_CUR] GEQA .FCB [FCB$A_RECORD_END]
    THEN
	BEGIN
	CHAR_READ = %C' ';
	PFV [PFV$V_VALID] = 0;
	FCB [FCB$V_LAZY] = 1;	! Start lookahead for next line
	END
    ELSE
	CHAR_READ = CH$RCHAR_A (FCB [FCB$A_RECORD_CUR]);

    !+
    ! Do end-of-READ processing.
    !-

    PAS$$END_READ (PFV [PFV$R_PFV], FCB [FCB$R_FCB]);

    RETURN .CHAR_READ;				! Return character

    END;					! End of routine PAS$READ_CHAR

!<BLF/PAGE>

%SBTTL 'PAS$READV_CHAR - Read character from string'
GLOBAL ROUTINE PAS$READV_CHAR (
	STRING: REF BLOCK [, BYTE],			! String descriptor
	ERROR						! Error unwind address
    ) =

!++
! FUNCTIONAL DESCRIPTION:
!
!	This function reads a character from the specified string
!	and returns it as the function value.
!
! CALLING SEQUENCE:
!
!	Character.wbu.v = PAS$READV_CHAR (STRING.mt.ds [, ERROR.ja.r])
!
! FORMAL PARAMETERS:
!
!	STRING		- The string to read from, passed as a class S
!			  (assumed) descriptor.  The length and pointer
!			  are updated to reflect the unread string.
!
!	ERROR		- Optional.  If specified, the address to unwind to
!			  in case of an error.
!
! IMPLICIT INPUTS:
!
!	NONE
!
! IMPLICIT OUTPUTS:
!
!	NONE
!
! ROUTINE VALUE:
!
!	The value of the character read.
!
! SIDE EFFECTS:
!
!	NONE
!
! SIGNALLED ERRORS:
!
!	NONE
!
!--

    BEGIN

    LOCAL
	PFV: $PAS$PFV_FILE_VARIABLE,	! Pascal File Variable
	RESULT,				! Result value
	ARG_LIST: VECTOR [2, LONG],	! Argument list
	PFV_ADDR: VOLATILE,		! Enable argument
	ERROR_ADDR: VOLATILE;		! Enable argument

    BUILTIN
	ACTUALCOUNT;			! Count of arguments

    ENABLE
	PAS$$IO_HANDLER (PFV_ADDR, ERROR_ADDR);	! Enable error handler

    !+
    ! Get ERROR parameter, if present.
    !-

    IF ACTUALCOUNT () GEQU 2
    THEN
	ERROR_ADDR = .ERROR;		! Set unwind address

    PFV_ADDR = PFV;			! Set PFV address

    !+
    ! Set up ARG_LIST. 
    !-

    ARG_LIST [0] = 1;			! One argument
    ARG_LIST [1] = PFV;			! PFV address

    !+
    ! Call PAS$$DO_READV to do the work, giving it the address of
    ! PAS$READ_CHAR to call.
    !-

    PAS$$DO_READV (PFV, .STRING, ARG_LIST, PAS$READ_CHAR; RESULT);

    RETURN .RESULT;

    END;					! End of routine PAS$READV_CHAR

!<BLF/PAGE>

END						! End of module PAS$READ_CHAR

ELUDOM
