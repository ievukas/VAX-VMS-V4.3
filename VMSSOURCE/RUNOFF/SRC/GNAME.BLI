%TITLE 'GNAME - Get name from input string and convert it to uppercase'
MODULE gname (	IDENT = 'V04-000'
		%BLISS32[, ADDRESSING_MODE (EXTERNAL    = LONG_RELATIVE,
					    NONEXTERNAL = LONG_RELATIVE)]
		) =
BEGIN
!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982, 1984 BY				    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************
!
!++
! FACILITY:	DSR (Digital Standard RUNOFF) / DSRPLUS
!
! ABSTRACT:	Collects letters and digits to construct a name.
!
! ENVIRONMENT:	Transportable
!
! AUTHOR:	R.W.Friday	 CREATION DATE: April, 1978
!


%SBTTL 'Revision History'
!
! MODIFIED BY:
!
!	006	REM00006	Ray Marshall	7-Jun-1984
!		Modified previous fix to test for the substitution flag instead
!		  of a hard coded dollar sign.
!
!	005	REM00005	Ray Marshall	6-Jun-1984
!		Modified Ken's fix.  Now it checks to see that the first
!		  character is a letter or a dollar sign (for DSRPLUS).  The
!		  other way caused all .IFs and related directives to process
!		  a null parameter and can also ACCVIO if the user attempted to
!		  pop the IF stack too many times (with .ENDIFs) with null
!		  parameters.  This was because a null parameter was causing
!		  this routine to return normal.  All other places that call
!		  this routine test the length of the input string first and
!		  know that there is data in it.  Only the call from IFIFNE
!		  failed.
!
!	004	KFA00004	Ken Alden	23-Sep-1983
!		For DSRPLUS, removed first character restriction.
!
!	003	RER00003	Ron Randall	07-Mar-1983
!		Global edit of all modules. Updated module names, idents,
!		copyright dates. Changed require files to BLISS library.
!
!--


%SBTTL 'Module Level Declarations'

!
! TABLE OF CONTENTS:
!
! INCLUDE FILES:
!
LIBRARY 'NXPORT:XPORT';			! XPORT Library
REQUIRE 'REQ:RNODEF';			! RUNOFF variant definitions

%IF DSRPLUS %THEN
LIBRARY 'REQ:DPLLIB';			! DSRPLUS BLISS Library
%ELSE
LIBRARY 'REQ:DSRLIB';			! DSR BLISS Library
%FI

!
! EXTERNAL REFERENCES:
!
EXTERNAL
    flgt	: flgt_definition,	! Flag characters.
    khar;


GLOBAL ROUTINE GNAME (INPUT_STRING, OUTPUT_STRING) =

!++
! FUNCTIONAL DESCRIPTION:
!
!	GNAME processes an unbroken sequence of letters
!	and digits, stopping when
!		1) a nonletter or nondigit is found, or
!		2) the end of the INPUT_STRING is reached, or
!		3) OUTPUT_STRING is about to overflow.
! All characters accepted are transferred to OUTPUT_STRING.
!
! GNAME also checks to be sure that the first character is
! indeed a letter in RUNOFF only.
!
! FORMAL PARAMETERS:
!
!	INPUT_STRING is where the name is to come from.
!	The collected string of letters is left in OUTPUT_STRING.
!
! IMPLICIT INPUTS:	None
!
! IMPLICIT OUTPUTS:	None
!
! ROUTINE VALUE:
! COMPLETION CODES:
!
!	See GNCC.REQ for completion codes returned.
!
! SIDE EFFECTS:		None
!--

    BEGIN
    MAP
	output_string : REF fixed_string;
    BIND
	ira = input_string : REF fixed_string;

    FS_INIT (OUTPUT_STRING);

    IF  (NOT letter (.khar)		! First character must be a letter.
%IF DSRPLUS %THEN
	      OR			! or a substitution flag character
	.khar NEQ .flgt [sub_flag, flag_character]
%FI)
    THEN
	RETURN gname_no_name;
    WHILE 1 DO
	BEGIN

	IF  ( NOT LETTER (.KHAR)) AND
	    ( NOT DIGIT (.KHAR))		!Stop on an illegal character
	THEN
	    RETURN GNAME_NORMAL;

	IF  .FS_LENGTH (OUTPUT_STRING) EQL .FS_MAXSIZE (OUTPUT_STRING)
	THEN
	    (WHILE (LETTER (.KHAR) OR DIGIT (.KHAR)) DO
		(KCNS ()); RETURN GNAME_TOO_LONG);	!Throw away excess characters.

	FS_WCHAR (OUTPUT_STRING, 		!Convert to upper case and output the character.
	    (IF LOWER_LETTER (.KHAR)
	    THEN
		UPPER_CASE (.KHAR) ELSE .KHAR));

	KCNS ()					!Get next character.
	END					!end of processing loop.

    END;					!End of GNAME

END						!End of module
ELUDOM
