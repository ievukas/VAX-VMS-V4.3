MODULE fjnfnj (	IDENT = 'V04-000'
		%BLISS32[, ADDRESSING_MODE (EXTERNAL    = LONG_RELATIVE,
					    NONEXTERNAL = LONG_RELATIVE)]
		) =
BEGIN
!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982, 1984 BY				    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************
!
!++
! FACILITY:	DSR (Digital Standard RUNOFF) / DSRPLUS
!
! ABSTRACT:
!
!	Processes .AUTOFILL, .NO AUTOFILL, .FILL, .JUSTIFY, .NO FILL, 
!	.NO JUSTIFY, .KEEP, .NO KEEP, .AUTOJUSTIFY, and .NO AUTOJUSTIFY
!
! ENVIRONMENT:	Transportable
!
! AUTHOR:	R.W.Friday	CREATION DATE:	May, 1978
!


%SBTTL 'Revision History'
!
! MODIFIED BY:
!
!	005	RER00005	Ron Randall	07-Mar-1983
!		Global edit of all modules. Updated module names, idents,
!		copyright dates. Changed require files to BLISS library.
!
!--


%SBTTL 'Module Level Declarations'

!
! TABLE OF CONTENTS:
!
!
! INCLUDE FILES:
!
LIBRARY 'NXPORT:XPORT';			! XPORT Library
REQUIRE 'REQ:RNODEF';			! RUNOFF variant definitions

%IF DSRPLUS %THEN
LIBRARY 'REQ:DPLLIB';			! DSRPLUS BLISS Library
%ELSE
LIBRARY 'REQ:DSRLIB';			! DSR BLISS Library
%FI

!
! EXTERNAL REFERENCES:
!
EXTERNAL
    GCA : GCA_DEFINITION,
    SCA : SCA_DEFINITION;


GLOBAL ROUTINE FJNFNJ (HANDLER_CODE) : NOVALUE =

!++
! FUNCTIONAL DESCRIPTION:
!
!	FJNFNJ makes sure that the fill and justification
!	modes get set correctly.
!	It also controls whether or not empty records get kept when
!	in .NO FILL mode.
!
! FORMAL PARAMETERS:
!
!	HANDLER_CODE indicates which command is to be processed
!
! IMPLICIT INPUTS:	None
!
! IMPLICIT OUTPUTS:	None
!
! ROUTINE VALUE:
! COMPLETION CODES:	None
!
! SIDE EFFECTS:		None
!--

    BEGIN
    !SCA_FILL and SCA_JUSTIFY are TRUE for the respective functions.
    !SCA_CROCK is for compatibility with TOPS-10 RUNOFF.  Basically,
    !if you say .NO FILL, you "probably" don't want justification
    !any more; on the other hand, when you say .FILL you probably
    !do want justification, assuming you did not say .NO JUSTIFY.
    !In other words, SCA_CROCK is used to remember whether or not you
    !want justification when you say .FILL.

%IF DSRPLUS %THEN
    !.NO AUTOJUSTIFY will now prevent .HL, CHAPTER, and APPENDIX from
    !resetting fill and justify after their operation.
%FI

    SELECTONE .HANDLER_CODE OF
	SET

	[H_FILL] :
	    BEGIN
	    SCA_FILL = TRUE;
	    SCA_JUSTIFY = .SCA_CROCK;
	    SCA_KER = FALSE;			!Empty records have no significance.
	    END;

	[H_JUSTIFY] :
	    BEGIN
	    SCA_JUSTIFY = TRUE;
	    SCA_CROCK = TRUE;
	    END;

	[H_NO_FILL] :
	    BEGIN
	    SCA_JUSTIFY = FALSE;
	    SCA_FILL = FALSE;
	    SCA_KER = .GCA_KEEP;		!Empty records generate blank lines if user said .KEEP.
	    END;

	[H_NO_JUSTIFY] :
	    BEGIN
	    SCA_JUSTIFY = FALSE;
	    SCA_CROCK = FALSE;
	    END;

	[H_KEEP] :
	    BEGIN
	    GCA_KEEP = TRUE;			!User want's empty records kept in .NO FILL mode
	  
	    !Turn on .KEEP mode if already in .NO FILL mode, so the order of the
	    !commands is not important.
	    IF	NOT .SCA_FILL
	    THEN
		SCA_KER = TRUE;
	    END;

	[H_NO_KEEP] :
	    BEGIN
	    GCA_KEEP = FALSE;			!Discard empty records in .NO FILL mode

	    !Turn off .KEEP mode if already in .NO FILL mode, so the order of the
	    !commands is not important.
	    IF 	NOT .SCA_FILL
	    THEN
		SCA_KER = FALSE;
	    END;

	[H_AUTOJUSTIFY] :
	    BEGIN
	    GCA_AUTOJUST = TRUE;
	    END;

	[H_NO_AUTOJUSTIF] :
	    BEGIN
	    GCA_AUTOJUST = FALSE;		!This will now affect "autofill" and
	    END;				!prevent .HL, .CHAPTER, and .APPENDIX from
	TES;					!resetting fill and justify after their operation.

    END;					!End of FJNFNJ

END						!End of module
ELUDOM
