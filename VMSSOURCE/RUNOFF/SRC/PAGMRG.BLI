MODULE PAGMRG (IDENT = 'V04-000'
		%BLISS32[,ADDRESSING_MODE(EXTERNAL=LONG_RELATIVE,NONEXTERNAL=LONG_RELATIVE)]
		) =
BEGIN
!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982, 1984 BY				    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************
!

!++
! FACILITY: 
!   DSR (Digital Standard RUNOFF) /DSRPLUS DSRINDEX/INDEX Utility
!
! ABSTRACT:	 Compares page numbers to see if they can be merged.
!
! ENVIRONMENT:	 Transportable
!
! AUTHOR:	 R. W. Friday
!
! CREATION DATE: May, 1979
!
! MODIFIED BY:
!	002	JPK00024	23-May-1983
!		Modified lowercasing algorithm in PERMUTE. Now lowercase only
!		if word contains only 1 letter or if second letter in word is
!		lowercase. Picked up modules PAGMRG and POOL from DSR/DSRPLUS
!		since they are no longer used by DSR/DSRPLUS.
!
!--

!
! TABLE OF CONTENTS
!

FORWARD ROUTINE
    PAGMRG;

!
! INCLUDE FILES:
!

LIBRARY 'NXPORT:XPORT';

REQUIRE 'REQ:PAGEN';

!
! MACROS:
!
! EQUATED SYMBOLS:
!

LITERAL
    TRUE = 1,
    FALSE = 0;

!
! OWN STORAGE:
!
!
! EXTERNAL REFERENCES:
!

EXTERNAL ROUTINE
    DSPEQL;

%SBTTL 'PAGMRG -- Compare two page entries to see if they can be merged'
GLOBAL ROUTINE PAGMRG (ENTRY1, ENTRY2) =

!++
! FUNCTIONAL DESCRIPTION:
!
!	Compare two page numbers to see if they are adjacent pages.
!
! FORMAL PARAMETERS:
!
!	ENTRY1 and ENTRY2 are the page numbers to be compared.
!
! IMPLICIT INPUTS:
!
!	NONE
!
! IMPLICIT OUTPUTS:
!
!	NONE
!
! ROUTINE VALUE:
! COMPLETION CODES:
!
!	TRUE	- Specified page numbers are adjacent
!	FALSE	- Specified page numbers are not adjacent
!
! SIDE EFFECTS:
!
!	NONE
!
!--

    BEGIN

    MAP
	ENTRY1 : REF PAGE_DEFINITION,
	ENTRY2 : REF PAGE_DEFINITION;

    !
    ! Both entries must exist
    !
    IF (.ENTRY1 EQL 0) OR (.ENTRY2 EQL 0) THEN RETURN FALSE;

    !
    ! By definition, you can't merge page numbers if
    ! their display characteristics are different.  Test for this.
    !
    IF NOT DSPEQL (.ENTRY1, .ENTRY2) THEN RETURN FALSE;

    !
    ! Section type must match
    !
    IF .ENTRY1 [SCT_TYP] NEQ .ENTRY2 [SCT_TYP] THEN RETURN FALSE;

    !
    ! Section number must always match
    !
    IF .ENTRY1 [SCT_NUMBER] NEQ .ENTRY2 [SCT_NUMBER] THEN RETURN FALSE;

    !
    ! If sub-page exists, the page number must match and the sub-pages
    ! must differ only by 1
    !
    IF .ENTRY1 [SCT_SUB_PAGE] NEQ 0 
    THEN
	BEGIN

	IF .ENTRY1 [SCT_PAGE] NEQ .ENTRY2 [SCT_PAGE] THEN RETURN FALSE;

	IF  (.ENTRY1 [SCT_SUB_PAGE] NEQ .ENTRY2 [SCT_SUB_PAGE] - 1)
	AND (.ENTRY1 [SCT_SUB_PAGE] NEQ .ENTRY2 [SCT_SUB_PAGE])
	THEN
	    RETURN FALSE;

	RETURN TRUE
	END;

    !
    ! If all else is OK, page numbers can differ only by 0 or 1
    !
    IF .ENTRY1 [SCT_PAGE] NEQ .ENTRY2 [SCT_PAGE] - 1
    THEN
	BEGIN

	IF .ENTRY1 [SCT_PAGE] NEQ .ENTRY2 [SCT_PAGE] THEN RETURN FALSE;
	END;

    RETURN TRUE;
    END;			!End of PAGMRG

END				!End of module
ELUDOM
