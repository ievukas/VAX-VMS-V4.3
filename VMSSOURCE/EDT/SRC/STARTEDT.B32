%TITLE 'EDT$STARTEDT - set up and call EDT'
MODULE EDT$STARTEDT (				! set up and call EDT
		IDENT = 'V04-000',		! File: STARTEDT.BLI Edit: SMB1015
		MAIN = EDT$$STARTEDT
		) =
BEGIN
!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982, 1984 BY				    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************
!

!++
! FACILITY:	EDT -- The DEC Standard Editor
!
! ABSTRACT:
!
!	set up and call EDT
!
! ENVIRONMENT:	Runs at any access mode - AST reentrant
!
! AUTHOR: Shelly T. Solomon CREATION DATE: November 16, 1981
!
! MODIFIED BY:
!
! 1-001	- Original.  STS 16-Nov-1981.
! 1-002 - Changed names of all globals to avoid conflicts in linking with
!         sharable image. STS 07-Dec-1981
! 1-003 - Add command and journal file defaults.  STS 16-Dec-1981
! 1-004	- If we are tracing, open the terminal before parsing the command,
!	   for compatibility with old EDT.  JBS 17-Dec-1981
! 1-005 - Change journal file name to default to input file name. STS 17-Dec-1981
! 1-006 - Add B32 conditionals for TRACE code.  SMB 21-Dec-1981
! 1-007	- Run through PRETTY to remove leading blank line.  JBS 22-Dec-1981
! 1-008 - Pass address of EDT$FILEIO to main EDT routine. STS 25-Dec-1981
! 1-009 - Fix DSC$A_POINTER macro. STS 14-Jan=1982
! 1-010 - Pass address of EDT$WORKIO. STS 10-Feb-1982
! 1-011	- Use EDT$$V_COPTIONS.  JBS 24-Mar-1982
! 1-012	- Simplify the clearing of EDT$$V_COPTIONS to avoid the need for
!	   BL$PU2 on the PDP-11.  JBS 26-Mar-1982
! 1-013 - Make this a VAX specific module. STS 02-Jun-1982
! 1-014 - Add EDT$XLATE. STS 13-Aug-1982
! 1-015 - Simplify for use with new DCL command language interface.  SMB 18-Feb-1983
!--


%SBTTL 'Declarations'
!
! TABLE OF CONTENTS:
!

REQUIRE 'EDTSRC:TRAROUNAM';

FORWARD ROUTINE
    EDT$$STARTEDT;				! Main program for EDT utility

!
! INCLUDE FILES:
!

REQUIRE 'EDTSRC:EDTREQ';

REQUIRE 'EDTSRC:SYSSYM';

REQUIRE 'TRACESEL';

REQUIRE 'EDTSRC:TRACEMAC';

!
! MACROS:
!
!
! EQUATED SYMBOLS:
!
!	NONE
!
! OWN STORAGE:
!
!	NONE

GLOBAL
    EDT$$G_CRCOV_MOD,
    EDT$$Z_CINP_DESC : BLOCK [8, BYTE],		! Input file string descriptor
    EDT$$Z_COUT_DESC : BLOCK [8, BYTE],		! Output file string descriptor
    EDT$$Z_CCMD_DESC : BLOCK [8, BYTE],		! Command file string descriptor
    EDT$$Z_CJOU_DESC : BLOCK [8, BYTE],		! Journal file string descriptor
    EDT$$V_COPTIONS : BITVECTOR [32];

!
! EXTERNAL REFERENCES:
!
!	In the routine

%SBTTL 'EDT$$STARTEDT - set up and call EDT'
ROUTINE EDT$$STARTEDT 				! set up and call EDT
    =

!++
! FUNCTIONAL DESCRIPTION:
!
!	set up and call EDT
!
! FORMAL PARAMETERS:
!
!	NONE
!
! IMPLICIT INPUTS:
!
!	none
!
! IMPLICIT OUTPUTS:
!
!	NONE
!
! ROUTINE VALUE:
!
!	Same as EDT$EDIT
!
! SIDE EFFECTS:
!
! 	Calls EDT$EDIT and does an edit
!
!--

    BEGIN

    EXTERNAL ROUTINE
	EDT$EDIT,				! this is callable EDT entry point

%IF EDT$$TR_ACT
%THEN
	EDT$$TI_OPN,				! open terminal and set up trace
%FI

	EDT$$PA_CMDLN;				! Parse command line

    EXTERNAL
	EDT$WORKIO,				! address of work i/o routine
    	EDT$XLATE,				! address of translation routine
	EDT$FILEIO;				! address of fileio routine

    LOCAL
	FILE_ROUT : VECTOR [2],
    	XLATE_ROUT : VECTOR [2],
	WORK_ROUT : VECTOR [2],
	CROUTVAL;

%IF EDT$$TR_ACT
%THEN
!+
! If we are tracing (only on VAX-11) open the terminal first, so
! the trace file is open and the trace records are in the same
! order as in previous configurations of EDT.  The initialization
! routine will call EDT$$TI_OPN later, but it will execute only once.
!-
    EDT$$TI_OPN ();
%FI

!+
! Do some initialization
!-
    BLOCK [EDT$$V_COPTIONS, 0, 0, 16, 0; 4, BYTE] = 0;
    BLOCK [EDT$$V_COPTIONS, 2, 0, 16, 0; 4, BYTE] = 0;
    FILE_ROUT [0] = EDT$FILEIO;
    FILE_ROUT [1] = 0;
    XLATE_ROUT [0] = EDT$XLATE;
    XLATE_ROUT [1] = 0;
    WORK_ROUT [0] = EDT$WORKIO;
    WORK_ROUT [1] = 0;
    EDT$$Z_CINP_DESC [DSC$B_DTYPE] = DSC$K_DTYPE_T;	! type of descriptor
    EDT$$Z_CINP_DESC [DSC$B_CLASS] = DSC$K_CLASS_D;	! dynamic strings
    EDT$$Z_CINP_DESC [DSC$A_POINTER] = 0;
    EDT$$Z_CINP_DESC [DSC$W_LENGTH] = 0;
    EDT$$Z_COUT_DESC [DSC$B_DTYPE] = DSC$K_DTYPE_T;
    EDT$$Z_COUT_DESC [DSC$B_CLASS] = DSC$K_CLASS_D;
    EDT$$Z_COUT_DESC [DSC$A_POINTER] = 0;
    EDT$$Z_COUT_DESC [DSC$W_LENGTH] = 0;
    EDT$$Z_CCMD_DESC [DSC$B_DTYPE] = DSC$K_DTYPE_T;
    EDT$$Z_CCMD_DESC [DSC$B_CLASS] = DSC$K_CLASS_D;
    EDT$$Z_CCMD_DESC [DSC$A_POINTER] = 0;
    EDT$$Z_CCMD_DESC [DSC$W_LENGTH] = 0;
    EDT$$Z_CJOU_DESC [DSC$B_DTYPE] = DSC$K_DTYPE_T;
    EDT$$Z_CJOU_DESC [DSC$B_CLASS] = DSC$K_CLASS_D;
    EDT$$Z_CJOU_DESC [DSC$A_POINTER] = 0;
    EDT$$Z_CJOU_DESC [DSC$W_LENGTH] = 0;

!+
! Process the command line. And get all this stuff back
!-
    EDT$$PA_CMDLN ();

!+
! We've set up all the preliminaries,  now call EDT.
!-
    RETURN (EDT$EDIT (EDT$$Z_CINP_DESC, EDT$$Z_COUT_DESC, EDT$$Z_CCMD_DESC, EDT$$Z_CJOU_DESC,
	    EDT$$V_COPTIONS, FILE_ROUT, WORK_ROUT, XLATE_ROUT));
    END;					! of routine EDT$STARTEDT
!<BLF/PAGE>

END

ELUDOM
