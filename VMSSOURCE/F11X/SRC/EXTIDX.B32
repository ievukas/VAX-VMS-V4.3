MODULE EXTIDX (
		LANGUAGE (BLISS32),
		IDENT = 'V04-000'
		) =
BEGIN

!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982, 1984 BY				    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************

!++
!
! FACILITY:  F11ACP Structure Level 2
!
! ABSTRACT:
!
!	This routine extends the volume's index file.
!
! ENVIRONMENT:
!
!	STARLET operating system, including privileged system services
!	and internal exec routines.
!
!--
!
!
! AUTHOR:  Andrew C. Goldstein,	 CREATION DATE:  14-Apr-1977  10:44
!
! MODIFIED BY:
!
!	V03-006	ACG0438		Andrew C. Goldstein,	2-Aug-1984  11:54
!		Add check of index file bitmap size to max files check
!
!	V03-005	CDS0003		Christian D. Saether	25-Apr-1984
!		Long addressing mode for certain external routines.
!
!	V03-004	ACG0409		Andrew C. Goldstein,	21-Mar-1984  21:58
!		Remove file number parameter; set index file EOF to end.
!
!	V03-003	CDS0002		Christian D. Saether	30-Dec-1983
!		Use L_NORM linkage and BIND_COMMON macro.
!
!	V03-002	CDS0001		Christian D. Saether	20-Sep-1983
!		Serialize reading of index file header.
!		Stuff FH2$L_HIGHWATER if appropriate.
!
!	V03-001	ACG0274		Andrew C. Goldstein,	23-Mar-1982  11:25
!		Allow use of alternate index file header
!
!	V02-002	STJ34965	Steven T. Jeffreys,	28-Feb-1981
!		Temporary fix to set FIB$V_NOCHARGE bit for the
!		index file, so that if will not be charged for diskquota.
!
!	B0101	ACG0121		Andrew C. Goldstein,	16-Jan-1980  21:39
!		Make context save and restore into subroutines
!
!	B0100	ACG00001	Andrew C. Goldstein,  10-Oct-1978  20:00
!	Previous revision history moved to [F11B.SRC]F11B.REV
!**


LIBRARY 'SYS$LIBRARY:LIB.L32';
REQUIRE 'SRC$:FCPDEF.B32';

GLOBAL ROUTINE EXTEND_INDEX : L_NORM NOVALUE =

!++
!
! FUNCTIONAL DESCRIPTION:
!
!	This routine extends the volume's index file.
!
! CALLING SEQUENCE:
!	EXTEND_INDEX ()
!
! INPUT PARAMETERS:
!	NONE
!
! IMPLICIT INPUTS:
!	CURRENT_VCB: address of volume VCB
!
! OUTPUT PARAMETERS:
!	NONE
!
! IMPLICIT OUTPUTS:
!	NONE
!
! ROUTINE VALUE:
!	NONE
!
! SIDE EFFECTS:
!	index file extended, index file window and index file FCB modified
!
!--

BEGIN

LOCAL
	MAX_FILES,			! max number of files on volume
	FILESIZE,			! size of index file after extend.
	FIB		: REF BBLOCK,	! address of FIB for extend operation
	HEADER		: REF BBLOCK,	! address of index file header
	FCB		: REF BBLOCK,	! address of index file FCB
	WINDOW		: REF BBLOCK,	! address of index file window
	FREE_POINTERS,			! number of free retrieval pointers
					! in index file window
	FILES_TO_GO,			! number of files likely to be created
					! on this volume
	BLOCKS_NEEDED;			! amount to extend index file by

BIND_COMMON;

EXTERNAL ROUTINE
	SERIAL_FILE	: L_NORM,
	RELEASE_SERIAL_LOCK	: L_NORM,
	SAVE_CONTEXT	: L_NORM,	! save reentrant context area
	RESTORE_CONTEXT	: L_NORM,	! restore reentrant context area
	READ_IDX_HEADER	: L_NORM,	! read index file header
	TURN_WINDOW	: L_NORM ADDRESSING_MODE (GENERAL), ! update file window
	EXTEND		: L_NORM,	! extend a file
	CHECKSUM	: L_NORM,	! compute file header checksum
	WRITE_HEADER	: L_NORM,	! write back file header
	RESET_LBN	: L_NORM,	! reassign LBN of buffer
	WRITE_BLOCK	: L_NORM,	! write a disk block
	INVALIDATE	: L_NORM,	! invalidate buffer
	INIT_FCB2	: L_NORM;	! update file control block


! Extending the index file is a secondary operation, so we must save away the
! primary context, and then set up the appropriate context for this operation.
!

SAVE_CONTEXT ();
FIB = SECOND_FIB;
CH$FILL (0, FIB$C_LENGTH, .FIB);
FIB[FIB$W_FID_NUM] = 1;
FIB[FIB$W_FID_SEQ] = 1;

PRIMARY_FCB = FCB = .CURRENT_VCB[VCB$L_FCBFL];
CURRENT_WINDOW = WINDOW = .FCB[FCB$L_WLFL];

! Serialize on the index file header.
!

PRIM_LCKINDX = SERIAL_FILE (FIB [FIB$W_FID]);

! Read the index file header.
!

HEADER = READ_IDX_HEADER ();

! Turn the index file window to VBN 3. Then compute the number of free
! retrieval pointers in the index file window, discounting pointers (if
! any) that only map the boot and home block.
!

KERNEL_CALL (TURN_WINDOW, .WINDOW, .HEADER, 3, 1);

FREE_POINTERS = (.WINDOW[WCB$W_SIZE]-WCB$C_LENGTH)/6 - .WINDOW[WCB$W_NMAP];
IF .WINDOW[WCB$L_STVBN] + .WINDOW[WCB$W_P1_COUNT] LEQU 3
THEN
    BEGIN
    FREE_POINTERS = .FREE_POINTERS + 1;
    IF .WINDOW[WCB$L_STVBN] + .WINDOW[WCB$W_P1_COUNT] + .WINDOW[WCB$W_P2_COUNT] LEQU 3
    THEN FREE_POINTERS = .FREE_POINTERS + 1;
    END;
IF .FREE_POINTERS LEQ 0 THEN FREE_POINTERS = 1;

! Check the amount of index file (as determined by max files and the
! size of the index file bitmap).
!

MAX_FILES = MINU (.CURRENT_VCB[VCB$L_MAXFILES], .CURRENT_VCB[VCB$B_IBMAPSIZE] * 4096);
FILES_TO_GO = .MAX_FILES - .FCB[FCB$L_FILESIZE]
	    + .CURRENT_VCB[VCB$B_IBMAPSIZE] + .CURRENT_VCB[VCB$W_CLUSTER]*4;
IF .FILES_TO_GO LEQ 0
THEN ERR_EXIT (SS$_IDXFILEFULL);

! Compute the number of files likely to still be created on the volume.  This
! is the minimum of the number permitted minus the current number and a
! fraction of the number of free blocks on the volume. The amount to extend
! the index file by is this quantity divided by the number of available
! retrieval pointers in the index file window.
!

FILES_TO_GO = MINU (.FILES_TO_GO,
		    .CURRENT_VCB[VCB$L_FREE] / .CURRENT_VCB[VCB$W_CLUSTER] / 4);

BLOCKS_NEEDED = MINU (.FILES_TO_GO / .FREE_POINTERS, 1000);

! Build the extend control in the FIB and call the EXTEND routine.
!

FIB[FIB$L_EXSZ] = .BLOCKS_NEEDED;
FIB[FIB$V_ALCON] = 1;
FIB[FIB$V_ALCONB] = 1;
FIB[FIB$V_ALDEF] = 1;
FIB[FIB$V_NOHDREXT] = 1;

! Set NOCHARGE bit so the index file will not be charged for diskquota.
!
FIB [FIB$V_NOCHARGE] = 1;

EXTEND (.FIB, .HEADER);

FILESIZE = .FIB[FIB$L_EXSZ] + .FIB[FIB$L_EXVBN] - 1;
BBLOCK [HEADER[FH2$W_RECATTR], FAT$L_HIBLK] = ROT (.FILESIZE, 16);
IF NOT .CURRENT_VCB[VCB$V_NOHIGHWATER]
THEN BBLOCK [HEADER[FH2$W_RECATTR], FAT$L_EFBLK] = ROT (.FILESIZE+1, 16);

! If this file header supports it, stuff the high water field to
! be the allocated size.
!

IF .HEADER [FH2$B_IDOFFSET] GEQU ($BYTEOFFSET (FH2$L_HIGHWATER)+4)/2
THEN
    HEADER [FH2$L_HIGHWATER] = .FILESIZE + 1;

! Now write the header, update the FCB, and restore the primary context.
!

CHECKSUM (.HEADER);
WRITE_HEADER ();
RESET_LBN (.HEADER, .CURRENT_VCB[VCB$L_IXHDR2LBN]);
WRITE_BLOCK (.HEADER);
INVALIDATE (.HEADER);
KERNEL_CALL (INIT_FCB2, .FCB, .HEADER);

RELEASE_SERIAL_LOCK (.PRIM_LCKINDX);
PRIM_LCKINDX = 0;

RESTORE_CONTEXT ();
USER_STATUS[1] = 0;

END;					! end of routine EXTEND_INDEX

END
ELUDOM
