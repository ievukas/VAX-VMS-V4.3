MODULE PAS$READ_UNSIGNED ( %TITLE 'Read an unsigned integer'
		IDENT = '1-002'			! File: PASREAUNS.B32 Edit: SBL1002
		) =
BEGIN
!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982, 1984 BY				    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************
!

!++
! FACILITY:	Pascal Language Support
!
! ABSTRACT:
!
!	This module contains procedures which read an unsigned integer
!	from a textfile or a string.
!
! ENVIRONMENT:	User mode - AST reentrant
!
! AUTHOR: Steven B. Lionel, CREATION DATE: 1-April-1981
!
! MODIFIED BY:
!
! 1-001	- Original.  SBL 1-April-1981
! 1-002 - Use PAS$$END_READ.  SBL 26-May-1982
!--


%SBTTL 'Declarations'
!
! PROLOGUE DEFINITIONS:
!

REQUIRE 'RTLIN:PASPROLOG';			! Externals, linkages, PSECTs, structures

!
! TABLE OF CONTENTS:
!

FORWARD ROUTINE
    PAS$READ_UNSIGNED,				! Read from textfile
    PAS$READV_UNSIGNED;				! Read from string

!
! MACROS:
!
!	NONE
!
! EQUATED SYMBOLS:
!
!	NONE
!
! FIELDS:
!
!	NONE
!
! OWN STORAGE:
!
!	NONE
!

%SBTTL 'PAS$READ_UNSIGNED - Read an unsigned integer from textfile'
GLOBAL ROUTINE PAS$READ_UNSIGNED (
	PFV: REF $PAS$PFV_FILE_VARIABLE, 		! File variable
	ERROR						! Error unwind address
    ) =

!++
! FUNCTIONAL DESCRIPTION:
!
!	This function reads an unsigned integer from the specified textfile
!	and returns it as the function value.
!
! CALLING SEQUENCE:
!
!	Unsigned.wlu.v = PAS$READ_UNSIGNED (PFV.mr.r [, ERROR.ja.r])
!
! FORMAL PARAMETERS:
!
!	PFV		- The Pascal File Variable (PFV) passed by reference.
!			  The structure of the PFV is defined in PASPFV.REQ.
!
!	ERROR		- Optional.  If specified, the address to unwind to
!			  in case of an error.
!
! IMPLICIT INPUTS:
!
!	NONE
!
! IMPLICIT OUTPUTS:
!
!	NONE
!
! ROUTINE VALUE:
!
!	The value of the unsigned integer read.
!
! SIDE EFFECTS:
!
!	If the file is the standard file INPUT or OUTPUT, it is implicitly opened.
!
! SIGNALLED ERRORS:
!
!	INVSYNUNS - invalid syntax for unsigned value
!	NOTVALTYP - "string" is not a value of type "type"
!
!--

    BEGIN

    LOCAL
	RESULT,				! Result value
	STRING_LENGTH,			! Length of string
	STRING_ADDR,			! Address of string
	FCB: REF $PAS$FCB_CONTROL_BLOCK,! File Control block
	PFV_ADDR: VOLATILE,		! Enable argument
	UNWIND_ACT: VOLATILE,		! Enable argument
	ERROR_ADDR: VOLATILE;		! Enable argument

    BUILTIN
	ACTUALCOUNT;			! Count of arguments

    ENABLE
	PAS$$IO_HANDLER (PFV_ADDR, UNWIND_ACT, ERROR_ADDR);	! Enable error handler

    !+
    ! Get ERROR parameter, if present.
    !-

    IF ACTUALCOUNT () GEQU 2
    THEN
	ERROR_ADDR = .ERROR;		! Set unwind address

    PFV_ADDR = PFV [PFV$R_PFV];		! Set PFV address

    !+
    ! Validate PFV and get PFV.
    !-

    PAS$$VALIDATE_PFV (PFV [PFV$R_PFV]; FCB);

    !+
    ! Set unwind action to unlock file.
    !-

    UNWIND_ACT = PAS$K_UNWIND_UNLOCK;

    !+
    ! Do common initialization.
    !-

    PAS$$INIT_READ (PFV [PFV$R_PFV], FCB [FCB$R_FCB]; FCB);

    !+
    ! Call utility routine to find a string that looks like an unsigned integer.
    ! If we can't find one, signal an error.
    !-

    IF NOT PAS$$GET_UNSIGNED (PFV [PFV$R_PFV], FCB [FCB$R_FCB]; 
	STRING_ADDR, STRING_LENGTH, FCB)
    THEN
	$PAS$IO_ERROR (PAS$_INVSYNUNS,3,.STRING_LENGTH,.STRING_ADDR,
	    .FCB [FCB$L_RECORD_NUMBER])

    !+
    ! Call convert routine.  If it fails, signal an error.
    !-

    ELSE IF NOT LIB$CVT_DTB (.STRING_LENGTH, .STRING_ADDR, RESULT)
    THEN
	BEGIN
	LOCAL
	    DESCR: BLOCK [8,BYTE];
	DESCR [DSC$W_LENGTH] = .STRING_LENGTH;
	DESCR [DSC$B_CLASS] = 0;
	DESCR [DSC$B_DTYPE] = 0;
	DESCR [DSC$A_POINTER] = .STRING_ADDR;
	$PAS$IO_ERROR (PAS$_NOTVALTYP,3,DESCR,
	    UPLIT BYTE (%CHARCOUNT('UNSIGNED'),'UNSIGNED'),
	    .FCB [FCB$L_RECORD_NUMBER]);
	END;

    !+
    ! Do end-of-READ processing.
    !-

    PAS$$END_READ (PFV [PFV$R_PFV], FCB [FCB$R_FCB]);

    RETURN .RESULT;

    END;					! End of routine PAS$READ_UNSIGNED

!<BLF/PAGE>

%SBTTL 'PAS$READV_UNSIGNED - Read an unsigned from string'
GLOBAL ROUTINE PAS$READV_UNSIGNED (
	STRING: REF BLOCK [, BYTE],			! String descriptor
	ERROR						! Error unwind address
    ) =

!++
! FUNCTIONAL DESCRIPTION:
!
!	This function reads an unsigned integer from the specified string
!	and returns it as the function value.
!
! CALLING SEQUENCE:
!
!	Unsigned.wlu.v = PAS$READV_UNSIGNED (STRING.mt.ds [, ERROR.ja.r])
!
! FORMAL PARAMETERS:
!
!	STRING		- The string to read from, passed as a class S
!			  (assumed) descriptor.  The length and pointer
!			  are updated to reflect the unread string.
!
!	ERROR		- Optional.  If specified, the address to unwind to
!			  in case of an error.
!
! IMPLICIT INPUTS:
!
!	NONE
!
! IMPLICIT OUTPUTS:
!
!	NONE
!
! ROUTINE VALUE:
!
!	The value of the unsigned integer read.
!
! SIDE EFFECTS:
!
!	NONE
!
! SIGNALLED ERRORS:
!
!	NONE
!
!--

    BEGIN

    LOCAL
	PFV: $PAS$PFV_FILE_VARIABLE,	! Pascal File Variable
	RESULT,				! Result value
	ARG_LIST: VECTOR [2, LONG],	! Argument list
	PFV_ADDR: VOLATILE,		! Enable argument
	UNWIND_ACT: VOLATILE,		! Enable argument
	ERROR_ADDR: VOLATILE;		! Enable argument

    BUILTIN
	ACTUALCOUNT;			! Count of arguments

    ENABLE
	PAS$$IO_HANDLER (PFV_ADDR, UNWIND_ACT, ERROR_ADDR);	! Enable error handler

    !+
    ! Get ERROR parameter, if present.
    !-

    IF ACTUALCOUNT () GEQU 2
    THEN
	ERROR_ADDR = .ERROR;		! Set unwind address

    PFV_ADDR = PFV [PFV$R_PFV];		! Set PFV address

    !+
    ! Set up ARG_LIST.  
    !-

    ARG_LIST [0] = 1;			! One argument
    ARG_LIST [1] = PFV [PFV$R_PFV];	! PFV address

    !+
    ! Call PAS$$DO_READV to do the work, giving it the address of
    ! PAS$READ_UNSIGNED to call.
    !-

    PAS$$DO_READV (PFV [PFV$R_PFV], .STRING, ARG_LIST, PAS$READ_UNSIGNED; 
	RESULT);

    RETURN .RESULT;

    END;					! End of routine PAS$READV_UNSIGNED

!<BLF/PAGE>

END						! End of module PAS$READ_UNSIGNED

ELUDOM
