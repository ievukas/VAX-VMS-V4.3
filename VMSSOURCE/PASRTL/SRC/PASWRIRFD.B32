MODULE PAS$WRITE_REALF_D ( %TITLE 'Write a D_floating in F format'
		IDENT = '1-003'			! File: PASWRIRFD.B32 Edit: DG1003
		) =
BEGIN
!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982, 1984 BY				    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************
!

!++
! FACILITY:	Pascal Language Support
!
! ABSTRACT:
!
!	This module contains a procedure which writes a D_floating in
!	fixed-point notation to a textfile.
!
! ENVIRONMENT:	User mode - AST reentrant
!
! AUTHOR: Steven B. Lionel, CREATION DATE: 1-April-1981
!
! MODIFIED BY:
!
! 1-001	- Original.  SBL 1-April-1981
! 1-002 - Make total-width a longword.  SBL 30-June-1982
! 1-003 - Allocate space for field width fraction specification.  DG 19-Oct-1983
!--


%SBTTL 'Declarations'
!
! PROLOGUE DEFINITIONS:
!

REQUIRE 'RTLIN:PASPROLOG';			! Externals, linkages, PSECTs, structures

!
! TABLE OF CONTENTS:
!

FORWARD ROUTINE
    PAS$WRITE_REALF_D: NOVALUE,			! Write to textfile
    PAS$WRITEV_REALF_D: NOVALUE;		! Write to string

!
! MACROS:
!
!	NONE
!
! EQUATED SYMBOLS:
!
!	NONE
!
! FIELDS:
!
!	NONE
!
! OWN STORAGE:
!
!	NONE
!

%SBTTL 'PAS$WRITE_REALF_D - Write D_floating in F format to textfile'
GLOBAL ROUTINE PAS$WRITE_REALF_D (
	PFV: REF $PAS$PFV_FILE_VARIABLE, 		! File variable
	VALUE_0,VALUE_1,				! Value to write
	TOTAL_WIDTH: SIGNED,				! Total field width
	FRAC_DIGITS: SIGNED,				! Digits in fraction
	ERROR						! Error unwind address
    ): NOVALUE =

!++
! FUNCTIONAL DESCRIPTION:
!
!	This procedure writes a D_floating value in fixed-point notation
!	to the specified textfile.
!
! CALLING SEQUENCE:
!
!	CALL PAS$WRITE_REALF_D (PFV.mr.r, VALUE.rd.v , TOTAL_WIDTH.rl.v
!			       , FRAC_DIGITS.rl.v [, ERROR.ja.r]])
!
! FORMAL PARAMETERS:
!
!	PFV		- The Pascal File Variable (PFV) passed by reference.
!			  The structure of the PFV is defined in PASPFV.REQ.
!
!	VALUE		- The D_floating value to write, by immediate value.
!			  Note that this takes up two argument list positions.
!
!	TOTAL_WIDTH	- Total field width.
!
!	FRAC_DIGITS	- Number of digits in fraction.
!
!	ERROR		- Optional.  Address to unwind to if an error occurs.
!
! IMPLICIT INPUTS:
!
!	NONE
!
! IMPLICIT OUTPUTS:
!
!	NONE
!
! ROUTINE VALUE:
!
!	NONE
!
! SIDE EFFECTS:
!
!	If the file is the standard file OUTPUT, it is implicitly opened.
!
! SIGNALLED ERRORS:
!
!	LINTOOLON - line too long
!	NEGWIDDIG - negative Width or Digits specification is not allowed
!
!--

    BEGIN

    BUILTIN
	ACTUALCOUNT;

    LOCAL
	FCB: REF $PAS$FCB_CONTROL_BLOCK,		! File control block
	FIELD_WIDTH: SIGNED,				! Minimum/actual width
	REMAINING_WIDTH,				! Maximum width
	PFV_ADDR: VOLATILE,				! Enable argument
	UNWIND_ACT: VOLATILE,				! Enable argument
	ERROR_ADDR: VOLATILE;				! Enable argument

    ENABLE
	PAS$$IO_HANDLER (PFV_ADDR, UNWIND_ACT, ERROR_ADDR);	! Enable error handler

    !+
    ! Get ERROR parameter, if present.
    !-

    IF ACTUALCOUNT () GEQU 6
    THEN
	ERROR_ADDR = .ERROR;		! Set unwind address

    PFV_ADDR = PFV [PFV$R_PFV];		! Set PFV address

    !+
    ! Validate PFV and get PFV.
    !-

    PAS$$VALIDATE_PFV (PFV [PFV$R_PFV]; FCB);

    !+
    ! Set unwind action to unlock file.
    !-

    UNWIND_ACT = PAS$K_UNWIND_UNLOCK;

    !+
    ! Do common initialization.
    !-

    PAS$$INIT_WRITE (PFV [PFV$R_PFV], FCB [FCB$R_FCB]; FCB);

    !+
    ! Get field width and remaining width.  Check for invalid width or digits.
    !-

    FIELD_WIDTH = .TOTAL_WIDTH;
    IF (.FIELD_WIDTH LSS 0) OR (.FRAC_DIGITS LSS 0)
    THEN
	$PAS$IO_ERROR (PAS$_NEGWIDDIG,0);
    REMAINING_WIDTH = .FCB [FCB$A_RECORD_END] - .FCB [FCB$A_RECORD_CUR];

    !+
    ! Convert to text and put in record buffer.  If an error occurs, signal it.
    !-

    IF NOT PAS$CVT_D_T (VALUE_0,		! Value
    			.FCB [FCB$A_RECORD_CUR],! Destination
    			FIELD_WIDTH,		! Minimum/actual width
    			.REMAINING_WIDTH,	! Maximum width
			.FRAC_DIGITS)		! Fraction digits
    THEN
	$PAS$IO_ERROR (PAS$_LINTOOLON,1,(.FIELD_WIDTH-.REMAINING_WIDTH));

    !+
    ! Advance buffer pointer.
    !-

    FCB [FCB$A_RECORD_CUR] = .FCB [FCB$A_RECORD_CUR] + .FIELD_WIDTH;

    !+
    ! Call WRITE epilogue routine to move the last character written to the
    ! user's buffer and to unlock the file variable.
    !-

    PAS$$END_WRITE (PFV [PFV$R_PFV], FCB [FCB$R_FCB]);

    RETURN;

    END;					! End of routine PAS$WRITE_REALF_D

!<BLF/PAGE>

%SBTTL 'PAS$WRITEV_REALF_D - Write F_floating in F format to string'
GLOBAL ROUTINE PAS$WRITEV_REALF_D (
	MAX_LENGTH: WORD,				! Maximum length of string
	STRING_LINE: REF VECTOR [, WORD],		! String to write to
	VALUE0,VALUE1,					! Value to write
	TOTAL_WIDTH: SIGNED,				! Total field width
	FRAC_DIGITS: SIGNED,				! Fraction digits
	ERROR						! Error unwind address
    ) : NOVALUE =

!++
! FUNCTIONAL DESCRIPTION:
!
!	This procedure writes a D_floating in fixed-point format 
!	to the specified string.
!
! CALLING SEQUENCE:
!
!	CALL PAS$WRITEV_REALF_D (MAX_LENGTH.rw.v, STRING_LINE.wvt.r,
!	   VALUE.rd.v, TOTAL_WIDTH.rl.v, FRAC_DIGITS.rl.v [, ERROR.j.r])
!
! FORMAL PARAMETERS:
!
!	MAX_LENGTH	- The maximum length of STRING_LINE.
!
!	STRING_LINE	- A varying string to which the output will be appended.
!
!	VALUE		- The value to write.
!
!	TOTAL_WIDTH	- The width of the field to write.
!
!	FRAC_DIGITS	- The number of digits in the fraction.
!
!	ERROR		- Optional.  If specified, the address to unwind to
!			  in case of an error.
!
! IMPLICIT INPUTS:
!
!	NONE
!
! IMPLICIT OUTPUTS:
!
!	NONE
!
! ROUTINE VALUE:
!
!	NONE
!
! SIDE EFFECTS:
!
!	NONE
!
! SIGNALLED ERRORS:
!
!	See PAS$WRITE_REALF_D
!
!--

    BEGIN

    LOCAL
	PFV: $PAS$PFV_FILE_VARIABLE,	! Pascal File Variable
	ARG_LIST: VECTOR [6, LONG],	! Argument list
	PFV_ADDR: VOLATILE,		! Enable argument
	UNWIND_ACT: VOLATILE,		! Enable argument
	ERROR_ADDR: VOLATILE;		! Enable argument

    BUILTIN
	ACTUALCOUNT;			! Count of arguments

    ENABLE
	PAS$$IO_HANDLER (PFV_ADDR, UNWIND_ACT, ERROR_ADDR);	! Enable error handler

    !+
    ! Get ERROR parameter, if present.
    !-

    IF ACTUALCOUNT () GEQU 6
    THEN
	ERROR_ADDR = .ERROR;		! Set unwind address

    PFV_ADDR = PFV [PFV$R_PFV];		! Set PFV address

    !+
    ! Set up ARG_LIST.
    !-

    ARG_LIST [0] = 5;			! Five arguments
    ARG_LIST [1] = PFV [PFV$R_PFV];	! PFV address
    ARG_LIST [2] = .VALUE0;		! Value to write
    ARG_LIST [3] = .VALUE1;		!
    ARG_LIST [4] = .TOTAL_WIDTH;	! Field width
    ARG_LIST [5] = .FRAC_DIGITS;	! Fraction digits

    !+
    ! Call PAS$$DO_WRITEV to do the work, giving it the address of
    ! PAS$WRITE_REALF_D to call.
    !-

    PAS$$DO_WRITEV (PFV [PFV$R_PFV], .MAX_LENGTH, STRING_LINE [0], ARG_LIST, 
	PAS$WRITE_REALF_D);

    RETURN;

    END;					! End of routine PAS$WRITEV_REALF_D

!<BLF/PAGE>

END						! End of module PAS$WRITE_REALF_D

ELUDOM
