	$BEGIN	NT0ACCESS,000,NF$NETWORK,<NETWORK ACCESS/DEACCESS>

;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982, 1984 BY				    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;

;++
; Facility: RMS
;
; Abstract:
;
;	This module performs network access/deaccess functions including
;	assigning a channel to the network device and altering the
;	characteristics of the network device as appropriate. The access
;	function creates a logical link between this process and either:
;	(1) the specified target task, or
;	(2) FAL (file access listener) for remote file access.
;
; Environment: VAX/VMS, executive mode
;
; Author: James A. Krycka,	Creation Date:	09-DEC-1977
;
; Modified By:
;
;	V03-009	JAK0146		J A Krycka	27-Jun-1984
;		Copy entire task specification string to the NCB in NT$ACCESS.
;
;	V03-008	JEJ0029		J E Johnson	17-Apr-1984
;		Fix bug caused by previous change.
;
;	V03-007	JEJ0005		J E Johnson	08-Mar-1984
;		Process quoted filename specifications here to determine if
;		a DAP or NSP request has been made.
;
;	V03-006	JAK0124		J A Krycka	06-Sep-1983
;		Make corresponding source code change for VMS V3.5 patch in
;		support of VAXELAN.
;
;	V03-005	RAS0174		Ron Schaefer	29-Jul-1983
;		Change reference of FAB$V_UFM to FAB$V_CHAN_MODE.
;
;	V03-004	JAK0105		J A Krycka	11-May-1983
;		Add comments.
;
;	V03-003	KBT0425		Keith B. Thompson	01-Dec-1982
;		Change IFB$W_DEVBUFSIZ to IFB$L_DEVBUFSIZ.
;
;	V03-002	KBT0309		Keith B. Thompson	27-Aug-1982
;		Fix some broken branches.
;
;	V03-001	JWH0001		Jeffrey W. Horn	29-Jun-1982
;		Fix broken BSBW branch.
;
;--

	.SBTTL	DECLARATIONS

;
; Include Files:
;

	$DAPPLGDEF			; Define DAP prologue symbols
	$DAPATTDEF			; Define DAP Attributes message
	$DEVDEF				; Define Device Characteristics
	$FABDEF				; Define File Access Block symbols
	$FWADEF				; Define File Work Area symbols
	$IFBDEF				; Define IFAB symbols
	$IODEF				; Define I/O function codes
	$NAMDEF				; Define NAM block symbols
	$NWADEF				; Define Network Work Area symbols
	$PSLDEF				; Define Process Status Longword symbols
	$RMSDEF				; Define RMS exit code symbols

;
; Macros:
;
;	None
;
; Equated Symbols:
;

	ASSUME	DAP$Q_DCODE_FLG EQ 0
	ASSUME	NWA$Q_FLG EQ 0

;
; Own Storage:
;
;	None
;

	.SBTTL	NT$ASSIGN - NETWORK ASSIGN CHANNEL

;++
; NT$ASSIGN - assigns a channel to the network device (i.e., _NET0:).
;
; Calling Sequence:
;
;	BSBW	NT$ASSIGN
;
; Input Parameters:
;
;	R8	FAB address
;	R9	IFAB address
;	R10	FWA address
;	R11	Impure Area address
;
; Implicit Inputs:
;
;	None
;
; Output Parameters:
;
;	R0	Status code (SS)
;	R1-R3	Destroyed
;
; Implicit Outputs:
;
;	FAB$B_ACMODES
;	FWA$V_OBJTYPE
;	IFB$W_CHNL
;	IFB$B_MODE
;	IFB$L_NWA_PTR
;	NWA$Q_NCB
;
; Completion Codes:
;
;	Standard system service status codes
;
; Side Effects:
;
;	None
;
;--

NT$ASSIGN::				; Entry point
	BBC	#FWA$V_QUOTED,(R10),5$	; Not quoted, must be DAP request
	BSBB	PARSE_QUOTED_STRING	; Parse the quoted specification
	BLBC	R0,20$			; If parse failed, return the error

;
; Setup device name descriptor and associated string for input to the
; Assign Channel system service.
;

5$:	MOVL	IFB$L_NWA_PTR(R9),R1	; Get address of NWA
	MOVAQ	NWA$Q_NCB(R1),R2	; Get address of scratch descriptor
	MOVAB	NWA$T_NCBBUF(R1),R3	; Get address of scratch buffer
	MOVL	#6,(R2)			; Fill in string count
	MOVL	R3,4(R2)		; Fill in string address
	MOVQ	#^A\_NET0:  \,(R3)	; Store device name string

;
; Determine which mode to use in making the channel assignment.
;

	MOVL	#PSL$C_EXEC,R1		; Assume executive mode
	BBC	#FAB$V_UFO,-		; Branch if UFO clear
		FAB$L_FOP(R8),10$	;
	EXTZV	#FAB$V_CHAN_MODE,-	; Get requested mode
		#FAB$S_CHAN_MODE,-	;
		FAB$B_ACMODES(R8),R1	;
	CMPB	IFB$B_MODE(R9),R1	; Maximize this with caller's mode
	BLEQU	10$			;
	MOVZBL	IFB$B_MODE(R9),R1	; Switch to caller's mode

;
; Create a control/information path to NETACP in preparation for
; non-transparent network I/O. Do not associate a mailbox with the channel,
; as secondary inbound connect initiates and interrupt messages are not
; suppported.
;

10$:	$ASSIGN_S-			; Assign the channel
		DEVNAM=(R2)-		;  Address of device name descriptor
		CHAN=IFB$W_CHNL(R9)-	;  Address to return channel #
		ACMODE=R1		;  Access mode
20$:	RSB				; Exit with system service code in R0

;++
; Parse the quoted file name string to determine the network specific
; information.
;
;   (1) Set the OBJTYPE flag if the quoted string contains an equals character
;	to indicate that it is of the "objecttype=..." form. This is form 1.
;
;   (2) Set the NETSTR flag if the quoted string contains a slash character
;	after the equals character to indicate that it is of the
;	"objecttype=taskname/netacp_data" form. Note that the logical name
;	translation of SYS$NET yields an equivalence string containing a
;	quoted string of this form. This is form 2.
;
;   (3) In addition, if the quoted string is of the form given in 3, then
;	store a character count that represents the number of characters
;	in the substring /netacp_data" (including the trailing quote).
;
;   (4) Set the WILDCARD character flag if test 1 failed and the quoted string
;	contains one of the following characters: asterisk, percent sign, or
;	question mark.
;
;   (5) Finally, copy the quoted string (including the quotes) to buffer in NWA.
;
;--

PARSE_QUOTED_STRING:			; Entry point
	PUSHR	#^M<R4,R5,R6,R7>	; Save working registers
	MOVQ	FWA$Q_QUOTED(R10),R6	; Get the quoted string descriptor.

;
; Check for a task specification string enclosed in quotes, i.e.,
; node::"objecttype=..." form.  (1) or (2).
;

	LOCC	#^A\=\,R6,(R7)		; Search for '=' within quoted string
	BEQL	10$			; Branch if no match (R0=0 on no match)
	SSB	#FWA$V_OBJTYPE,(R10)	; Flag 'objecttype=...' form
					;  of quoted string

;
; Now check if it is of form (2).
;

	LOCC	#^A\/\,R0,(R1)		; Search for '/' within quoted string
	BEQL	30$			; Branch if no match
	CMPW	R0,#4			; Length of /netacp_data" must be
	BLSSU	ERRQUO			;  at least 4 characters
	SSB	#FWA$V_NETSTR,(R10)	; Flag 'objecttype=taskname/...' form
	BRB	30$			;  of quoted string

;
; Check for a wildcard foreign file specification string enclosed in quotes,
; i.e., node::"foreign-filespec" form.
;
; Note:	Since the parse operation is perform entirely at the local node, there
;	is no way to tell for sure whether or not the file specification
;	contains any wildcard characters (as understood by the remote system).
;	The best we can do here is to scan the quoted string for likey wildcard
;	characters (*, %, and ?), and set the FWA$V_WILDCARD bit if any are
;	found.
;

10$:	LOCC	#^A\*\,R6,(R7)		; Search for '*' within quoted string
	BNEQ	20$			;  and branch if found
	LOCC	#^A\%\,R6,(R7)		; Search for '%' within quoted string
	BNEQ	20$			;  and branch if found
	LOCC	#^A\?\,R6,(R7)		; Search for '?' within quoted string
	BEQL	30$			;  and branch if not found

;
; Store the wildcard status here as a generic wildcard only.
;

20$:	SSB	#FWA$V_WILDCARD,(R10)	; Signal wildcard character present
	MOVL	FAB$L_NAM(R8),R7	; Get the name block pointer
	JSB	RM$CHKNAM		; Ensure that the NAM block is good.
	BLBC	R0,30$			; Branch if not.
	SSB	#NAM$V_WILDCARD,-	; Declare the generic wildcard.
		NAM$L_FNB(R7)		;

;
; Save the number of bytes in the optional "/netacp_data" in the NWA.
;

30$:	MOVL	IFB$L_NWA_PTR(R9),R2	; get address of NWA
	MOVB	R0,NWA$B_NETSTRSIZ(R2)	; store # bytes in /netacp_data"

	RMSSUC
	POPR	#^M<R4,R5,R6,R7>	; Restore working registers
	RSB				; Exit successfully

;
; Process quoted string error.
;

ERRQUO:	RMSERR	QUO			; Declare error in quoted string
	POPR	#^M<R4,R5,R6,R7>	; Restore working registers
	RSB

	.SBTTL	NT$MOD_DEV_CHAR - MODIFY DEVICE CHARACTERISTICS

;++
; NT$MOD_DEV_CHAR - modifies the characteristics of the network device and
;	denotes whether network I/O will be performed at the DAP or NSP level.
;
; Calling Sequence:
;
;	BSBW	NT$MOD_DEV_CHAR
;
; Input Parameters:
;
;	R8	FAB address
;	R9	IFAB address
;	R10	FWA address
;	R11	Impure Area address
;
; Implicit Inputs:
;
;	FWA$V_OBJTYPE
;	FWA$B_SUBNODCNT
;
; Output Parameters:
;
;	None
;
; Implicit Outputs:
;
;	IFB$V_DAP
;	IFB$V_NSP
;	IFB$L_AS_DEV
;	IFB$L_PRIM_DEV
;	IFB$L_DEVBUFSIZ
;
; Completion Codes:
;
;	None
;
; Side Effects:
;
;	None
;
;--

NT$MOD_DEV_CHAR::			; Entry point

;
; Determine whether the network request is for file access via a remote FAL or
; for task-to-task communication.
;
; Note:	If more than one node spec string was specified (manual routing), then
;	treat this as a DAP level access so that RMS will connect to FAL at the
;	adjacent node (which is actually an intermediate node).
;

	TSTB	FWA$B_SUBNODCNT(R10)	; Branch if more than one node spec
	BNEQ	10$			;  was specified
	BBS	#FWA$V_OBJTYPE,(R10),20$; Branch if device name string is
					;  in NSP objtype type format

;
; Denote that network I/O thru RMS will be at the file access level using
; DAP to communicate with the remote FAL.
;
; Note:	IFB$L_DEVBUFSIZ may be adjusted upward by NT$EXCH_CNF after the
;	negotiated DAP buffer size has been determined. For record I/O mode
;	access to a remote file, IFB$L_DEVBUFSIZ limits the maximum record size
;	that can be supported because it is used at $CONNECT time to allocate
;	the BDB buffer through which all records must pass.
;

10$:	$SETBIT	#IFB$V_DAP,(R9)		; Set DAP flag
	MOVZWL	#512,IFB$L_DEVBUFSIZ(R9); Establish initial device buffer size
	BRB	30$			; Join common code

;
; Denote that network I/O thru RMS will be at the task-to-task level using
; NSP to communicate with the remote partner process.
;

20$:	$SETBIT	#IFB$V_NSP,(R9)		; Set NSP flag
	MOVZWL	#4096,IFB$L_DEVBUFSIZ(R9); Establish device buffer size

;
; Alter the device characteristics.
;

30$:	$SETBIT	#DEV$V_REC,IFB$L_PRIM_DEV(R9)
					; Say its a record oriented device
	$CLRBIT	#DEV$V_MBX,IFB$L_PRIM_DEV(R9)
					; Say its a not a mailbox-like device
	MOVL	IFB$L_PRIM_DEV(R9),-	; Copy device characteristics
		IFB$L_AS_DEV(R9)	;
40$:	RSB				; Exit

	.SBTTL	NT$MAP_DEV_CHAR - MAP DEVICE CHARACTERISTICS

;++
; NT$MAP_DEV_CHAR - takes the device characteristics returned by FAL and maps
;	them into an RMS bit pattern, then saves them in NWA for use later.
;	If the remote device is a terminal or mailbox, this is noted also.
;
; Calling Sequence:
;
;	BSBW	NT$MAP_DEV_CHAR
;
; Input Parameters:
;
;	R7	NWA (=DAP) address
;	R8	FAB address
;	R9	IFAB address
;	R10	FWA address
;	R11	Impure Area address
;
; Implicit Inputs:
;
;	DAP$L_DEV
;	DAP$V_DEV
;
; Output Parameters:
;
;	R0-R2	Destroyed
;
; Implicit Outputs:
;
;	NWA$L_DEV
;	NWA$V_DEVCHAR
;	NWA$V_DEVMBX
;	NWA$V_DEVTRM
;
; Completion Codes:
;
;	None
;
; Side Effects:
;
;	None
;
;--

NT$MAP_DEV_CHAR::			; Entry point
	BBS	#DAP$V_DEV,-		; Branch if partner returned device
		DAP$L_ATTMENU(R7),10$	;  characteristics
	RSB				; Exit
10$:	$SETBIT	#NWA$V_DEVCHAR,(R7)	; Flag receipt of characteristics

;
; Map DAP bit definitions into RMS bit definitions for the field and store them
; in NWA for use later.
;

	MOVL	DAP$L_DEV(R7),R1	; Get DEV bits returned by FAL
	CLRL	R2			; Clear corresponding RMS bits
	$MAPBIT	DAP$V_DEVREC,DEV$V_REC	; Map REC bit
	$MAPBIT	DAP$V_DEVCCL,DEV$V_CCL	; Map CCL bit
	$MAPBIT	DAP$V_DEVTRM,DEV$V_TRM	; Map TRM bit
	$MAPBIT	DAP$V_DEVDIR,DEV$V_DIR	; Map DIR bit
	$MAPBIT	DAP$V_DEVSDI,DEV$V_SDI	; Map SDI bit
	$MAPBIT	DAP$V_DEVSQD,DEV$V_SQD	; Map SQD bit
	$MAPBIT	DAP$V_DEVSPL,DEV$V_SPL	; Map SPL bit
	$MAPBIT	DAP$V_DEVNET,DEV$V_NET	; Map NET bit
	$MAPBIT	DAP$V_DEVFOD,DEV$V_FOD	; Map FOD bit
	$MAPBIT	DAP$V_DEVSHR,DEV$V_SHR	; Map SHR bit
	$MAPBIT	DAP$V_DEVGEN,DEV$V_GEN	; Map GEN bit
	$MAPBIT	DAP$V_DEVAVL,DEV$V_AVL	; Map AVL bit
	$MAPBIT	DAP$V_DEVMNT,DEV$V_MNT	; Map MNT bit
	$MAPBIT	DAP$V_DEVMBX,DEV$V_MBX	; Map MBX bit
	$MAPBIT	DAP$V_DEVDMT,DEV$V_DMT	; Map DMT bit
	$MAPBIT	DAP$V_DEVELG,DEV$V_ELG	; Map ELG bit
	$MAPBIT	DAP$V_DEVALL,DEV$V_ALL	; Map ALL bit
	$MAPBIT	DAP$V_DEVFOR,DEV$V_FOR	; Map FOR bit
	$MAPBIT	DAP$V_DEVSWL,DEV$V_SWL	; Map SWL bit
	$MAPBIT	DAP$V_DEVIDV,DEV$V_IDV	; Map IDV bit
	$MAPBIT	DAP$V_DEVODV,DEV$V_ODV	; Map ODV bit
	$MAPBIT	DAP$V_DEVRND,DEV$V_RND	; Map RND bit
	$MAPBIT	DAP$V_DEVRTM,DEV$V_RTM	; Map RTM bit
	$MAPBIT	DAP$V_DEVRCK,DEV$V_RCK	; Map RCK bit
	$MAPBIT	DAP$V_DEVWCK,DEV$V_WCK	; Map WCK bit
	MOVL	R2,NWA$L_DEV(R7)	; Save characteristics for use later
	BBC	#DEV$V_TRM,R2,20$	; Branch if device is not a terminal
	$SETBIT	#NWA$V_DEVTRM,(R7)	; Flag remote device as a terminal
20$:	BBC	#DEV$V_MBX,R2,30$	; Branch if device is not a mailbox
	$SETBIT	#NWA$V_DEVMBX,(R7)	; Flag remote device as a mailbox
30$:	RSB				; Exit

	.SBTTL	NT$RET_DEV_CHAR - RETURN DEVICE CHARACTERISTICS

;++
; NT$RET_DEV_CHAR - returns the true device characteristic information to the
;	user's FAB iff all of the following conditions are met:
;	(1) FAL returned device characteristics in the DAP Attributes message.
;	(2) FAL is implemented to DAP V5.6 or later.
;	(3) The remote node is running VAX/VMS or VAXELAN or the file accessed
;	    is a relative or an indexed file.
;
;	This restriction is here to accomodate the VMS COPY utility which uses
;	the FAB$V_BRO and RAB$V_BIO options to defer the decision of whether
;	to use record I/O ($GET/$PUT) or block I/O ($READ/$WRITE) for sequential
;	files until $CONNECT time. COPY always uses block I/O to transfer
;	relative and indexed files.
;
;	COPY examines the device characteristics returned on $OPEN (and $CREATE)
;	in conjunction with other inputs to determine whether to use record
;	or block I/O. Since it does not know at open time if block I/O will be
;	used, it sets the FAB$V_BRO bit in FAB$B_FAC on $OPEN, and if block I/O
;	mode is chosen, it sets the RAB$V_BIO bit in RAB$L_ROP on $CONNECT to
;	specify block I/O mode.
;
;	Note that NT$GET_FAC_SHR and NT$ENCODE_ROP send the DAP$V_BRO and
;	DAP$V_ROPBIO bits, respectively, based on a similar system specific
;	check.
;
;	Note that the algorithm used to return device characteristics may become
;	less restrictive in the future, especially if COPY is modified to avoid
;	using the FAB$V_BRO and RAB$V_BIO options.
;
; Calling Sequence:
;
;	BSBW	NT$RET_DEV_CHAR
;
; Input Parameters:
;
;	R8	FAB address
;	R9	IFAB address
;	R10	FWA address
;	R11	Impure Area address
;
; Implicit Inputs:
;
;	DAP$V_GEQ_V56
;	DAP$V_VAXVMS, DAP$V_VAXELAN
;	IFB$L_NWA_PTR
;	NWA$B_ORG
;	NWA$L_DEV
;	NWA$V_DEVCHAR
;
; Output Parameters:
;
;	R1	Destroyed
;
; Implicit Outputs:
;
;	FAB$L_DEV
;	FAB$L_SDC
;
; Completion Codes:
;
;	None
;
; Side Effects:
;
;	None
;
;--

NT$RET_DEV_CHAR::			; Entry point
	MOVL	IFB$L_NWA_PTR(R9),R1	; Get address of NWA (and DAP)
	BBC	#NWA$V_DEVCHAR,(R1),20$	; Branch if partner did not return
					;  device characteristic information
	BBC	#DAP$V_GEQ_V56,(R1),20$ ; Branch if partner uses DAP before V5.6
	BBS	#DAP$V_VAXVMS,(R1),10$	; Branch if partner is VAX/VMS
	BBS	#DAP$V_VAXELAN,(R1),10$	; Branch if partner is VAXELAN
	CMPB	NWA$B_ORG(R1),#NWA$K_SEQ; Branch if SEQ organization
	BEQL	20$			;  else fall thru if REL or IDX
10$:	MOVL	NWA$L_DEV(R1),R1	; Get actual device characteristics
	$SETBIT	#DEV$V_NET,R1		; Declare this a remote network device
	MOVL	R1,FAB$L_DEV(R8)	; Update user DEV field in FAB
	MOVL	R1,FAB$L_SDC(R8)	; Update user SDC field in FAB
20$:	RSB				; Exit

	.SBTTL	NT$ACCESS - PERFORM NETACP ACCESS FUNCTION

;++
; NT$ACCESS - creates a logical link.
;
; Calling Sequence:
;
;	BSBW	NT$ACCESS
;
; Input Parameters:
;
;	R8	FAB address
;	R9	IFAB address
;	R10	FWA address
;	R11	Impure Area address
;
; Implicit Inputs:
;
;	IFB$V_DAP
;	IFB$W_CHNL
;	FWA$Q_NODE
;	FWA$V_NETSTR
;	NWA$Q_QUOTED
;
; Output Parameters:
;
;	R0	Status code (SS)
;	R1-R6	Destroyed
;	AP	Destroyed
;
; Implicit Outputs:
;
;	IFB$L_IOS
;	IFB$V_ACCESSED
;	NWA$Q_NCB
;
; Completion Codes:
;
;	Standard system service status codes
;
; Side Effects:
;
;	None
;
;--

NT$ACCESS::				; Entry point

;+
; Build a Network Connect Block (NCB) to be used as input for the NETACP
; access function.
;
; The NCB consists of a string with the following general syntax:
;
;	nodename"access_control_string"::"objecttype=taskid/netacp_data"
;
; Where:
;   (1)	the access control string in the node spec is present only if provided
;	by the user (directly or via logical name translation).
;   (2)	for accessing a remote file, the quoted string used by RMS is "FAL="
;	(to request the services of the remote file access listener).
;   (3)	for user task-to-task communication, the quoted string used is the one
;	supplied which may include an optional data counted string. Note that
;	the logical name SYS$NET used as a file specification translates to a
;	string of the node::"objecttype=..." form.
;
; Obtain the node spec string.
;-

	MOVL	IFB$L_NWA_PTR(R9),R1	; Get address of NWA
	MOVAQ	NWA$Q_NCB(R1),R6	; Get address of scratch descriptor
	MOVAB	NWA$T_NCBBUF(R1),R3	; Get address of scratch buffer
	MOVL	R3,4(R6)		; Fill in descriptor address
	MOVC3	FWA$Q_NODE1(R10),-	; Copy primary (first) node spec string
		@FWA$Q_NODE1+4(R10),(R3);  to NCB including double colon

;
; Obtain the quoted string.
;
; Determine whether the network request is for file access via a remote FAL or
; for task-to-task communication.
;

	BBS	#IFB$V_DAP,(R9),10$	; Branch if file access request

;
; It is a task-to-task communication request.
;

	$TSTPT	NTACC_NSP		;
	MOVC3	FWA$Q_QUOTED(R10),-	; Append quoted string to NCB
		@FWA$Q_QUOTED+4(R10),(R3)
	BRB	20$			; We're finished building NCB

;
; It is a remote file acces request.
;

10$:	$TSTPT	NTACC_DAP		;
	MOVL	#^A\"FAL\,(R3)+		; Request object type FAL
	MOVW	#^A\="\,(R3)+		;

;
; Calculate the size of the NCB and store it in the NCB descriptor block.
;

20$:	SUBL3	4(R6),R3,(R6)		; Fill in descriptor size

;+
; Perform the NETACP access function. It will be either an NSP connect
; initiate or an NSP connect confirm function. Both use the same function and
; subfunction codes, but NETACP differentiates based on the context of the call
; (whether or not the taskspec string in the NCB contains a slash followed by
; a two-byte nonzero DECnet link identifier).
;-

	JSB	RM$SETEFN		; Request event flag number to use
	MOVL	(SP)+,R2		;  and store it
	MOVL	R6,R1			; Copy address of NCB descriptor
	POPL	R6			; Save return PC
	$QIO_S-				; Issue connect initiate/confirm
		EFN=R2-			;  Event flag #
		CHAN=IFB$W_CHNL(R9)-	;  Channel #
		FUNC=#IO$_ACCESS!IO$M_ACCESS- ;  Function code
		IOSB=IFB$L_IOS(R9)-	;  I/O status block
		ASTADR=L^RM$STALLAST-	;  AST address
		ASTPRM=R9-		;  AST parameter
		P1=0-			;  Must be zero
		P2=R1			;  Address of NCB descriptor
	BLBC	R0,30$			; Branch on failure
	JSB	RM$STALL		; Await completion
	BLBC	R0,30$			; Branch on failure

;
; State that ACP access function has been performed successfully. This will
; trigger the network deaccess code at close time.
;

	$SETBIT	#IFB$V_ACCESSED,(R9)	; File has been accessed
30$:	JMP	(R6)			; Return to caller

	.SBTTL	NT$DEACCESS - PERFORM NETACP DEACCESS FUNCTION

;++
; NT$DEACCESS - destroys a logical link.
;
; Calling Sequence:
;
;	BSBW	NT$DEACCESS
;
; Input Parameters:
;
;	R8	FAB address
;	R9	IFAB address
;	R10	FWA address
;	R11	Impure Area address
;
; Implicit Inputs:
;
;	IFB$W_CHNL
;
; Output Parameters:
;
;	R0	Status code (SS)
;	R1-R4	Destroyed
;	AP	Destroyed
;
; Implicit Outputs:
;
;	IFB$L_IOS
;
; Completion Codes:
;
;	Standard system service status codes
;
; Side Effects:
;
;	None
;
;--

NT$DEACCESS::				; Entry point
	$TSTPT	NTDEACCES		;
	POPL	R4			; Save return PC
	JSB	RM$SETEFN		; Request event flag number to use
	MOVL	(SP)+,R2		;  and store it
	$QIO_S-				; Issue synchronous disconnect
		EFN=R2-			;  Event flag #
		CHAN=IFB$W_CHNL(R9)-	;  Channel #
		FUNC=#IO$_DEACCESS!IO$M_SYNCH- ;  Function code
		IOSB=IFB$L_IOS(R9)-	;  I/O status block
		ASTADR=L^RM$STALLAST-	;  AST address
		ASTPRM=R9-		;  AST parameter
		P1=0-			;  Must be zero
		P2=#0			;  Specify no userdata to return
	BLBC	R0,10$			; Branch on failure
	JSB	RM$STALL		; Await completion
10$:	JMP	(R4)			; Return to caller

	.END				; End of module
