 	.TITLE	NETEVTLOG - Process Event logging needs
	.IDENT	'V04-000'
	.DEFAULT DISPLACEMENT,WORD

;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982, 1984 BY				    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;
;
; FACILITY:	NETWORK ACP
;
; ABSTRACT:
;
;	This module performs the bulk of processing required to
;	take care of network event logging needs.
;
; ENVIRONMENT:
;
;	MODE = KERNEL
;
; AUTHOR:	Scott G. Davis, CREATION DATE: 03-JUL-1980
;
; MODIFIED BY:
;
;	V015	RNG0015		Rod Gamache	18-Jun-1984
;		Log Data Base Re-used events.
;
;	V014	TMH0014		Tim Halvorsen	28-Apr-1983
;		Make LDO use "Adjacent node" rather than
;		"Expected node".
;
;	V013	RNG0013		Rod Gamache	21-Apr-1983
;		Save/Restore R1 in NET$DBC_EFI/ESI.
;
;	V012	TMH0012		Tim Halvorsen	07-Apr-1983
;		Allow caller to specify that no REASON parameter is to
;		be logged on TPL events.
;
;	V011	TMH0011		Tim Halvorsen	29-Mar-1983
;		Add "aborted service request".
;
;	V010	TMH0010		Tim Halvorsen	22-Dec-1982
;		Modify a number of events to log packet beginning
;		(first 16 bytes) rather than packet header, which
;		implies a Phase III route header.
;
;	V009	TMH0009		Tim Halvorsen	05-Nov-1982
;		Add code to suppress the area number in node
;		addresses, if area routing is being hidden.
;		Fix area reachability chage so that it reports
;		the source area, not the source node.
;
;	V008	TMH0008		Tim Halvorsen	16-Sep-1982
;		Add support for automatic line counters.
;
;	V007	TMH0007		Tim Halvorsen	27-Jul-1982
;		Add support to handle Phase IV events.
;		Increase size of event buffer to allow for a large
;		number of "node reachable" events at startup time.
;		Rewrite READ_EVENTS so that it correctly transfers
;		only as many WHOLE events as will fit into the caller's
;		buffer, and so that it correctly shifts the remaining
;		events in the event buffer correctly.  The previous
;		code was returning partial event records to EVL, and
;		causing the number of bytes "left" to be incorrectly
;		computed to be a very small number, thus wasting most
;		of the event buffer.
;
;	V006	TMH0006		Tim Halvorsen	30-Jun-1982
;		Dynamically allocate event buffer, rather than having
;		it statically defined in impure own storage.
;		Remove all explicit addressing specifiers, and make
;		the default addressing = word for the entire module.
;
;	V005	TMH0005		Tim Halvorsen	12-Apr-1982
;		Get address of utility buffer from cell, rather than
;		referencing a statically defined location.
;		Fix STARTUP_EVL to queue a WQE to do the job, since
;		STARTUP_OBJ calls CNF action routines, some of which
;		allocate the CNF static temporary buffer.  Unfortunately,
;		this is required because we may be logging counters while
;		having the static temporary buffer allocated (CNT does this).
;		Fix code to search database using FNDMIN operator to expect
;		that the matched CNF will be returned in R10.
;		Fix bug which prevented the node address from being shown
;		in the event display if there is no node name currently
;		associated with that address.
;		Fix bug in reporting of "packet format error" event which
;		showed garbage after "packet beginning" parameter.
;		Add formatting for "local node state change",
;		"locally initiated state change", and "remotely initiated
;		state change" events.
;
;	V03-04	ADE0025		A.Eldridge	01-Feb-1981
;		Remove parameter count in front of counter block.
;		Fix database determination while processing the counter timer.
;
;	V03-03	ADE0024		A.Eldridge	19-Jan-1981
;		Include the "packet beginning" and not the "packet header"
;		as part of the event data for circuit initialization failure
;		events.
;
;	V03-02	ADE0023		Al Eldridge	30-Nov-1981
;		Added zero counter event.
;
;	V03-01			Al Eldridge	01-Nov-1981
;		Udgrade to V3.0.0 Network management.  The changes are
;		primarily related to the change to the new Circuit/Line
;		model of the datalink layer.
;
;	V022	ADE0022		Al Eldridge	05-Sep-1980
;		Further fixes to counter logging.
;
;	V021	TMH0021		Tim Halvorsen	04-Sep-1980
;		Pass null string as SYS$NET to EVL process.  Preserve all 
;		registers in NET$DBC_EFI,ESI.  Remove temporary definition of 
;		EVC$C_VMS_DBC (was decimal 2000, should be hex 2000) and use 
;		$EVCDEF.
;
;	V020	ADE0020		Al Eldridge	20-Aug-1980
;		Log internally detected events. Log counters.
;

	.SBTTL	DECLARATIONS
;
; MACROS
;
	$ADJDEF
	$MSGDEF
	$NETSYMDEF
	$NETUPDDEF
	$NFBDEF
	$CNFDEF
	$CNRDEF
	$PRVDEF
	$RCBDEF
	$NMADEF
	$EVCDEF
	$RAWDEF
	$WQEDEF

;
; EQUATED SYMBOLS:
;

NMA$C_PTY_CM1	= 193			; && until it gets added to $NMADEF


EVL_OBJ		= 26			; Event logger object number
NET$C_EVTTHRESH	= 5			; Event threshold
NET$C_EVTTIMER	= 10*1000*1000*5	; Timer constant
NET$C_EVTBUFLTH	= 8000			; Length of event buffer
NET$C_LSTEVTLTH	= 32			; Length of "lost event" event
NET$C_AVLBUFLTH = NET$C_EVTBUFLTH -	; Length for normal events
		- <2*NET$C_LSTEVTLTH>

;
; mailbox message mask definitions
;

MBX$V_EVTAVL	= 1			; Mask bit for MSG$_EVTAVL
MBX$V_EVTRCVCHG	= 2			; Mask bit for MSG$_EVTRCVCHG
MBX$V_EVTXMTCHG	= 3			; Mask bit for MSG$_EVTXMTCHG

;
; OWN STORAGE:
;
	.PSECT	NET_IMPURE,WRT,NOEXE,LONG

CNX_PLI_OLDTIM:	.LONG	0			; Old CNF timer for PLI's
CNX_CRI_OLDTIM:	.LONG	0			; Old CNF timer for CRI's
CNX_NDI_OLDTIM:	.LONG	0			; Old CNF timer for NDI's

EVT_B_FLAGS:	.BYTE	EVT$M_EVTAVL		; Allow immediate event message

	$VIELD	EVT,0,-				; Define the flags
	<-
	    <EVTAVL,1,M>,-	; Flag implies MSG$_EVTAVL can be sent
	    <LOSTEVENT,1,M>,-	; Flag implies "lost event" event occurred
	    <DBCEVENT,1,M>,-	; Database change event logged
	    <CST_PLI,1,M>,-	; Line counter suppression timer ticking
	    <CST_CRI,1,M>,-	; Circuit counter suppression timer ticking
	    <CST_NDI,1,M>,-	; Node counter suppression timer ticking
	>

EVT_W_THRESH:	.BLKW	1			; No. of events available
EVT_W_LOST:	.WORD	0			; # event bytes lost
EVT_W_PEAK:	.WORD	0			; Peak value of EVT_W_LOST
BASE_TIME:	.LONG	0			; Base time for counter logging

		.ALIGN	LONG
EVT_L_BUFFER:	.LONG	0			; Address of event buffer
EVT_L_BUFPTR:	.LONG	0			; Ptr to next buffer location

LOST_EVENT:					; Block to hold "lost event" 
		.WORD	10$-LOST_EVENT		; Length of event
		.BLKQ	1			; For time-stamp
		.WORD	EVC$C_NMA_LOS		; Event code
		.BYTE	-1			; No source for this event
		.BLKB	17			; No event-ID
10$:

DBC_EVENT:					; Block containing "DBC event"
		.WORD	10$-DBC_EVENT		; Length of event
		.BLKQ	1			; For time-stamp
		.WORD	EVC$C_VMS_DBC		; Event code
		.BYTE	-1			; No source for this event
		.BLKB	17			; No event-ID
10$:

NET$AB_EVT_WQE::
		.BLKB	WQE$C_LENGTH		; Common WQE for event reporting


	.PSECT	NET_PURE,LONG,NOWRT,NOEXE


	CNX$B_SPARE	=  0	; Spare, reserved for future use
	CNX$B_TIM_SUP	=  1	; RCB suppression timer bit i.d.
	CNX$W_ID_CTM	=  2	; WQE timer REQIDT field and database i.d.
	CNX$L_COUNTER	=  4	; CNF field i.d. of counter string
	CNX$L_DEL_TIME	=  8	; CNF field i.d. of delta timer value
	CNX$L_ABS_TIME	= 12	; CNF field i.d. of absolute timer value
	CNX$L_OLD_TIME  = 16	; Ptr to oldest CNF absolute due time value
	CNX$L_CNR_PTR	= 20	; Ptr to CNR pointer
	CNX$C_LENGTH	= 24

CNX_PLI:			; PLI CNX
	.BYTE	0		; Spare
	.BYTE	evt$v_cst_pli	; Log datalink counter suppression timer id
	.WORD	evc$c_src_lin	; WQE REQIDT value for datalinks
	.CNFFLD	pli,s,cnt	; Datalink counter string field i.d.
	.CNFFLD	pli,l,lct	; Datalink counter timer field i.d.
	.CNFFLD	pli,l,cta	; Datelink absolute timer field i.d.
	.LONG	cnx_pli_oldtim	; Due time of oldest CNFs
	.LONG	net$gl_cnr_pli	; Address of CRI CNR pointer

CNX_CRI:			; CRI CNX
	.BYTE	0		; Spare
	.BYTE	evt$v_cst_cri	; Log datalink counter suppression timer id
	.WORD	evc$c_src_cir	; WQE REQIDT value for datalinks
	.CNFFLD	cri,s,cnt	; Datalink counter string field i.d.
	.CNFFLD	cri,l,lct	; Datalink counter timer field i.d.
	.CNFFLD	cri,l,cta	; Datelink absolute timer field i.d.
	.LONG	cnx_cri_oldtim	; Due time of oldest CNFs
	.LONG	net$gl_cnr_cri	; Address of CRI CNR pointer

CNX_NDI:			; NDI CNX
	.BYTE	0		; Spare
	.BYTE	evt$v_cst_ndi	; Log node counter suppression timer id
	.WORD	evc$c_src_nod	; WQE REQIDT value for nodes
	.CNFFLD	ndi,s,cnt	; Node counter string field i.d.
	.CNFFLD	ndi,l,cti	; Node counter timer field i.d.
	.CNFFLD	ndi,l,cta	; Node absolute timer field i.d.
	.LONG	cnx_ndi_oldtim	; Due time of oldest CNFs
	.LONG	net$gl_cnr_ndi	; Address of NDI CNR pointer


	.PSECT	NET_CODE,NOWRT,LONG,EXE

	.SBTTL	Event timer action routine
;+
; EVT_TIMER - This routine is called when the event timer threshold expires.
;
; FUNCTIONAL DESCRIPTION:
;
; Set the EVENT AVAILABLE flag (NET$V_EVTAVL)
;
;-
EVT_TIMER:
	PUSHL	R5			; Save timer block address
	BISB2	#EVT$M_EVTAVL,-		; Set the flag
		     EVT_B_FLAGS	;
	TSTW	EVT_W_THRESH		; Any events?
	BEQL	10$			; If EQL no msgs, yet
	BSBW	SEND_EVT_MSG		; Send MBX MSG
10$:	POPL	R0			; Recover timer block
	BSBW	WQE$DEALLOCATE		; Deallocate it
	RSB				; Done

	.SBTTL	Internal inbound raw event processing
;+
; NET$EVT_INTRAW - Process raw event detected internally
;
; FUNCTIONAL DESCRIPTION:
;
; A raw event is passed internally via a WQE.  It is formatted and put into
; the event buffer.
;
; INPUTS:	R11	CNR pointer as appropriate
;		R10	CNF pointer as appropriate
;		R9-R7	Scratch
;		R6	LPD pointer if datalink event
;			XWB pointer if logical link event
;			else srcatch
;		R5	WQE pointer if approriate
;
; OUTPUTS:	All registers are preserved
;
;-
NET$EVT_INTRAW::			; Process internal raw event
	BSBW	NET$GETUTLBUF		; Get permission to use the utility 
					; buffer (co-routine call)

	PUSHR	#^M<R0,R1,R2,R3,R4,R5,R6,R7,R8,R9,R10,R11>

	ADDL3	NET$GL_UTLBUF,#2,R3	; Setup output pointer
	MOVQ	G^EXE$GQ_SYSTIME,(R3)+	; Enter standard quadword time
	MOVZWL	WQE$W_EVL_CODE(R5),R0	; Get the raw event code
	MOVW	R0,(R3)+		; Enter the code
	BSBB	50$			; Dispatch to complete building the
					; event
	BLBC	R0,40$			; If LBC then abort logging
	MOVL	NET$GL_UTLBUF,R8	; Get original output pointer
	SUBL3	R8,R3,R7		; Calculate the data length
	MOVW	R7,(R8)			; Store as the length field
	BSBW	INTERNAL_EVENT		; Stuff it into the event buffer

40$:	POPR	#^M<R0,R1,R2,R3,R4,R5,R6,R7,R8,R9,R10,R11>
	RSB

50$:	;
	;  Dispatch to finish building the event.  The $DISPATCH skip chain is
	;  used instead of one large $DISPATCH since the index codes are closely
	;  packed within a class but widely separated from one class to another.
	;
	$DISPATCH  R0,-
	<-
	    <EVC$C_TPL_APL,  NON_PKT>,	-; Aged packet loss
	    <EVC$C_TPL_UPL,  CIR_PKT>,	-; Unreachable packet loss
	    <EVC$C_TPL_RPL,  CIR_PKT>,	-; Out-of-range packet loss
	    <EVC$C_TPL_OPL,  CIR_PKT>,	-; Oversized packet loss
	    <EVC$C_TPL_PFM,  CIR_BEG>,	-; Packet format error
	    <EVC$C_TPL_PRU,  PRU>	-; Partial routing update loss
	    <EVC$C_TPL_VFR,  VFR>,	-; Verification reject
	    <EVC$C_TPL_LDO,  LDO>,	-; Circuit down, operator fault
	    <EVC$C_TPL_LDS,  LDS>,	-; Circuit down, software fault
	    <EVC$C_TPL_LDF,  LDF>,	-; Circuit down, Circuit fault
	    <EVC$C_TPL_LUP,  CIR_ADJ>,	-; Circuit up
	    <EVC$C_TPL_IOF,  IOF>,	-; Init failed, operator fault
	    <EVC$C_TPL_ISF,  ISF>,	-; Init failed, software fault
	    <EVC$C_TPL_ILF,  ILF>,	-; Init failed, Circuit fault
	    <EVC$C_TPL_RCH,  RCH>,	-; Node reachability change
	    <EVC$C_TPL_AUP,  CIR_ADJ>,	-; Adjacency up
	    <EVC$C_TPL_ARJ,  CIR_ADJ>,	-; Adjacency rejected
	    <EVC$C_TPL_ACH,  ACH>	-; Area reachibility change
	>

	$DISPATCH  R0,-
	<-
	    <EVC$C_NMA_CTR,  COUNTER>,	-; Automatic counter timer
	    <EVC$C_NMA_ZER,  COUNTER>,	-; NCP ZERO counters command
	    <EVC$C_NMA_ABS,  ABS>,	-; Aborted service request
	>

	$DISPATCH  R0,-
	<-
	    <EVC$C_NSL_DBR,  COUNTER>,	-; Data base re-used event
	>

	$DISPATCH  R0,-
	<-
	    <EVC$C_SCL_LNS,  LNS>,	-; Local node state change
	>

	$DISPATCH  R0,-
	<-
	    <EVC$C_DLL_LSC,  LSC>,	-; Locally initated state change
	    <EVC$C_DLL_RSC,  RSC>,	-; Remotely initiated state change
	>
	CLRL	R0			; Event unknown
	RSB				; Done

COUNTER:
	MOVZBL	WQE$B_EVL_DT1(R5),R0	; Get counter database i.d.
	$DISPATCH  R0,-			; Dispatch on database type
	<-
	    <EVC$C_SRC_NOD,  NOD_COU>,-	; Log and clear node counters
	    <EVC$C_SRC_CIR,  CIR_COU>,-	; Log and clear circuit counters
	    <EVC$C_SRC_LIN,  LIN_COU>,-	; Log and clear line counters
	>
	CLRL	R0			; Database unknown
	RSB				; Done

NOD_COU:				; Node counters
	BSBW	ENTER_SRCNOD		; Enter source node i.d.
	BRB	COU			; Enter the counters
CIR_COU:				; Circuit counters
	BSBW	ENTER_SRCCIR		; Enter Circuit i.d.
	BRB	COU			; Enter the counters
LIN_COU:				; Line counters
	BSBW	ENTER_SRCLIN		; Enter Line ID

COU:					; Log and clear the counters
	PUSHL	R5			; Save reg
	MOVZBL	WQE$B_EVL_DT2(R5),R0	; Get length of counter block
	MOVC3	R0,-			;
		@WQE$L_EVL_PKT(R5),(R3)	; Move the counter block
	MOVL	#1,R0			; Indicate success
	POPL	R5			; Recover WQE pointer
	RSB
	
NON_PKT:				; No source, packet header
	BSBW	ENTER_NO_SRC		; Enter null source field
	BRW	ENTER_PKTHDR		; Enter the packet header

CIR_PKT:				; Circuit source, adj, packet header
	BSBW	ENTER_SRCCIR		; Enter the source Circuit
	MOVW	#EVC$C_TPL_PADJ,(R3)+	; Identify next field
	BSBW	PNA_NODE		; Enter partner node id
	BRW	ENTER_PKTHDR		; Enter the packet header

CIR_BEG:				; Circuit source, adj, packet begining
	BSBW	ENTER_SRCCIR		; Enter the source Circuit
	MOVW	#EVC$C_TPL_PADJ,(R3)+	; Identify next field
	BSBW	PNA_NODE		; Enter partner node id
	BRW	ENTER_PPKB		; Enter packet begining

PRU:					; Partial routing update loss
	BSBW	ENTER_SRCCIR		; Enter source Circuit
	BSBW	ENTER_PPKB		; Enter the packet header
	MOVW	#EVC$C_TPL_PHIA,(R3)+	; Identify next field
	MOVB	#NMA$C_PTY_DU2,(R3)+	; Identify field format

	ASSUME	WQE$B_EVL_DT2-WQE$B_EVL_DT1 EQ 1

	MOVW	WQE$B_EVL_DT1(R5),(R3)+	; Enter partner's highest 
					; reachable node address
	MOVW	#EVC$C_TPL_PADJ,(R3)+	; Identify adjacent node
	BSBW	PNA_NODE		; Enter partner node id
	MOVB	#1,R0			; Success
	RSB
VFR:					; Verification reject
	BSBW	ENTER_SRCCIR		; Enter the source Circuit
	MOVW	#EVC$C_TPL_PNOD,(R3)+	; Identify next field
	BRW	PNA_NODE		; Enter partner node id
	
IOF:					; Init failure, operator fault
	BSBB	ISF			; Same as ISF, except add:
	MOVW	#EVC$C_TPL_PVRS,(R3)+	; Identify next field (version)
	MOVB	#NMA$C_PTY_CM3,(R3)+	; Enter format type
	MOVB	#NMA$C_PTY_DU1,(R3)+	; Enter format type
	MOVB	NET$GL_INITVER,(R3)+	; Enter version number
	MOVB	#NMA$C_PTY_DU1,(R3)+	; Enter format type
	MOVB	NET$GL_INITVER+1,(R3)+	; Enter ECO number
	MOVB	#NMA$C_PTY_DU1,(R3)+	; Enter format type
	MOVB	NET$GL_INITVER+2,(R3)+	; Enter user ECO number
	MOVB	#1,R0			; Success
	RSB
ISF:					; Init failure, software fault
	BSBB	CIR_REASON		; Enter circuit id, reason
	BRW	ENTER_PPKB		; Enter packet header

LDO:
LDS:					; Adjacency forced down by software
	BSBB	CIR_REASON		; Enter common header
	MOVW	#EVC$C_TPL_PADJ,(R3)+	; Identify next field
	BSBW	PNA_NODE		; Enter partner node id
	BRW	ENTER_PPKB		; Enter packet header

CIR_ADJ:				; Enter circuit id, adjacent node
	BSBW	ENTER_SRCCIR		; Enter source Circuit id
	MOVW	#EVC$C_TPL_PADJ,(R3)+	; Identify adjacent node
	BRW	PNA_NODE		; Enter partner node id

ILF:					; Init failure, circuit fault
LDF:					; Circuit failure, Circuit fault
CIR_REASON:				; Enter circuit id, reason code
	BSBW	ENTER_SRCCIR		; Enter source Circuit id
	TSTB	WQE$B_EVL_DT1(R5)	; Any reason specified?
	BLSS	90$			; Exit if not
	MOVW	#EVC$C_TPL_PRSN,(R3)+	; Identify next field
	BRB	CD1			; Enter field's value
90$:	MOVB	#1,R0			; Signal success
	RSB

RCH:					; Node reachability change
	BSBW	ENTER_SRCNOD		; Enter the source node
	MOVW	#EVC$C_TPL_PSTS,(R3)+	; Identify next field
	BRB	CD1

ACH:					; Area reachability change
	BSBW	ENTER_SRCAREA		; Enter the source area
	MOVW	#EVC$C_TPL_PSTS,(R3)+	; Identify next field

CD1:	MOVB	#NMA$C_PTY_CD1,(R3)+	; Enter field format type
	MOVB	WQE$B_EVL_DT1(R5),(R3)+	; Enter qualifying data byte
	MOVB	#1,R0			; Signal success
	RSB

LNS:	BSBW	ENTER_NO_SRC		; Enter no source ID
	MOVW	#EVC$C_SCL_PRSN,(R3)+	; Enter "reason" parameter type
	MOVB	#NMA$C_PTY_CD1,(R3)+	; Enter field format type
	MOVB	WQE$L_EVL_PKT(R5),(R3)+	; Enter reason code
	MOVW	#EVC$C_SCL_POLD,(R3)+	; Enter "old state" parameter type
	BSBB	CD1			; Enter coded byte from DT1
	MOVW	#EVC$C_SCL_PNEW,(R3)+	; Enter "new state" parameter type
CD1_2:	MOVB	#NMA$C_PTY_CD1,(R3)+	; Enter field format type
	MOVB	WQE$B_EVL_DT2(R5),(R3)+	; Enter qualifying data byte
	MOVB	#1,R0			; Signal success
	RSB

LSC:
RSC:	BSBW	ENTER_SRCCIR		; Enter source circuit
	MOVW	#EVC$C_DLL_POLD,(R3)+	; Enter "old state" parameter type
	BSBB	CD1			; Enter coded byte from DT1
	MOVW	#EVC$C_DLL_PNEW,(R3)+	; Enter "new state" parameter type
	BRB	CD1_2			; Enter coded byte from DT2; and exit

ABS:					; "Aborted service request"
					; Enter circuit id, reason code
	BSBW	ENTER_SRCCIR		; Enter source Circuit id
	MOVW	#EVC$C_NMA_PRSN,(R3)+	; Identify next field
	BRB	CD1			; Enter field's value

ENTER_NO_SRC:				; Enter null source field
	MOVB	#EVC$C_SRC_NON,(R3)+	; No source
	CLRL	R7			; Init count field
	BRB	ENT_17			; Zero the source field

ENTER_SRCAREA:				; Enter source area
	MOVB	#EVC$C_SRC_ARE,(R3)+	; Enter source type
	MOVB	WQE$W_REQIDT(R5),(R3)+	; Store the area number
	PUSHL	R5			; Save registers
	MOVC5	#0,(SP),#0,#16,(R3)	; Zero rest of 17 byte fixed field
	POPL	R5			; Restore registers
	MOVL	#1,R0			; Success
	RSB

ENTER_SRCNOD:				; Enter source node
	MOVB	#EVC$C_SRC_NOD,(R3)+	; Enter source type
	MOVZWL	WQE$W_REQIDT(R5),R1	; Get the node address
	BNEQ	10$			; Branch if not local node
	MOVL	NET$GL_PTR_VCB,R0	; Get the RCB address
	MOVZWL	RCB$W_ADDR(R0),R1	; Enter the local node address
10$:	BSBW	SUPPRESS_AREA		; Suppress area, if necessary
	MOVW	R1,(R3)+		; Enter the node address
	$CNFFLD	ndi,s,nna,R9		; Identify the node name field
	BSBB	ENT_SRC			; Enter padded node name
	TSTW	-(R3)			; Backup two bytes to account for
	RSB				; node address at begining in order
					; to keep a total of 17 bytes

ENTER_SRCCIR:				; Enter source Circuit id
	MOVB	#EVC$C_SRC_CIR,(R3)+	; Enter source type
	$CNFFLD	cri,s,nam,r9		; Get the Circuit name field i.d.
ENT_SRC:BSBW	CNF$GET_FIELD		; Get the source i.d. name
ENT_17:	PUSHL	R5			; Save critical reg
	MOVB	R7,(R3)+		; Enter length of name
	MOVC5	R7,(R8),#0,#16,(R3)	; Enter the name
	POPL	R5			; Restore reg
	MOVB	#1,R0			; Success
	RSB

ENTER_SRCLIN:				; Enter source Line id
	MOVB	#EVC$C_SRC_LIN,(R3)+	; Enter source type
	$CNFFLD	pli,s,nam,r9		; Get the Line name field i.d.
	BRB	ENT_SRC			; Store the parameter value

ENTER_PKTHDR:
	MOVL	WQE$L_EVL_PKT(R5),R0	; Get msg pointer
	BEQL	90$			; Skip if none
	MOVW	#EVC$C_TPL_PPKH,(R3)+	; Enter field i.d.
	MOVB	#NMA$C_PTY_CM4,(R3)+	; Format type for mulitple field
	MOVB	#NMA$C_PTY_H1,(R3)+	; Format type for message flags
	MOVB	(R0)+,(R3)+		; Enter message flags
	MOVB	#NMA$C_PTY_DU2,(R3)+	; Format type for dst node
	MOVW	(R0)+,(R3)+		; Enter dst node address
	MOVB	#NMA$C_PTY_DU2,(R3)+	; Format type for src node
	MOVW	(R0)+,(R3)+		; Enter src node address
	MOVB	#NMA$C_PTY_H1,(R3)+	; Format type for visits field
	MOVB	(R0)+,(R3)+		; Enter visits field
90$:	MOVB	#1,R0			; Success
	RSB

ENTER_PPKB:				; Enter packet begining
	MOVL	WQE$L_EVL_PKT(R5),R0	; Get packet header pointer
	BEQL	90$			; Skip if none
	MOVW	#EVC$C_TPL_PPKB,(R3)+	; Identify next field
	MOVB	#NMA$C_PTY_HI,(R3)+	; Enter format type
	MOVB	#16,(R3)+		; Number of bytes to be entered
	MOVQ	(R0)+,(R3)+		; Enter first 8 bytes
	MOVQ	(R0)+,(R3)+		; Enter final 8 bytes
90$:	MOVB	#1,R0			; Success
	RSB

PNA_NODE:
	MOVZWL	WQE$W_ADJ_INX(R5),R8	; Get ADJ index
	BSBW	NET$FIND_ADJ		; Find the associated ADJ
	BLBC	R0,50$			; If LBC then none found
	MOVZWL	ADJ$W_PNA(R7),R1	; Get the node address
	BEQL	50$			; If zero, then skip it
	BSBW	SUPPRESS_AREA		; Suppress area, if necessary
	BSBB	GET_NDI			; Find the NDI block
	TSTB	R7			; Is there a node name ?
	BNEQ	5$			; If NEQ, then found
	MOVB	#NMA$C_PTY_CM1,(R3)+	; Enter only 1 field
	MOVB	#NMA$C_PTY_DU2,(R3)+	; Enter the address format type
	MOVW	R1,(R3)+		; Enter the address
	RSB				; and skip the node name
5$:	MOVB	#NMA$C_PTY_CM2,(R3)+	; Enter the complex format type
	MOVB	#NMA$C_PTY_DU2,(R3)+	; Enter the address format type
	MOVW	R1,(R3)+		; Enter the address
	MOVB	#NMA$C_PTY_AI,(R3)+	; Enter the node name format type
	MOVB	R7,(R3)+		; Enter the count field
10$:	MOVB	(R8)+,(R3)+		; Enter the text field
	SOBGTR	R7,10$			;
	MOVL	#1,R0			; Indicate success
	RSB
50$:	SUBL	#2,R3			; Remove parameter code
	RSB

GET_NDI:
	PUSHR	#^M<R1,R10,R11>		; Save regs
	MOVL	R1,R8			; Copy node address
	MOVL	NET$GL_CNR_NDI,R11	; Get NDI CNR
	BSBW	NET$NDI_BY_ADD		; Find the NDI by address in R8
	CLRQ	R7			; Nullify R7,R8
	BLBC	R0,10$			; No NDI CNF if LBC
	$GETFLD	ndi,s,nna		; Get the node name -- returns
					; R7,R8 = 0 if LBC in R0
10$:	POPR	#^M<R1,R10,R11>		; Restore regs
	MOVL	#1,R0			; Report success (null node name is 
	RSB				; okay)

	.SBTTL	Inbound raw event processing
;+
; NET$LOG_EVENT - Put a raw event into the event buffer
;
; FUNCTIONAL DESCRIPTION:
;
; A raw event is passed to NETACP.  If a "lost event" event is already in
; the raw event buffer, then the operation is ignored.  If there is no more
; room for events, the "lost event" event is placed in the buffer and the
; flag is set to so indicate.  If an event is placed in the buffer, and the
; EVTAVL flag is set, then a mailbox message (MSG$_EVTAVL) is broadcast.
; Events put into the buffer are time-stamped.
;
; INPUTS:	NET$GL_SIZ_P2 - size of input event
;		NET$GL_PTR_P2 - address of input event
;
; OUTPUTS:	MBX message may be broadcast (MSG$_EVTAVL)
;		R0 - Status
;
;-
	.ENABL	LSB

NET$LOG_EVENT::				; Entry point
	MOVL	NET$GL_SIZ_P2,R7	; Get no. of bytes in event
	MOVL	NET$GL_PTR_P2,R8	; Get address of event data

INTERNAL_EVENT:				; Local entry point
	CMPW	R7,(R8)			; Counts must match
	BEQL	5$			; If EQL OK
	MOVL	S^#SS$_BADPARAM,R0	; Set error code
	BRW	200$			; Take common exit
	;
	;   Ignore event if EFI database is empty (no events get transmitted)
	;
5$:	CMPW	RAW$W_EVTCODE(R8),#EVC$C_VMS_DBC ; EFI database change
	BEQL	10$			; If so, buffer regardless of EFI list
	MOVL	NET$GL_CNR_EFI,R0	; Get address of EFI listhead
	CMPL	R0,(R0)			; Is list empty?
	BEQL	14$			; If so, exit ignoring the event
	;
	;   If this is the first event to be buffered, then allocate an
	;   buffer to stored the event records until EVL picks them up.
	;
10$:	TSTL	EVT_L_BUFFER		; Buffer allocated yet?
	BNEQ	11$			; Branch if so
	MOVL	#12+NET$C_EVTBUFLTH,R1	; Set size of buffer needed
	BSBW	NET$ALLOCATE		; Allocate the buffer
	BLBC	R0,11$			; If error, skip event reporting
	MOVAB	12(R2),EVT_L_BUFFER	; Store buffer pointer
	MOVL	EVT_L_BUFFER,EVT_L_BUFPTR ; Point to first available position
	;
	;   If "lost event" already reported, allow 1 data base change event
	;   to get thru
	;
11$:	BBC	#EVT$V_LOSTEVENT,-	; If BC then try to buffer event
		EVT_B_FLAGS,20$
	ADDW	R7,EVT_W_LOST		; Keep total of events lost
	CMPW	RAW$W_EVTCODE(R8),-	; No space - see if database change
		#EVC$C_VMS_DBC		;
	BNEQ	12$			; If NEQ no - ignore event
	BBCS	#EVT$V_DBCEVENT,-	; If BC, database change not yet logged
		EVT_B_FLAGS,15$
12$:	BSBW	STARTUP_EVL		; Start EVL process (if possible) in
					; case it died and left our buffer full
14$:	BRW	100$			; Nothing to do
15$:	MOVAB	DBC_EVENT,R8		; Put in "DBC event" event
	BRB	25$			; Log the database change
	;
	;   If only room for one more event in buffer, insert "lost event"
	;
20$:	SUBL3	EVT_L_BUFFER,EVT_L_BUFPTR,R0	; Compute # bytes in use
	SUBL3	R0,#NET$C_AVLBUFLTH,R0	; Compute # bytes left
	CMPW	R7,R0			; Enough space for this event?
	BLEQU	30$			; If LEQU yes
	ADDW	R7,EVT_W_LOST		; Keep total of events lost
	BISB2	#EVT$M_LOSTEVENT,-	; Show that an event has been lost
		EVT_B_FLAGS		;
	MOVAB	LOST_EVENT,R8		; Put in "lost event" event
25$:	MOVZWL	(R8),R7			; Get the length of the event
	;
	;   Insert event into buffer
	;
30$:	MOVQ	G^EXE$GQ_SYSTIME,-	; Time-stamp the event
		RAW$T_SYSTIM(R8)	;
	MOVC3	R7,(R8),@EVT_L_BUFPTR	; Move event into the buffer
	MOVL	R3,EVT_L_BUFPTR		; Update the pointer
	INCW	EVT_W_THRESH		; Another event in buffer
	;
	;   If the event threshold has been reached, broadcast "events available" message
	;
	CMPW	#NET$C_EVTTHRESH,-	; Has the threshold been reached?
		EVT_W_THRESH		;
	BGEQU	90$			; If GEQU no
	BISB2	#EVT$M_EVTAVL,-		; Set the flag
		EVT_B_FLAGS		;

	ASSUME	EVT$V_EVTAVL EQ 0

90$:	BLBC	EVT_B_FLAGS,100$	; If LBC can't send mbx msg yet

;
;  It's OK to inform the world that the event buffer should be read
;
SEND_EVT_MSG:
	;
	;   Startup EVL process if not already running
	;
	BSBB	STARTUP_EVL		; Startup EVL process if needed
	;
	;   Reset the threshold timer
	;
	CLRL	R1			; Set up REQIDT for canceling timer
	MOVAB	EVT_TIMER,R2		; Get action routine address for timer
	MOVQ	#NET$C_EVTTIMER,R3	; Let this much time elapse 
	BSBW	WQE$RESET_TIM		; Cancel previous timer, set new one
	;
	;   Now send the mailbox message
	;
	MOVL	#<1@MBX$V_EVTAVL>,R3	; Set mask
	MOVZWL	#MSG$_EVTAVL,R2		; Set mbx msg code
	BSBB	BROADCAST		; Broadcast the message
100$:	MOVZWL	S^#SS$_NORMAL,R0	; Indicate success
200$:	RSB

	.DSABL	LSB

	.SBTTL	STARTUP_EVL - Start EVL process
;+
; STARTUP_EVL - Start EVL process
;
; Start EVL process (if possible).  This is done by queueing a WQE
; to do the job, since STARTUP_OBJ calls CNF action routines, some
; of which allocate the CNF static temporary buffer.  Unfortunately,
; this is required because we may be logging counters while having
; the static temporary buffer allocated (specifically, CNT does this).
;
; Inputs:
;
;	None
;
; Outputs:
;
;	None
;
;	R0 destroyed.
;-

STARTUP_EVL:
	BSBW	WQE$FORK		; Fork to work queue level
	CLRQ	R2			; Pass nothing as SYS$NET to EVL
	CLRQ	R4			; Use default process name
	MOVZBL	#EVL_OBJ,R8		; Object number of EVL
	BSBW	NET$STARTUP_OBJ		; Create EVL process
					; ....ignore any errors
	RSB

	.SBTTL	Event logging database changes
;+
; NET$DBC_ESI - note the receiver database changed
; NET$DBC_EFI - note the xmitter database changed
;
; INPUTS:	NONE
;
; OUTPUTS:	R0	Low bit set
;
;		All other registers are preserved
;
;-
NET$DBC_EFI::
	PUSHR	#^M<R1,R2,R3,R4,R5,R6,R7,R8,R9,R10,R11>
	BSBB	STARTUP_EVL		; Startup EVL if needed
	MOVAB	DBC_EVENT,R8		; Point to event buffer
	MOVZWL	(R8),R7			; Get length of item
	BSBW	INTERNAL_EVENT		; Inform EVL of EFI database change
	MOVZWL	#MSG$_EVTXMTCHG,R2	; This is the mailbox message code
	MOVL	#<1@MBX$V_EVTXMTCHG>,R3	; Set mask
	BRB	DBC_COMMON		; Finish in common code

NET$DBC_ESI::
	PUSHR	#^M<R1,R2,R3,R4,R5,R6,R7,R8,R9,R10,R11>
	BSBB	STARTUP_EVL		; Startup EVL if needed
	MOVZWL	#MSG$_EVTRCVCHG,R2	; This is the mailbox message code
	MOVL	#<1@MBX$V_EVTRCVCHG>,R3	; Set mask

DBC_COMMON:
	BSBB	BROADCAST		; Broadcast the message
	POPR	#^M<R1,R2,R3,R4,R5,R6,R7,R8,R9,R10,R11>
	MOVL	#1,R0			; Always successful
	RSB				; Done
;+
;
; BROADCAST - broadcast event related message
;
; INPUTS:
;		R2 - MSG$ code
;		R3 - mask bit for mailbox filtering   !*** not yet used
;
;-
BROADCAST:
	MOVL	NET$GL_PTR_UCB0,R5	; Point to a NET UCB address
	CLRL	R4			; No message text
	MOVL	#NETUPD$_BRDCST,R0	; Function is "broadcast"
	BSBW	CALL_NETDRIVER		; Call driver comm routine
	RSB				; Done

	.SBTTL	Outbound raw event processing
;+
; NET$READ_EVENT - Read out event buffer
;
; FUNCTIONAL DESCRIPTION:
;
;	This routine places the event buffer into the specified result (P4)
;	buffer. Flags, pointers, and thresholds are reset for more logging.
;
; INPUTS:	NET$GL_PTR_P3 - Address of result length word
;		NET$GL_PTR_P4 - Address of result buffer
;		NET$GL_SIZ_P4 - Size of result buffer
;
; OUTPUTS:	P3, P4 have length of result buffer and result buffer
;		R0 - low word has status (NORMAL); high word has byte count
;
;-
NET$READ_EVENT::			; Entry
	SUBL3	EVT_L_BUFFER,-		; Compute no. of bytes in buffer
		EVT_L_BUFPTR,R0
	MOVL	R0,R6			; Save bfr lth
	MOVL	NET$GL_SIZ_P4,R7	; Get size of result buffer
	CMPL	R7,R6			; Is there room in the result bfr?
	BGEQU	10$			; If GEQU yes
	;
	;   There's not enough room in the caller's buffer to hold
	;   all the events we have buffered.  Scan our event buffer
	;   and find the last event that will fit, so that we always
	;   copy "whole" event records.
	;
	MOVL	EVT_L_BUFFER,R1		; Get buffer pointer
	MOVL	R7,R2			; Get length of user's buffer
5$:	MOVZWL	(R1),R3			; Get length of next event
	CMPL	R3,R2			; Will next event fit in buffer?
	BGTRU	8$			; If not, then stop here
	SUBL	R3,R2			; If it fits, then include it
	ADDL	R3,R1			; Skip to next event
	BRB	5$			; Keep scanning
8$:	SUBL3	R2,R7,R0		; Compute size of bytes to move
	;
	;   The number of bytes to be moved has been determined.
	;   Store the byte count in the P3 result length word.
	;
10$:	MOVL	NET$GL_PTR_P3,R1	; Get address of result length
	BEQL	20$			; If EQL there is none
	MOVW	R0,(R1)			; Store the result length
	;
	;   Construct the final IOSB with the byte count
	;
20$:	MOVW	R0,-(SP)		; Byte count to high word
	MOVW	S^#SS$_NORMAL,-(SP)	; Store I/O status in low word
	;
	;   Move the events into the caller's buffer
	;
	MOVC3	2(SP),@EVT_L_BUFFER,-	; Move event buffer to result bfr
		@NET$GL_PTR_P4
	;
	;   Shift down any remaining events that couldn't be copied
	;   to the front of the buffer.
	;
	MOVZWL	2(SP),R2		; Get the number of bytes we moved
	SUBL3	R2,R6,R0		; Compute # bytes of remaining events
	SUBL	R2,EVT_L_BUFPTR		; Adjust buffer pointer
	MOVC3	R0,(R1),@EVT_L_BUFFER	; Move remaining evts to bfr top
	CMPW	EVT_W_LOST,EVT_W_PEAK	; Did we hit peak "lost bytes"?
	BLEQU	30$			; Branch if not
	MOVW	EVT_W_LOST,EVT_W_PEAK	; Store new peak "lost bytes"
30$:	CLRW	EVT_W_LOST		; Clear lost count statistic
	CLRBIT	#EVT$V_LOSTEVENT,-	; There's now room in the buffer
		EVT_B_FLAGS		;
	CMPW	2(SP),R6		; Did we empty the buffer?
	BEQL	50$			; If so, then indicate buffer empty
	SETBIT	#EVT$V_EVTAVL,EVT_B_FLAGS ; Tell EVL to read more events
	BRB	100$			; Proceed
50$:	CLRB	EVT_B_FLAGS		; Reset the flags
	CLRW	EVT_W_THRESH		; Reset the event threshold
100$:	POPL	R0			; Get 1st IOSB longword
	RSB				; Done

	.SBTTL	NET$SET_CTR_TIMER - Reset automatic counter timer
;+
; NET$SET_CTR_TIMER -  Reset automatic counter timer
;
; FUNCTIONAL DESCRIPTION
;
; This routine is called whenever the a data base is updated to start or
; reset the automatic counter timer.  When the counter timer fires, the
; counters will be logged on whatever CNFs are due.  The timer is then
; reset to the next earliest due time.
;
; Inputs:
;
;	R11 = CNR address
;	R10 = CNF address
;
; Outputs:
;
;	None
;
;	R0-R9 are destroyed.
;-

NET$SET_CTR_TIMER::			; Reset logging counter timer
	MOVAB	CNX_CRI,R6		; Assume CRI data base
	CMPL	R11,NET$GL_CNR_CRI	; Is it ?
	BEQL	10$			; If EQL then yes
	MOVAB	CNX_NDI,R6		; Assume NDI data base
	CMPL	R11,NET$GL_CNR_NDI	; Is it the NDI data base
	BEQL	10$			; If EQL then yes
	MOVAB	CNX_PLI,R6		; Assume PLI data base
	CMPL	R11,NET$GL_CNR_PLI	; Is it?
	BEQL	10$			; Branch if so
	RSB				; Else, unsupported database

	;
	;   Since it is common for many CNF blocks to be updated by the
	;   network manager at the same time, it is possible to reduce the
	;   total amount of work to be done somewhat by waiting a short time,
	;   the so called "suppression interval", before running the timer
	;   update algorithm after any given CNF block is updated.  This has
	;   the effect of batching the requests and reduces the work by making
	;   better use of each scan of the data base.
	;
	;   The suppression timer interval is 2 seconds.  This is long enough
	;   for a typical  NCP>SET KNOWN NODES ALL  command to complete, and
	;   short enough not to be noticed by the issuer of the command.
	;
10$:	MOVL	CNX$L_DEL_TIME(R6),R9	; Get the counter timer field i.d.
	BSBW	CNF$GET_FIELD		; Get its value
	BLBC	R0,15$			; If LBC then its not set
	ADDL	G^EXE$GL_ABSTIM,R8	; Convert to absolute time
	MOVL	CNX$L_ABS_TIME(R6),R9	; Get field i.d.
	BSBW	CNF$PUT_FIELD		; Store it
	MOVZBL	CNX$B_TIM_SUP(R6),R0	; Get the suppression timer bit no.
	BBSS	R0,EVT_B_FLAGS,15$	; If BS then update suppression timer
					; is ticking
	MOVL	#2,R8			; Suppress processing request for 2 sec
	BRB	40$		 	; Set the timer
15$:	BRW	50$			; Continue


20$:	;
	;   Entry point called when timer fires.
	;
	;   Determine database
	;
	MOVL	R5,R0			; Get the timer WQE for deallocation
	EXTZV	#16,#16,R1,R5		; Get timer database i.d.
	BSBW	NET$DEALLOCATE		; Deallocate WQE
	MOVAB	CNX_CRI,R6		; Assume CRI timer
	CMPW	R5,#EVC$C_SRC_CIR	; Is it?
	BEQL	25$			; If EQL yes
	MOVAB	CNX_NDI,R6		; Assume NDI timer
	CMPW	R5,#EVC$C_SRC_NOD	; Is it?
	BEQL	25$			; If EQL yes
	MOVAB	CNX_PLI,R6		; Assume PLI timer
	CMPW	R5,#EVC$C_SRC_LIN	; Is it?
	BEQL	25$			; Branch if so

	BUG_CHECK  NETNOSTATE,FATAL	; Timer i.d. unknown

25$:	MOVL	@CNX$L_CNR_PTR(R6),R11	; Get the CNR pointer
	MOVZBL	CNX$B_TIM_SUP(R6),R0	; Get the suppression timer bit no.
	CLRBIT	R0,EVT_B_FLAGS		; Suppression timer no longer ticking
	BSBB	TICK			; Process CNF timers
	; 
	;   Determine the next earliest CNF due time
	;
	CLRL	R10			; Start from the head of the CNF list
	MOVL	CNX$L_ABS_TIME(R6),R9	; Get absolute time field i.d.
	MOVL	#NFB$C_OP_FNDMIN,R1	; Fct is "find minimum value"
	BSBW	CNF$KEY_SEARCH		; Find minimum value
	BLBC	R0,50$			; If no CNF found, no timers are set
	BSBW	CNF$GET_FIELD		; Get due time of minimum CNF
	BLBC	R0,50$			; Branch if cannot get it
	MOVL	R8,@CNX$L_OLD_TIME(R6)	; Store the absolute due time
	CMPL	G^EXE$GL_ABSTIM,R8	; Have we passed that time yet?
					; (this could happen if the event
					;  buffer is full)
	BLSSU	35$			; If LSSU then no
	MOVL	#2,R8			; Try again in 2 seconds
	BRB	40$			; Continue
35$:	SUBL	G^EXE$GL_ABSTIM,R8	; Convert to delta time
	ADDL	#2,R8			; CNF timers are grouped into 2 second
					; buckets to batch the work
40$:	;
	;   Reset the timer
	;
	EMUL	R8,#10*1000*1000,#0,R3	; Get quadword timer interval
	MOVAB	20$,R2			; Setup timer routine address
	MOVL	CNX$W_ID_CTM-2(R6),R1	; Setup timer i.d. in high order word
	MOVW	#WQE$C_QUAL_CTM@8,R1	; Setup timer qualifier
	BSBW	WQE$RESET_TIM		; Reset the counter timer
50$:	RSB




TICK:	MOVL	G^EXE$GL_ABSTIM,-	; Get seconds since boot to be used
		BASE_TIME		; as the common base for updating timers
	CLRL	R10			; Start from the head of the CNF list
10$:	;
	;   Find the next CNF whose timer is due.   Must first check for
	;   CNF entries whose time is past due to prevent finding the same
	;   CNFs over and over again when there are more entries than can fit
	;   in the event buffer.
	;
	SUBL3	EVT_L_BUFFER,EVT_L_BUFPTR,R0	; Compute # bytes in use
	SUBL3	R0,#NET$C_AVLBUFLTH,R0	; Compute # bytes left
	CMPW	#100,R0			;&Enough room in buffer?
	BGTRU	40$			; If GTRU then no	
	MOVL	CNX$L_ABS_TIME(R6),R9	; Get field i.d
	ADDL3	#4,@CNX$L_OLD_TIME(R6),R8 ; Get due time of oldest CNFs 
	CMPL	R8,G^EXE$GL_ABSTIM	; Use 4 second interval but don't
	BGTRU	13$			; exceed the current time
	MOVL	S^#NFB$C_OP_GTRU,R1	; Match on key value GTRU CNF field
	BSBW	CNF$KEY_SEARCH		; Find Appropriate CNF
	BLBS	R0,15$			; If LBS then found one
	CLRL	R10			; Start next scan from head of CNF list
13$:	ADDL3	#1,G^EXE$GL_ABSTIM,R8	; Bias current time.  The "+1" is used
					; to help smooth the coarseness of the
					; timer and to amortize the timer over-
					; head across a number of CNFs.
	MOVL	S^#NFB$C_OP_GTRU,R1	; Match on key value GTRU CNF field
	BSBW	CNF$KEY_SEARCH		; Find Appropriate CNF
	BLBC	R0,40$			; If LBC then no such CNF
15$:	REMQUE	@NET$GQ_TMP_BUF,R0	; Drain temp buffer queue since
					; the search below could fill it
	BVS	17$			; If VS then none
	BSBW	NET$DEALLOCATE		; Deallocate the block
	BRB	15$			; Loop
17$:	;
	;   Snapshot the counters and log the event records.  The CNT
	;   action routine will log the event record because the CLRCNT
	;   flag is set.
	;
	PUSHL	NET$GL_FLAGS		; Save current flags
	SETBIT	#NET$V_CLRCNT,NET$GL_FLAGS ; Counters are to be cleared
	MOVL	CNX$L_COUNTER(R6),R9	; Get counter field i.d.
	BSBW	CNF$GET_FIELD		; Read/clear the counters
	POPL	NET$GL_FLAGS		; Restore flags
	;
	;   Calculate its next due time and store it in the CNF
	;
	MOVL	CNX$L_DEL_TIME(R6),R9	; Get delta time field i.d.
	BSBW	CNF$GET_FIELD		; Fetch it
	MOVL	CNX$L_ABS_TIME(R6),R9	; Get absolute time field i.d.
	BLBS	R0,20$			; If LBS then delta time was valid
	BSBW	CNF$CLR_FIELD		; Else the timer has been cancelled
	BRB	30$			; Continue
20$:	ADDL	BASE_TIME,R8		; Determine when timer should fire next
	BSBW	CNF$PUT_FIELD		; Store it
30$:	BRW	10$			; Loop
40$:	RSB


.END

