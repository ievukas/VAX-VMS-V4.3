	.TITLE	SYSGENMN - SYSGEN UTILITY MAIN ROUTINE
	.IDENT	'V04-000'

;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982, 1984 BY				    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;

;++
; FACILITY:	SYSGEN
;
; ABSTRACT:
;	This module contains the main routine for the SYSGEN utility.   
;
; ENVIRONMENT:	USER, EXEC, AND KERNEL MODES
;
; AUTHOR:  STEVE BECKHARDT,	CREATION DATE:  19-SEP-1979
;	   (ORIGINAL AUTHOR - LEN KAWELL)
;
; MODIFIED BY:
;
;	V03-004	WHM0001		Bill Matthews		20-May-1983
;		Do an implicit SET/OUTPUT=SYS$OUTPUT: in order to detect whether
;		or not SYS$OUTPUT is a terminal.
;
;	V03-003	MSH0003		Maryann Hinden		13-Jul-1983
;		No echo argument to BOO$GETPARAM.
;
;	V03-002	MSH0002		Maryann Hinden		03-Jun-1983
;		Fix lock id specification for exit handler.
;
;	V03-001	MSH0001		Maryann Hinden		10-June-1983
;		Move PUTERROR to separate module.
;--

	.SBTTL	DECLARATIONS
;
; INCLUDE FILES:
;

;
; MACROS:
;

	$CLIDEF				; DEFINE CLI CODES AND VALUES

;
; EQUATED SYMBOLS:
;

;
; OWN STORAGE:
;
EXIT_BLOCK:	.LONG	0			; Data block for exit handler
		.LONG	EXIT_HANDLER
		.LONG	1
		.LONG	EXIT_STATUS
EXIT_STATUS:	.BLKL	1

OUTFILE:	.ASCIC	/SYS$OUTPUT:/


	.PSECT	PAGED_CODE	rd,nowrt,exe,long

	.SBTTL	Main routine
;++
; Functional Description:
;	SYSGEN is the control module for the sysgen utility program which
;	provides functional commands for the creation, examination and
;	editing of parameter files, the creation of I/O data base and the loading
;	of device drivers.
;
; Calling Sequence:
;	CALLG ARGLIST,BOO$SYSGEN	called by the image activator
;
; Input Parameters:
;
; Output Parameters:
;
;--
BOO$SYSGEN::	.WORD	0
	$LKWSET_S	INADR=BOO$GQ_LIMITS,-	;
			RETADR=BOO$GQ_RETADR	; Lock entire image in working set
	BLBC	R0,10$			; EXIT IF ERROR LOCKING PAGES, FATAL
	$DCLEXH_S	desblk	= EXIT_BLOCK    ; Declare exit handler
	BLBC	R0,10$			; Exit if can't do it
	CLRQ	-(SP)			; Null call back arguments
	PUSHAB	L^BOO$AL_CLIBLK		; Address of request block
	CALLS	#3,@CLI$A_UTILSERV(AP)	; Call utility service routine
	CMPB	#CLI$K_VERB_FORE,L^BOO$AL_CLIBLK+CLI$B_RQSTAT ; Foreign command?
	BEQL	5$			; Branch if yes
	CLRQ	L^BOO$GQ_CMDESC		; Clear command string descriptor
5$:	CALLS	#0,BOO$USEACT		; Use ACTIVE parameters
	MOVB	OUTFILE,G^BOO$GB_FILELEN; Set file length of SYS$OUTPUT:
	MOVAB	OUTFILE+1,G^BOO$GL_FILEADDR; Set file address of SYS$OUTPUT:
	CALLS	#0,BOO$SET_OUTPUT	; Do a SET/OUTPUT=SYS$OUTPUT: command
	CALLS	#0,L^BOO$GETPARAM	; READ AND PROCESS COMMANDS
	CMPL	#RMS$_EOF,R0		; CHECK FOR END OF FILE
	BNEQ	10$			; NO, RETURN STATUS
	MOVL	#1,R0			; SET NORMAL STATUS
10$:	RET				;
	.PAGE
	.SBTTL	EXIT_HANDLER
;++
;
;  PURPOSE
;	Dequeue SYSGEN database lock - if being held.	
;
;  INPUT
;	BOO$LOCK_ID - identification of database lock.
;
;  OUTPUT
;	Lock is dequeued.	
;
;--

	.ENTRY	EXIT_HANDLER, ^M<>

10$:	$CMEXEC_S	routin=DQLOCK
	MOVZWL	#SS$_NORMAL,R0
	RET

;
;  Exec mode routine to dequeue locks 
;
	.ENTRY	DQLOCK,^M<>

	$DEQ_S	lkid	= BOO$LOCK_ID
	RET


	.END	BOO$SYSGEN
