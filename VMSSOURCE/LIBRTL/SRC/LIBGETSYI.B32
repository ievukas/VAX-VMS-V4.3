MODULE LIB$GETSYI ( %TITLE 'Get System-Wide Information'
		IDENT = '1-007'		! File: LIBGETSYI.B32 Edit: MDL1007
		) =
BEGIN
!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982, 1984 BY				    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************
!

!++
! FACILITY:	General Utility Library
!
! ABSTRACT:
!
!	LIB$GETSYI obtains a specified item of System-Wide
!	and formats it in an appropriate manner.
!
! ENVIRONMENT:	User mode - AST reentrant
!
! AUTHOR: Steven B. Lionel, CREATION DATE: 11-Jan-1983
!
! MODIFIED BY:
!
! 1-001	- Original.  SBL 11-Jan-1983
! 1-002 - Change format codes to LIB$K_FMT_.  SBL 11-Mar-1983
! 1-003 - Change string length to 512.  SBL 11-Mar-1983
! 1-004 - Add support for new argument to SYS$GETSYI - NODENAME and
!	  CSID.  DG 19-Oct-1983
! 1-005 - Take out support added in 004 temporarily.  DG 18-Feb-1984
! 1-006 - Add support from 004 again - this time only passing the 
!	  optional arguments if they are present.  DG 20-Mar-1984
! 1-007 - Optional argument CSID should be passed as a 0 by value if not
!	  present, not by reference.  MDL 19-Jul-1984
!--


%SBTTL 'Declarations'
!
! PROLOGUE FILE:
!

REQUIRE 'RTLIN:LIBPROLOG';			! Switches, PSECTS, macros

!
! LINKAGES:
!
!	NONE
!
! TABLE OF CONTENTS:
!

FORWARD ROUTINE
    LIB$GETSYI;					! Get System-Wide Information

!
! MACROS:
!
!	NONE
!
! EQUATED SYMBOLS:
!
!	NONE
!
! FIELDS:
!
!	NONE
!
! OWN STORAGE:
!
!	NONE
!
! EXTERNALS:
!

EXTERNAL ROUTINE
    LIB$$GETSYI,				! Internal routine
    LIB$ANALYZE_SDESC_R2: LIB$ANALYZE_SDESC_R2$LINKAGE,	! Get length and pointer
    LIB$GET_EF,					! Allocate event flag number
    LIB$FREE_EF: NOVALUE,			! Free event flag number
    LIB$SCOPY_R_DX6: LIB$SCOPY_R_DX6$LINKAGE;	! Copy string by reference.

EXTERNAL LITERAL
    LIB$_INVARG,				! Invalid argument
    LIB$_STRTRU;				! String truncated

%SBTTL 'LIB$GETSYI - Get Device/Volume Information'
GLOBAL ROUTINE LIB$GETSYI (
    ITEM_CODE: REF VECTOR [, WORD],	! Code of desired item
    OUT_VALUE: REF VECTOR [, LONG],	! Output numeric value
    OUT_STRING: REF BLOCK [, BYTE],	! Output string descriptor
    OUT_LEN: REF VECTOR [, WORD],	! Output string length
    CSID: REF VECTOR [, LONG],		! Node identification (CSID)
    NODE_NAME: REF BLOCK [, BYTE]	! Node name descriptor
    ) =

!++
! FUNCTIONAL DESCRIPTION:
!
!	LIB$GETSYI  provides a simplified interface to the $GETSYI system
!	service.  It provides status and identification information about
!	the system. 
!
!	LIB$GETSYI provides the following features in addition  to  those
!	provided by the $GETSYI system service:
!
!	      o  Instead of a list of  item  descriptors,  which  may  be
!	         difficult  to  construct  in  high-level  languages, the
!	         single item desired is  specified  as  an  integer  code
!	         which  is  passed  by reference.  Results are written to
!	         separate arguments.
!
!	      o  For items which return numeric  values,  LIB$GETSYI  can
!	         optionally  provide a formatted string interpretation of
!	         the value.
!
!	      o  For string arguments, all string  classes  supported  by
!	         the Run-Time Library are understood.
!
!	      o  Calls to LIB$GETSYI are synchronous.   LIB$GETSYI  calls
!	         LIB$GET_EF  to  allocate  a  local event flag number for
!	         synchronization.
!
!	LIB$GETSYI does not provide the ability to obtain more  than  one
!	item of information in a single call.  
!
! CALLING SEQUENCE:
!
!	ret-status.wlc.v = LIB$GETSYI (
!		item-code.rw.r,
!		[out-value.wz.r]
!		[, [out-string.wt.dx]
!		[, [out-len.wwu.r] ]]
!		[, csid.mlu.r]
!		[, node-name.rt.dx])
!
! FORMAL PARAMETERS:
!
!  item-code
!	A longword integer item identifier code that specifies which item
!	of information you are requesting.  All valid $GETSYI item codes,
!	whose names begin with SYI$_, are accepted.
!
!  out-value
!	A longword or quadword into which is placed the numeric
!	value of the information requested. If an item only returns
!	a string value, this parameter is ignored. 
!
!  out-string
!	A string into which is placed the string representation of
!	the information requested. If out-string is not specified,
!	and the value returned has only a string representation, the
!	error status LIB$_INVARG is returned. 
!
!  out-len
!	A word integer into which is placed the number of significant
!	characters written to out-string, not including blank padding
!	or truncated characters.
!
!  csid
!	A longword specifying the node identification (CSID) of the 
!	node for which information is to be returned.  If not
!	specified, node-name is used. The longword is updated to
!	contain the CSID actually used, which may be different than 
!	what was originally specified if node-name is specified or 
!	if "wild card node searching" is used. 
!
!  node-name
!	A string specifying the name of the node for which
!	information is to be returned.  If not specified, csid
!	is used.  If neither node-name or csid are
!	specified, the caller's node is used.  See the
!	description of the csid argument for more information.
!
! IMPLICIT INPUTS:
!
!	NONE
!
! IMPLICIT OUTPUTS:
!
!	NONE
!
! COMPLETION STATUS:
!
!	SS$_NORMAL	Normal successful completion
!	LIB$_STRTRU	String truncated.  This is an alternate success status.
!	LIB$_INSEF	Insufficient event flags
!	LIB$_INVSTRDES	Invalid string descriptor
!	LIB$_WRONUMARG	Wrong number of arguments
!	LIB$_xxx	Any error status from LIB$SCOPY_R_DX
!	SS$_BADPARAM	The item code is not recognized as valid.
!	SS$_xxx		Any error status from $GETSYI
!
! SIDE EFFECTS:
!
!	NONE
!
!--

    BEGIN

    LOCAL
	RET_STRING: VECTOR [512, BYTE],		! Local string for value
	RET_LENGTH: WORD,			! Length of RET_STRING
	RET_NUMBER: VECTOR [2, LONG],		! Local quadword for value
	RET_TYPE,				! Returned type code
	EVENT_FLAG,				! Event flag number
	EF_STATUS, SYI_STATUS, COPY_STATUS,	! Return statuses
	STR_STATUS,
	LCL_NODE_NAM_DSC: BLOCK [8, BYTE],	! Local descriptor for NODE_NAM
	NODE_NAM_DSC_ADR;			! Address of NODE_NAM descriptor

    BUILTIN
	NULLPARAMETER;

    !+
    ! Validate argument count.
    !-

    $LIB$VALIDATE_ARGCOUNT (2,6);

    !+
    ! Build static descriptor for NODE_NAME, if any.
    !-

    IF NULLPARAMETER (6)
    THEN
	NODE_NAM_DSC_ADR = 0	! Omitted
    ELSE
	BEGIN
	LCL_NODE_NAM_DSC [DSC$B_DTYPE] = DSC$K_DTYPE_T;
	LCL_NODE_NAM_DSC [DSC$B_CLASS] = DSC$K_CLASS_S;
	STR_STATUS = LIB$ANALYZE_SDESC_R2 (NODE_NAME [0,0,0,0];
	    LCL_NODE_NAM_DSC [DSC$W_LENGTH], LCL_NODE_NAM_DSC [DSC$A_POINTER]);
	IF NOT .STR_STATUS
	THEN
	    RETURN .STR_STATUS;
	NODE_NAM_DSC_ADR = LCL_NODE_NAM_DSC;
	END;

    !+
    ! Allocate an event flag number to use for the $GETSYI.
    !-

    EF_STATUS = LIB$GET_EF (EVENT_FLAG);
    IF NOT .EF_STATUS
    THEN
	RETURN .EF_STATUS;

    !+
    ! Call LIB$$GETSYI to do the work.
    !-

    SYI_STATUS = LIB$$GETSYI (
	.ITEM_CODE [0],
	RET_STRING,
	RET_NUMBER,
	RET_LENGTH,
	RET_TYPE,
	.EVENT_FLAG,
    	( IF NULLPARAMETER (5)
    	  THEN
	      0	! Omitted
	  ELSE
	      CSID ),
	.NODE_NAM_DSC_ADR);

    !+
    ! Free the event flag.
    !-

    LIB$FREE_EF (EVENT_FLAG);

    !+
    ! Check for errors.
    !-

    IF NOT .SYI_STATUS
    THEN
	RETURN .SYI_STATUS;

    !+
    ! Copy the numeric value, if desired.
    !-

    IF OUT_VALUE [0] NEQA 0
    THEN
	BEGIN
	IF .RET_TYPE GTRU LIB$K_FMT_MAXSTRING	! Is it a number?
	THEN
	    BEGIN
	    OUT_VALUE [0] = .RET_NUMBER [0];
	    IF .RET_TYPE EQL LIB$K_FMT_DATE OR
	       .RET_TYPE EQL LIB$K_FMT_PRIVILEGE
	    THEN
		OUT_VALUE [1] = .RET_NUMBER [1]; ! Store second longword
	    END;
	END;

    !+
    ! Store string value if desired.
    !-

    IF NOT NULLPARAMETER (3)
    THEN
	BEGIN
	COPY_STATUS = LIB$SCOPY_R_DX6 (.RET_LENGTH, RET_STRING,
	    OUT_STRING [0,0,0,0]);
	IF NOT NULLPARAMETER (4)
	THEN
	    BEGIN
	    !+
	    ! Store result string length.
	    !-
	    OUT_LEN [0] = .RET_LENGTH;
	    IF .COPY_STATUS EQLU LIB$_STRTRU
	    THEN
		LIB$ANALYZE_SDESC_R2 (OUT_STRING [0,0,0,0]; OUT_LEN [0]);
	    END;
	RETURN .COPY_STATUS;
	END
    ELSE IF .RET_TYPE LEQU LIB$K_FMT_MAXSTRING
    THEN
	RETURN LIB$_INVARG;	! Only string value, but nothing to return it in

    RETURN SS$_NORMAL;	! Success

    END;					! End of routine LIB$GETSYI

END						! End of module LIB$GETSYI

ELUDOM
