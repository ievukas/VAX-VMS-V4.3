	.TITLE	MCRCANCEL - CANCEL SCHEDULED WAKEUPS
	.IDENT	'V04-000'
 
;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982, 1984 BY				    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;
; ABSTRACT:
;
; 	Cancel scheduled wakeup requests for a process
;
; AUTHOR:
;
;	Melanie Johnson, November 1982 - based on DCL's CANCEL.MAR
;		by Peter George,  August 1982
;
; MODIFIED BY:
;
;---

;
; MACRO LIBRARY CALLS
;
	PTRDEF				;DEFINE RESULT PARSE TOKENS
	$CLIMSGDEF			;DEFINE CLI RELATED ERRORS
	$PBKDEF				;DEFINE PASSBACK STRUCTURE 
	PRCDEF				;DEFINE PROCESS-PERMANENT WORK OFFSETS
;
; LOCAL DATA
;
	.PSECT	MCR$ZCODE,BYTE,RD,NOWRT
HEXTAB:					;VALID HEX NUMERALS
	.ASCII	/FEDCBA9876543210/

	.SBTTL	CANCEL SCHEDULED WAKEUPS
;+
; MCR$CANCEL - CANCEL SCHEDULED WAKEUP REQUESTS FOR A PROCESS
;
; THIS ROUTINE IS CALLED AS AN INTERNAL COMMAND TO EXECUTE THE CANCEL
; MCR COMMAND.
;
; INPUTS:
;
;	R8 = ADDRESS OF SCRATCH BUFFER DESCRIPTOR.
;	R9 = ADDRESS OF SCRATCH STACK.
;	FP = BASE ADDRESS OF COMMAND WORK AREA.
;	R11 = BASE ADDRESS OF PROCESS WORK AREA.
;
; OUTPUTS:
;
;	ALL SCHEDULED WAKEUP REQUESTS FOR THE SPECIFIED PROCESS ARE
;	CANCELLED.
;-
 
MCR$CANCEL::				; CANCEL WAKEUP REQUEST
	CLRL	R7			; CLEAR PROCESS NAME
	CLRL	-(R9)			; CLEAR PID
10$:	BSBW	MCR$GETDVAL		; GET FIRST TOKEN
	CMPL	#PTR_K_COMDQUAL,R5	; /ID QUALIFIER?
	BNEQ	30$			; NO, THEN MAYBE PARAMETER

	CLRL	(R9)			; YES, ZERO PREVIOUS PID
	BSBW	MCR$GETDVAL		; GET PID VALUE
	MOVQ	R1,R2			; SAVE THE LENGTH

20$:	DECL	R2			; GET A CHARACTER
	BLSS	10$			; BRANCH IF NONE LEFT
	LOCC	(R3)+,#16,HEXTAB	; IS IT A LEGIT HEX CHAR
	BEQL	50$			; NO, THEN ERROR
	MULL	#16,(R9)		; SHIFT PREVIOUS VALUE
	ADDL	R0,(R9)			; ADD LATEST CHARACTER
	DECL	(R9)			; ADJUST BY ONE
	BRB	20$			; GET NEXT CHARACTER

30$:	CMPL	#PTR_K_PARAMETR,R5	; PROCESS NAME?
	BNEQ	40$			; NO, THEN MUST BE END OF LINE
	MOVL	R8,R7			; GET SCRATCH SPACE
	MOVQ	R1,(R8)			; COPY THE PROCESS NAME THERE
	BRB	10$			; ANY MORE QUALIFIERS?

40$:	TSTL	PRC_L_OWNRPID(R11)	;IS THIS A SUBPROCESS?
	BEQL	46$			;IF EQL, NO
	MOVL	PRC_L_PBKPTR(R11),R6	;GET PASSBACK STRUCTURE POINTER
	SETBIT	PBK$V_PASSBACK,PBK$W_FLAGS(R6) ;SET PASSBACK FLAG
	MOVZBL	#PBK$C_CANCEL,PBK$L_COMMANDS(R6) ;THIS IS THE CANCEL COMMAND
	MOVAL	PBK$A_INFO(R6),R6	;GET ADDRESS OF PASSBACK INFORMATION BUFFER
	MOVL	(R9),(R6)+		;PUT PID INTO INFO BUFFER
	CLRW	(R6)			;ASSUME NO PROC. NAME
	TSTL	R7			;IS THERE A PROC. NAME?
	BEQL	45$			;IF EQL, NO
	MOVW	(R7),(R6)+		;PUT LENGTH OF PROCESS NAME IN INFO. BUFFER
	MOVC	(R7),4(R7),(R6)		;PUT PROCESS NAME IN PASSBACK INFO BUFFER
	BRB	47$			;DON'T TRY TO $CANWAK HERE - MAIN PROC. WILL DO IT
45$:	TSTL	(R9)			;WAS A PID SPECIFIED?
	BNEQ	47$			;IF NEQ, YES, DON'T DO THE $CANWAK NOW
46$:	$CANWAK_S (R9),(R7)		; CANCEL SCHEDULED WAKEUPS
	BLBC	R0,48$			;IF LBC, ERRO
47$:	STATUS	NORMAL			;
48$:	RSB

50$:	STATUS	IVVALU			; PID SYNTAX ERROR
	RSB

	.END
