	.TITLE	BAS$MAT_TRN
	.IDENT	/1-013/			; File: BASMATTRN.MAR	Edit: SBL1013

;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982, 1984 BY				    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;

;++
; FACILITY: BASIC code support
;
; ABSTRACT:
;
;	This module writes the transpose of a matrix into a second matrix.
;
; ENVIRONMENT: User Mode, AST Reentrant
;
;--
; AUTHOR: R. Will, CREATION DATE: 10-Jul-79
;
; MODIFIED BY:
;++
; 1-001	- Original 
; 1-002 - Fix test for 'same array' for virtual.  RW  15-Feb-1980
; 1-003 - Add support for byte, g & h floating.  PLL 22-Sep-81
; 1-004 - More modifications for new data types. PLL 24-Sep-81
; 1-005 - Change shared external references to G^ RNH 25-Sep-81
; 1-006 - Substitute a macro for the calls to the array fetch and store
;	  routines.  This should speed things up.  PLL 9-Nov-81
; 1-007 - STORE macro must handle g & h floating.  PLL 11-Nov-81
; 1-008 - Correct a run-time expression in the FETCH and STORE macros.
;	  PLL 20-Jan-82
; 1-009 - Don't list macro expansions.  PLL 16-Mar-82
; 1-010 - Remove FETCH and STORE macros; they are now located in macro
;	  library MATRIXMAC.OLB.  Also added support for arrays of
;	  descriptors.  LB 19-May-82
; 1-011 - Change own storage to stack storage.  LB 9-Jul-1982
; 1-012 - Allow gfloat results to be stored in a double destination, and
;	  vice versa.  PLL 7-Oct-1982
; 1-013 - Use G^ for ALL externals.  SBL 16-Nov-1982
;--

	.SBTTL	DECLARATIONS
;
; INCLUDE FILES:
;

	$DSCDEF					; define descriptor offsets
	$SFDEF					; use to get scale

;
; EXTERNAL DECLARATIONS:
;

	.DSABL	GBL				; Prevent undeclared
						;  symbols from being
						;  automatically global.
	.EXTRN	BAS$K_ARGDONMAT			; signalled if all 3 blocks
						;  not present in array desc
						;  or dimct = 0
	.EXTRN	BAS$K_DATTYPERR			; signalled if dtype of array
						; isn't word long float double
	.EXTRN	BAS$K_MATDIMERR			; signalled if src matrix has
						;  only 1 dimension
	.EXTRN	BAS$K_ILLOPE			; signalled if DSC$A_POINTER is
						;  same in src and dest matrices
	.EXTRN	BAS$STO_FA_B_R8			; array element store for byte
	.EXTRN	BAS$STO_FA_W_R8			; array element store for word
	.EXTRN	BAS$STO_FA_L_R8			; array element store for long
	.EXTRN	BAS$STO_FA_F_R8			; array element store - float
	.EXTRN	BAS$STO_FA_D_R8			; array element store - double
	.EXTRN	BAS$STO_FA_G_R8			; array element store - gfloat
	.EXTRN	BAS$STO_FA_H_R8			; array element store - hfloat
	.EXTRN	BAS$FET_FA_B_R8			; array element fetch - byte
	.EXTRN	BAS$FET_FA_W_R8			; array element fetch - word
	.EXTRN	BAS$FET_FA_L_R8			; array element fetch - long
	.EXTRN	BAS$FET_FA_F_R8			; array element fetch - float
	.EXTRN	BAS$FET_FA_D_R8			; array element fetch - double
	.EXTRN	BAS$FET_FA_G_R8			; array element fetch - gfloat
	.EXTRN	BAS$FET_FA_H_R8			; array element fetch - hfloat
	.EXTRN	BAS$MAT_REDIM			; check if redimensioning of
						;  dest array is necessary, if
						;  so, do it
	.EXTRN	BAS$$SCALE_R1			; scale the double procision
	.EXTRN	MTH$DINT_R4			; truncate dbl precision number
	.EXTRN	BAS$$STOP			; signal fatal errors
	.EXTRN	BAS$FETCH_BFA
	.EXTRN	BAS$STORE_BFA

;
; MACROS:
;

;	$BAS$MAT_TRN	transpose loop algorithm,  see next page
;	FETCH		fetch an element from an array (found in macro
;			library MATRIXMAC.OLB)
;	STORE		store an element into an array (found in macro
;			library MATRIXMAC.OLB)

;
; EQUATED SYMBOLS:
;

	first_arg = 4				; arg offset for str copy
	second_arg = 8				; arg offset for str copy
	index1 = 12				; stack offset for str copy
	index2 = 16				; stack offset for str copy
	temp_desc = 20				; stack offset for str copy
	lower_bnd2 = 0				; stack offset for temp
	lower_bnd1 = 4				; stack offset for temp
	upper_bnd1 = 8				; stack offset for temp
	value_desc = 12				; output descriptor
	str_len = 12				; length field in desc
	dtype = 14				; data type field in desc
	class = 15				; class field within desc
	pointer = 16				; pointer to data
	data = 20				; data
	dsc$l_l1_1 = 24				; desc offset if 1 sub
	dsc$l_u1_1 = 28				; desc offset if 1 sub
	dsc$l_l1_2 = 28				; desc offset if 2 sub
	dsc$l_u1_2 = 32				; desc offset if 2 sub
	dsc$l_l2_2 = 36				; desc offset if 2 sub
	dsc$l_u2_2 = 40				; desc offset if 2 sub

;
; OWN STORAGE:
;


;
; PSECT DECLARATIONS:
;
	.PSECT _BAS$CODE PIC, USR, CON, REL, LCL, SHR, -
			EXE, RD, NOWRT, LONG


;+
; This macro contains the looping mechanism for accessing all elements of
; an array.  It also contains all the logic for all the combinations of data
; types and scaling.  A macro is used to make it easy to maintain the parallel
; code for all the different data types.
;-

	.MACRO	$BAS$MAT_TRN src_dtype, dest_dtype ; transpose algorithm

;+
; Loop through all the rows.  Row and column upper and lower bounds have been
; initialized on the stack.
;-

LOOP_1ST_SUB'src_dtype'dest_dtype':
	MOVL	lower_bnd2(SP), R11		; R11 has 2nd lower bound

;+
; Loop through all the elements (columns) of the current row.  Column lower
; bound is initialized in R11.  Column upper bound is on the stack.
; Distinguish array by data type so that the correct fetch routine can 
; retrieve the data, the correct conversion can be done and the correct
; store routine can be called.
;-

LOOP_2ND_SUB'src_dtype'dest_dtype':

;+
; Get the data from source array
;-

	MOVL	R10, R0				; pointer to array dest
	MOVL	lower_bnd1(SP), R1		; current row
	MOVL	R11, R2				; current col
	FETCH	'src_dtype'			; fetch data from src array

;+
; If the data types of the source and destination arrays is different,
; convert the data to the destination type.  If scaling is needed (ie if
; at least one but not both of the arrays is double) scale the data.
;-

	.IF	DIF	src_dtype, dest_dtype	; src and dest arrays are not
						; save data type
	.IF	IDN	src_dtype, G		; source is gfloat
	.IF	IDN	dest_dtype, D		; don't try to CVTGD
	CVTGH	R0, R0				; promote source to hfloat
	.IFF					; dest is not dbl
	CVT'src_dtype'dest_dtype'	R0, R0	; OK to cvt to dest type
	.ENDC
	.IFF
	.IF	IDN	src_dtype, D		; source is double
	MOVD	R0, -(SP)			; save the data
	MOVL	SF$L_SAVE_FP(FP), R0		; pass FP to get scale
	JSB	G^BAS$$SCALE_R1			; get scale in R0 & R1
						; call a BLISS routine because
						; the frame offsets are only
						; defined for BLISS
	DIVD3	R0, (SP)+, R0			; scale
	.IF	IDN	dest_dtype, G		; can't CVTDG
	CVTDH	R0, R0				; so promote to hfloat
	.IFF					; dest is not gfloat
	CVT'src_dtype'dest_dtype' 	R0, R0	; cvt src to dest type
	.ENDC
	.IFF
	CVT'src_dtype'dest_dtype'	R0, R0	; convert data from R0 into R0
	.IF	IDN	dest_dtype, D		; dest is double
	MOVD	R0, -(SP)			; save the data
	MOVL	SF$L_SAVE_FP(FP), R0		; pass FP to get scale
	JSB	G^BAS$$SCALE_R1			; get scale in R0 & R1
						; call a BLISS routine because
						; the frame offsets are only
						; defined for BLISS
	MULD2	(SP)+, R0			; scale
	JSB	G^MTH$DINT_R4			; integerize
	.ENDC
	.ENDC
	.ENDC
	.ENDC

;+
; Now store the data in the destination array.
; Hfloat passed by value takes 4 words, gfloat and double take 2 words, and
; all other supported daty types take 1 longword.
;-

	.IF	IDN	dest_dtype, H		; dtype is hfloat
	MOVL	dest_matrix(AP), R4		; pointer to array desc
	MOVL	lower_bnd1(SP), R6		; current row, put in col
	MOVL	R11, R5				; current col, put in row
	.IFF
	.IF	IDN	dest_dtype, G		; dtype is gfloat
	MOVL	dest_matrix(AP), R2		; pointer to array desc
	MOVL	lower_bnd1(SP), R4		; current row, put in col
	MOVL	R11, R3				; current col, put in row
	.IFF
	.IF	IDN	dest_dtype, D		; see if dtype is double
	MOVL	dest_matrix(AP), R2		; pointer to array desc
	MOVL	lower_bnd1(SP), R4		; current row, put in col
	MOVL	R11, R3				; current column, put in row
	.IFF					; all other data types here
	MOVL	dest_matrix(AP), R1		; pointer to array desc
	MOVL	lower_bnd1(SP), R3		; current row, put in col
	MOVL	R11, R2				; current col, put in row
	.ENDC
	.ENDC
	.ENDC					; code now same for all dtypes
	MOV'dest_dtype'	R0, DATA(SP)		; store value in DATA
	STORE	'dest_dtype'			; store in array
	INCL	R11				; get next column
	CMPL	R11, R9				; see if last column done
	BGTR	3$
	BRW	LOOP_2ND_SUB'src_dtype'dest_dtype' ; no, continue inner loop

;+
; Have completed entire row.  See if it was the last row.  If not,
; continue with next row.
;-

3$:	INCL	lower_bnd1(SP)			; get next row
	CMPL	lower_bnd1(SP), upper_bnd1(SP)	; see if last row done
	BGTR	5$
	BRW	LOOP_1ST_SUB'src_dtype'dest_dtype' ; no, continue outer loop

5$:	RET					; yes, finished

	.ENDM

	.SBTTL	BAS$MAT_TRN  - Transpose one matrix into another
;++
; FUNCTIONAL DESCRIPTION:
;
;	Transpose one matrix into another.  If the src matrix has 2 dimensions,
;	redimension the output matrix to have the number of rows that the src
;	has columns and the number of columns that the src has rows.  (thereby
;	ensuring that the dest matrix also has 2 dimensions).  Initialize all
;	the necessary looping information on the stack.  Conversions will have
;	to be done from the source data type to the destination data type, so
;	divide the looping portion according to the data types.
;
; CALLING SEQUENCE:
;
;	CALL BASMAT_TRN (src_matrix.rx.da, dest_matrix.wx.da)
;
; INPUT PARAMETERS:
;
	src_matrix = 4
;
; IMPLICIT INPUTS:
;
;	Scale from the callers frame to scale double precision.
;
; OUTPUT PARAMETERS:
;
	dest_matrix = 8
;
; IMPLICIT OUTPUTS:
;
;	NONE
;
; FUNCTION VALUE:
; COMPLETION CODES:
;
;	NONE
;
; SIDE EFFECTS:
;
;	This routine calls the redimensioning routine and the array element
;	fetch and store routines and therefore may signal any of their errors.
;	It may also signal any of the errors listed in the externals section.
;	It may also cause the destination array to have different dimensions.
;
;--

	.ENTRY BAS$MAT_TRN, ^M<R2,R3,R4,R5,R6,R7,R8,R9,R10,R11,IV>

;+
;	REGISTER USAGE
;	R0 - R8 destroyed by fetch and store routines
;	R9	upper bound for 2nd subscript
;	R10	pointer to array descriptor
;	R11	current value of 2nd subscript
;-

;+
; Put routine arguments into registers for ease of use.
; If block 3 of array descriptor (bounds) is not present then error.
;-

	CLRQ	-(SP)
	CLRQ	-(SP)
	CLRQ	-(SP)
	MOVL	src_matrix(AP), R10		; ptr to array descr in R10
	BBC	#DSC$V_FL_BOUNDS, DSC$B_AFLAGS(R10), ERR_ARGDONMAT
						; exit if block 3 not
						; present in descriptor

;+
; Set up limits for looping through all elements
;-

	CMPB	DSC$B_DIMCT(R10), #2		; determine # of subscripts
	BEQLU	INIT_SUBS			; 2 subs, initialize loop
						; not 2 subs, fall into error

ERR_MATDIMERR:
	PUSHL	#BAS$K_MATDIMERR		; signal error, not 2 dims
	CALLS	#1, G^BAS$$STOP			;  for src matrix

ERR_ARGDONMAT:
	PUSHL	#BAS$K_ARGDONMAT		; signal error,
	CALLS	#1, G^BAS$$STOP			;  block 2 or 3 absent

ERR_ILLOPE:
	PUSHL	#BAS$K_ILLOPE			; signal error, DSC$A_POINTER
	CALLS	#1, G^BAS$$STOP			;  same for src & dest matrices

;+
; There are 2 subscripts.  Make sure the source and dest matrices are not the
; same.  Check and redimension the destination array if
; necessary.  Put the upper bound for both subscripts on the
; stack and make sure that the lower bound for both subscripts will start
; at 1 (do not alter row or col 0)
;-

INIT_SUBS:
	MOVL	dest_matrix(AP), R11		; get pointer to dest matrix
	CMPB	DSC$B_CLASS(R10), #DSC$K_CLASS_A ; is src virtual
	BNEQU	VIRTUAL				; src is virtual go process
	CMPB	DSC$B_CLASS(R11), #DSC$K_CLASS_A ; is dest virtual?
	BNEQU	INIT_SUBS_2			; yes, cant be same array
	CMPL	DSC$A_POINTER(R10), DSC$A_POINTER(R11) ; are matrices the same?
	BEQLU	ERR_ILLOPE			; yes, error
	BRB	INIT_SUBS_2
VIRTUAL:
	CMPB	DSC$B_CLASS(R11), #DSC$K_CLASS_A ; is dest virtual
	BEQLU	INIT_SUBS_2			; no, cant be same array
	CMPL	DSC$L_LOGUNIT(R10), DSC$L_LOGUNIT(R11) ; are matrices same
	BNEQ	INIT_SUBS_2			; no
	CMPL	DSC$L_BYTEOFF(R10), DSC$L_BYTEOFF(R11) ; are the matrices
						; the same, (test for same by
						;  same dsc$l_byteoff, dsc$l_logunit)
	BEQL	ERR_ILLOPE			; yes, error
INIT_SUBS_2: 
	PUSHL	dsc$l_u1_2(R10)			; 2nd upr bnd, make 1st in dest
	PUSHL	dsc$l_u2_2(R10)			; 1st upr bnd, make 2nd in dest
	PUSHL	R11				; dest array pointer
	CALLS	#3, G^BAS$MAT_REDIM		; redimension destination
	PUSHL	dsc$l_u1_2(R10)			; 1st upper bound
	PUSHL	dsc$l_l1_2(R10)			; 1st lower bound
	BGTR	1$				; not row 0 or neg, do cols
	MOVL	#1, (SP)			; start with row 1
1$:	MOVL	dsc$l_u2_2(R10), R9		; 2nd upper bound
	PUSHL	dsc$l_l2_2(R10)			; 2nd lower bound
	BGTR	SEPARATE_DTYPES			; not col 0 or neg, go loop
	MOVL	#1, (SP)			; start with col 1

;+
; Algorithm now differs according to data types
;-

SEPARATE_DTYPES:
	MOVL	R10, R5				; save original pointer
5$:	CASEB	DSC$B_DTYPE(R5), #DSC$K_DTYPE_B, #<DSC$K_DTYPE_D - DSC$K_DTYPE_B>
2$:	.WORD	BYTE-2$				; code for byte dtype
	.WORD	WORD-2$				; code for word dtype
	.WORD	LONG-2$				; code for long dtype
	.WORD	ERR_DATTYPERR-2$		; quad not supported
	.WORD	FLOAT-2$			; code for float dtype
	.WORD	DOUBLE-2$			; code for double dtype

;+
; G and H floating fall outside the range of the CASEB, so check for them
; separately.
;-

	CMPB	DSC$B_DTYPE(R5), #DSC$K_DTYPE_G
	BNEQ	3$
	BRW	GFLOAT

3$:	CMPB	DSC$B_DTYPE(R5), #DSC$K_DTYPE_H
	BNEQ	4$
	BRW	HFLOAT

4$:	CMPB	DSC$B_DTYPE(R5), #DSC$K_DTYPE_DSC
	BNEQ	ERR_DATTYPERR
	MOVL	4(R5), R5			; R5 <-- addr of descriptor
	BRB	5$				; CASE again on dtype in desc

ERR_DATTYPERR:
	PUSHL	#BAS$K_DATTYPERR		; Signal error, unsupported
	CALLS	#1, G^BAS$$STOP			; dtype in array desc

	.PAGE
;+
; Source array is a byte array.  Now differentiate on the destination type.
;-

BYTE:	MOVL	R11, R5				; move pointer into R5
5$:	CASEB	DSC$B_DTYPE(R5), #DSC$K_DTYPE_B, #<DSC$K_DTYPE_D - DSC$K_DTYPE_B>
1$:	.WORD	BYTE_TO_BYTE-1$			; code for byte dtype
	.WORD	BYTE_TO_WORD-1$			; code for word dtype
	.WORD	BYTE_TO_LONG-1$			; code for long dtype
	.WORD	ERR_DATTYPERR-1$		; quad not supported
	.WORD	BYTE_TO_FLOAT-1$		; code for float dtype
	.WORD	BYTE_TO_DOUBLE-1$		; code for double dtype

	CMPB	DSC$B_DTYPE(R5), #DSC$K_DTYPE_G
	BNEQ	2$
	BRW	BYTE_TO_GFLOAT

2$:	CMPB	DSC$B_DTYPE(R5), #DSC$K_DTYPE_H
	BNEQ	3$
	BRW	BYTE_TO_HFLOAT

3$:	CMPB	DSC$B_DTYPE(R5), #DSC$K_DTYPE_DSC
	BNEQ	4$
	MOVL	4(R5), R5			; R5 <-- addr of descriptor
	BRB	5$				; CASE again on dtype in desc

4$:	BRW	ERR_DATTYPERR			; unsupported dtype

;+
; Now type of source and destination arrays are known.  Use the macro to
; generate the code for each case
;-

	.PAGE
BYTE_TO_BYTE:	$BAS$MAT_TRN	B, B

	.PAGE
BYTE_TO_WORD:	$BAS$MAT_TRN	B, W

	.PAGE
BYTE_TO_LONG:	$BAS$MAT_TRN	B, L

	.PAGE
BYTE_TO_FLOAT:	$BAS$MAT_TRN	B, F

	.PAGE
BYTE_TO_DOUBLE:	$BAS$MAT_TRN	B, D

	.PAGE
BYTE_TO_GFLOAT: $BAS$MAT_TRN	B, G

	.PAGE
BYTE_TO_HFLOAT: $BAS$MAT_TRN	B, H

	.PAGE
;+
; Source array is a word array.  Now differentiate on the destination type.
;-

WORD:	MOVL	R11, R5				; move original pointer in R5
5$:	CASEB	DSC$B_DTYPE(R5), #DSC$K_DTYPE_B, #<DSC$K_DTYPE_D - DSC$K_DTYPE_B>
1$:	.WORD	WORD_TO_BYTE-1$			; code for byte dtype
	.WORD	WORD_TO_WORD-1$			; code for word dtype
	.WORD	WORD_TO_LONG-1$			; code for long dtype
	.WORD	ERR_DATTYPERR-1$		; quad not supported
	.WORD	WORD_TO_FLOAT-1$		; code for float dtype
	.WORD	WORD_TO_DOUBLE-1$		; code for double dtype

	CMPB	DSC$B_DTYPE(R5), #DSC$K_DTYPE_G
	BNEQ	2$
	BRW	WORD_TO_GFLOAT

2$:	CMPB	DSC$B_DTYPE(R5), #DSC$K_DTYPE_H
	BNEQ	3$
	BRW	WORD_TO_HFLOAT

3$:	CMPB	DSC$B_DTYPE(R5), #DSC$K_DTYPE_DSC
	BNEQ	4$
	MOVL	4(R5), R5			; R5 <-- addr of descriptor
	BRB	5$				; CASE again on dtype in desc

4$:	BRW	ERR_DATTYPERR			; unsupported dtype

;+
; Now type of source and destination arrays are known.  Use the macro to
; generate the code for each case
;-

	.PAGE
WORD_TO_BYTE:	$BAS$MAT_TRN	W, B

	.PAGE
WORD_TO_WORD:	$BAS$MAT_TRN	W, W

	.PAGE
WORD_TO_LONG:	$BAS$MAT_TRN	W, L

	.PAGE
WORD_TO_FLOAT:	$BAS$MAT_TRN	W, F

	.PAGE
WORD_TO_DOUBLE:	$BAS$MAT_TRN	W, D

	.PAGE
WORD_TO_GFLOAT: $BAS$MAT_TRN	W, G

	.PAGE
WORD_TO_HFLOAT: $BAS$MAT_TRN	W, H

	.PAGE
;+
; Source array is a longword array.  Now differentiate on the destination type
;-

LONG:	MOVL	R11, R5				; recover original pointer
5$:	CASEB	DSC$B_DTYPE(R5), #DSC$K_DTYPE_B, #<DSC$K_DTYPE_D - DSC$K_DTYPE_B>
1$:	.WORD	LONG_TO_BYTE-1$			; code for byte dtype
	.WORD	LONG_TO_WORD-1$			; code for word dtype
	.WORD	LONG_TO_LONG-1$			; code for long dtype
	.WORD	ERR_DATTYPERR-1$		; quad not supported
	.WORD	LONG_TO_FLOAT-1$		; code for float dtype
	.WORD	LONG_TO_DOUBLE-1$		; code for double dtype

	CMPB	DSC$B_DTYPE(R5), #DSC$K_DTYPE_G
	BNEQ	2$
	BRW	LONG_TO_GFLOAT

2$:	CMPB	DSC$B_DTYPE(R5), #DSC$K_DTYPE_H
	BNEQ	3$
	BRW	LONG_TO_HFLOAT

3$:	CMPB	DSC$B_DTYPE(R5), #DSC$K_DTYPE_DSC
	BNEQ	4$
	MOVL	4(R5), R5			; R5 <-- addr of descriptor
	BRB	5$				; CASE again on dtype in desc

4$:	BRW	ERR_DATTYPERR			; unsupported dtype

;+
; Now type of source and destination arrays are known.  Use the macro to
; generate the code for each case
;-
	.PAGE
LONG_TO_BYTE:	$BAS$MAT_TRN	L, B

	.PAGE
LONG_TO_WORD:	$BAS$MAT_TRN	L, W

	.PAGE
LONG_TO_LONG:	$BAS$MAT_TRN	L, L

	.PAGE
LONG_TO_FLOAT:	$BAS$MAT_TRN	L, F

	.PAGE
LONG_TO_DOUBLE:	$BAS$MAT_TRN	L, D

	.PAGE
LONG_TO_GFLOAT: $BAS$MAT_TRN	L, G

	.PAGE
LONG_TO_HFLOAT: $BAS$MAT_TRN	L, H

	.PAGE
;+
; Source array is a floating array.  Now differentiate on the destination type
;-

FLOAT:	MOVL	R11, R5			; recover original pointer
5$:	CASEB	DSC$B_DTYPE(R5), #DSC$K_DTYPE_B, #<DSC$K_DTYPE_D - DSC$K_DTYPE_B>
1$:	.WORD	FLOAT_TO_BYTE-1$		; code for byte dtype
	.WORD	FLOAT_TO_WORD-1$		; code for word dtype
	.WORD	FLOAT_TO_LONG-1$		; code for long dtype
	.WORD	ERR_DATTYPERR-1$		; quad not supported
	.WORD	FLOAT_TO_FLOAT-1$		; code for float dtype
	.WORD	FLOAT_TO_DOUBL-1$		; code for double dtype

	CMPB	DSC$B_DTYPE(R5), #DSC$K_DTYPE_G
	BNEQ	2$
	BRW	FLOAT_TO_GFLOA

2$:	CMPB	DSC$B_DTYPE(R5), #DSC$K_DTYPE_H
	BNEQ	3$
	BRW	FLOAT_TO_HFLOA

3$:	CMPB	DSC$B_DTYPE(R5), #DSC$K_DTYPE_DSC
	BNEQ	4$
	MOVL	4(R5), R5			; R5 <-- addr of descriptor
	BRB	5$				; CASE again on dtype in desc

4$:	BRW	ERR_DATTYPERR			; unsupported dtype

;+
; Now type of source and destination arrays are known.  Use the macro to
; generate the code for each case
;-
	.PAGE
FLOAT_TO_BYTE:  $BAS$MAT_TRN	F, B

	.PAGE
FLOAT_TO_WORD:	$BAS$MAT_TRN	F, W

	.PAGE
FLOAT_TO_LONG:	$BAS$MAT_TRN	F, L

	.PAGE
FLOAT_TO_FLOAT:	$BAS$MAT_TRN	F, F

	.PAGE
FLOAT_TO_DOUBL: $BAS$MAT_TRN	F, D

	.PAGE
FLOAT_TO_GFLOA: $BAS$MAT_TRN	F, G

	.PAGE
FLOAT_TO_HFLOA:	$BAS$MAT_TRN	F, H

	.PAGE
;+
; Source array is a double array.  Now differentiate on the destination type.
;-

DOUBLE:	MOVL	R11, R5			; recover original pointer
5$:	CASEB	DSC$B_DTYPE(R5), #DSC$K_DTYPE_B, #<DSC$K_DTYPE_D - DSC$K_DTYPE_B>
1$:	.WORD	DOUBLE_TO_BYTE-1$		; code for byte dtype
	.WORD	DOUBLE_TO_WORD-1$		; code for word dtype
	.WORD	DOUBLE_TO_LONG-1$		; code for long dtype
	.WORD	ERR_DATTYPERR-1$		; quad not supported
	.WORD	DOUBLE_TO_FLOAT-1$		; code for float dtype
	.WORD	DOUBLE_TO_DOUBL-1$		; code for double dtype

	CMPB	DSC$B_DTYPE(R5), #DSC$K_DTYPE_G
	BNEQ	2$
	BRW	DOUBLE_TO_GFLOA

2$:	CMPB	DSC$B_DTYPE(R5), #DSC$K_DTYPE_H
	BNEQ	3$
	BRW	DOUBLE_TO_HFLOA

3$:	CMPB	DSC$B_DTYPE(R5), #DSC$K_DTYPE_DSC
	BNEQ	4$
	MOVL	4(R5), R5			; R5 <-- addr of descriptor
	BRB	5$				; CASE again on dtype in desc

4$:	BRW	ERR_DATTYPERR			; unsupported dtype

;+
; Now type of source and destination arrays are known.  Use the macro to
; generate the code for each case
;-
	.PAGE
DOUBLE_TO_BYTE: $BAS$MAT_TRN	D, B

	.PAGE
DOUBLE_TO_WORD:	$BAS$MAT_TRN	D, W

	.PAGE
DOUBLE_TO_LONG:	$BAS$MAT_TRN	D, L

	.PAGE
DOUBLE_TO_FLOAT: $BAS$MAT_TRN	D, F

	.PAGE
DOUBLE_TO_DOUBL: $BAS$MAT_TRN	D, D

	.PAGE
DOUBLE_TO_GFLOA: $BAS$MAT_TRN	D, G

	.PAGE
DOUBLE_TO_HFLOA: $BAS$MAT_TRN	D, H

	.PAGE
;+
; Source array is a gfloat array.  Now differentiate on the destination type.
;-

GFLOAT:	MOVL	R11, R5				; recover original pointer
5$:	CASEB	DSC$B_DTYPE(R5), #DSC$K_DTYPE_B, #<DSC$K_DTYPE_D - DSC$K_DTYPE_B>
1$:	.WORD	GFLOAT_TO_BYTE-1$		; code for byte dtype
	.WORD	GFLOAT_TO_WORD-1$		; code for word dtype
	.WORD	GFLOAT_TO_LONG-1$		; code for long dtype
	.WORD	ERR_DATTYPERR-1$		; quad not supported
	.WORD	GFLOAT_TO_FLOAT-1$		; code for float dtype
	.WORD	GFLOAT_TO_DOUBL-1$		; code for double dtype

	CMPB	DSC$B_DTYPE(R5), #DSC$K_DTYPE_G
	BNEQ	2$
	BRW	GFLOAT_TO_GFLOA

2$:	CMPB	DSC$B_DTYPE(R5), #DSC$K_DTYPE_H
	BNEQ	3$
	BRW	GFLOAT_TO_HFLOA

3$:	CMPB	DSC$B_DTYPE(R5), #DSC$K_DTYPE_DSC
	BNEQ	4$
	MOVL	4(R5), R5			; R5 <-- addr of descriptor
	BRB	5$				; CASE again on dtype in desc

4$:	BRW	ERR_DATTYPERR			; unsupported dtype

;+
; Now type of source and destination arrays are known.  Use the macro to
; generate the code for each case
;-

	.PAGE
GFLOAT_TO_BYTE:	$BAS$MAT_TRN	G, B

	.PAGE
GFLOAT_TO_WORD:	$BAS$MAT_TRN	G, W

	.PAGE
GFLOAT_TO_LONG:	$BAS$MAT_TRN	G, L

	.PAGE
GFLOAT_TO_FLOAT: $BAS$MAT_TRN	G, F

	.PAGE
GFLOAT_TO_DOUBL: $BAS$MAT_TRN	G, D

	.PAGE
GFLOAT_TO_GFLOA: $BAS$MAT_TRN	G, G

	.PAGE
GFLOAT_TO_HFLOA: $BAS$MAT_TRN	G, H

	.PAGE
;+
; Source array is a hfloat array.  Now differentiate on the destination type.
;-

HFLOAT:	MOVL	R11, R5			; recover original pointer
5$:	CASEB	DSC$B_DTYPE(R5), #DSC$K_DTYPE_B, #<DSC$K_DTYPE_D - DSC$K_DTYPE_B>
1$:	.WORD	HFLOAT_TO_BYTE-1$		; code for byte dtype
	.WORD	HFLOAT_TO_WORD-1$		; code for word dtype
	.WORD	HFLOAT_TO_LONG-1$		; code for long dtype
	.WORD	ERR_DATTYPERR-1$		; quad not supported
	.WORD	HFLOAT_TO_FLOAT-1$		; code for float dtype
	.WORD	HFLOAT_TO_DOUBL-1$		; code for double dtype

	CMPB	DSC$B_DTYPE(R5), #DSC$K_DTYPE_G
	BNEQ	2$
	BRW	HFLOAT_TO_GFLOA

2$:	CMPB	DSC$B_DTYPE(R5), #DSC$K_DTYPE_H
	BNEQ	3$
	BRW	HFLOAT_TO_HFLOA

3$:	CMPB	DSC$B_DTYPE(R5), #DSC$K_DTYPE_DSC
	BNEQ	4$
	MOVL	4(R5), R5			; R5 <-- addr of descriptor
	BRB	5$				; CASE again on dtype in desc

4$:	BRW	ERR_DATTYPERR			; unsupported dtype

;+
; Now type of source and destination arrays are known.  Use the macro to
; generate the code for each case
;-

	.PAGE
HFLOAT_TO_BYTE:	$BAS$MAT_TRN	H, B

	.PAGE
HFLOAT_TO_WORD:	$BAS$MAT_TRN	H, W

	.PAGE
HFLOAT_TO_LONG:	$BAS$MAT_TRN	H, L

	.PAGE
HFLOAT_TO_FLOAT: $BAS$MAT_TRN	H, F

	.PAGE
HFLOAT_TO_DOUBL: $BAS$MAT_TRN	H, D

	.PAGE
HFLOAT_TO_GFLOA: $BAS$MAT_TRN	H, G

	.PAGE
HFLOAT_TO_HFLOA: $BAS$MAT_TRN	H, H

	.END					; end of BAS$MAT_TRN
