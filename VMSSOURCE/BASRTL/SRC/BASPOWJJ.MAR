	.TITLE	BAS$POWJJ		; BASIC integer ** integer
	.IDENT	/1-006/			; File: BASPOWJJ.MAR	Edit: LB1006

;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982, 1984 BY				    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;

;++
; FACILITY: Basic Support Library
;
; ABSTRACT:
;
;	This module contains entry points to support exponentiation 
;	(** or ^) in BASIC-PLUS-2 for LONG ** LONG.
;
; ENVIRONMENT: User Mode, AST Reentrant
;
;--
; AUTHOR: R. Will	, CREATION DATE: 22-NOV-78
;
; MODIFIED BY:
;
; R. Will,	 : VERSION 01
; 1-01	- Original 
; 1-02  - Fix comments, change BRW to JMP. RW  7-Dec-78
; 1-003	- Add "_" to the PSECT directive.  JBS 22-DEC-78
; 1-004	- Redo the case analysis of the BASE leq 0 case for
;	   compatability with the PDP-11.  JBS 24-APR-1979
; 1-005 - Make all external references GENERAL mode addressing.  SBL 15-June-1981
; 1-006 - Check the flags word in the BASIC frame for the setting of
;	  the integer overflow bit and use that value as the setting
;	  (or clearing) of the PSW.  Also added entry point
;	  BAS$$POWJJ_NIV to ensure that the IV bit in the PSL is
;	  cleared before giving control to OTS$POWJJ.  LB 15-May-1982
;

	.SBTTL	DECLARATIONS
;
; INCLUDE FILES:
;

;
; EXTERNAL DECLARATIONS:
;
	.DSABL	GBL			; Prevent undeclared
					; symbols from being
					; automatically global.

	.EXTRN	OTS$POWJJ		; OTS$ long ** long exponentation
	.EXTRN	BAS$K_DIVBY_ZER		; Divide by Zero
	.EXTRN	BAS$$STOP		; Error reporting routine
	.EXTRN	BAS$HANDLER		; BASIC condition handler

;
; MACROS:
;

;
; EQUATED SYMBOLS:
;

;
; OWN STORAGE:
;

;
; PSECT DECLARATIONS:
;
	.PSECT _BAS$CODE PIC, USR, CON, REL, LCL, SHR, -
			EXE, RD, NOWRT, LONG


	.SBTTL	BAS$POWJJ  - BASIC word ** word
;++
; FUNCTIONAL DESCRIPTION:
;
;	This routine takes BASE ** EXP, using the following table
;	for unusual cases:
;
;	BASE > 0			Call OTS$POWJJ, normal case.
;	BASE = 0, EXP > 0		Return 0.
;	BASE = 0, EXP = 0		Return 1.
;	BASE = 0, EXP < 0		Error: divide by zero
;	BASE < 0, EXP even 		Call OTS$POWJJ with -BASE
;	BASE < 0, EXP odd 		Call OTS$POWJJ with -BASE, negate result
;
; CALLING SEQUENCE:
;
;	CALL result.ww.v = BAS$POWJJ (base.rl.v, exponent.rl.v)
;
; INPUT PARAMETERS:
;
	base = 4
	exponent = 8
;
; IMPLICIT INPUTS:
;
;	NONE
;
; OUTPUT PARAMETERS:
;
;	NONE
;
; IMPLICIT OUTPUTS:
;
;	NONE
;
; FUNCTION VALUE:
; COMPLETION CODES:
;
;	long result of exponentiation
;
; SIDE EFFECTS:
;
;	Will signal Divide By Zero if its arguments are bad,
;	and  OTS$POWJJ may also signal.
;
;--

BAS$POWJJ::	.MASK OTS$POWJJ		; Entry point
					; Since this routine uses no 
					; registers and usually transfers
					; control to OTS$POWJJ, we copy
					; its register save mask and then
					; JMP past its save mask and only
					; save the registers once

;+
; On a call to BAS$POWJJ, the flags word contained in the BASIC frame
; defines whether integer overflow should or should not be enabled.
; The value of the flag should dictate the setting within the PSL.
;-
	MOVL	12(FP),R2		; Fetch the saved frame pointer
	MOVAL	G^BAS$HANDLER,R1	; Fetch addr of BAS$HANDLER
	CMPL	0(R2),R1		; Check if this is a BASIC frame
	BNEQ	9$			; Branch if not a BASIC frame
	MOVW	-26(R2),R2		; Fetch flags word from BASIC frame
	BICW	#^XF7FF,R2		; Clear all but IV bit
	TSTW	R2			; Check if integer overflow is set
	BEQL	8$			; Branch if clear
	BRB	9$			; Continue as usual
8$:	BICPSW	#^X20			; Clear integer overflow in PSW
9$:	TSTW	base(AP)		; Test base relationship to zero
	BLEQ	1$			; If base leq 0, do case analysis
	JMP	G^OTS$POWJJ+2		; Transfer control to the OTS$
					; routine to do exponentiation
;+
; Come here if the base is less than or equal to zero.  We must filter
; several special cases, as described above.
;-
1$:	BEQL	4$			; Branch if base = 0
	PUSHL	exponent(AP)		; Stack EXP as parameter to OTS$POWJJ
	MNEGL	base(AP), -(SP)		; Stack -BASE as param to OTS$POWJJ
	TSTW	R2			; Check if IV is set
	BNEQ	7$			; Do the regular CALL to OTS$POWJJ
	CALLS	#2,G^BAS$$POWJJ_NIV	; Clear IV before JMPing to OTS$POWJJ
	BRB	10$
7$:	CALLS	#2, G^OTS$POWJJ		; Call integer power routines
10$:	BLBC	exponent(AP),2$		; Branch if exponent even
	MNEGL	R0, R0			; Exponent odd, negate the result
2$:	RET				; and return with it.
;+
; Come here if the base is equal to zero.  The value we return depends
; upon the sign of the exponent.
;-
4$:	TSTL	exponent(AP)		; Test the exponent against zero
	BLSS	6$			; Branch if exponent lss 0
	BEQL	5$			; Branch if exponent is 0
;+
; Come here if the base is zero and the exponent is greater than zero.
; BASIC defines this as 0.
;-
	CLRL	R0			; R0 = 0
	RET				; Return to caller
;+
; Come here if the base is zero and the exponent is zero.  BASIC defines
; this as 1.
;-
5$:	MOVL	#1, R0			; R0 = 1
	RET				; Return to caller.
;+
; Come here if the base is zero and the exponent is less than zero.
; BASIC defines this as an error.
;-
6$:	MOVZBL	#BAS$K_DIVBY_ZER, -(SP)	; Divide by zero
	CALLS	#1, G^BAS$$STOP		; Report error, never return.
;

	.SBTTL	BAS$$POWJJ_NIV

;++
; Functional Description:
;
;	This routine is an internal entry point, called only by BAS$POWJJ
;	whose sole purpose in this world is to turn off the integer
;	overflow bit in the PSL for the case where the base is less than
;	zero, and where the flags bit in the BASIC frame indicate to turn
;	off integer overflow.
;
; Calling Sequence:
;
;	CALL BAS$POWJJ (base.rw.v, exponent.rw.v)
;
; Input Parameters:
;
;	None
;
; Output Parameters:
;
;	None
;
; Side Effects:
;
;	OTS$POWJJ may signal
;--

BAS$$POWJJ_NIV::
		.MASK	OTS$POWJJ	; Entry point
		BICPSW	#^X20		; Clear IV bit in PSL
		JMP	G^OTS$POWJJ+2	; Transfer control to the OTS$
					; routine to do exponentiation
		
	.END

