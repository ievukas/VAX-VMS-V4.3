	$BEGIN	RMS0SDFP,000,RM$RMS,<SET DEFAULT FILE PROTECTION>

;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982, 1984 BY				    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;
;++
; Facility: rms32
;
; Abstract:
;		this routine sets the default file protection in
;		the process i/o control page.
;
; Environment:
;		star processor running starlet exec.
;
; Author: l f laverdure,	 creation date: 22-APR-1977
;
; Modified By:
;
;	V03-003	RAS0237		Ron Schaefer		16-Jan-1984
;		Change LMP0133 to use CTL$GL_PCB, instead of SCH$GL_CURPCB
;		so this code works on the secondary of a dual processor.
;
;	V03-002	LMP0133		L. Mark Pilant,		3-Aug-1983  15:05
;		Get default protection from PCB instead of P1 space.
;
;	V03-001	KBT0192		Keith B. Thompson	23-Aug-1982
;		Reorganize psects and rename entry point to single '$'
;
;	V02-006	REFORMAT	P S Knibbe	25-Jul-1980
;
;	V005	RAN0002		R A Newell	18-SEP-1978	09:19
;		rms32 isam modification.  redefinition of entry point to
;		resolve out of range branches.
;
; 01	-
;--


	.SBTTL	DECLARATIONS

;
; Include Files:
;

;
; Macros:
;

	$PSLDEF
	$RMSDEF
	$PCBDEF

;
; Equated Symbols:
;

;
; Own Storage:
;


	.SBTTL	RMS$SETDFPROT - SET DEFAULT FILE PROTECTION ROUTINE

;++
; RMS$SETDFPROT - Set default file protection
;
;  this routine optionally returns and/or sets the value of the
;  default file protection word in the process i/o control page.
;
; Calling sequence:
;
;	entered from exec as a result of user's calling sys$setdfprot
;
; Input Parameters:
;
;	ap	user's argument list addr
;		first argument gives the address of the new default file
;		protection word if any, else 0.
;		contains a second argument optionally giving the address of
;		a word in which the current default protection may be returned.
;
; Implicit Inputs:
;
;
; Output Parameters:
;
;	r1	destroyed
;	r0	status code
;	(note: this routine uses r0-r3 as saved in the system vector entry mask)
;
; Implicit Outputs:
;
;	optionally, pio$gw_dfprot set to the value of the input word
;	and/or the current default protection is returned to the caller
;
; Completion Codes:
;
;	standard rms, in particular, suc and ial.
;
; Side Effects:
;
;	none
;
;--


	$ENTRY	RMS$SETDFPROT
	$TSTPT	SETDFPROT

;
; see if the caller wants the protection returned
;

	RMSERR	IAL			; anticipate error
	MOVL	4(AP),R2		; get 1st argument
	BEQL	3$			; branch if no value to set
	IFNORD	#2,(R2),EXIT		; is the word readable?
	MOVW	(R2),R3			; save new value
3$:	MOVL	8(AP),R1		; possible address of return length
	BEQL	5$			; if caller doesn't want it, o.k.
	IFNOWRT	#2,(R1),EXIT		; writeable?
	MOVL	G^CTL$GL_PCB,R0		; get currect process PCB address
	MOVW	PCB$L_DEFPROT(R0),(R1)	; yes, so return it to the caller
5$:	TSTL	R2			; was there a new value?
	BEQL	7$			; branch if not
	$CMKRNL_S	SETPROT		; else copy it over
7$:	RMSSUC
EXIT:
	SSB	#16,R0
	RET

SETPROT:
	.WORD	0
	MOVL	G^CTL$GL_PCB,R0		; get currect process PCB address
	MOVW	R3,PCB$L_DEFPROT(R0)	; else copy it over
	RET

	.END
