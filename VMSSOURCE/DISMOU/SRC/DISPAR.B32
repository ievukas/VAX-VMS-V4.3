MODULE DISPAR (
		MAIN = DISMOUNT_PARSE,
		LANGUAGE (BLISS32),
		IDENT = 'V04-000'
		) =
BEGIN

!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982, 1984 BY				    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************

!++
!
! FACILITY:  MOUNT Utility Structure Level 1
!
! ABSTRACT:
!
!	This module contains the data base and utilities used to acquire the
!	DISMOUNT command line from the CLI parser.
!
! ENVIRONMENT:
!
!	STARLET operating system, including privileged system services
!	and internal exec routines.
!
!--
!
!
! AUTHOR:  Andrew C. Goldstein,	 CREATION DATE:  24-Oct-1977  10:45
!
! MODIFIED BY:
!
!	V03-003	HH0004		Hai Huang		28-Feb-1984
!		Add cluster-wide mount support (/CLUSTER qualifier).
!
!	V03-002	HH0003		Hai Huang		16-Feb-1984
!		Add forced dismount support (/ABORT qualifier).
!
!	V03-001	MCN0134		Maria del C. Nasr	31-Oct-1983
!		Change to new CLI interface.
!
!	V02-004	STJ0141		Steven T. Jeffreys	13-Nov-1981
!		Change interface to use the $DISMOU system service to
!		complete the dismount.  The version 2 command line
!		parser must be used.
!
!	V02-003 PCG0001		Peter C. George		03-Feb-1981 10:00
!		Change MOUNTMSG require to DISMOUMSG.
!
!	V02-002	ACG0181		Andrew C. Goldstein,	9-Oct-1980  16:01
!		Fix cross facility source reference
!
!	X0101	ACG0003		Andrew C. Goldstein,  29-Nov-1978  20:32
!	Add multi-volume disk support (/UNIT switch)
!
!	X0100	ACG0001		Andrew C. Goldstein,  24-Oct-1978  13:47
!	Previous revision history moved to [DISMOU.SRC]DISMOUNT.REV
!**


LIBRARY 'SYS$LIBRARY:LIB.L32';
REQUIRE 'LIB$:MOUDEF.B32';
REQUIRE 'LIBD$:[VMSLIB.OBJ]DISMOUMSG.B32';
LIBRARY 'SYS$LIBRARY:CLIMAC.L32';
LIBRARY 'SYS$LIBRARY:TPAMAC.L32';



! CLI routines
!

EXTERNAL ROUTINE
    CLI$PRESENT,
    CLI$GET_VALUE;

! Define descriptors for the CLI parser. 
!

BIND
    UNLOAD_DESC = $DESCRIPTOR ('UNLOAD'),
    UNIT_DESC   = $DESCRIPTOR ('UNIT'),
    ABORT_DESC  = $DESCRIPTOR ('ABORT'),
    CLUSTER_DESC= $DESCRIPTOR ('CLUSTER');


! CLI return status codes
!

EXTERNAL LITERAL
    CLI$_PRESENT,
    CLI$_NEGATED,
    CLI$_DEFAULTED;


GLOBAL ROUTINE DISMOUNT_PARSE : NOVALUE =

!++
!
! FUNCTIONAL DESCRIPTION:
!
!	This routine parses the DISMOUNT command line by calling the CLI
!	result parse routines.
!
! CALLING SEQUENCE:
!	DISMOUNT_PARSE
!
! INPUT PARAMETERS:
!	None
!
! IMPLICIT INPUTS:
!	NONE
!
! OUTPUT PARAMETERS:
!	NONE
!
! IMPLICIT OUTPUTS:
!	Dismount status code
!
! ROUTINE VALUE:
!	NONE
!
! SIDE EFFECTS:
!	NONE
!
!--

BEGIN


LOCAL
    DEVICE_DESC : $BBLOCK [DSC$C_S_BLN],	! descriptor for device
    TEMP_MASK	: BITVECTOR [32],		! mask for qualifiers
    STATUS;


! Initialize descriptor
!
CH$FILL (0, DSC$C_S_BLN, DEVICE_DESC);
DEVICE_DESC [DSC$B_CLASS] = DSC$K_CLASS_D;

! Get device name
!
CLI$GET_VALUE ( $DESCRIPTOR ('DEVICE'), DEVICE_DESC );

! Initialize mask to hold the correct dismount option bits.
!
TEMP_MASK = 0;

! Look for qualifiers, and set option bits accordingly.
!

IF CLI$PRESENT ( UNIT_DESC )
THEN
    TEMP_MASK [$BITPOSITION (DMT$V_UNIT)] = 1;

SELECTONE CLI$PRESENT ( UNLOAD_DESC ) OF
SET
    [CLI$_PRESENT,
     CLI$_DEFAULTED] : TEMP_MASK [$BITPOSITION (DMT$V_NOUNLOAD)] = 0;

    [CLI$_NEGATED]   : TEMP_MASK [$BITPOSITION (DMT$V_NOUNLOAD)] = 1;
TES;


IF CLI$PRESENT ( ABORT_DESC )
THEN
    TEMP_MASK [$BITPOSITION (DMT$V_ABORT)] = 1;


IF CLI$PRESENT ( CLUSTER_DESC )
THEN
    TEMP_MASK [$BITPOSITION (DMT$V_CLUSTER)] = 1;


! Call the dismount system service to finish the dismount.
!

STATUS = $DISMOU (DEVNAM=DEVICE_DESC, FLAGS=.TEMP_MASK);

$EXIT (CODE = .STATUS);

END;					! end of routine DISMOUNT_PARSE

END
ELUDOM
