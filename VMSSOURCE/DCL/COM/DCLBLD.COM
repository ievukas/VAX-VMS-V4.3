$ !
$ !	COMMAND FILE TO BUILD THE VAX/VMS DCL COMMAND INTERPRETER
$ !
$ GOTO 'PHASE'
$
$INIT:
$ !
$ !	CREATE THE DIRECTORIES
$ !
$ @SYSBLDCOM$:CREATEDIR
$ EXIT
$
$UPDATE:
$ !
$ !	Create updated sources.
$ !
$ SRCUPDATE CLIMSG.MSG			! Used to generate MLB file
$ PUTSYSMSG SRC$:CLIMSG.MSG		! Insert in SYSMSG.TLB
$ SRCUPDATE CLITABDEF.SDL
$ SRCUPDATE INTDEF.SDL
$ SRCUPDATE DCLDEF.MDL
$ SRCUPDATE CLIMAC.MAR
$ SRCUPDATE INTIMAGES.MAR
$ !
$ ! VMSLIB needs the next file during SRCLIB phase
$ !
$ DELSHRLIB LIB$:CLIMSG.B32
$ DELSHRLIB LIB$:CLIMSG.MAR
$ DELETE LIB$:CLIMSG.MAR;*, LIB$:CLIMSG.B32;*
$ MESSAGE/MDL=SRC$:CLIMSG/NOOBJ SRC$:CLIMSG
$ MCR MDL LIB$:CLIMSG=SRC$:CLIMSG
$ CPYSHRLIB LIB$:CLIMSG.B32		! Share file with DIR, CLIUTL, and VMSLIB
$ CPYSHRLIB LIB$:CLIMSG.MAR		! Share file with MCR, and VMSLIB
$
$ EXIT
$
$SRCLIB:
$ !
$ !	DELETE AND RE-CREATE MACRO LIBRARIES
$ !
$ DELSHRLIB LIB$:CLITABDEF.R32
$ DELSHRLIB LIB$:DCL.MLB
$ DELETE LIB$:CLITABDEF.MAR;*, LIB$:CLITABDEF.R32;*
$ SDL/LANGUAGE=(MACRO=LIB$:CLITABDEF,BLISS=LIB$:CLITABDEF)/VMS SRC$:CLITABDEF
$ CPYSHRLIB LIB$:CLITABDEF.R32		! Share file with CDU
$ CPYSHRLIB LIB$:CLITABDEF.MAR		! Share file with MCR
$ DELETE LIB$:INTDEF.MAR;*, LIB$:INTDEF.R32;*
$ SDL/LANGUAGE=(MACRO=LIB$:INTDEF,BLISS=LIB$:INTDEF)/VMS SRC$:INTDEF
$ CPYSHRLIB LIB$:INTDEF.R32		! Share file with MCR
$ DELETE LIB$:DCLDEF.MAR;*, LIB$:DCLDEF.B32;*
$ MCR MDL LIB$:DCLDEF=SRC$:DCLDEF
$ CPYSHRLIB LIB$:DCLDEF.B32		! Share file with CLE for PRC_L_TAB_VEC
$ DELETE LIB$:DCL.MLB;*
$ LIB/CREATE/MACRO LIB$:DCL -
	SRC$:CLIMAC,INTIMAGES,-
	LIB$:CLIMSG,CLITABDEF,DCLDEF,INTDEF,-
	SHRLIB$:PPDDEF			! Get file from LOGIN
$ CPYSHRLIB LIB$:DCL.MLB		! Share file with CLE(INTIMAGES), LOGIN, MCR, and CLIUTL
$ EXIT
$
$ASSEM:
$ !
$ !	DELETE AND RE-CREATE THE DCL OBJECTS AND LISTINGS
$ !
$ PURGE OBJ$:*.OBJ,LIS$:*.LIS,OBJ$:*.OBB
$ @SYSBLDCOM$:FASM DCL "" N 'UPDATES'
$ RENAME OBJ$:CLIGBL.OBJ;* LIB$:CLIGBL.STB	! For VMSLIB
$ CPYSHRLIB OBJ$:CLINT.OBJ			! For VMSLIB
$ RENAME OBJ$:CLINT.OBJ;* OBJ$:CLINT.OBB
$ RENAME OBJ$:EXPRESS.OBJ;* OBJ$:EXPRESS.OBB
$ RENAME OBJ$:STASTUB.OBJ;* OBJ$:STASTUB.OBB
$ EXIT
$
$OBJLIB:
$ !
$ !	Create object library from object modules.
$ !
$ @SYSBLDCOM$:OBJLIB DCL 200 512 40
$ LIB/REPLACE/OBJECT/NOGLOBALS LIB$:DCL OBJ$:EXPRESS.OBB,OBJ$:STASTUB.OBB
$ EXIT
$
$LNKLIB:
$ !
$ ! Null LNKLIB phase
$ !
$ EXIT
$
$LINK:
$ !
$ !  RECREATE MAP AND IMAGE FILES FOR DCL
$ !
$ @COM$:DCLLNK
$ EXIT
$
$RESULT:
$ !
$ !	COPY THE IMAGE AND MAP FILES TO SYSTEM AREAS
$ !
$ CPYRESEXE EXE$:DCL.EXE
$ CPYRESMAP MAP$:DCL.MAP
$ CPYRESLIS LIS$:*.LIS DCL.LSS
