%TITLE 'Set date to a user-specified date'
MODULE SETDAT (
		IDENT = 'V04-000'
%BLISS32[,
	ADDRESSING_MODE(EXTERNAL=LONG_RELATIVE,NONEXTERNAL=LONG_RELATIVE)
	]
		) =
BEGIN
!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982, 1984 BY				    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************
!
!++
! FACILITY:	 DSR (Digital Standard RUNOFF) / DSRPLUS
!
! ABSTRACT: Sets the date to a user-specified date.
!
!
! ENVIRONMENT: Transportable
!
! AUTHOR: R.W.Friday	 CREATION DATE: April, 1979
!

%SBTTL 'Revision History'
!
! MODIFIED BY:
!
!	002	REM00002	Ray Marshall	07-Mar-1983
!		Global edit of all modules. Updated module names, idents,
!		copyright dates. Changed require files to BLISS library.
!
!--

%SBTTL 'Module Level Declarations'
!
! TABLE OF CONTENTS:
!
!
! INCLUDE FILES:
!

LIBRARY 'NXPORT:XPORT';			! XPORT Library
REQUIRE 'REQ:RNODEF';			! RUNOFF variant definitions

%IF DSRPLUS %THEN
LIBRARY 'REQ:DPLLIB';			! DSRPLUS BLISS Library
%ELSE
LIBRARY 'REQ:DSRLIB';			! DSR BLISS Library
%FI

!
! MACROS:
!
MACRO
    DOPARM (P,MAX) =
	!Process this parameter only if it was specified.
	IF  .NMLST_DESCR (P) NEQ NM_NULL
	THEN
	    BEGIN
	    IF  .NMLST_DESCR (P) EQL NM_PLUS
			         OR
		.NMLST_DESCR (P) EQL NM_MINUS
	    THEN
		TEMP_DATIM [3-P] = .TEMP_DATIM [3-P] + .NMLST_VALUE (P)
	    ELSE
		TEMP_DATIM [3-P] = .NMLST_VALUE (P);

	    !Check for negative or GTR MAX value
	    IF  .TEMP_DATIM [3-P] LEQ 0
			 OR
		.TEMP_DATIM [3-P] GTR MAX
	    THEN
		!Illegal number value.
		BEGIN
		ERMA (RNFINM, FALSE);
		RETURN;
		END
	    END			%;
!
! EQUATED SYMBOLS:
!
!
! OWN STORAGE:
!
!
! EXTERNAL REFERENCES:
!

EXTERNAL
    DATTIM : VECTOR [6],			!Date and time.
    NMLST : NUMBER_LIST;			!GLNM returns command parameters here

!
EXTERNAL LITERAL				!Error messages
    RNFINM;

!

EXTERNAL ROUTINE
    ERMA,
    GLNM,
    RNODAT;

!


%SBTTL 'SETDAT  --'
GLOBAL ROUTINE setdat (handler_code) : NOVALUE =

!++
! FUNCTIONAL DESCRIPTION:
!
!	SETDAT calls GLNM to pick up the day, month, and year, in that order.
!	If the user says nothing, then the date is set to the system date.
!
! FORMAL PARAMETERS:
!
!	HANDLER_CODE is not examined.  It is passed to this routine
!	for reasons of conformity only.  It will always have the
!	value H_SET_DATE.
!
! IMPLICIT INPUTS:	None
!
! IMPLICIT OUTPUTS:	None
!
! ROUTINE VALUE:
! COMPLETION CODES:	None
!
! SIDE EFFECTS:		None
!
!--

    BEGIN

    LOCAL
	temp_datim : VECTOR [6];

    glnm (3);					!Request the day, month, and year.

    IF	.nmlst_count EQL 0  THEN
	BEGIN
	rnodat (temp_datim);	!Get entire system date/time.

		!Save just the system time part.

	dattim [0] = .temp_datim [0];		!Year
	DATTIM [1] = .TEMP_DATIM [1];		!Month
	DATTIM [2] = .TEMP_DATIM [2];		!Day

	RETURN;
	END;

    ! Assume the most complicated case: that the user is adjusting settings by
    ! adding/subtracting values.  Make a copy of the current time.

    temp_datim [0] = .dattim [0];		!Year
    temp_datim [1] = .dattim [1];		!Month
    temp_datim [2] = .dattim [2];		!Day

    !Validate the day, if given
    doparm (1,31);				!First parameter can have a maximum value of 31

    !Validate the month, if given
    IF	.nmlst_count GEQ 2  THEN
	doparm (2,12);				!Second paramater can have a maximum value of 12

    !Validate the year, if given
    IF	.nmlst_count GEQ 3  THEN
	doparm (3,9999);				!Maximum of 9999 

    !Make computed time currend
    dattim [0] = .temp_datim [0];		!Year
    dattim [1] = .temp_datim [1];		!Month
    dattim [2] = .temp_datim [2];		!Day

    END;					!End of SETDAT

END						!End of module
ELUDOM
