MODULE TPROBE (					!
		IDENT = 'V04-000'
%BLISS32[,
	ADDRESSING_MODE(EXTERNAL=LONG_RELATIVE,NONEXTERNAL=LONG_RELATIVE)
	]
		) =
BEGIN
!
!****************************************************************************
!*									    *
!*  COPYRIGHT (c) 1978, 1980, 1982, 1984 BY				    *
!*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
!*  ALL RIGHTS RESERVED.						    *
!* 									    *
!*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
!*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
!*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
!*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
!*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
!*  TRANSFERRED.							    *
!* 									    *
!*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
!*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
!*  CORPORATION.							    *
!* 									    *
!*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
!*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
!* 									    *
!*									    *
!****************************************************************************
!
!++
! FACILITY:	 DSR (Digital Standard RUNOFF) / DSRPLUS
!
! ABSTRACT: Checks to see if a keyword matches an entry.
!
!
! ENVIRONMENT: Transportable
!
! AUTHOR: R.W.Friday	 CREATION DATE: May, 1978
!

%SBTTL 'Revision History'
!
! MODIFIED BY:
!
!	004	KAD00004	Keith Dawson	17-Mar-1983
!		Fixed module name (was TPROB, now TPROBE).
!
!	003	KAD00003	Keith Dawson	07-Mar-1983
!		Global edit of all modules. Updated module names, idents,
!		copyright dates. Changed require files to BLISS library.
!
!--

%SBTTL 'Module Level Declarations'

!
! TABLE OF CONTENTS:
!
!
! INCLUDE FILES:
!

LIBRARY 'NXPORT:XPORT';			! XPORT Library
REQUIRE 'REQ:RNODEF';			! RUNOFF variant definitions

%IF DSRPLUS %THEN
LIBRARY 'REQ:DPLLIB';			! DSRPLUS BLISS Library
%ELSE
LIBRARY 'REQ:DSRLIB';			! DSR BLISS Library
%FI

!
! EQUATED SYMBOLS:
!

LITERAL
    ch_equal = 0,
    ch_lss = -1,
    ch_gtr = 1;

!
! OWN STORAGE:
!

GLOBAL ROUTINE tprobe (KEY, SIZE, ITEM) =

!++
! FUNCTIONAL DESCRIPTION:
!
!	Checks to see if 'key' is described by 'item'. If not, returns a 
!	condition code indicating how the search is to proceed. This
!	routine is called only from FIND.
!
! FORMAL PARAMETERS:
!
!	'Key' is the keyword to be checked. 'Size' is the length
!	of the keyword. 'Item' is the entry in the keyword table against
!	which 'key' is checked.
!
! IMPLICIT INPUTS:	None
!
! IMPLICIT OUTPUTS:	None
!
! ROUTINE VALUE:
! COMPLETION CODES:
!
!	See TPROB.REQ for the completion codes.
!
! SIDE EFFECTS:	None
!
!--

    BEGIN

    MAP
	item : REF VECTOR;

    SELECTONE true OF
	SET

	[.size EQL .item_length (item)] :

	    BEGIN
	    LOCAL
		r;
	    r = CH$COMPARE (.size, .key, .size, CH$PTR (p_word (item)));

	    CASE .r FROM -1 TO 1 OF
		SET
		[ch_equal]	:	RETURN found;
		[ch_lss]	:	RETURN try_above;
		[ch_gtr]	:	RETURN try_below;
		TES;
	    END;

	[.size GTR .item_length (item)] :
	    BEGIN
	    LOCAL
		r,
		siz;

	    siz = .item_length (item);
	    r = CH$COMPARE (.siz, .key, .siz, CH$PTR (p_word (item)));

	    CASE .r FROM -1 TO 1 OF
		SET
		[ch_equal] :
		    IF .kwitem_abbreviation (item)
		    THEN
			RETURN try_below
		    ELSE
			RETURN give_up;
		[ch_lss]	:	RETURN try_above;
		[ch_gtr]	:	RETURN try_below;
		TES;

	    END;

	[.size LSS .item_length (item)] :
	    BEGIN
	    LOCAL
		r;

	    r = CH$COMPARE (.size, .key, .size, CH$PTR (p_word (item)));

	    CASE .r FROM -1 TO 1 OF
		SET
		[ch_equal] :
		    IF .kwitem_abbreviation (item)
		    THEN
			RETURN try_above
		    ELSE
			IF .size GEQ .kwitem_minimum_match (item)
			THEN
			    RETURN found
			ELSE
			    RETURN try_above;

		[ch_lss]	:	RETURN try_above;

		[ch_gtr]	:	RETURN try_below;
		TES;
	    END;
	TES			!No semicolon -- avoid compiler compalint.

    END;					!End of TPROBE

END						!End of module
ELUDOM
