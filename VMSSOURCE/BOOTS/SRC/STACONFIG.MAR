	.TITLE	STACONFIG - MAIN PROGRAM FOR STANDALONE CONFIGURE
	.IDENT	'V04-000'

;
;****************************************************************************
;*									    *
;*  COPYRIGHT (c) 1978, 1980, 1982, 1984 BY				    *
;*  DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASSACHUSETTS.		    *
;*  ALL RIGHTS RESERVED.						    *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED   *
;*  ONLY IN  ACCORDANCE WITH  THE  TERMS  OF  SUCH  LICENSE  AND WITH THE   *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE OR  ANY  OTHER   *
;*  COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY   *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE IS  HEREBY   *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS  SUBJECT TO CHANGE WITHOUT NOTICE   *
;*  AND  SHOULD  NOT  BE  CONSTRUED AS  A COMMITMENT BY DIGITAL EQUIPMENT   *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY OF ITS   *
;*  SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.		    *
;* 									    *
;*									    *
;****************************************************************************
;

;++
; FACILITY:	STANDALONE CONFIGURE
;
; ABSTRACT:
;	THIS ROUTINE IS THE MAIN PROGRAM AND SUBROUTINES FOR
;	STANDALONE CONFIGURE.  THIS PROCESS IS USED TO CONFIGURE ALL 
;	DISK AND PORT DRIVERS (CO-INCIDENTALLY, PAPER TAPE AS WELL) FROM
;	SYSINIT.  ALL MSCP- AND HSC-SERVED DEVICES ARE CONFIGURED AS WELL,
;       IF A QUORUM DISK IS NEEDED.
;
; ENVIRONMENT:	USER, EXEC, AND KERNEL MODE
;
; AUTHOR:  MARYANN HINDEN,	CREATION DATE:	18-SEP-1979
;	   (ADAPTED FROM STASYSGEN)
;
; MODIFIED BY:
;
;	V03-005	RSH0075		R. Scott Hanna	14-oct-1983
;		Change the call to the ast routine AST_REC to cause
;		it to execute in exec mode.
;
;	V03-004	RSH0073		R. Scott Hanna	10-Oct-1983
;		Change the call to the ast routine AST_REC to cause
;		it to execute in kernel mode.
;
;	V03-003	WMC0003		Wayne Cardoza	09-Aug-1983
;		Put D devices back.
;
;	V03-002	WMC0002		Wayne Cardoza	05-Aug-1983
;		Add lbr$output_help.
;		Disable autoconfigure of D devices until this module is made
;		to deal with noncontigous drivers.
;
;	V03-001	WMC0001		Wayne Cardoza	01-Aug-1983
;		Add B00$EXEOPEN, BOO$FILCLOSE, BOO$UFOOPEN, EXE$LOAD_CODE
;
;--

	.SBTTL	DECLARATIONS
;
; INCLUDE FILES:
;
	$CLUBDEF			; Define cluster symbols

;
; MACROS:
;

;
; EQUATED SYMBOLS:
;

;
; OWN STORAGE:
;

	.PSECT	BOO$SYSGEN,WRT

BOO$GL_CMDOPT::	.LONG 0			; Options longword

SELECT_LIST:				; Select list for AUTOCONFIGURE ALL
	.ASCIC	/D/			; All disks
	.ASCIC	/P/			; All port drivers (and paper tape!)
	.BYTE	0			; End of list

ASCII_TIME:	.ASCID	/0 00:00:05.00/
BIN_TIME:	.BLKQ	1
BLANKS:		.ASCII  /                /	; 16 blanks


	.SBTTL	BOO$STACONFIG - main program
;++
; FUNCTIONAL DESCRIPTION:
;	This is the main program for standalone CONFIGURE.  It does the
;	following:
;
;		1)  Locks the entire image into the working set.
;		2)  Autoconfigures disks and port devices.
;		3)  If we are not using a quorum disk in order to
;		    form a cluster, the image exits.
;		4)  Sets a timer. Every 5 seconds, a check is made
;		    to see if we are in a cluster yet.  If so, we
;		    exit; else we reset the timer.
;		5)  Call BOO$CONFIGURE, which will configure all MSCP-
;		    and HSC-served devices.  At some point the disk which
;		    is needed to form the cluster will be configured, the
;		    cluster will form, and the image will exit.
;
;	Note that no determination is made about which specific disk is to
;	be the quorum disk - they are simply all configured.
;
; CALLING SEQUENCE:
;
;	Called by SYSINIT (via the image activator)
;
; INPUT PARAMETERS:
;
;	NONE
;
; OUTPUT PARAMETERS:
;
;	NONE
;
; ERROR INDICATIONS:
;
;	Various errors printed on the system console
;
;--

	.ENTRY	BOO$STACONFIG,^M<R2,R3>
	$LKWSET_S -			; Lock entire image in working set
		INADR=BOO$GQ_LIMITS, -	;
		RETADR=BOO$GQ_RETADR	;

;
; Get current values for local copy of SYSPARAM
;
	CALLS	#0,BOO$USEACT

;
; Autoconfigure disks and port devices.
;
	MOVAB	SELECT_LIST,BOO$GL_SELECT ; Use builtin select list
	CALLS	#0,BOO$CONFIGALL	; Autoconfigure all adapters

;
; If the quorum disk system parameter is non-blank, then we configure
; all "served" devices in case one of them is the quorum disk; else
; the image exits.
;
	CMPC3	#4*4,G^CLU$GB_QDISK,BLANKS
	BEQL	10$

; 
; Get binary equivalent of 5 seconds to set timer. 
;
	$BINTIM_S	timbuf = ASCII_TIME,-
			timadr = BIN_TIME
	$CMEXEC_S	AST_REC			; Check if we are a cluster member

;
; Start threads which will configure MSCP- and HSC-served disks.
;
	CALLS	#0,BOO$CONFIGURE

10$:	RET

AST_REC:	.WORD ^M<R2>

	MOVL	G^CLU$GL_CLUB,R2		; Get cluster info block
	BBC	#CLUB$V_CLUSTER,-		; Br if we are not a cluster member
		CLUB$L_FLAGS(R2),10$
	
;
; Cluster has been formed - exit
;
	$EXIT_S
	RET

;
; Cluster not yet formed
;
10$:	$SETIMR_S	efn    = #3,-		; Reset timer
			daytim = BIN_TIME,-
			astadr = AST_REC
	RET					; Dismiss AST


	.SBTTL	Dummy entry points

;
; These entry points are need to resolve references to routines
; not linked with the standalone version of CONFIG.
;

EXE$LOAD_CODE::
LBR$OUTPUT_HELP::
	.WORD	0			; ENTRY MASK
	CLRL	R0
	RET

BOO$READFILE::
BOO$FILCLOSE::
BOO$UFOOPEN::
BOO$EXEOPEN::
	CLRL	R0
	RSB


	.END	BOO$STACONFIG
